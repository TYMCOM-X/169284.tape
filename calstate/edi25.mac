TITLE EDITOR
SUBTTL NANCY AVNER
	VERNUM=25
	LOC 137
	EXP VERNUM

	LOC 41
	JSR UUOH
	JOBREN=124
	LOC JOBREN
	EXP GETCOM

	JOBFF=121
	JOBREL=44
	JOBDDT=74
	JOBAPR=125
	JOBTPC=127

	RDNXT=-14
	CORE=11
	EXIT=12
	MLON
	IFNDEF PURE,<PURE==1>
	IFN PURE,<TWOSEG>
	IFN PURE, <RELOC 400000>

OPDEF DEC[1B8]
	OPDEF PRMSG [2B8]	;PRINT MESSAGE
	OPDEF PRVAL [3B8]	;PRINT VALUE

;AC DEFINITIONS
P=17
F=16
NEW=15
OLD=14
CH=13
NUM=12
DIC=11	;HOLDS DICTIONARY POINTER
LINE=10 ;USED AS COUNTER TO BE SAVED(USUALLY LINE NO)
SERCH=7 ;USED IN ADRCAL
SPT=SERCH	;USED AS SEARCH POINTER IN FNDTXT
N1=7
N=6
SCH=6 ;SEARCH CHAR
TXP=5 ;TEXT POINTER
X4=4
X3=3
X2=2
X1=1


;CHANNEL DEFINITIONS
TTY=0
TMP=1
INF=2
OUTF=3
COMF=4 ;	COMMAND FILE CHANNEL
SYS=5
XTMP=6	;PHONY CHANNEL
REPF=7	;REPLACE FILE CHANNEL
;FLAG DEFINITIONS

;CHARACTER FLAGS

	ACT.F= 400000	;ACTION REQUIRED
	DEL.F= 200000	;DELIMETER
	NLEG.F= 100000 ;NOT LEGAL IN COMMAND WAIT MODE
	NUM.F= 40000	;NUMBER
	LET.F= 20000	;LETTER
	TEX.F= 1000	;TXT DELIM,',",[,]
	TERM.F= 400 ;TERMINATOR FLAG
	CNTRL= 200	;CNTRL CHAR 
	LOAD.F= 100	; RESPOND TO LOAD COMMAND
	ADD2.F= 40 ;ADD 2 TO FIND MATCHING



;FLAGS (LEFT HALF)

	COMW.F= 400000 ;COMMAND WAIT MODE
	DEL.F= 200000	;DELIMITER MODE
	VMOD.F= 100000 ;^V TYPED
	E.F=	40000	;^E TYPED
	FIRST= E.F	;FIRST ATOM CALCULATED
	NOPR.F= 20000 ;DON'T PRINT
	IGNOR= NOPR.F	;FLAG TO IGNOR BLANKS
	HFLAG= 10000	;^H TYPED
	DBSP.F=HFLAG	;DOUBLE SPACE
	ALT.F= 4000	;ALTMODE
	TELLNO=2000	;TELL NO. ALL PURPOSE SHORT FLAG
	MOVE.F=TELLNO
	EDI.F=1000	;EDIT MODE FLAG
	BLANK.F=EDI.F	;FLANG USED TO SEE IF BLANKS
	JOIN.F=400
	CRORBL=200	;CR OR BLANK-USED IN ADRCAL
	COMBLK=CRORBL	;COMPRESS BLANKS IN WRITE
	TAGFLG=100	;USED IN ADRCAL
	LOAD.F=TAGFLG	;USED TO IDENTIFY LOAD COMMAND
	SUBS.F= 40 ;CUBSTITUTE COMMAND
	APPEN.F=20	;APPEND COMMAND
	QLIN=10 ;USED FOR ^Q IN APPEND ,INSERT CHANGE
SUBMAD=4	;USED FOR SUBST MADE IN FNDTXT AND MAKSUB
	PAG.F=2
	DC.F=	1	;^D FLAG


;FLAGS (RIGHT HALF)
;THESE FLAGS REMAIN CONSTANT FROM COMMAND TO COMMAND

	COMF.F= 400000 ;TAKE COMMANDS FROM COMMAND FILE
	BUF.F= 200000	;TAKE COMMANDS FROM	INTERNAL BUFFER
	TAB.CH=100000	;TABS WERE CHANGED DUMDUM
	ONR.F=	40000	;USE BUFFER POINTER IN RINBUF
	FND.F= 20000	;FIND COMMAND
	MARK.F=10000	;SHOULD WE PRINT TOTAL
	KEEPV=4000	;EDIT MODE TO KEEP ^V FLAG SET
	TRAP.F=2000	;FINISH UP BEFORE STOPPING
	MIN.F=1000	;MINUS 1 FOUND
	WAIT.F=400	;WAIT FOR OK FROM USER TO SUBST.
	COMMA=200	;COMMA SEEN
	A.F=100 ;^A OR ^N TYPED
	INS.F=40	;INSERT,APPEND ,CHANGE MODE
	RPG.F=20	;ENTERED FROM RPG
	FIL.EQ=10	;USE FILENAME =


;UUO HANDLER
UUOHAN: PUSH P,UUOH	;RETURN ADDRESS ON PDL
	LDB X1,[POINT 9,40,8]	;GET THE NUMBER OR UUO

UUOTBL: JRST @.(X1)
	EXP DEC
	EXP PRTMES
	EXP PRTVAL





;CHARACTER TABLE



	DEFINE R(A)
	<REPEAT A,<	EXP	.-CTTAB
>
>
CTTAB:	0
	XWD	ACT.F+CNTRL+LOAD.F,ACNTRL
	XWD	ACT.F+CNTRL,BCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,CCNTRL
	XWD	ACT.F+CNTRL+LOAD.F+NLEG.F,DCNTRL
	XWD	ACT.F+CNTRL,ECNTRL
	XWD	ACT.F+CNTRL+NLEG.F,FCNTRL
	XWD	ACT.F+CNTRL+LOAD.F,GCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,HCNTRL
	XWD	ACT.F+CNTRL+LOAD.F,ICNTRL
	XWD	ACT.F+TERM.F,JCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,KCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,LCNTRL
	XWD	ACT.F+TERM.F,MCNTRL
	XWD	ACT.F+CNTRL,NCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,OCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,PCNTRL
	XWD	ACT.F+CNTRL+LOAD.F,QCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,RCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,SCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,TCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,UCNTRL
	XWD	ACT.F+CNTRL+LOAD.F,VCNTRL
	XWD	ACT.F+CNTRL+LOAD.F,WCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,XCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,YCNTRL
	XWD	ACT.F+CNTRL+NLEG.F,ZCNTRL

	XWD	ACT.F+LOAD.F,ALTMD

;IO CODES 34-40
	R(5)

	XWD	DEL.F,41	; !
	XWD	DEL.F+TEX.F,42 ;"

;IO CODES 43-46
	R(4)
	XWD	DEL.F+TEX.F,47 ;'

;IO CODES 50-56
	R(7)
	XWD	ACT.F,SLASH	;/

;NUMBERS

REPEAT 12,<	XWD	NUM.F,.-CTTAB
>

	XWD	DEL.F,72	; :

	EXP .-CTTAB
	XWD	DEL.F+ADD2.F,74 ;<
	XWD	ACT.F,EQUAL	;=
	XWD	DEL.F,76 ;>

	R(2)

;ALPHA UPPER CASE
REPEAT 32,<	XWD	LET.F,.-CTTAB
>

	XWD	DEL.F+ADD2.F+TEX.F,133 ;[
	EXP	.-CTTAB
	XWD	DEL.F+TEX.F,135 ;]

	XWD	ACT.F+TERM.F,UPARW	;^
	XWD	ACT.F,BKARW	;_

	R(1)

	REPEAT 32,<	XWD	LET.F,.-CTTAB
>
	R(2)

	XWD	ACT.F,ALTMD
	XWD	ACT.F,ALTMD
	EXP .-CTTAB

;COMMAND LIST- 1ST LIST IS THE NUMBER OF CHARACTERS REQUIRED
;SECOND LIST RT HALF IS ADRES WHERE TO GET FULL COMAND
; LFT HALF IS INCREMENT TO ADD TO RT HALF AS THE DISPATCH ADR

DEFINE U(A)
< ASCIZ/A/
>
CMDLST: U A
	U B
	U C
	U CL
	U COM
	U COP
	U D
	U E
	U EX
	U F
	U G
	U GO
	U I
	U J
	U K
	U LIN
	U LIS
	U L
	U MA
	U M
	U M
	U MOV
	U O
	U PA
	U P
	U Q
	U R
	U REP
	U SA
	U S
	U T
	U U
	U V
	U W
	BYTE (7) 15,0,0,0,0
	BYTE (7) 12,0,0,0,0
	U ^
	ASCIZ*/*
	U _
	U =
CMDEND=.-CMDLST

	NAB=400000	;NOTHING ALLOWED BEFORE COMAND
	ONE=200000	;MUST HAVE ONLY 1 THING BEFORE
	MHSB=100000	;MUST HAVE SOMETHIG BEFORE
	AFTER=40000	;MUST HAVE INFO AFTER
	NOAFT=20000	;NO INFO ALOW AFTER
	ONORN=10000	;ONE ONLY OR NOTHING ALLOWED BEFORE
	FND=4000	;MAY BE USED WITH FIND
	MRK=2000	;MAY BE USED WITH MARK

CMDADR: XWD MRK+ FND+ 2,APPEND
	XWD ONE+NOAFT+2,BUFFER
	XWD MRK+NOAFT+2,CHANGE
	XWD MRK+ NOAFT+1,CLEAR
	XWD NAB+AFTER+1,COMMND
	XWD ONE+AFTER+1,COPY
	XWD MRK+ FND+ NOAFT+2,DELETE
	XWD MRK+ FND+ NOAFT+1,EDIT
	XWD +1,EXITCM
	XWD MRK+ AFTER+1,FIND
	XWD MRK+ FND+ MHSB+NOAFT+1,GET
	XWD 0,GO
	XWD MRK+ FND+ NOAFT+2,INSERT
	XWD ONE+NOAFT+1,JOIN
	XWD ONE+NOAFT+1,KILL
	XWD NAB+1,LINES
	XWD MRK+FND+1,LIST
	XWD MRK+ FND+ 1,LOAD
	XWD MRK+ FND+ MHSB+AFTER+1,MARK
	XWD MRK+FND,M	;BEC. OF DUMMIES
	XWD MRK+ FND+2,MODIFY
	XWD ONE+AFTER+1,MOVE
	XWD NAB+AFTER+2,ONRING
	XWD 1,PAGE
	XWD MRK+ FND+ NOAFT+1,PRINT
	XWD NAB+NOAFT+1,QUIT
	XWD ONORN+1,READ
	XWD MRK+ FND+ 1,REPLAC
	XWD MRK+ FND+ 1,SAVE
	XWD MRK+ FND+ NOAFT+2,SUBSTI
	XWD NAB+1,TABS
	XWD MRK+ FND+ MHSB+AFTER+2,UNMARK
	XWD NAB+NOAFT+2,VERSN
	XWD MRK+ FND+ 1,WRITE
	XWD FND+ 0,CARRET
	XWD NAB,LINEFD
	XWD NAB,UPCOM
	XWD MRK+ FND+ 0,SLCOM
	XWD MRK+ FND+ ONE,BKCOM
	XWD MRK+ FND+ ONE,EQCOM
;HERE WE GO BOYS AND GIRLS -START 'ER UP

START0: JRST [SETZM RPG
		JRST STAR01]
	SETOM RPG
STAR01: RESET
	MOVE 0,[INIDAT,,NEWPNT]
	BLT 0,PLIST
	SETZM PLIST+1
	MOVE 0,[PLIST+1,,PLIST+2]
	BLT 0,DICT
	MOVE X1,JOBREL
	MOVEM X1,SVJOB
	SETZ F,
	SKIPE RPG
	TRO F,RPG.F
	GETPPN
	HLRZM PROJEC
	SKIPE JOBDDT
	JRST START
	MOVE P,PLIST
	SETO X1,
	TTCALL 6,X1
	MOVEM X1,SAVTTY
	PUSHJ P,STARTA
START:	MOVEI X1,GETREE
	MOVEM X1,JOBREN
	MOVEI X1,TRPENT
	MOVEM X1,JOBAPR
	;PUSHJ P,APR
	MOVE P,PLIST
	MOVEI X1,DICT
	MOVEM X1,LASLIN
	MOVEM X1,CURLIN
	PUSHJ P,INITUP	;INIT UPDATE AREA POINTER AND COUNTER
	PUSHJ P,INITMP	;INITIALIZE TMP FILE
	PUSHJ P,INITTY
STARTB: SETO X1,
	TTCALL	6,X1	;GET CHAR
	TLO X1,1
	TTCALL 7,X1	;NO ECHO TAB
	TRNN F,RPG.F
	JRST GETCOM
	MOVE X1,[2,,BLOCK]	;START TMPCOR UUO
	TMPCOR X1,	;READ AND DELETE
	PUSHJ P,NOCORF	;POOEY
	MOVE NEW,[POINT 7,BUFFA]
	ILDB CH ,NEW
	CAIN CH,40	;BLANK?
	JRST .-2
	SKIPN CH
	JRST QUEST
	MOVEI X1,RPGFIL
	PUSHJ P,FILNAM
	SETZM COLON
	HLRZ X1,RPGFIL+1
	CAIE X1,(SIXBIT/SIM/)
	CAIN X1,(SIXBIT/MAC/)
	SETOM COLON
	CAIN X1,(SIXBIT/FAL/)
	SETOM COLON
	CAIN CH,175
	JRST STARTC	;NEWFILE
	MOVE X1,[RPGFIL,, INFIL]
	BLT X1,INFIL+3
	SETZ F,
	TRO F,RPG.F
	TLO F,TELLNO
	SETZM FLRNG
	JRST READ+1


FILIUS: ASCIZ/FILE IN USE
/

STARTC: INIT SYS,17
	SIXBIT/DSK/
	0
	JRST IOBAD
	LOOKUP SYS,RPGFIL
	JRST GETCOM	;NEW FILE OK
	SETZM	RPG
	TTCALL 3,FILIUS
	JRST QUIT1	;LET THEM KNOW
NOCORF: PJOB
	MOVEI X3,3
	IDIVI 0,12
	ADDI X1,20
	LSHC X1,-6
	SOJG X3,.-3
	HLLM X2,SYSTMP
	INIT SYS,17
	SIXBIT/DSK/
	0
	JRST IOBAD
	LOOKUP SYS,SYSTMP
	JRST IOBAD
	INPUT SYS,BLOCK+1
	POPJ P,

STARTA: SETO X1,
	HRRI X1,6
	CALLI X1,41	;JBTPRV
	JFCL
	MOVEM X1,SAVJBT
	TRO X1,200
	CALLI X1,-11
	SETO 0,
	GETTMC
	MOVEM TMCSAV
	TLZ 36
	SETTMC
	LSH -^D23
	MOVEM TTYWID
	POPJ P,
INITUP: MOVE X1,INICNT
	MOVEM X1,UPCNT
	MOVE X1,INIPNT
	MOVEM X1,UPPNT
	POPJ P,

INICNT: 1200 ;=1200*NOBLKS
INIPNT: POINT 7,UPDATE

INITMP: INIT TMP,16
	SIXBIT/DSK/
	0
	JRST SYSFAL
INIT1:	CALLI X1,14	;DTAE
	CALLI X2,22	;CLOCK
	LSH X1,22
	IOR X1,X2
	MOVEM X1,TMPFIL
	MOVSI X1,(SIXBIT/TMP/)
	MOVEM X1,TMPFIL+1
	LOOKUP TMP,TMPFIL
	JRST INIT2	;GOOD NO FILE
	CLOSE TMP,
	JRST INIT1	;TRY AGAIN
INIT2:	PUSHJ P,ENTR
	PUSHJ P,LOKUP
	PUSHJ P,ENTR
	MOVEI X1,1
	HRRM X1,TMPBLK	;THE NEXT BLOCK NO TO DO UPDATING
	INIT XTMP,16
	SIXBIT/DSK/
	0
	JRST SYSFAL
	PUSHJ P,ZERTMP
	LOOKUP XTMP,TMPFIL
	JRST SYSFAL
	SETZ X1,
	RENAME XTMP,X1
	JFCL	;KEEP GOING
	CLOSE XTMP,
	POPJ P, ;DONE
ENTR: PUSHJ P,ZERTMP
	ENTER TMP,TMPFIL
	JRST SYSFAL
	SETOM TMPSTA
	POPJ P,
LOKUP:	PUSHJ P,ZERTMP
	CLOSE TMP,
	LOOKUP TMP,TMPFIL
	JRST SYSFAL
	SETZM TMPSTA
	POPJ P,
ZERTMP: SETZM TMPFIL+2
	SETZM TMPFIL+3
	HLLZS TMPFIL+1
	POPJ P,

;	ROUTINE TO GET COMMAND- COMMAND WAIT MODE


QUIREE: PUSHJ P,STARTA
	MOVEI X1,GETREE
	MOVEM X1,JOBREN
	JRST STARTB

TRPENT: TRZN F,TRAP.F	;SHOULD WE FINISH?
	JRST GETREA	;NO
	TLO F,ALT.F
	JRSTF @JOBTPC	;GO GET UM

APR:	MOVEI X1,2000
	APRENB X1,
	POPJ P,

GETREE:; PUSHJ P,APR
GETREA:		SETZM OTTY+2
	SETZM TTYBUF+3
	MOVE 0 ,[XWD TTYBUF+3,TTYBUF+4]
	BLT 0,TTYBUF+22
	TRZ F,600000
	MOVE X1,PRECUR
	MOVEM X1,CURLIN
GETCOM: MOVE P,PLIST	;CLEAR UP PDL
        SETZM DUMDUM
	MOVE X1,CURLIN
	MOVEM X1,PRECUR
	SETZM DONCHG
	PUSHJ P,SETLI1
	PUSHJ P,SWITCH
	PUSHJ P,SETUP	;INIT POINTERS
	TRZ F,37777	;LEAVE COMF.F,BUF.F,TAB.CH,ONR.F
	TLZ F,777777	;CLEAR LEFT HALF LEAVE RIGHT
	PUSHJ P,INITTY	;DEAZUM
	TLO F,COMW.F	;SET MODE
	SETO X1,
	GETLCH X1
	TLZE X1,10	;NO TAB SET
	SETLCH X1	;YES
	TRNN F,COMF.F!BUF.F
	TTCALL 1, CTTAB+52	;GIVE US A KISS
	PUSHJ P,GETLIN	;GET A CHARACTER
	JRST .-1	;JUMP BACK IF NOT DONE
	JRST DECODE	;GO CHECK SYNTAX AND CARRY ON

SETUP:	PUSHJ P,SETLI1
	MOVEI X1,^D259
	MOVNM X1,NCOUNT
	SETZM OCOUNT
	MOVE OLD ,OLDPNT
	POPJ P,

;ROUTINE TO GET CHAR AND ACT UPON IF NECESS
;SKIP RETURN IF CHAR TERMINATES LINE

GETLIN: PUSHJ P,GNC	;GET CHAR
	TLNE CH,LET.F!NUM.F	;LETTER OR NUM?
	JRST DEPO1	;YES
	TLNN F,COMW.F	;COMMAND WAIT MODE?
	JRST GET2	;NO
	TLNE CH,DEL.F	;DELIMITER?
	JRST	[TLOE F,DEL.F	;FIRST TIME ,FLAG ON?
	JRST GET3	;YES FLAG ON NOT 1ST
	HRRZ X1,CH
	TLNE CH,ADD2.F ;SHOULD WE ADD 2
	ADDI X1,2	;YES
	MOVEM X1,DELIM ;NO
        SETZM DUMDUM
	JRST DEPO1	]
	TLNN F,SUBS.F	;SUBST COMMAND?
	JRST GETLI0
	CAMN CH,[XWD 500300,4]
	JRST CPOPJ1

GETLI0: TRNN F,BUF.F!COMF.F
	JRST GET2	;NO
	TLNE CH,NLEG.F	;CHAR OK?
	JRST QUEST+1	;NO SO GET RID OF MODE
GET2: TLNE CH,ACT.F	;ACTION REQUIRED?
	JRST @CTTAB(CH) ;YES

DEPO1:DEPOS:	PUSHJ P,CKCNT	;SEE IF TOO MANY CHARS
	CAIN CH,15
	JRST MCNTRL
	PUSHJ P,DEPOT1	;ALL 0K SO DEPOSIT CHAR
	POPJ P, ;DONE SO RETURN

GET3:   HRRZ X1,CH
        TLNN CH,ADD2.F
        JRST GET3A
        ADDI X1,2
        CAMN X1,DELIM
        JRST [AOS DUMDUM
                HLLI CH,0
                JRST DEPO1]
GET3A:	HLLI CH,0
	 CAMN CH,DELIM	;SAME DELIMETER
	JRST	[TLZ F,DEL.F	;YES SO TURN FLAG OFF
                SETZM DUMDUM
	SETZM DELIM
	JRST DEPO1	]	;TURN OFF
	JRST DEPO1	;GO DEPOSIT


;ROUTINE TO DEPOSIT CHAR

DEPOT1: IDPB CH,NEW	;DEPOSIT
	TLNE F,E.F	;^E TYPE?
	POPJ P, ;YES SO RETURN
	AOS OCOUNT	;ADD TO OLD COUNT
	IBP OLD ;INC OLD POINTER
	POPJ P, 

;ROUTINE TO DETERMINE IF TOOMNY CHARS

CKCNT:	AOS NCOUNT
	SKIPGE NCOUNT	;OVER 259
	POPJ P, ;NO
	CAIN CH,15	;CR?
	POPJ P,; YES SO OK

TOOMNY:TTCALL 3, LONGLN
	MOVEI CH,15
	POPJ P ,
CPOPJ1: AOS	(P)
CPOPJ:	POPJ P,

;ROUTINE TO CALC CHAR COUNT AND DEPOSIT IN OCNTR
DEPCNT: MOVEI X1,^D259
	ADDM X1,NCOUNT
	POPJ P,

;ROUTINE TO GET CHAR FROM TTY,COMMAND FILE OR INTERNAL BUFFER

GNC:	TRNE F,BUF.F	;BUFFER FLAG?
	JRST GNC2	;YES
	TRNE F,COMF.F	;COM FILE?
	JRST GNC1
GETTY:	TRNN F,A.F	;^A SEEN?
	JRST GETTY2	;NO
	TTCALL 0,CH
	CAIE CH,16	;^N
	CAIN CH,1	;^A
	JRST GETTY5
	TTCALL 1,CTTAB +134	;\
	TRZ F,A.F	;TURN OFF FLAG
	CAIN CH,11	;^I
	JRST .+3
	HLL 0,CTTAB(CH)
	TLNN 0,CNTRL	;CONTROL CHAR?
	TTCALL 1,CH
GETTY5: TLNE F,COMW.F	;COM WAIT?
	JRST GETTY4	;YES
	TRNE F,TAB.CH	;TABS CHANGED?
	JRST GETTY4	;YES
	PUSHJ P,INITTT
	JRST GNC3
GETTY4: PUSHJ P,INITTY
	JRST GNC3
GETTY2: SOSLE ITTY+2
	JRST GETTY1
	IN TTY,
	JRST GETTY1
	JRST IOBAD
GETTY1: ILDB CH,ITTY+1
GNC3: CAIE CH,177	;RUBOUT ?
	SKIPN CH	;NULL?
	JRST GETTY	;YES
GNC3A:	TLNN F,COMW.F	;COMWAIT?
	JRST GNC3B	;NO DON'T BOTHER WITH LOWER CASE
	TLNE F,DEL.F	;DEL FLAG ON?
	JRST GNC3B	;YES DON'T CONVERT-
	CAIGE CH,141
	JRST GNC3B
	CAIG CH,172
	SUBI CH,40	;YES SUH
GNC3B:	HLL CH,CTTAB(CH)	;GET FLAGS
	POPJ P,

;COMMANFD FILE
GNC1: SOSLE ICOMF+2
	JRST GNCA
	IN COMF,
	JRST GNCA
	TRZ F,COMF.F	;ALL FINISHED WITH COMMAND FILE
	JRST GETCOM
GNCA: ILDB CH,ICOMF+1
	JUMPE CH,GNC1
	JRST GNC3A

;BUFFER FILE

GNC2: ILDB CH,BUFPT
	SKIPE CH
	JRST GNC3A
	AOS BUFLIN	;ADD TOBUF LEIN
	MOVE X1,BUFLIN
	MOVE DIC,(X1)
	SKIPN DIC
	JRST GNC2A	;EOF BUFFER MODE
	SETOM DONZER
	PUSHJ P,MAKPNT
	JRST GNC2A
	SETZM DONZER
	PUSHJ P,PUTBCM
	JRST GNC2

PUTBCM: PUSHJ P,SETBUF
	MOVE X2,BUFPTR
GNC2C: ILDB X1,DIC
	IDPB X1,X2
	CAIE X1,33
	JRST GNC2B
	SETZ X1,
	DPB X1,X2
	JRST GNC2D
GNC2B: CAIE X1,15
	JRST GNC2C
	MOVEI X1,12
	IDPB X1,X2
	SETZ X1,
	IDPB X1,X2
GNC2D: MOVE X1,BUFPTR
	MOVEM X1,BUFPT
	POPJ P,

BUFPTR: POINT 7,BUFCOM
GNC2A:	TRZ F,BUF.F	;E OF BUFFER MODE
	SETZM DONZER
	TLNE F,COMW.F	;COMMAND WAIT?
	JRST GETCOM	;YES
	JRST GNC
SETBUF: SETZM BUFCOM
	MOVE X1,[XWD BUFCOM,BUFCOM+1]
	BLT X1,BUFCOM+52
	MOVE X1,BUFPTR
	MOVEM X1,BUFPT
	POPJ P,


;ERROR MESSAGES
SYSBAD: ASCIZ/
SYSTEM FAILURE
/
NOFILE: ASCIZ/FILE NOT AVAILABLE
/
BIGCOR: ASCIZ/
OUT OF CORE
/
BING: EXP 7
LONGLN: ASCIZ/LINE TOO LONG
/
QUESTN: ASCIZ/?
/
BIGRNG: ASCIZ/
UNABLE TO INTERPRET RANGE-TOO MANY ARGUMENTS
/
LF:	BYTE (7) 12,0,0,0,0
CRLF: BYTE (7) 15,12,0,0,0
CR:	BYTE(7) 15,0,0,0,0

BACNEW: DEC NEW
	SOS NCOUNT
	POPJ P,

BACOLD: DEC OLD
	SOS OCOUNT
	POPJ P,

BUFFIL: ASCIZ/
BUFFER OVERFLOW
/
NOROOM: TTCALL 3, BUFFIL
	JRST GETCOM
RING1:	DEC OLD ;BLEK
RING: TTCALL 1, BING	;BING BONG
	TRNN F,COMF.F!BUF.F
	POPJ P,
	TRNE F,ONR.F	;ANY PLACE TO GO?
	JRST QUEST1
	TRZ F,COMF.F+BUF.F
	JRST GETCOM


SYSFAL: TTCALL 3,OVRQUO
	SETZM JOBREN
	CALLI 1, EXIT
	JRST .-1
OVRQUO: ASCIZ/
OVER QUOTA
/
OVER: TTCALL 3,OVRQUO
	JRST GETCOM
IOBAD:	TTCALL 3, SYSBAD
	JRST GETCOM
FINWHY: TTCALL 3, NOFILE
	JRST GETCOM
QUEST:	TTCALL 3, QUESTN
	TRZE F,ONR.F
	JRST [PUSHJ P,QUEST1
		JRST GETCOM]
	TRZ F,COMF.F+BUF.F
	JRST GETCOM
QUEST1: TRNN F,BUF.F	;ARE WE USING BUFFER?
	POPJ P,
	MOVE X1,RINBUF
	IMULI X1,^D9
	ADDI X1,BUF0
	SOJ X1,
	MOVEM X1,BUFLIN
	PUSHJ P,SETBUF
	TRO F,BUF.F
	POPJ P,

;CONTROL CHARACTERS

;^A DELETES PRECEDING CHAR,^N BACKSPACES IN OLD AND NEW LINES
NCNTRL: TLO F,NOPR.F
ACNTRL: CAMN NEW,NEWPNT ;BEG OF LINE?
	JRST [TRO F,A.F
		JRST RINGR]	;YES
	LDB CH,NEW
	TLNN F,COMW.F+DEL.F
	JRST ACN2
	HLL CH,CTTAB(CH)
	TLNN CH,DEL.F
	JRST ACN2
	TLNE CH,ADD2.F
	ADDI CH,2
	HRRZ X1,CH
	SKIPN DELIM	;WAS IT END DELIMITER?
	JRST [TLO F,DEL.F
        SETZM DUMDUM
		MOVEM X1,DELIM
		JRST ACN4]	;YES SO MAKE IT DELIMMODE AGAIN
	CAME X1,DELIM
	JRST ACN4
        TLNN CH,ADD2.F
        JRST ACN7
        SKIPN DUMDUM
        JRST ACN7
        SOS DUMDUM
        JRST ACN4
ACN7:	SETZM DELIM
        SETZM DUMDUM
	TLZ F,DEL.F
ACN4:	TLNE CH,ADD2.F
	SUBI CH,2
ACN2:	TRNE F,COMF.F!BUF.F
	JRST ACN3
	TRON F,A.F	;^A TYPED BEFORE
	TTCALL 1,CTTAB+134	;\
	HLLI CH,0
	PUSHJ P,PRTCHR
ACN3:	SETZ CH,
	DPB CH,NEW
	PUSHJ P,BACNEW	;BACKUP NEW POINTER
	TRNE F,COMF.F!BUF.F
	JRST ACN5
	PUSHJ P,SILENC
ACN5:	TLNE F,COMW.F
	POPJ P,
	TLZN F,NOPR.F	;NO PRINT? OR ^N
	POPJ P,
	CAMN OLD,OLDPNT
	JRST RINGR
	PUSHJ P,BACOLD	;YES SO BACKUP OLD POINTER 
	POPJ P,

RINGR:	PUSHJ P,RING
SILENC: INIT TTY,200	;NO TALKEE
	SIXBIT/TTY/
	0
	JRST IOBAD
	POPJ P,
;^B TAKE COMMANDS OR TEXT FROM NEXT BUFFER NO TYPED
BCNTRL: PUSHJ P,CKE
	TRNE F,COMF.F!BUF.F
	JRST BCNTR1
	TTCALL 1, CTTAB+43	;#
	PUSHJ P,CKALT
	TTCALL 3,CRLF
	HLL CH,CTTAB(CH)
	SKIPA
BCNTR1: PUSHJ P,GNC
	TLZN CH,NUM.F
	JRST QUEST
	SUBI CH,60
	IMULI CH,^D9
	ADDI CH,BUF0
	SOJ CH,
	MOVEM CH,BUFLIN
	PUSHJ P,SETBUF
	TRO F,BUF.F
	POPJ P,


CKALT:	TTCALL 0,CH
	CAIE CH,33
	CAIN CH,175
	PUSHJ P,ALTMD
	POPJ P,

;^C COPY AND PRINT NEXT CHAR IN OLD LINE

CCNTRL: PUSHJ P,CKE
	ILDB CH,OLD	;GET CHAR
	SKIPN CH
	JRST RING1
	PUSHJ P,CKCNT
	TRNN F,COMF.F!BUF.F
	PUSHJ P,PRTCHR
	AOS OCOUNT
	IDPB CH,NEW
	CAIE CH, 15
	POPJ P,
	MOVEI CH,12
	TRNN F,COMF.F!BUF.F
	TTCALL 1,CH
	AOS NCOUNT
	JRST CPOPJ1

;^D COPY AND PRINT REST OF OLD LINE TO NEW AND END EDIT, ALSO TERMINATE COMMAND
;^F DOES SAME EXCEPT DOES NOT PRINT
;^Y COPIES,DOES NOT PRINT,CONTINUES EDIT AT BEG OF NEWLIN

YCNTRL: PUSHJ P,CKE
YCN1:	TLO F,HFLAG+NOPR.F
	TRNN F,COMF.F!BUF.F
	PUSHJ P,D1
	PUSHJ P,D2
	PUSHJ P,SWITCH
	PUSHJ P,SETUP
	HRLI OLD,440700	;FOOL ^D 
	AOJ OLD,
	TLZ F,HFLAG+NOPR.F
	POPJ P, ;START OVER
FCNTRL: PUSHJ P,CKE
	TLO F,NOPR.F
	JRST D1
DCNTRL: PUSHJ P,CKE
	TLNE F,SUBS.F	;SUBST COMMAND?
	JRST CPOPJ1	;YES
	CAME OLD,OLDPNT
	JRST D1D
	CAME NEW,NEWPNT ;BEG OF LINE
	JRST D1D
DPRINT: TRNE F,INS.F
	JRST DPR1
	TLO F,QLIN	;INDICATE BEGGINNNNINNNG
		ILDB CH,OLD
	SKIPN CH
	MOVEI CH,15
	PUSHJ P,PRTCHR
	CAIE CH,15
	JRST	DPRINT
	MOVEI CH,12
	TTCALL 1,CH
	MOVE CH,DFLG
	POPJ P,
DPR1:	TRNE F,FND.F	;FIND COMMAND
	POPJ P,
	JRST GETCOM
D1D:	TLO F,DC.F	;PRINT NO MATTER WHAT

D1:	IBP OLD
D1A: LDB CH,OLD
	PUSHJ P,CKCNT
	SKIPN CH	;NULL CHR?
	MOVEI CH,15	;END THE LINE
	TRNE F,COMF.F!BUF.F
	JRST D1B
	TLNE F,DC.F
	JRST D1C
	TLNE F,NOPR.F	;NO PRINT ON
	JRST D1B	;YES
	TLNE F,EDI.F	;EDIT MODE (SHOULD WE PRINT?)
D1C:	PUSHJ P,PRTCHR
D1B:	PUSHJ P,DEPOT1;DEPOSIT
	CAIE CH,15	;CR?
	JRST D1A	;NO
	TLNE F,HFLAG	;^H MODE
	POPJ P, ;YES SO RETURN TO ^H
	PUSHJ P,D2
	JRST CPOPJ1	;SKIP RETURN
D2:	TRNE F,COMF.F!BUF.F
	JRST D2A
	TLZE F,NOPR.F
	TTCALL 1,CH
D2A:	MOVEI CH,12
	TRNN F,COMF.F!BUF.F
	TTCALL 1,CH
	TLZ F,DC.F
	POPJ P,

P: CAIE CH,15
	CAILE CH,32
	JRST PRTCH1	;NOT ^CHAR
	CAIN CH,12
	JRST PRTCH1
	PUSH P,CH
	MOVEI CH,46	;&
	TTCALL 1,CH
	POP P,CH
	ADDI CH,100
	TTCALL 1,CH
	SUBI CH,100
	POPJ P,
PRTCH1: TTCALL 1,CH
	POPJ P,
;^E IS INSERTTEXT INTO OLD LINE ,1ST ^E GIVES<,2ND GIVS >

CKE:	TLNN F,E.F
	POPJ P,
ECNTRL: TLCN F,E.F	;DO COMPLE WAS IT ON?
	JRST E1 ;NOT ON 1ST TIME
	TRNN F,COMF.F!BUF.F
	TTCALL 1, CTTAB+76	;>
	POPJ P,
E1:	TRNN F,COMF.F!BUF.F
	TTCALL 1, CTTAB+74	;<
	POPJ P,

;^G USED IN SUBSTITUTE TO MATCH ANY CHAR
GCNTRL: TRNN F,COMF.F!BUF.F
	TTCALL 1, CTTAB+41	;GIVE A !
	MOVEI CH,177;	GET A NULL
	PUSHJ P,CKCNT
	CAIN CH ,15
	JRST MCNTRL
	PUSHJ P,DEPOT1
	POPJ P,

;^HCOPY AND PRINT UP TO CR AND CONTINUE EDIT FROM THERE
;^L DOES SAME AS ^H ONLY DOES NOT PRINT

LCNTRL: PUSHJ P,CKE
 TLO F,NOPR.F
	TRNN F,COMF.F!BUF.F
	TTCALL 1, CTTAB+53	;+
	SKIPA
HCNTRL: PUSHJ P,CKE
	IBP OLD
HCNTR2: LDB CH,OLD
	SKIPN CH
	JRST RING1
	PUSHJ P,CKCNT
	CAIN CH,15
	JRST HCNT1	;END OF LINE
	TRNE F,COMF.F!BUF.F
	JRST .+3
	TLNN F,NOPR.F
	PUSHJ P,PRTCHR
	PUSHJ P,DEPOT1
	JRST HCNTR2
HCNT1:	PUSHJ P,BACOLD
	SOS NCOUNT
	TLZ F,HFLAG+NOPR.F
	POPJ P,

;^I -TAB TO NEXT TAB STOP
;^U-CPOY AND PRINT OLD LINE UP TO NEXT TABSTOP IN NEW LINE
UCNTRL: PUSHJ P,CKE
	TLO F,NOPR.F
	JRST IC1A
ICNTRL: MOVEI CH,40	;GET A SPACE
IC1A: PUSHJ P,IC1
	SETZM TABB
	IBP OLD
SPACE:	CAML X3,X2	;IS POSIT =TABSTOP
	JRST S2 ;YES
SPA1: LDB X1,OLD	;YES
	TLNE F,NOPR.F	;^U
	MOVE CH,X1	;YES
	SKIPN CH
	JRST RING1
	PUSHJ P,CKCNT
	PUSHJ P,DEPOT1
	TRNE F,COMF.F!BUF.F
	JRST SPA2	;DON'T ECHO
	TLNE F,NOPR.F	;^U
	JRST SPA2A
	TLNE F,COMW.F	;COMM WAIT?
	JRST SPA2	;YES ,MONITOR DID IT ALREADY
	TRNN F,TAB.CH	;HAVE TABS BEEN CHANGED?
	JRST SPA2
SPA2A:	PUSHJ P,PRTCHR;YES SO WE BETTER ECHO
SPA2: TLNE F,NOPR.F
 JRST SPA3
	CAIE X1,11
	JRST SPA4
	SKIPE TABB
	JRST SPA4
	MOVEM	OLD,TABB
	JRST SPA4
SPA3:	CAIN X1,11
	JRST S2 ;YES END
SPA4:		CAIN CH,15	;CR?
	JRST S3 ;YES
	AOJA X3,SPACE	;KEPEP GOING

S3:	TTCALL 3, LF
	AOS (P)
S2:	TLZ F,NOPR.F	;^U
	SKIPE TABB
	MOVE OLD,TABB
	PUSHJ P,BACOLD
	POPJ P, ;NO
IC1:	SETZ X1,
	MOVEI X3,^D260
	ADD X3,NCOUNT

I1:	MOVE X2,TABES(X1)
	CAMLE X2,X3	;IS POSIT>TAB
	POPJ P, ;YES START SPACING
	AOJ	X1,
	CAIE X1,12	;COVERED ALL TABSTOPS
	JRST I1 ;NO
	ADDI X2,10;	IN THAT CASE JUMP 5 SPACES
	CAMG X3,X2
	POPJ P,
	JRST .-3

; ^J(LF) PERMITS LINE CONTINUATION
;^M(CR) ENDS COMAND OR LINE
 
JCNTRL: TRNN F,COMF.F!BUF.F
	TTCALL 3, CR
	PUSHJ P,CKCNT
	CAIN CH,15
	JRST MCNTRL
	PUSHJ P,DEPOT1
	TLNE F,COMW.F	;COMMAND MODE?
	JRST CPOPJ1	;YES SKIP RETURN
	POPJ P,

MCNTRL: PUSHJ P,CKE
	PUSHJ P,CKCNT
	PUSHJ P,DEPOT1
	TLNE F,SUBS.F	;SUBST COMMAND?
	JRST CPOPJ1	;YES
	PUSHJ P,GNC	;GET THE LF
	MOVEI CH,12
	AOS NCOUNT
	PUSHJ P,DEPOT1
	JRST CPOPJ1	;SKIP RETURN?


;^S-DELETES NEXT CHAR IN LINE PRINTS A %
;^K DELETES NEXT CHAR BUT PRINTS CHARACTER

SCNTRL: PUSHJ P,CKE
TLO F,NOPR.F
	ILDB CH,OLD
	SKIPN CH
	JRST RING1
	TRNN F,COMF.F!BUF.F
	TTCALL 1, CTTAB+45	;GIVE A %
	SKIPA
KCNTRL: PUSHJ P,CKE
	AOS OCOUNT
	TLZE F,NOPR.F	;^S
	POPJ P, ;YES
	ILDB CH,OLD	;GET CHAR
	SKIPN CH
	JRST RING1
	PUSHJ P,PRTCHR
	POPJ P,

;^P DEL NEXT CHAR IN OLD LINE UP TO BUT NOT INCLUD CHAR TYPE AFTER^P
;^X DEL UP TO AND INCLUDING CHAR TYPED NEXT
;^O COPIES UP TO BUT NOT INCLUDING CHAR TYPED AFTER,PRINTING CHARS COPIED
;^Z COPIES UP TO AND INCLUDING CHAR TYPED NEXT

ZCNTRL: TLO F,HFLAG
	TLZ F,NOPR.F
	JRST OCN1
XCNTRL: TLO F,HFLAG+NOPR.F
	JRST OCN1
PCNTRL: TLO F,NOPR.F
	TLZ F,HFLAG
	JRST OCN1
OCNTRL: TLZ F,NOPR.F+HFLAG
OCN1: PUSHJ P,CKE
	PUSH P,NEW
	PUSH P,OLD
	PUSH P,NCOUNT
OCNTR1: TRNN F,COMF.F!BUF.F
	JRST [PUSHJ P,SILENC
		PUSHJ P,CKALT
		JRST GETT1A]
	PUSHJ P,GNC
	JRST GETT1F
GETT1A: TRNE F,TAB.CH	;TABS CHANGED?
	JRST GETT1C	;YES
	PUSHJ P,INITTT	;NO
	JRST	GETT1F
GETT1C: PUSHJ P,INITTY
	PUSHJ P,CKTAB
GETT1F: HRRZ X1,CH
PRESER: TLNN F,HFLAG	;^Z OR^X
	IBP OLD ;NO
	ILDB CH,OLD
	SKIPE CH
	CAIN CH,15
	JRST QUESTR
	CAME CH,X1
	JRST PRESER+2
	POP P,NCOUNT
	POP P,OLD
	POP P,NEW
	JRST GETT1G

QUESTR: POP P,NCOUNT
	POP P,OLD
	POP P,NEW
	TLZ F,HFLAG+NOPR.F
	JRST RING
GETT1G: PUSH P, X1
	TLNE F,HFLAG	;^Z OR^X
	JRST GETT1D	;YES
	ILDB CH,OLD
	TLNE F,NOPR.F	;NO PRINT
	JRST [MOVEI CH,"%"
	JRST GETT1E]	;YES
	PUSHJ P,CKCNT	;NO
	PUSHJ P,DEPOT1
	DEC OLD
GETT1E: TRNN F,BUF.F!COMF.F
	PUSHJ P,PRTCHR
GETT1D: POP P,X1
	IBP OLD

O1:	LDB CH,OLD	;GET CHAR FROM OLD LINE
	CAMN CH,X1	;IS IT THE ONE WE WANT
	JRST O3 ;YES WE IS DONE BABY
	CAIN CH,15	;END OF LINE?
	JRST QUEST	;YES BAD SEARCH
	TLNE F,NOPR.F	;^P MODE(DELETE)
	JRST O2 ;YUP
	PUSHJ P,O6
	JRST O1
O6:	PUSHJ P,CKCNT
	PUSHJ P,DEPOT1
	TRNN F,BUF.F!COMF.F
	PUSHJ P,PRTCHR
	POPJ P,

O2:	PUSHJ P,O5
	JRST O1
O5:	IBP OLD
	AOS OCOUNT
O8:	TRNN F,COMF.F!BUF.F
	TTCALL 1, CTTAB+45	;%
	POPJ P,

O3:	TLZE F,HFLAG	;^Z OR ^X?
	JRST O4 ;YES
	PUSHJ P,BACOLD
	TLZ F,NOPR.F
	POPJ P,
O4:	TLZE F,NOPR.F	;^X?
	JRST O8 ;YES
O7:	PUSHJ P,O6	;COPY THAT LAST CHAR
	PUSHJ P,BACOLD
	POPJ P,




;^Q TO DELETE LINE BEING TYPED
QCNTRL: TRNE F,COMF.F!BUF.F
	JRST QCN1
	MOVEI X1,136
	TTCALL 1, X1	;GIVE A ^
QCN2:	TTCALL 3, CRLF	;
QCN1:	TLZ F,E.F
 CAMN OLD,OLDPNT	;BEG OF LINE

	CAME NEW,NEWPNT
	TLZA F,QLIN	;NO
	TLO F,QLIN	;YES
	PUSHJ P,SETUP
	TLNN F,COMW.F
	POPJ P,
	TLNN F,SUBS.F
	TLZ F,DEL.F	;NO SO	TURN OFF
        SETZM DUMDUM
	SETZM DELIM
	POPJ P,

;^R GIVES LF,COPIES REST OF LINE,RET CR,COPIES NEW LINE UP TO POINT WHERE
;TYPED
;^T IS SAME AS ^R ONLY ALIGNS THEM 

TCNTRL: PUSHJ P,CKE
	TTCALL 3, CRLF
	MOVEI CH,40	;BLANK
	MOVEI X3,^D259
	MOVNS X3,X3
T1:	CAMN X3,NCOUNT	;ARE WE AT POSIT NOW?
	JRST R0 ;YES
	PUSHJ P,PRTCHR
	AOJA X3,T1

RCNTRL: PUSHJ P,CKE
	TTCALL 3, LF
R0:	MOVE X1,OLD	;GET REST OF LINE
	MOVE X2,NEWPNT	;GET BEG OF LINE
R1:	ILDB CH,X1	;START FINISHING UP OLD LINE
	SKIPN CH
	MOVEI CH,15
	PUSHJ P,PRTCHR
	CAIE CH,15	;CR?
	JRST R1 ;NO
	TTCALL 3, LF	;YES
R2:	CAMN X2,NEW	;ARE WE WHERE WE WERE WHEN IT STARTED?
	POPJ P, ;YES 
	ILDB CH,X2	;START PRINTING BEGINNING OF LINE
	PUSHJ P,PRTCHR
	JRST R2

;^V SAYS IGNORE NEXT CONTROL CHAR 
VCNTRL: TRNE F,COMF.F!BUF.F
	JRST [PUSHJ P,GNC
	JRST GETLI1]
	PUSHJ P,SILENC
	PUSHJ P,CKALT
	HLL CH,CTTAB(CH)
	TLNE F,COMW.F	;COMM WAIT
	JRST VCN2	;	;YES
	TRNE F,TAB.CH	;TABS CHANGED?
	JRST VCN2
	PUSHJ P,INITTT	;NO
	SKIPA
VCN2:	PUSHJ P,INITTY
GETLI1: TRNE F,KEEPV	;EDIT MODE?
	TLO F,VMOD.F	;TURN BACK ON
		HRRZ X1,CH
	CAILE X1,32
	JRST [TTCALL 1,CH
	JRST DEPO1A]
	TLNE CH,CNTRL
	JRST GETLI2
	CAIE X1,12
	JRST GETLI3
	TRNN F,COMF.F!BUF.F
	TTCALL 3,CRLF
	TLNE F,DEL.F;BOTH?
	JRST DEPO1
	TLNN F,COMW.F	;OR NEITHER
	JRST DEPO1A
	POPJ P,
GETLI3: TRNN F,COMF.F!BUF.F
	TTCALL 3,CR
		TLNE F,LOAD.F
	POPJ P,
		PUSHJ P,DEPO1
	TLNN F,COMW.F	
	POPJ	P,	;NO
	PUSHJ P,GNC	;YES	;GET LF
	JRST DEPO1
GETLI2: TRNE F,COMF.F!BUF.F
	JRST DEPO1A
	ADDI X1,100
	TTCALL 1, CTTAB+46	; &
	TTCALL 1, X1
DEPO1A: TLNE F,LOAD.F
	POPJ P,
	JRST DEPO1	;NO
;^W DELETES PRECEEDING WORD
WCNTRL: TLZ F,NOPR.F	;TURN OFF IN CASE ON
	CAMN NEW,NEWPNT ;BEG OF LINE?
	JRST RING	;YES DUM DUM
	LDB CH,NEW
	TTCALL 1,CTTAB+134
	JRST W2A
W2:	PUSHJ P,BACNEW	;BACK UP POINTER
	CAMN NEW,NEWPNT ;BEG OF LINE?
	JRST W3 ;YES
	LDB CH,NEW	;GET CHAR
W2A:	CAIE CH,40	;SPACE?
	CAIN CH,11	;OR TAB?
	JRST W1 ;YES
	TLO F,NOPR.F	;TURN ON FLAG
	PUSHJ P,PRTCHR
	JRST W2 ;CONTINUE
W1:	TLNN F,NOPR.F	;FLAG ON?(BEFORE OR AFTER WORD?)
	JRST [PUSHJ P,PRTCHR
	JRST W2 ];OFF(BEFORE WORD)
W3:	TLZ F,NOPR.F	;TURN OFF
	TRNN F,COMF.F!BUF.F
	TTCALL 1, CTTAB+134	;GIVE A \
	POPJ P,

;ALTMODE GARBAGE

ALTMD: TLNE F,COMW.F	;COMWAIT?
	JRST ALTM2
ALTM1:	TLCN F,ALT.F	;FLAG ON?
	JRST RING	;GIVE WARING
ALTM2:	TTCALL 3,CRLF
	JRST GETCOM

EQUAL: PUSHJ P,DEPO1
	TLNN F,COMW.F
	POPJ P,
	TLNN F,DEL.F
	AOS (P)
	POPJ P,

SLASH:UPARW:BKARW:	PUSHJ P,DEPO1
	TLNN F,COMW.F	;COM WAIT MODE
	POPJ P, ;NO
	TLNN F,DEL.F	;DELIMITER MODE
	JRST [TRNN F,COMF.F!BUF.F
	TTCALL 3, CRLF
	JRST CPOPJ1]
	POPJ P,

;
; ROUTINE TO BACK UP POINTER IN REF. IN 40 1 BYTE
DEC:	MOVSI X1,070000
	ADD X1,@40
	TLNE X1,400000	
	ADD X1,[XWD 347777,-1]
DEC1:	MOVEM X1,@40
	POPJ P,



;ROUTINE TO SWITCH POINTERS OF NEWLIN AND OLDLINE

SWITCH: MOVE X1,NEWPNT
	EXCH X1,OLDPNT
	MOVEM X1,NEWPNT
	POPJ P,

;COMMAND DECODER ROUTINE

DECODE: MOVE NEW,NEWPNT
	TLZ F,777777
	PUSHJ P,SETSRC	;SETUP SEARCH LIST	FOR ADDRESSES
	PUSHJ P,FNDCOM	;FIND THE COMMAND
	PUSHJ P,TSTCOM	;SEE IF COMMAND HAS RIGHT NO OF ARGS
	PUSHJ P,ADRCAL	;NOW CALC ADR IF COMMAND OK
	LDB CH,NEW
	HLL CH,CTTAB(CH)
	JRST	@COMAN	;AWAY WE GO

;ROUTINE TO GET CHAR AND FLAGS WITH POINTER CONTAINED IN NEW
;GETCH IMPLIES TO IGNOR BLANKS

GETCH:	TLO F,IGNOR
GETCHR: ILDB CH,NEW
	HLL CH,CTTAB(CH)
	TLNN F,IGNOR	;IGNOR BLANKS?
	POPJ P, ;NO
	CAIE CH,40	;BLANK?
	JRST [TLZ F,IGNOR
	POPJ P,]	;NO RETURN
	JRST GETCHR


;ROUTINE TO SETUP SRCH LST FOR ADRES RANGE
;THE FOLLOWING CODES ARE USED IN BITS 14-17 OF EACH WORD IN THE SEARCH LST
;
;0-END OF SEARCH LST
;1-AT THIS LOC-INCREM TOADD TO DICT 
;2-SEARCH FOR LABEL
;3-SEARCH FOR TEXT
;4-ADD THIS INCREMENT
;5-SUBTRACT THIS INCREM
;6-BEGIN SEARCH 2(END RANGE)
;7-SEARCH FOR TAG BUT IGNORE PRECEEDEING BLANKS
;10- USE VALUE OF CALCULATED CURRNET LINE
;11-BUFFER NO.
;12-MARK NO.

ACPNT:	POINT 4,SRCLST(LINE),17 ;POINTER AS DESCR ABOVE

SETSRC: SETOM FLRNG	;USE FLRNG AS COUNTER -1= NOTHING BEFORE
	;0=1 ADRS BEF
	;>=1 MEASN MORE THAN 1 ADRES
	SETZM SRCLST
	MOVE 0,[XWD SRCLST,SRCLST+1]
	BLT 0,ENDSRC
	SETZ LINE,	;LINE USED AS COUNTER INTO SRCLST
ATOM: PUSHJ P,GETCH	;GET CHAR IGNOR BLANKS
	CAIN CH,"@"
	JRST AT

ATOM1:	TLNE CH,NUM.F	;NUMBER?
	PUSHJ P,ATOM1A	;YES
ATOMN:	TLNE CH,DEL.F	;DELIM?
	JRST TAGTEX	;YES
	CAIN CH,56	; .?
	JRST CURRT	;YES
	CAIN CH,44	; $?
	JRST LAST	;YES
	CAIN CH,54	; ,?
	JRST COMA	;YES
ATOMED: CAIN CH,73	; ;?
	PUSHJ P,SEMI	;YES
	TLNN CH,LET.F!ACT.F	;BEGIN OF COMMAND?
	JRST QUEST	;BAD 
	TLZE F,FIRST	;FIRST ON?
	JRST [TRZ F,COMMA
	AOS FLRNG
	POPJ P,]
	TRZE F,COMMA	;(FIRST OFF) IS COMMA ON?
	JRST QUEST	;YES BAD
	POPJ P,

;ROUTINE TO INTERP. NUMBER
ATOM1A: PUSHJ P,MAKOCT ;MAKE NUMBER
	HRRZM NUM,SRCLST(LINE) ;DEPOSIT
	MOVEI X2,1	;CODE
	PUSHJ P,DEPSR1	;DEPOSIT
	TLO F,FIRST	;TURN ON FLAG TO INDICAT PAST FIRST ATOM
	POPJ P,

;ROUTINE TO MAKE OCTAL NO AND PUT IN AC CALLED NUM

MAKOCT: SETZ NUM,
MAK1: TLZN CH,NUM.F	;NUMBER?
	JRST MAK2	;NO
	TRZ CH,60	;SUB 60
	IMULI NUM,12
	ADD NUM,CH
	PUSHJ P,GETCHR	;DONT IGNOR BLANKS
	JRST MAK1
MAK2: CAIN CH,40
	PUSHJ P,GETCH	;GET NEXT NON BLANK
	POPJ P,

;ROUTINE TO SETUP POINTER FOR TAG OR TEXT SEARCH

TAGTEX: TLNE CH,TEX.F	;TEXT DELIMETER
	JRST TEX1	;YES
TAG1: CAMN CH,CTTAB+74	; <?
	JRST TAG3	;YES
	MOVEI X2,2
	JRST FINDA	;GO FIND END OF STRING
TAG3: MOVE CH,CTTAB+76	; > (END OF STRING DELIM)
	MOVEI X2,7	;CODE TO IGNORE PREC BLANKS
	JRST FINDA
TEX1: MOVEI X2,3	;CODE FOR TEXT
	CAME CH,CTTAB+133	; [?
	JRST FINDA	;NO
	MOVE CH,CTTAB+135	;]
FINDA:	MOVEM CH,DELIM
	PUSHJ P,DEPSRC	;DEPOSIT POINTER AND CODE
FIND1:	PUSHJ P,GETCHR
	SKIPN CH	;NULL?
	JRST QUEST	;YES DIDNT FIND END OF STRING
	CAME CH,DELIM	;SAME DELIM?
	JRST FIND1	;NO
	JRST ADDATM	;GO SEE IF HAS + OR - ATTACHED

;CODE FOR	USE OF .
CURRT:	MOVEI X2,10
	PUSHJ P,DEPSR1
	JRST ADDATM

; CODE FOR $
LAST:	SKIPE EOF
	PUSHJ P,FINUP	;TOO BAD POOPSIE
		MOVE X3,LASLIN
LAST1:	SUBI X3,DICT
	HRRZM X3,SRCLST(LINE)
	MOVEI X2,1
	PUSHJ P,DEPSR1
	JRST ADDATM

;CODE FOR ,
COMA: TLZN F,FIRST	;	DO WE HAVE COMPLETE NUMBER BEFORE?
	JRST QUEST	;NO
	TROE F,COMMA	;FIRST COMMA?(WAS FLAG ON)
	JRST QUEST	;NOT FIRST,COMMA WAS ON
	AOS FLRNG
	MOVEI X2,6	;BEGIN SRCH 2
	PUSHJ P,DEPSR1
	PUSHJ P,GETCH
	JRST ATOM1


;CODE FOR @-MARK LIST
AT:	PUSHJ P, GETCH
	TLNN CH,NUM.F
	JRST QUEST
	PUSHJ P,MAKOCT
	CAIL NUM,1
	CAILE NUM,2
	JRST QUEST
	HRRZM NUM,SRCLST(LINE)
	MOVEI X2,12
	PUSHJ P,DEPSR1
	AOS FLRNG
	TRO F,MARK.F
	SETZM POLISH
	SETZM ENDCM
	JRST ADDATM+1

;CODE FOR ; -IMPLIES NUMBER TO FOLLOW(BUFFER)
SEMI:	PUSHJ P,GETCH
	TLNN CH,NUM.F	;NUMB?
	JRST QUEST	;NO
	PUSHJ P, MAKOCT 
	HRRZM NUM,SRCLST(LINE)
	MOVEI X2,11	;CODE FOR BUFFER
	PUSHJ P,DEPSR1
	AOS FLRNG
	POPJ P, ;GO GET COMMAND

;ROUTINE TO FIND + OR - AFTER ATOM

ADDATM: PUSHJ P,GETCH
	CAIN CH,53	; +?
	JRST [MOVEI X2,4
	JRST ADD1]	;YES ADD
	TLNE CH,NUM.F	;NUMB?
	JRST	[MOVEI X2,4
	JRST ADD2	]	;YES DAT' S OK
	CAIE CH,55	; -?
	JRST TAGT1	;NO NOT EITHER CONTINUE ON
	MOVEI X2,5
ADD1: PUSHJ P,GETCH	;GET NUMBER HOPEFULLY TO FOLLOW
	TLNN CH,NUM.F	;NO?
	JRST QUEST	;NOPE
ADD2: PUSHJ P,MAKOCT
	HRRZM NUM,SRCLST(LINE)
	PUSHJ P,DEPSR1
TAGT1:	TRNE F,MARK.F
	JRST ATOMED
	TLO F,FIRST
	JRST ATOMN	;OVER AGAIN
;ROUTINE TO GET CODING INTO SRCLST
DEPSRC: MOVEM NEW,SRCLST(LINE)
DEPSR1: DPB X2,ACPNT
	AOJ LINE,
	CAIGE LINE,ENDSRC
	POPJ P,
ADRER1: TTCALL 3, BIGRNG
	JRST GETCOM

WRDPNT: POINT 7,WORD
;ROUTINE TO FIND OUT WHICH COMMAND IS DESIRED

FNDCOM: SETZM WORD
	SETZM WORD+1
	MOVE X1,WRDPNT
	MOVEI X2,1
	IDPB CH,X1	;START BUILDING COMMAND IN WORD SPACE
LET1: PUSHJ P,GETCHR
	SKIPE CH	;NUL?
	TLNE CH,TERM.F	;OR TERMINATOR?
	JRST COMPAR	;YES GO FIND OUT
	TLNN CH,LET.F	;LETTER?
	JRST	[TLNE F,ACT.F
	JRST QUEST
	JRST COMPAR]
	IDPB CH,X1
	CAIG X2,12	;TOO MANY LETTERS
	AOJA X2,LET1	;DO AGAIN
	JRST QUEST

COMPAR: CAIN CH," "
	PUSHJ P,GETCH	;GET FIRST NONBLANK CHAR
	SETZ NUM,
COMP1:	MOVE X1,WRDPNT
	HRRI X2,CMDLST(NUM)
	HLL X2,WRDPNT
COMP10: ILDB X3,X1
	ILDB X4,X2
	SKIPN X4
	JRST COMP4	;CHANGE POINTER
	CAMN X4,X3
	JRST COMP10	;KEEP COMPARING
COMPA5: CAIL NUM,CMDEND ;DID WE COMPARE LAST ONE
	JRST QUEST	;YUP	TOO BAD
	AOJA NUM,COMP1
COMP4:	HLL X2,WRDPNT
	HRR X2,CMDADR(NUM)	;GET REST OF COMMAND
COMP4A: ILDB X4,X2	
	SKIPN X3	;END OF COMMAND?
	JRST COMP3	;YES
	SKIPN X4	;IF BLANK U IN TROBS
	JRST COMPA5
	CAME X4,X3	;SHOULD BE SAME
	JRST COMPA5
	ILDB X3,X1
	JRST COMP4A	;
COMP3:	HLRZ X2,CMDADR(NUM)	;GET ADRES
	TRZ X2,777000	;CLEAR FLAGS
	ADD X2,CMDADR(NUM)	;ADD ADRES
	HRRZM X2,COMAN
	POPJ P,

TSTCOM: HLRZ X2,CMDADR(NUM)	;GET FLAGS
	TRNN F,MARK.F	;MARK ADR?
	JRST TSTCO1	;NO
	TRNN X2,MRK	;ALOWED?
	JRST QUEST
	POPJ P,
TSTCO1: TRNN X2,NAB	;NOTHING ALLOWED FLAG ON?
	JRST COMP5	;NO
	SKIPL FLRNG
	JRST QUEST
	JRST COMP7
COMP5:	TRNN X2,ONE	;ONLY AND MUST ONE FLAG ON?
	JRST COMP5A	;NO
	SKIPE FLRNG
	JRST QUEST
	JRST COMP7
COMP5A: TRNN X2,ONORN	;ONE OR NOTHING ALLOWED
	JRST COMP6	;NOO
	SKIPLE FLRNG
	JRST QUEST
	JRST COMP7
COMP6:	TRNN X2,MHSB	;MUST HAVE SOM BEFOR?
	JRST COMP7	;NO
	SKIPGE FLRNG
	JRST QUEST
COMP7:	TRNN X2,AFTER	;MUST HAVE SOM AFTER?
	JRST COMP8	;NO
	TLNE CH,TERM.F	; TERMINAT?
	JRST QUEST	;YES BAD
	POPJ P,
COMP8:	TRNN X2,NOAFT	;NOTHING ALLOW AFTER
	POPJ P, ;NO
	TLNE CH,TERM.F	;TERM?
	POPJ P, ;YES GOOD
	JRST QUEST	;BAD
;ROUTINE TO EVALUATE SRCLST- AND SETUP RANGES FLRNG AND CERNG
;WILL ALSO CALC BUFFER NO IF INDICATED
;CURTMP IS CURRENT TEMPORY LINE BEING CALCULATED
;TMPLIN USED FOR CALC AND IS ZEROED AFTER BEING DEPOSITED

ADRCAL: SKIPL CURLIN
	JRST ADRCA1
	SKIPE EOF
	JRST ADRCA1
	MOVE X1,LASLIN
	MOVEM X1,CURLIN
	JRST ADRCA2
ADRCA1: MOVE X1,CURLIN ;GET WHERE WE'RE AT-BEGIN OF SEARCH
	SKIPN X1
	MOVEI X1,DICT+1
ADRCA2:	MOVEM X1,CURTMP
	MOVEM X1,STSER	;START SEARCH
	SETO LINE,
	SETZM TMPLIN
	SETZM FLRNG
	SETZM CERNG
	SETZM BUFNO
	SETZM MODMAR

ADRC1:	AOJ LINE,
	LDB X1,ACPNT	;GET CODE AS DESC BEFORE SSETSRC
	JRST .+1(X1)
	JRST ENCAL	;END OF CALC
	JRST USENO	;USE LINE NO.
	JRST SERLAB	;SERCH FOR LABEL
	JRST SERTEX	;SERCH FOR TEXT
	JRST ADDINC	;ADD INCREMENT
	JRST SUBINC	;SUBTRACT INCREM
	JRST SECSER	;START SECOND SERCH
	JRST SERTAG	;SERCH FOR TAG
	JRST USE.	;USE CURRENT POINTER
	JRST BNO	;GET BUFFER NO.
	JRST MNO.	;GET MARK NO.

USENO:	HRRZ X1,SRCLST(LINE)	;GET NUMBER
	ADDI X1,DICT	;GET ADDRES
	PUSHJ P,ADDIN1
	CAMLE X1,LASLIN
	JRST CHKLO
	HRRM X1,STSER
	HRRM X1,TMPLIN
	JRST ADRC1

CHKLO:  CAIG X1,DICT+11
        CAIGE X1,DICT
        JRST QUEST
        SKIPE SRCLST+1(LINE)
        JRST QUEST
        MOVE X2,COMAN
        CAIE X2,LOAD+1
        CAIN X2,BUFFER+2
        JRST CHKLO1
        CAIE X2,KILL+1
        JRST QUEST
CHKLO1:        HRRZM X1,TMPLIN
        JRST ENCAL

ADDINC: TRNE F,MARK.F
	JRST ADDIC1
	 HRRZ X1,SRCLST(LINE)
	ADD X1,TMPLIN
	PUSHJ P,ADDIN1
	CAMLE X1,LASLIN
	JRST QUEST
	MOVEM X1,TMPLIN
	MOVEM X1,STSER
	JRST ADRC1

ADDIC1: HRLZI X1,271400 ;ADDI LINE,
	HRR X1,SRCLST(LINE)
	MOVEM X1,MODMAR
	JRST ADRC1



ADDIN1: PUSH P,LINE
ADDINB: SKIPN EOF
	JRST ADDIN2
	CAMGE X1,LASLIN
	JRST ADDIN2
	PUSHJ P,INLINE
	JRST ADDINB
ADDIN2: POP P,LINE
	POPJ	P,

SUBINC: TRNE F,MARK.F
	JRST[ HRLZI X1,275400
		JRST ADDIC1+1]
	 HRRZ X1,TMPLIN
	HRRZ X2,SRCLST(LINE)
	SUB X1,X2
SUBIN1:	CAIGE X1,DICT+1
	JRST QUEST
	HRRM X1,TMPLIN
	HRRM X1,STSER
	JRST ADRC1

SECSER: MOVE X1,TMPLIN
	MOVEM X1,FLRNG
	SETZM TMPLIN
	JRST ADRC1

BNO: MOVE X1,TMPLIN
	SKIPE FLRNG ;FLLOOR CALC?
	JRST BNO1	;YES
	MOVEM X1,FLRNG
	JRST BNO2
BNO1: SKIPN CERNG	;CEILING CALC?
	MOVEM X1,CERNG	;NO
BNO2: HRRZ X1,SRCLST(LINE)
	CAIL X1,0
	CAILE X1,11
	JRST QUEST
	SKIPN X1
	SETO X1,
	MOVEM X1,BUFNO
	SETZM TMPLIN
	POPJ P, ;ALL DONE


MNO.:	HRRZ X1,SRCLST(LINE)
	MOVEM X1,MARKNO
	SETZM TMPLIN
	JRST ADRC1

ENCAL:	SKIPN TMPLIN	;HAVE WE DEPOSITED ALL CALC?
	POPJ P, ;YES
	MOVE X1,TMPLIN
	SKIPE FLRNG
	JRST ENCAL1
	MOVEM X1,FLRNG
	POPJ P,
ENCAL1: SKIPE CERNG
	JRST ENCAL2
	MOVEM X1,CERNG
	POPJ P,
ENCAL2: SKIPN BUFNO
	MOVEM X1,BUFNO
	POPJ P,

USE.:	SKIPG CURTMP
	JRST [PUSHJ P,FINUP
		MOVE X1,LASLIN
		MOVEM X1,CURTMP
		MOVEM X1,STSER
		JRST USE1]
		MOVE X1,CURTMP
	CAML X1,LASLIN
	PUSHJ P,ADDIN1
	CAMLE X1,LASLIN
	JRST QUEST
USE1:	MOVEM X1,TMPLIN
	JRST ADRC1

SERTAG: TLO F,TAGFLG
SERLAB: MOVE SERCH,SRCLST(LINE) ;GET POINTER
	TLZ SERCH,17	;TURN OFF AC FIELD
	MOVEM SERCH,SSER	;SAV SERCH
	LDB CH,SERCH	;GET DELIM
	CAIN CH,"<"
	MOVEI CH,">"
	MOVEM CH,DELIM
	PUSH P,LINE ;SAVE LINE
	HRRE LINE,STSER
	SKIPG LINE
	MOVE LINE,LASLIN
	CAIG LINE,DICT
	JRST QUEST
	MOVEM LINE,LINFND
	MOVEM LINE,ENDSER
	ILDB CH,SERCH	;GET NEXT CHAR AFTER DELIM
	CAMN CH,DELIM	;SAME?
	TLO F,CRORBL	;YES SO LOOK FOR CR OR BLANK LEADING LINE
SERLA1: MOVE LINE,LINFND
	AOJ LINE,	;GET NEXT LINE
	CAMLE LINE,LASLIN
	MOVEI LINE,DICT+1
	MOVEM LINE,LINFND
	MOVE DIC,(LINE)
	PUSHJ P,MAKPNT
	JRST SERLA0 ; DIC=0
SERLA:	ILDB X1,DIC	; GET FIRST CHAR FROM FILE
	TLNE F,CRORBL;	SHOULD WE TEST FOR CROR BLANK?
	JRST SERLA2	;YES
	TLNN F,TAGFLG	;ARE WE SEARCH FRO TAG?
	JRST SERLA4	;NO
	CAIE X1,11	;TAB
	CAIN X1,40	;BLANK?
	JRST SERLA	;YES GET NEXT
SERLA4: CAIN CH,177
	JRST [CAIN X1,15
		JRST SERLA0
		JRST SERLA7]
	CAME CH,X1	;SAME
	JRST SERLA0	;NO 
	CAIE CH,15
	JRST SERLA7
	ILDB CH,SERCH	;GET LF
	ILDB CH,SERCH	;GET NEXT
	CAMN CH,DELIM	;END?
	JRST SERLA5	;HURRAHHHHHH
SERLA9:	CAML LINE,LASLIN
	JRST [SKIPN EOF
		JRST SERLA0
		PUSHJ P,ADDIN3
		JRST SERLA9]
	AOJ LINE,
	MOVE DIC,(LINE)
	PUSHJ P,MAKPNT
	JRST SERLA0 ;DIC=0
	JRST SERLA8
SERLA7: ILDB CH,SERCH	;YES GET NEXT
	CAMN CH,DELIM	;DELIM?
	JRST SERLA5	;YES SUCCESS
SERLA8: ILDB X1,DIC	;GET NEXT FILE GUY
	JRST SERLA4	;COMPARE SOME MORE

SERLA0: MOVE LINE,LINFND
		CAMN LINE,ENDSER
	JRST [SKIPE EOF
		JRST SERLAA
		JRST QUEST]
SERLAA:	MOVE SERCH,SSER ;GET POINTER
	ILDB CH,SERCH	;GET FIRST CHAR
	JRST SERLA1+1

SERLA2: CAIN X1,11
	JRST SERLA3
	CAIN X1,40	;BLANK?
	JRST SERLA3	;YEAH!!!!
	JRST SERLA1	;BOOOOOOO
SERLA5: CAIN CH,"!"
	JRST SERLA3	;DON7T CHECKK FOR CR OR BLK
SERLA6: ILDB X1,DIC
	CAIN X1,11
	JRST SERLA3
	CAIE X1,40
	CAIN X1,15
	JRST SERLA3
	TLNE F,TAGFLG
	JRST SERLA0	;BAD
	SKIPE COLON
	CAIE X1,":"
	JRST SERLA0
SERLA3: MOVE LINE,LINFND
	MOVEM LINE,STSER
		MOVEM LINE, CURTMP
	MOVEM LINE,TMPLIN
	POP P,LINE	;RESTORE LINE
	TLZ F,CRORBL+TAGFLG
	JRST ADRC1

SERTEX: MOVE SERCH,SRCLST(LINE)
	TLZ SERCH,17
	MOVEM SERCH,SSER
	PUSH P,LINE
	HRRE LINE,STSER
	SKIPG LINE
	MOVE LINE,LASLIN
	CAIG LINE,DICT
	JRST QUEST
	MOVEM LINE ,ENDSER
	MOVEM LINE,LINFND
	LDB CH,SERCH	;GET DELIM
	CAIN CH,"["
	MOVEI CH,"]"
	MOVEM CH,DELIM

	JRST SERTE0
SERTE5: MOVE LINE ,LINFND
	CAMN LINE,ENDSER
	JRST [SKIPE EOF
		JRST SERTE0
		JRST QUEST]
SERTE0: MOVE SERCH,SSER
	AOJ LINE,
	CAMLE LINE,LASLIN
	MOVEI LINE ,DICT+1
	MOVEM LINE,LINFND
	MOVE DIC,(LINE)
	PUSHJ P,MAKPNT

	JRST SERTE5 ;DIC =0
	MOVEM DIC,BEGSER
	TLZ F,FIRST	;HAVE NOT FOUND FIRST MATCH
SERTE3: ILDB CH,SERCH
	CAMN CH,DELIM
	JRST SERTE1	;SUCCESS!
SERTE2: ILDB X1,DIC
	CAIN CH,177
	JRST [CAIN X1,15
		JRST SERTE5
		JRST SERTE3]
	CAME CH,X1
	JRST [CAIE X1,15
	JRST SERTE4
	JRST SERTE5]
	CAIE X1,15
	JRST SERTE6
	ILDB CH,SERCH	;LF
	ILDB CH,SERCH	;NEXT
	CAMN CH,DELIM
	JRST SERTE1	;SUCCESS
SERTE8:	CAML LINE,LASLIN
	JRST [SKIPN EOF
		JRST SERTE5
		PUSHJ P,ADDIN3
		JRST SERTE8]
	AOJ LINE,
	MOVE DIC,(LINE)
	PUSHJ P,MAKPNT
	JRST SERTE5 ;DIC=0
	JRST SERTE2
SERTE6: TLOE F,FIRST
	JRST SERTE3
		MOVEM DIC,BEGSER
	DEC BEGSER
	JRST SERTE3
SERTE4: IBP BEGSER
	CAME LINE,LINFND
	JRST	SERTE5
SERTE7: MOVE DIC,BEGSER
	MOVE SERCH,SSER
	TLZ F,FIRST
	JRST SERTE3


SERTE1: MOVE LINE,LINFND
	MOVEM LINE,STSER
		MOVEM LINE, CURTMP
	MOVEM	LINE,TMPLIN
	POP P,LINE
	TLZ F,FIRST
	JRST ADRC1


; ^ COMMAND- PRINT PREV LINE ON TERMINAL
UPCOM:	SOS CURLIN
	SKIPL CURLIN
	JRST LINEFD+1
	PUSHJ P,FINUP
	MOVE X1,LASLIN
	SOJ X1,
	MOVEM X1,CURLIN
	JRST LINEFD+1

DISM:	SETZ X1,
	APRENB X1,
	POPJ P,

DISARG: 402000
	JOBTPC
	0

; SLASH COMMAND
; FORMAT R/

FNDSL: MOVEI X1,FNDPR
	MOVEM X1,COMAN
	;PUSHJ P,DISM
	POPJ P,

	JRST FNDSL
SLCOM:;M:	PUSHJ P,DISM
	SKIPE FLRNG
	JRST .+4
	MOVE X1,LASLIN
	CAIN X1,DICT
	JRST GETCOM
		PUSHJ P,CKRNG
	TRNE F,MARK.F
	JRST [	PUSHJ P,FNDSL
	JRST MARKST]
SLCOM1: MOVE LINE,FLRNG
	CAIN LINE ,DICT
	JRST QUEST
SLCOM2: MOVE DIC,(LINE)
	SKIPN DIC
	JRST SLCOM5
	PUSHJ P,MAKPN1
	JRST SLCOM5 ;DIC=0
SLCOM3: ILDB CH,DIC
	CAIE CH,12
	JRST SLCOM4
	MOVEI CH,15
	PUSHJ P,PRTTEX
	MOVEI CH, 12
	PUSHJ P,PRTTEX
	JRST SLCOM3
SLCOM4: CAIE CH,14
	JRST .+3
	PUSHJ P,SKIPLN
	JRST SLCOM3
	CAIE CH,11	;TABOH?
	PUSHJ P,ISUCN	;IS U CONTROL CHAR?
	PUSHJ P,PRTTEX
	CAIE CH,15	;CR?
	JRST SLCOM3
	MOVEI CH,12
	PUSHJ P,PRTTEX
	SKIPN DONCHG
	MOVEM LINE,CURLIN
	AOJ LINE,	;GET NEXT LINE
	CAMG LINE,CERNG ;DONE
	JRST SLCOM2	;NO
SLCOM5: SKIPLE OTTY+2	;ANYMORE CHAR
	PUSHJ P,PRTTE1	;YES
	JRST GETCOM


;ROUTINE TO INIT TTY
INITTY: INIT TTY,1100 ;USE 2ND BREAK SET(3100)
	SIXBIT/TTY/
	XWD OTTY,ITTY
	JRST IOBAD
	SETO X1,
	GETLCH	X1
	TLZ X1,200
	SETLCH X1
	MOVEI X1,TTYBUF
	MOVEM X1,JOBFF
	OUTBUF TTY,1
	MOVEI X1,INTTY
	MOVEM X1,JOBFF
	INBUF TTY,1
	POPJ P,

;PRTTEX IS ROUTINE TO PRINT TEXT OUT ON TTY
PRTTEX: SOSG OTTY+2
	PUSHJ P,PRTTE1
PRTTE2: IDPB CH,OTTY+1
	POPJ P,
PRTTE1: OUT TTY,
	POPJ P,
	JRST IOBAD

PRTMES: PUSH P,CH
	PUSH P,X1
	HRLI X1,440700
	HRR X1,40
PRMS2:	ILDB CH,X1
	SKIPN CH
	JRST PRMS1
	PUSHJ P,PRTTEX
	JRST PRMS2
PRMS1:	POP P,X1
	POP P,CH
	POPJ P,

PRTVAL: PUSH P,CH
	HRRZ CH,40
	HRR CH,(CH)
	PUSHJ P,PRTTEX
	POP P, CH
	POPJ P,



;ROUTINE TO SEE IF FLRNG AND CERNG CALCULATED

CKRNG:	SKIPE FLRNG
	JRST CKRNG1
	MOVEI X1,DICT+1
	MOVEM X1,FLRNG
	SKIPE EOF
	SKIPA X1,BIGNO
	MOVE X1,LASLIN
	MOVEM X1,CERNG
	SETOM DONCHG	;DON7T CHANGE CURLIN
	JRST CKRNG2
CKRNG1: SKIPE CERNG
	JRST CKRNG2
	MOVE X1,FLRNG
	MOVEM X1,CERNG
CKRNG2: MOVE X1,FLRNG
	MOVE X2,CERNG
	CAMLE X1,X2
	JRST QUEST
	CAME X2,BIGNO
	CAMG X2,LASLIN
	CAIGE X1,DICT+1
	JRST QUEST
	POPJ P,

;EQUAL COMMAND
;PRINTS LINE NO OF LINE ADRESSED IN FLRNG

FNDEQ: MOVEI X1,FNDEQ1
	TTCALL 3,CRLF
	TTCALL 3,FPRINT
	PUSHJ P,YORN
	MOVEI X1,FNDEQ2
	MOVEM X1,COMAN
	POPJ P,

FNDEQ1: PUSH P,FLRNG
	MOVEM LINE,FLRNG
	PUSHJ P,EQCOM1
	PUSHJ P,FNDPR
	POP P,FLRNG
	POPJ P,

FNDEQ2: PUSH P,FLRNG
	MOVEM LINE,FLRNG
	PUSHJ P,EQCOM1
	POP P, FLRNG
	POPJ P,

	JRST FNDEQ
EQCOM: TRNE F,MARK.F
	JRST [SETZM FLRNG
	PUSHJ P,CKRNG
	PUSHJ P,FNDEQ
		SETOM GIVCNT
	JRST MARKST]
EQCOM1: MOVE X1,FLRNG
EQCOM2:	CAILE X1,DICT
	CAMLE X1,LASLIN
	JRST QUEST
	SUBI	X1,DICT
	PUSHJ P,PRTNUM
	TTCALL 3, CRLF
	TRNE F,FND.F!MARK.F
	POPJ P,
	JRST CARRET


; CAR RET.-SET CURRENT LINE TO THAT ADR IN FLRNG

FNDCAR: TTCALL 3,FPRINT
	MOVEI X1,FNDPR
	PUSHJ P,YORN
	MOVEI X1,POP
	MOVEM X1,COMAN
	POPJ P,

POP:	POPJ P,
FPRINT: ASCIZ/PRINT? /

FNDPR: MOVE DIC,(LINE)
	PUSHJ P,MAKPN1
	POPJ P, ;DIC=0
FNDPR2: ILDB CH,DIC
	TTCALL 1,CH
	CAIE CH,15
	JRST FNDPR2
	MOVEI CH,12
	TTCALL 1,CH
	POPJ P,

	JRST FNDCAR
CARRET: MOVE DIC,FLRNG
	SKIPN DIC
	JRST GETCOM
	CAILE DIC,DICT
	CAMLE DIC,LASLIN
	JRST QUEST
	MOVEM DIC,CURLIN
	JRST GETCOM


; LF COMMAND- PRINT NEXT LINE ON TERMINAL

LINEFD: AOS CURLIN
	MOVE DIC , CURLIN
	SKIPE CURLIN
	CAMLE DIC,LASLIN
	JRST [SOS CURLIN
	JRST QUEST]
	CAIN DIC,DICT	;LINE 0?
	JRST [AOS CURLIN
	JRST QUEST]
	MOVEM DIC,FLRNG
	MOVEM DIC,CERNG ;SETUP FOR SLASH
	JRST SLCOM

; _ COMMAND- PRINTS LINE LABEL OF ADRESS GIVEN OR CLOSEST LINE PRECEEDING
;FORMAT- A_

FNDBK: MOVEI X1,FNDBK2
	TTCALL 3,FPRINT
	PUSHJ P,YORN
	MOVEI X1,FNDBK1
	MOVEM X1,COMAN
	POPJ P,

FNDBK2: PUSH P,LINE
	 PUSHJ P,FNDBK1
	POP P ,LINE
	PUSHJ P,FNDPR
	POPJ P,
FNDBK1: PUSH P,FLRNG
	MOVEM LINE ,FLRNG
	PUSHJ P,BKCOMA
	POP P,FLRNG
	POPJ P,

	JRST FNDBK
BKCOM: TRNE F,MARK.F
	JRST [SETZM FLRNG
	PUSHJ P,CKRNG
	PUSHJ P,FNDBK
	JRST MARKST]
BKCOMA: SETZM TMPLIN	;USE TMPLIN AS COUNTER
	MOVE LINE ,FLRNG
	CAIL LINE,DICT
	CAMLE LINE,LASLIN
	JRST QUEST
BKCOM1: CAIN LINE,DICT
	JRST [ MOVE X1,FLRNG
	SUBI X1,DICT
	JRST BKCOM4]
	MOVE DIC,(LINE)
	PUSHJ P,MAKPN1
	JRST BKCOM5 ;DIC=0
	ILDB CH,DIC
	CAIN CH,11
	JRST BKCOM6
	CAIE CH,40
	CAIN CH,15
	JRST BKCOM6
BKCOM2: TTCALL 1,CH
	ILDB CH,DIC
	CAIN CH,11
	JRST .+5
	CAIE CH,40
	CAIN CH,15
	SKIPA
	JRST BKCOM2
BKCOM5:	SKIPN X1,TMPLIN
	JRST BKCOM3
	TTCALL 1, CTTAB+53	;GIVE A +
BKCOM4: PUSHJ P,PRTNUM ;PRINT HOW FAR AWAY IT IS
BKCOM3: TTCALL 3, CRLF
	TRNE F,FND.F
	POPJ P,
	MOVE X1,FLRNG
	MOVEM X1,CURLIN
	JRST GETCOM
BKCOM6: AOS TMPLIN
	SOJA	LINE,BKCOM1

;APPEND COMMAND
;FORMAT R APPEND FILENAME
; OR APPEND

CRFLG: XWD 400400,15	;CAR RET WITH FLAGS
FNDAP:TLO F,APPEN.F
FNDWR: TRO F,FND.F
	PUSHJ P,FNDAP1
	MOVEI X1,WRIT7
	MOVEM X1,COMAN
	MOVEI X1,FNDAP2
	MOVEM X1,ENDCM
	POPJ P,


	JRST FNDAP
APPEND: ASCIZ/PPEND/
	SKIPE EOF
	PUSHJ P,FINUP
	TLO F,APPEN.F
	CAME CH,CRFLG	;CR?
	JRST WRITE+1
	MOVE X1,LASLIN
	AOJ X1,
	MOVEM X1,FLRNG
	JRST INSERT+2


;COMMAND TO PRINT OUT CONTENTS OF BUFFER N
; FORMAT N BUFFER
BUFFER: ASCIZ/UFFER/
	MOVE LINE,FLRNG
	SUBI LINE,DICT	;UNDO WORK OF ADRCAL
	CAIL LINE,0
	CAILE LINE,11
	JRST QUEST
	MOVEI CH,42
	IMULI LINE,^D9
	ADDI LINE,BUF0
BUFF3: MOVE DIC,(LINE)
	SKIPN DIC
	JRST BUFF1
	PUSHJ P,MAKPNT
	JRST BUFF1	;DIC=0
 ILDB X1,DIC
	CAIN X1,33
	JRST BUFF1
	MOVEI CH,42
	PUSHJ P,PRTTEX
	MOVE CH,X1
	JRST BUFF4
BUFF2:	ILDB CH,DIC
	CAIN CH,33
	JRST BUFF1
BUFF4:	PUSHJ P,ISUCN 
	CAIE CH,15
	JRST [PUSHJ P,PRTTEX
		JRST BUFF2]
	MOVEI X4,1
BUFF6:	MOVEI CH,15
	PUSHJ P,PRTTEX
	MOVEI CH,12
	PUSHJ P,PRTTEX
	SKIPN X4
	JRST [MOVEI CH,42
                JRST BUFF7]
	MOVEI CH,42
	PUSHJ P,PRTTEX
	SOJGE X4,BUFF6
BUFF7:	AOJA LINE,BUFF3
BUFF1:	SKIPLE OTTY+2	;HAVE WE SPIT IT ALL OUT
	PUSHJ P,PRTTE1	;NO
	CAIE CH,42
	TTCALL 1, CTTAB+42	;ANOTHER QUOTE
	TTCALL 3, CRLF
	JRST GETCOM




ISUCN:	CAIE CH,15
	CAILE CH,32
	POPJ P,
	PUSH P,CH
	MOVEI CH,46	;&
	PUSHJ P,PRTTEX
	POP P,CH
	ADDI CH,100
	POPJ P,
;CLEAR COMMAND

ALL: ASCIZ/ALL? /
CLEAR:	ASCIZ/EAR/
	TRNE F,MARK.F
	JRST CLEAR1
	TRNN F,COMF.F!BUF.F
	TTCALL 3, ALL
	PUSHJ P,YORN	;FIND OUT ANSWER
	JRST GETCOM	;NO
	PUSHJ P,DELTMP
	SETZM TMPFIL	;BOMBS AWAY
	MOVE X2,LASLIN
	MOVE X1, [XWD TMPFIL,TMPFIL+1]
	BLT X1,(X2)
	MOVE X1,[XWD TABINT,TABES]
	HRRZ X2,SVJOB
	BLT X1,TABES+11
	CALLI X2,CORE
	JRST SYSFAL
	TRZ F,TAB.CH+RPG.F
	JRST START

CLEAR1: MOVEI LINE,DICT+1
	MOVE X1,MARKNO
CLEAR2: MOVE DIC,(LINE)
	TRZ DIC,(X1)
	MOVEM DIC,(LINE)
	AOJ LINE,
	CAMG LINE,LASLIN
	JRST CLEAR2
	JRST GETCOM
YORN:	TRNN F,COMF.F!BUF.F
	JRST YORN1
	PUSHJ P,GNC
	JRST YORN2
YORN1:	TLO F,COMW.F
	PUSHJ P,CKALT
	TTCALL 3, CRLF
YORN2:	HLLI CH,0
	CAIE CH,156	;LITTLE N
	CAIN CH,"N"
	JRST [TLZ F,COMW.F
		POPJ P,]
	CAIE CH,171	;BITTY Y
	CAIN CH,"Y"
	JRST [TLZ F,COMW.F
		JRST CPOPJ1]	;SKIP RETURN
	TRZ F,COMF.F!BUF.F
	TTCALL 3, WHAT
	PUSHJ P,CKALT
	CAIN CH,12
	JRST YORN1
	CAIE CH,15
	JRST .-4
	TTCALL 0,CH	;GET LF
	JRST YORN1	;OK NOW
INITTT: INIT TTY,101	;FULL CHAR AND LINE MODE
	SIXBIT/TTY/
	XWD 0,ITTY
	JRST IOBAD
	MOVEI X1,INTTY
	MOVEM X1,JOBFF
	INBUF TTY,1
	POPJ P,

WHAT: ASCIZ/WHAT ? /

DELTMP: SETZ X1,
	RENAME TMP,X1	;DELETE TMP FILE
	POPJ P, ;KEEP GOING IF FAILS
	POPJ P,

;COMAMND TO GET COMMANDS FROM A DSK FILE
;FORMAT - COMMAND FILENAME

COMMND: ASCIZ/MAND/
	TLNE CH,TERM.F	;ANYTHING AFTER?
	PUSHJ P,GETFIL	;NO GET FILE NAME
	MOVEI X1,COMFIL
	PUSHJ P,FILNAM
	HLRZ X1,COMFIL
	CAIN X1,(SIXBIT'T')
	JRST COMM2	;YES
	INIT COMF,0
	SIXBIT/DSK/
	XWD 0,ICOMF
	JRST IOBAD
	MOVEI X1,COMBUF
	MOVEM X1,JOBFF
	INBUF COMF,1
	LOOKUP COMF,COMFIL
	JRST FINWHY	;NOT FOUND
	TRO F,COMF.F
	JRST GETCOM

COMM2:	MOVE X1,[XWD COMBUF,COMBUF+1]
	SETZM COMBUF
	BLT X1,COMBUF+202
	TRZ F,COMF.F
	JRST GETCOM


;COPY COMMAND
; FORMAT A COPY R

COPY: ASCIZ/Y/
	MOVE X1,FLRNG	;GET LINE TO BE INSERTED IN FRONT OF
	CAIG X1,DICT
	MOVEI X1,DICT+1
	CAMG X1,LASLIN
	JRST .+3
	MOVE X1,LASLIN
	AOJ X1,
	PUSH P,X1
	MOVEM X1,CURLIN
	CAIE CH,40	;IS NEXT CHAR A BLANK
	DEC NEW ;NO SO BAK UP POINTER
	PUSHJ P,SETSRC	;GET LINES TO BE COPIED
	TLNN CH,TERM.F	;END OF COMMAND
	JRST QUEST	;NO
	PUSHJ P,ADRCAL	;FIND WHERE LINES ARE
	MOVE LINE,FLRNG ;GET BEGINNG
	SKIPN X2,CERNG
	MOVE X2,FLRNG
	CAMGE X2,LINE	;SEE IF CERNG>=FLRNG
	JRST QUEST	;NO
	CAIL LINE,DICT+1
	CAMLE X2,LASLIN
	JRST QUEST
	MOVEM X2,CERNG
	POP P,X1
	MOVEM X1,CURLIN
	MOVEM X1,TMPLIN
	TLNN F,MOVE.F	;MOVE COMMAND?
	JRST COP00	;NO
	MOVE X1,TMPLIN
	CAME X1,FLRNG
	CAMN X1,CERNG
	JRST GETCOM
	CAMG X1,FLRNG
	JRST COP00
	CAMGE X1,CERNG
	JRST GETCOM
COP00:	SETOM MODMAD
	MOVE NUM,CERNG	;START OPENING UP SPACE
	SUB NUM,FLRNG
	AOJ NUM,
	ADD NUM,LASLIN
	CAMG NUM,JOBREL ;ENOUGH ROOM
	JRST .+3
	PUSHJ P,GETCOR	;NO
	JRST .-3
	MOVE X1,LASLIN
	MOVEM NUM,LASLIN	;MAKE NEW LASLIN
COP0: CAMGE X1,TMPLIN	;ARE WE DONE
	JRST COP1	;YES
	MOVE X2,(X1)
	MOVEM X2,(NUM)
	SOJ X1,
	SOJA NUM,COP0
COP1: MOVE X1,TMPLIN
	CAMLE X1,FLRNG
	SKIPA	;A>R1
	JRST ALES	;A<=R1<=R2
	CAMGE X1,CERNG
	SKIPA	;A<R2
	JRST AGRTA	;A>=R2>=R1

	;THIS MEANS R1<A<R2 ;MUST DO MOVE IN TWO STEPS
	;FIRST BLT- X1/ R1,A
	;X2/A+((A-1)-R1)=2A-R1-1
	HRL X1,FLRNG
	HLLM X1,WORD	;INCASE MOVE WORD/R1,A-1	DELETE
	HRR X1,TMPLIN
	HRRM X1,WORD
	SOS WORD
	MOVE X2,TMPLIN
	IMULI X2,2
	SUB X2,FLRNG
	SOJ X2,
	PUSH P,X2
	BLT X1,(X2)
	POP P,X1	;START OF SECBLT
	;X1/ NEW X2+1,OLD X2+1
	;X2/ R2-R1+A
	MOVE X2,TMPLIN
	ADD X2,CERNG
	SUB X2,FLRNG
	HRLM X2,X1
	AOBJN X1,QUEST
	HLLM X1,WORD+1	;SETUP FOR MOVE
	HRR X3,X1
	MOVE X4,X2
	SUB X4,X3
	HLR X3,X1
	ADD X4,X1
	HRRM X1,WORD+1
	BLT X1,(X2)
COP2:	MOVEM X2,DONCHG
	MOVEM X2,CURLIN
		TLZN F,MOVE.F	;MOVE COMMAND?
	JRST COP5
COP6:	SETZ LINE,
COP4: HLRZ X2,WORD(LINE)
	SKIPN X2
	JRST COP3
	HRRM X2,FLRNG
	HRR X2,WORD(LINE)
	HRRM X2,CERNG
	MOVE X3,CERNG
	CAMG X3,DONCHG
	PUSHJ P,RECALC
	PUSH P,LINE
	PUSHJ P,DELLOC
	POP P,LINE
	AOJA LINE,COP4
COP3:	MOVE X1,DONCHG
	MOVEM X1,CURLIN
	JRST GETCOM
COP5:	SKIPN EOF
	JRST GETCOM
	MOVE X1,LASLIN
	MOVEM X1,READLN
	JRST GETCOM

RECALC: MOVE	X1,DONCHG
	SUB X1,CERNG
	ADD X1,FLRNG
	SOJ X1,
	MOVEM X1,DONCHG
	POPJ P,

ALES: MOVE X2,CERNG	;X1/R2+1,A
	;X2/(R2-R1)+A
	AOJ X2,
	HRL X1,X2
	HRR X1,TMPLIN
	SOJ X2,
	SUB X2,FLRNG
	ADD X2,TMPLIN
	HLLM X1,WORD
	HLR X3,X1
	ADD X3,CERNG
	SUB X3,FLRNG
	HRRM X3,WORD
	BLT X1,(X2)
	JRST COP2

AGRTA:	HRL X1,FLRNG	;X1/ R1,A
	;X2/ A+(R2-R1)
	HRR X1,TMPLIN
	HLLM X1,WORD
	MOVE X2,CERNG
	HRRM X2,WORD
	ADD X2,TMPLIN
	SUB X2,FLRNG
	BLT X1,(X2)
	JRST COP2

WHRMIN: SETZM SVPTR
	MOVE X2,READLN
	CAML X2,X1	;IS READLN LESS THAN TMPLIN(OR ABOVE)
	POPJ P,		;NO DON'T WORRY
	SETOM SVPTR	;MUST BE ABOVE
	MOVE X2,LASLIN
	MOVEM X2,SVNUM	;SAVE OLD LASLIN
	POPJ P,

RELOCT: MOVE X1,LASLIN	;GET NEW LASTLIN
	SUB X1,SVNUM	;SUBTRACT OLD LASLIN
	ADDM X1,TMPLIN	;ADD DIFFERENCE TO TMPLIN TO RELOCATE
	POPJ P,


;ROUTINE TO TAKE CURRENT LINE IN CURLIN
;AND PUT INTONEWLINE

SETLIN: PUSH P,LINE
 MOVE LINE,CURLIN ;START PUTTING CURRENT LINE IN NEW LIN
	PUSHJ P,SETLI1
	CAIG LINE,DICT
	JRST SETLI2	;ZONK DE OLD
	MOVE DIC,(LINE)
	SKIPN DIC
	JRST SETLI2+1
	PUSHJ P, MAKPN1 ;CONVERT TO POINTER
	JRST SETLI2+1	;DIC =0
	MOVE OLD ,DIC
	MOVE NEW,NEWPNT
	MOVEI X1,^D259
	MOVNM X1,NCOUNT
	IBP OLD
PUT2:	LDB CH,OLD
	PUSHJ P,CKCNT
	TLNE F,EDI.F
	PUSHJ P,PRTCHR
	PUSHJ P,DEPOT1
	CAIE CH,15
	JRST PUT2
SETLI2: PUSHJ P,SWITCH
	POP P,LINE
	POPJ P, ;YES

SETLI1: MOVE NEW ,NEWPNT
	SETZM (NEW)
	HRR X1,NEW
	AOJ X1,
	HRL X1,NEW
	BLT X1,63(NEW)
	POPJ P,
;CHANGE COMMAND
; FORMAT R CHANGE

CHANGE: ASCIZ/HANGE/
	SKIPN FLRNG
	JRST CLEAR+1	;MUST BE CLEAR COMMAND
	TRNE F,MARK.F
	JRST CLEAR1
	PUSH P, FLRNG	;SAVE FIRST ARGUMENT
	SKIPE CERNG ;SEC ARG?
	JRST CHANG1	;YES
	MOVE X1,FLRNG
	MOVEM X1,CERNG
CHANG1: PUSHJ P,DELLOC ;DELETE THOSE LINE S TO BE CHANGED
	POP P, TMPLIN
	SETOM MODMAD	;MODIFICATION MADE
	PUSHJ P,SETUP
	TRO F,INS.F	;INSERT MODE
	PUSHJ P,CKTAB
	JRST INS1	;START INSERTING NEWLINE AT TMPLIN


;DELETE COMMAND
; FORMAT R DELETE OR DELETE(LF),OR DELETE ^

FNDDE:	TTCALL 3,WAIT
	PUSHJ P,YORN
	TRZA F,WAIT.F
	TRO F,WAIT.F
	MOVEI X1,FNDDEL
	MOVEM X1,COMAN
	POPJ P,

FNDDEL: TRNN F,WAIT.F
	JRST FNDDE1
	PUSHJ P,FNDPR
	TTCALL 3,OK
	PUSHJ P,YORN
	POPJ P,

FNDDE1: PUSH P,FLRNG
	PUSH P,CERNG
	MOVE X1,LINE
	MOVEM X1,FLRNG
	MOVEM X1,CERNG
	PUSHJ P,DELLOC
	SETOM MODMAD
	MOVE X1,FLRNG
	SOJ X1,
	MOVEM X1,SVLIN
	POP P, CERNG
	POP P,FLRNG
	SOS CERNG
	POPJ P,

	JRST FNDDE
DELETE: ASCIZ/ELETE/
	TRNE F,MARK.F
	JRST [PUSHJ P,CKRNG
	PUSHJ P,FNDDE
	JRST MARKST]
	PUSHJ P,LFORUP	;LFOR UPARW
DELE1:	PUSHJ P,DELLOC
	SETOM MODMAD
	JRST GETCOM


;ROUTINE TO SEE IF LF OR UPAR FOLLOWS COMMAND

LFORUP: SKIPE FLRNG	;ANYTHING BEFORE COMMAND?
	JRST LFORU0	;YES SO RANGE ALREADY CALC
	HLLI CH,0	;CLEAR LEFT 
	CAIN CH,12	;LF?
	JRST [MOVE X1,CURLIN
	AOJ X1,
	JRST LFORU2]
	CAIE CH,"^"
	JRST QUEST
	MOVE X1,CURLIN
	SOJ X1,
LFORU2: CAIN X1,DICT
	JRST QUEST
	MOVEM X1,FLRNG
	MOVEM X1,CERNG
	JRST CKRNG2
LFORU0: SKIPE CERNG
	JRST CKRNG2
	MOVE X1,FLRNG
	MOVEM X1,CERNG
	JRST CKRNG2

;ROUTINE TO DEL BLOCK OF LINES BEGING AT FLRNG AND END AT CERNG

DELLOC: MOVE X1,FLRNG
	CAIG X1,DICT
	MOVEI X1,DICT+1
	MOVE X2,CERNG
	CAMG X1,X2
	CAMLE X2,LASLIN
	JRST QUEST
	SKIPN EOF
	JRST DELEA
	EXCH X1,X2
	PUSH	P,X1
	PUSHJ P,ADDIN1
	POP P,X1
	EXCH X1,X2
DELEA:	SOJ X1,
	CAIN X1,DICT
	MOVEI X1,DICT+1
	MOVEM X1,CURLIN
	MOVE X1,LASLIN
	PUSH P,LASLIN	;SVAE OLD LASLINE
	SUB X1,CERNG	;X1 CONTAINS NO OF LINES AVAIL
	;TO MOVE BEG AT CERNG+1
	JUMPN X1,DELE3
	MOVE X2,FLRNG	;DEL FROM FLRNG DOWN
	SOJ X2,
	JRST DELE4
DELE3:	MOVE X2,X1
	SOJ X2, ;X2/LASLIN-CERNG+FLRNG-1
	ADD X2,FLRNG
	MOVE X1,CERNG
	AOJ X1,
	HRL X1,X1
	HRR X1,FLRNG	;X1 CONTAINDS [CERNG+1,FLRNG]
	BLT X1,(X2)
DELE4:	MOVEM X2,LASLIN
	CAIN X2,DICT
	MOVEM X2,CURLIN
	MOVE X1,X2
	AOJ X1,
	SETZM (X1)
	POP P, X3	;GET OLD LASLIN
	CAMN X1,X3
	JRST DELE5 
	HRL X1,X1
	AOJ X1,
	BLT X1,(X3)
DELE5:	HRLI X2,0
	CALLI X2,CORE	;SHRINK CORE IF NECES
	JRST IOBAD
	SKIPN EOF
	POPJ P,
	MOVE X1,LASLIN
	MOVEM X1,READLN
	POPJ P,

MIN1:	-1


;EDIT COMMAND
; FORMAT- R EDIT OR EDIT(LF) OR EDIT ^

FNDMD:	CAME CH,CRFLG
	JRST QUEST
FNDMD1:	TLZA F,EDI.F
FNDED: TLO F,EDI.F
	TRO F,KEEPV
FNDED1:	MOVEI X1,EDIT1
	MOVEM X1,COMAN
	PUSHJ P,CKTAB
	POPJ P,
	JRST FNDED
EDIT: ASCIZ/DIT/
	TLO F,EDI.F	;EDIT MODE- IMPLIES PRINT LINE TO SUB D1
	TRO F,KEEPV
	PUSHJ P,CKTAB
	TRNE F,MARK.F
	JRST [PUSHJ P,CKRNG
	PUSHJ P,FNDED1
	JRST MARKST]
	PUSHJ P,LFORUP	;FIND OUT LF,UP OR RANGE?
	MOVE LINE,FLRNG
EDIT1:	MOVEM LINE,CURLIN
	TLO F,MOVE.F	;FLAG TO IMPLY POPJ P FROM SETLIN
	PUSHJ P,SETLIN	;PUT LINE INTO NEWLIN AND PRINT AT SAME TIME IF EDIT MODE
	PUSHJ P,SETUP
	TLNE F,EDI.F	;EDIT MODE?
	TTCALL 3, LF	;YES
EDIT3:	PUSHJ P,GETLIN	;GO GET CHARS AND JUNK
	JRST CKDD
EDIT4:	PUSHJ P,DEPCNT
	PUSHJ P,ADDLIN	;EXIT HERE WHEN DONE- ADD LINE TO UPDATE AREA
	PUSHJ P,MAKDIC	;MAKE A DICT POINTER
	SKIPN MRKDIC
	JRST EDIT2
	MOVE X1,MRKDIC
	TRO DIC,(X1)
EDIT2:	MOVEM DIC,(LINE)	;ADD TO DICT
	MOVEM LINE,CURLIN
EDIT2A: SETOM MODMAD
 TRNE F,FND.F
	JRST [MOVEM LINE,SVLIN
		POPJ P,]
	AOJ LINE,
	CAMG LINE,CERNG ;ARE WE THROUGH WITH RANGE
	JRST EDIT1	;NO
	PUSHJ P,SWITCH
	JRST GETCOM

DFLG:	500300,,4
CKDD:		CAME CH,DFLG	;^D?
	JRST CKDD1
	TLZE F,VMOD.F	;^V MODE
	JRST CKDD3	; ;YES
	TLZE F,QLIN	;BEGGINNING OF LINE?
	JRST EDIT2A	;YES
	PUSHJ P,D1D	;COPY REST OF LINE
	JRST EDIT4
	JRST EDIT4
CKDD1:	CAME CH,VFLG
	TLZ F,VMOD.F
CKDD3:		CAME CH,CRFLG
	JRST EDIT3	;NOT CR
	PUSHJ P,GNC	;DO GET THE LF DARLING!
	TLNN	F,E.F	;INSERTING M'LOVE?
	DEC OLD ;NO, MOVE BACK FOR CR
	PUSH P,OLD
	PUSHJ P,DEPCNT
	PUSHJ P,ADDLIN
	PUSHJ P,MAKDIC
	SKIPN MRKDIC
	JRST CKDD2
	MOVE X1,MRKDIC
	TRO DIC,(X1)
CKDD2: MOVEM LINE,TMPLIN
	PUSHJ P,INSER
	MOVEM LINE,CURLIN
	AOS CERNG
	AOJ LINE,
	PUSHJ P,SETUP
	POP P,OLD
	JRST EDIT3
VFLG: XWD 400300,26

; R EXIT COMMAND- WRITE FILE AND QUIT

EXITCM: ASCIZ/IT/
	SKIPN RPG
	JRST EXITC1	;LET HIM QUIT ONLY
	TRO F,FIL.EQ
	MOVE X1,[RPGFIL,,OUTFIL]
	BLT X1, OUTFIL+3
EXITC1:	TLNN CH,TERM.F	;IS THERE A FILE NAME GIVEN?
	SETOM MODMAD
 SKIPN MODMAD
	JRST .+3
	TRO F,RPG.F
	PUSHJ P,WRITE+1
	PUSHJ P,QUIT+1
        JRST QU2



;

;
;FIND COMMAND-
;FORMAT- R FIND (CONDITIONAL EXP) COMMAND
;FCMND-CONTAINS FIND COMMAND
;SOURCE- REFINED EXPRESSION
;SRCPNT- CONTAINS POINTERS TO FCMND
;POLISH- CONTAINS POLISH NOTATION OF CONDITIONAL EXPRES.
;OHIER-CONTAINS OPERATOR HIERARCHY

POLPNT: POINT 7,POLISH
FNDPNT: POINT 7,FCMND
FIND: ASCIZ/IND/
	PUSHJ P,CKRNG
	SETOM GIVCNT
	SETZM FCMND
	MOVE X1,[XWD FCMND,FCMND+1]
	BLT X1,OHIER+^D24
	MOVE OLD,FNDPNT
	IDPB CH,OLD
	ILDB CH,NEW
	CAIE CH,0
	JRST .-3
	MOVE NEW,FNDPNT
	PUSHJ P,GETCH
	MOVEI LINE,1	;COUNTER FOR SRCPNT
	MOVEI SPT,1	;COUNTER FOR SOURCE
	SETZM OCOUNT	;COUNTER FOR PARENT
	MOVEI SCH,101	;BEG STRING NO.

FIND5: TLNE CH,LET.F	;LETTER?
	JRST FFIND1	;YES
	TLNE CH,ACT.F	;COULD BE SLASH,ETC
	JRST FIND7	;BETTER BE COMMAND
FIND5A: CAIE CH,"("
	CAIN CH,")"
	JRST FIND2
	CAIN CH,"."
	JRST FIND3
	TLNE CH,DEL.F	;DELIM?
	JRST FIND4	;YES
	CAIN CH,";"
	JRST FNDSEM
	TLNN CH,TERM.F ;TERM?
	JRST QUEST	;NO
FIND6: CAMN CH,CRFLG	;CAR RET?
	JRST FIND7	;YES
	PUSHJ P,GETCH
	JRST FIND5	;LF

FNDSEM: PUSHJ P,GETCH
	TLNN CH,NUM.F
	JRST QUEST
	PUSHJ P,MAKOCT
	CAIL NUM,0
	CAILE NUM,11
	JRST QUEST
	MOVEM NUM ,BUFNO
	TLNN CH,LET.F
	JRST QUEST
	JRST FIND7	;BETTER BE COMMAND
DEFINE U(A)
<ASCIZ/A/
>

CONDIT: U(AND)
	U(AN)
	U(A)
	U(NOT)
	U(N)
	U(OR)
	U(O)
	U(ANN)
	U(ANNO)
CONEND=.-CONDIT

TCON:	U(4)
	U(4)
	U(4)
	U(6)
	U(6)
	U(3)
	U(3)
	U(46)
	U(46)
FFIND1: SETZ X3,
	SETZM WORD
	PUSH P,NEW
	PUSH P,CH
	MOVE X2,WRDPNT
FIND1B: IDPB CH,X2
	AOJ X3,
	PUSHJ P,GETCHR
	TLNN CH,LET.F
	JRST FIND1A
	CAIGE X3,5
	JRST FIND1B
FIND1A: SETZ X3,
	MOVE NUM,WORD
FIND1C: CAMN NUM,CONDIT(X3)
	JRST FIND1D
	AOJ X3,
	CAIG X3,CONEND
	JRST FIND1C
	POP P,CH
	POP P,NEW
	JRST FIND7	;HOPE IT'S A COMMAND

FIND1D: MOVEI X2,TCON(X3)
	HRLI X2,440700
FIND1E: ILDB X3,X2
	SKIPN X3
	JRST FIND1F
	MOVEM X3,SOURCE(SPT)
	AOJ SPT,
	JRST FIND1E
FIND1F: CAIN CH,40
	PUSHJ P,GETCH
	POP P,
	POP P,
	JRST FIND5
FIND2: CAIN CH,"("
	JRST FIND2A
	SOS OCOUNT
	SKIPGE OCOUNT
	JRST QUEST
	MOVEI CH,62
	JRST .+3
FIND2A: AOS OCOUNT
	MOVEI CH,61
	MOVEM CH,SOURCE(SPT)
	AOJ SPT,
	PUSHJ P,GETCH
	JRST FIND5
FIND3: MOVEI CH,65	;FOLLOW
	MOVEM	CH,SOURCE(SPT)
	PUSHJ P,GETCH
	CAIN CH,"."
	JRST .-2
	AOJ SPT,
	JRST FIND5

FIND4: MOVEM NEW,SRCPNT(LINE)	;SAV POINTER
	TLNE CH,ADD2.F
	ADDI CH,2
	TLZ CH,777777
	MOVEM CH,DELIM
	MOVE X1,NEWPNT
	ADDI X1,^D52
FIND4A: CAMN NEW,X1
	JRST QUEST
 ILDB CH,NEW
	CAMN CH,15
	JRST QUEST
	CAME CH,DELIM
	JRST FIND4A
	MOVEM SCH,SOURCE(SPT)
	AOJ LINE,
	AOJ SCH,
	AOJ SPT,
	PUSHJ P,GETCH
	JRST FIND5

FIND7: SKIPE OCOUNT	;PAREN OK
	JRST QUEST	;NO
	PUSHJ P,FNDCOM	;GET COMMAND
	HLRZ X2,CMDADR(NUM)
	TRNN X2,FND	;ALLOWED?
	JRST QUEST
	MOVE X3,COMAN
	TRZ X2,777000
	AOJ X2,
	SUB X3,X2	;FIND ADR-1
	MOVEM X3,COMAN	;COMAN CONTAINS PRELIM ADR
	MOVEM NEW,WORD+1	;SAVE NEW

;NOW WE DO THE POLKA!!

POLSTA: MOVE NEW,POLPNT
	MOVEM SPT,WORD	;WORD =SPT;1
	SETZM SOURCE(SPT)
	MOVEI SPT,1
	MOVEI SCH,2
	SETOM OHIER(SPT)
POL3: MOVE X1,SOURCE(SPT)
	CAIG X1,100	;OPERAND?
	JRST POL4	;NO
	IDPB X1,NEW	;DEP IN POLISH
	AOJ SPT,
	MOVE X1,OHIER-1(SCH)	;GET HIERARCHY
	MOVE X2,SOURCE(SPT)
POL6A:	CAILE X2,100
	SETZ X2,
POL6:	CAMGE X1,X2
	JRST	POL5
	IDPB X1,NEW
	SOJ SCH,
	MOVE X1,OHIER-1(SCH)
	JRST POL6

POL5:	CAMN SPT,WORD	;DONE?
	JRST POLEND	;YES
	JRST POL3

POL4:	SKIPN X1
	JRST QUEST
	 CAIE X1,62	;")"
	JRST POL7
	AOJ SPT,
	SOJ SCH,
	MOVE X1,OHIER-1(SCH)
	MOVE X2,SOURCE(SPT)
	JRST POL6A

POL7:	MOVE X1,SOURCE (SPT)
	MOVEM X1,OHIER(SCH)
	AOJ SPT,
	AOJ SCH,
	JRST POL3

POLEND: MOVE P,PLIST
	MOVE NEW ,POLPNT
WFF:	ILDB CH,NEW
	CAIG CH,100
	JRST WFF1
	PUSH P,
	JRST WFF
WFF1:	CAIE CH,0
	JRST WFF2
	CAMN P,PLIST
	JRST QUEST
	POP P,
	CAME P,PLIST
	JRST QUEST
	JRST FINDOK	;HOORAY FOR HOLLYWOOD!

WFF2:	CAIE CH,63
	CAIN CH,64
	JRST WFF3
	CAIN CH,65
	JRST WFF3
	JRST WFF3+3
WFF3:	CAMN P,PLIST
	JRST QUEST
	POP P,
	CAMN P,PLIST
	JRST QUEST
	POP P,
	PUSH P,
	JRST WFF



FINDOK: MOVE NEW,WORD+1
	LDB CH,NEW
	HLL CH,CTTAB(CH)
	TRO F,FND.F
	SETZM ENDCM
	PUSHJ P,@COMAN
MARKST: SETZM NOFNDS
	TRO F,FND.F
	MOVE LINE ,FLRNG
FINDO1: MOVEM LINE,SVLIN
POLST1: MOVE X1,POLPNT
	MOVEM X1,POLPT
	MOVE P,PLIST
	MOVE DIC,(LINE)
	TRNN F,MARK.F
	JRST POLST3
	PUSHJ P,TSTMAR
	JRST POLDO4	;NOT IN LIST
	SKIPN MODMAR
	JRST POLST3
	XCT MODMAR
	CAIG LINE,DICT
	JRST POLDO1
	MOVE X1,LINE
	PUSHJ P,ADDIN1
	CAMLE X1,LASLIN
	JRST POLDO5
	MOVE DIC,(LINE)
POLST3: PUSHJ P,MAKPN1
	JRST POLDO1 ;DIC=
POLST2: ILDB X1,POLPT
	SKIPN X1
	JRST POLDON
	CAIG X1,100
	JRST FNDOPR
	SUBI X1,100
	MOVE X1,SRCPNT(X1)
	PUSH P,X1
	JRST POLST2

TSTMAR: CAMN DIC,MIN1
	JRST POLDO5
	 MOVE X1,MARKNO
	TRNE DIC,(X1)
	AOS (P)
	POPJ P,

FNDOPR: SUBI X1,60
	JRST .(X1)
	JFCL
	JFCL
	JRST OR
	JRST AND
	JRST FOLLOW
	JRST NOT

OR:	POP P,X1
	POP P,X2
	CAIE X1,1
	CAIN X2,1
	JRST TRU
	MOVEM X1,SAVAC
	MOVEM X2,SAVAC+1
	SKIPN	X2
	JRST OR1	;X2=F,FIND X1
	MOVE SERCH,X2
	MOVEM DIC,BEGSER
	PUSHJ P,FNDTRT
	MOVE X2,TRUTH
	MOVEM X2,SAVAC+1
OR1:	MOVE X1,SAVAC
	SKIPN X1	;X1 FALSE
	JRST OR2
	MOVE SERCH,X1
	MOVEM DIC,BEGSER
	PUSHJ P,FNDTRT
	MOVE X1,TRUTH
OR2:	MOVE X2,SAVAC+1
		CAIE X1,1
	CAIN X2,1
	JRST TRU
FALS:	SETZM TRUTH
	PUSH P,TRUTH
	JRST POLST2
TRU:	MOVEI X1,1
	MOVEM X1,TRUTH
	JRST FALS+1

AND:	POP P,X1
	POP P,X2
	SKIPE X1
	SKIPN X2
	JRST FALS
	MOVEM X1,SAVAC
	MOVEM X2,SAVAC+1
	CAIN X2,1
	JRST AND1
	MOVE SERCH,X2
	MOVEM DIC,BEGSER
	PUSHJ P,FNDTRT
	MOVE X2,TRUTH
	MOVEM X2,SAVAC+1
AND1:	MOVE X1,SAVAC
	CAIN X1,1
	JRST AND2
	MOVE SERCH,X1
	MOVEM DIC,BEGSER
	PUSHJ P,FNDTRT
	MOVE X1,TRUTH
AND2:	MOVE X2,SAVAC+1
		SKIPE X1
	SKIPN X2
	JRST FALS
	JRST TRU

FOLLOW: POP P,X1
	POP P,X2
	SKIPE X1
	SKIPN X2
	JRST FALS
	MOVEM X1,SAVAC
	MOVEM X2,SAVAC+1
	CAIN X2,1
	JRST FOLL1
	MOVE SERCH,X2
	MOVEM DIC,BEGSER
	PUSHJ P,FNDTRT
	MOVE X2,TRUTH
	MOVEM X2,SAVAC+1
FOLL1:	MOVE X1,SAVAC
 CAIN X1,1
	JRST AND2
	SKIPE EOR
	JRST FALS
	MOVE SERCH,X1
	MOVE X3,SVPTR
	MOVEM X3,BEGSER
	PUSHJ P,FNDTRT
	MOVE X1,TRUTH
	JRST AND2

NOT:	POP P,X1
	SKIPN X1
	JRST TRU	;0=F,-F=T
	CAIN X1,1
	JRST FALS
	MOVE SERCH,X1
	MOVEM DIC,BEGSER
	PUSHJ P,FNDTRT
	SKIPE TRUTH
	JRST FALS
	JRST TRU

FNDTRT: TLZ F,TAGFLG+CRORBL
	SETZM EOR
	LDB CH,SERCH
	HLL CH,CTTAB(CH)
	TLZE CH,ADD2.F
	ADDI CH,2
	TLZE CH,TEX.F
	JRST FTEX
	HLLI CH,0
	CAIN CH,">"
	TLO F,TAGFLG
	MOVEM CH,DELIM

	ILDB CH,SERCH	;GET FIRST CHAR TO MATCH
	CAMN CH,DELIM
	TLO F,CRORBL
FTLA: ILDB X3,BEGSER
	TLNE F,CRORBL
	JRST FTLA7
	TLNN F,TAGFLG
	JRST FTLA4
	CAIE X3,11
	CAIN X3,40
	JRST FTLA
FTLA4: CAIN CH,177
	JRST [CAIE X3,15
		JRST FTLA5
		JRST FTLA0]
	CAME CH,X3
	JRST FTLA0	;SERCHFALS
	CAIE CH,15
	JRST FTLA5
	ILDB CH,SERCH
FTLA4A: ILDB CH,SERCH
	CAMN CH,DELIM
	JRST FTLA3
FTLA4B:	CAML LINE,LASLIN
	JRST [SKIPN EOF
		JRST FTLA0
		PUSHJ P,ADDIN3
		JRST FTLA4B]
	AOJ LINE,
	MOVE DIC,(LINE)
	PUSHJ P,MAKPN1
	JRST FTLA0 ;DIC=0
	MOVEM DIC,BEGSER
	JRST FTLA6
FTLA5:	ILDB CH,SERCH
	CAMN CH,DELIM
	JRST FTLA3	;SUCCES
FTLA6:	ILDB X3,BEGSER
	JRST FTLA4

FTLA3:	CAIN CH,"!"
	JRST FTLA3B	;DON'T CHECK
	PUSH P,BEGSER
	ILDB X3,BEGSER
	CAIN X3,11
	JRST FTLA3A
	CAIE X3,40
	CAIN X3,15
	JRST FTLA3A	;OKEEDOEKEE
	TLNE F,TAGFLG
	JRST [POP P,BEGSER
		JRST FTLA0]	;BADDY
	SKIPE COLON
	CAIE X3,":"
	JRST .-3
FTLA3A: POP P,BEGSER
FTLA3B: CAIN X3,15
		SETOM EOR
		MOVE X3,BEGSER
	MOVEM X3,SVPTR
	MOVEI X3,1
	MOVEM X3,TRUTH
	TLZ F,TAGFLG
	POPJ P,

FTLA0: SETZM TRUTH
	CAIN X3,15
	SETOM EOR
	MOVE X3,BEGSER
	MOVEM X3,SVPTR
	TLZ F,TAGFLG
	POPJ P,
FTLA7:	CAIN X3,11
	JRST FTLA3B
	CAIE X3,40
	CAIN X3,15
	JRST FTLA3B
	JRST FTLA0

FTEX:	HLLI CH,0
	MOVEM CH,DELIM
	MOVEM	SERCH,SSER
	MOVE X4,BEGSER
FTEX0: MOVE SERCH,SSER
	TLZ F,FIRST
FTEX3:	ILDB CH,SERCH
	CAMN CH,DELIM
	JRST FTEX1	;SUCCES
FTEX2: ILDB X3,X4
	CAIN CH,177
	JRST [CAIE X3,15
		JRST FTEX3
		JRST FTEX5]
	CAME CH,X3
	JRST [CAIE X3,15
	JRST FTEX4
	JRST FTEX5]	;FALSE
	CAIE CH,15
	JRST FTEX6
	ILDB CH,SERCH
FTEX2A: ILDB CH,SERCH
	CAMN CH,DELIM
	JRST FTEX1
FTEX2B:	CAML LINE,LASLIN
	JRST [SKIPN EOF
		JRST FTEX5
		PUSHJ P,ADDIN3
		JRST FTEX2B]
	AOJ LINE,
	MOVE DIC,(LINE)
	PUSHJ P,MAKPN1
	JRST FTEX5 ;DIC =0
	MOVEM DIC,X4
	JRST FTEX2
FTEX6:	TLOE F,FIRST
	JRST FTEX3
	MOVEM X4,BEGSER
	DEC BEGSER
	JRST FTEX3
FTEX4:	CAME LINE,SVLIN
	JRST FTEX5
		IBP BEGSER
	MOVE X4,BEGSER
	MOVE SERCH,SSER
	TLZ F,FIRST
	JRST FTEX3
FTEX5: SETZM TRUTH
	CAIN X3,15
	SETOM EOR
	MOVEM X4,SVPTR
	TLZ F,FIRST
	POPJ P,
FTEX1: MOVEM X4,SVPTR
	CAIN X3,15
	SETOM EOR
	MOVEI X3,1
	MOVEM X3,TRUTH
	TLZ F,FIRST
	POPJ P,

POLDON: CAMN P,PLIST
	JRST POLDO2
	POP P,X1
POLDO3: CAIN X1,1
	JRST POLDO2
	SKIPN X1
	JRST POLDO1
	MOVE SERCH,X1
	MOVEM DIC,BEGSER
	PUSHJ P,FNDTRT
	MOVE X1,TRUTH
	JRST POLDO3
POLDO2: PUSHJ P,GOTOCM	;TRUTH&WISDOM
POLDO1: MOVE LINE,SVLIN
POLDO4: AOJ LINE,
	CAMG LINE,CERNG
	JRST FINDO1
POLDO5:	SKIPE ENDCM
	PUSHJ P,@ENDCM
	TRZ F,FND.F
	TRNN F,MARK.F
	JRST .+3
	SKIPN GIVCNT
	JRST GETCOM
	SETZM GIVCNT
	MOVE X1,NOFNDS
	PUSHJ P,PRTNUM

	TTCALL 3,CRLF
	JRST GETCOM

GOTOCM: AOS NOFNDS
	SKIPN MODMAR
	MOVE LINE,SVLIN
	MOVEM LINE,CURLIN
	PUSHJ P, @COMAN
	POPJ P,

PRELIM: PUSHJ P,@COMAN
	POPJ P,
; GET ROUTINE 
;FORMAT N GET OR R;N GET
; PLACE LINES IN RANGE R IN BUFFER N AND DELETE THEM FROM TEXT AREA
FNDLD: TLO F,LOAD.F
FNDGET: TRO F,FND.F
	PUSHJ P,GET2A
	EXCH X1,WORD
	MOVEM X1,WORD+1
	MOVEI X1,FNDGE1
	MOVEM X1,COMAN
	POPJ P,

FNDGE1: MOVE X1,WORD
	CAMLE X1,WORD+1
	JRST QUEST
	MOVE X2,(LINE)
	MOVEM X2,(X1)
	AOS WORD
	TLNN F,LOAD.F	;LOAD COMMAND
	JRST FNDDE1	;DEL LOCATIONS
	POPJ P,

	JRST FNDGET
GET:	ASCIZ/ET/
	SKIPE BUFNO	;2ND FORM?
	JRST GET1 ;YES
	MOVE X1,FLRNG	;FLRNG HAS BUFFER NO
	SUBI X1,DICT
	CAIL X1,0
	CAILE X1,11
	JRST QUEST	;NOT BETWEEN 0 AND 9
	MOVEM X1,BUFNO
	MOVEI X1,DICT+1
	MOVEM X1,FLRNG
	MOVE X1,LASLIN
	MOVEM X1,CERNG
	JRST GET2A
GET1:	SKIPG BUFNO
	SETZM BUFNO
	PUSHJ P,CKRNG
GET2A: TRNE F,MARK.F
	JRST [PUSHJ P,FNDGET
	JRST MARKST]
	MOVE X1,BUFNO
	IMULI X1,^D9
	ADDI X1,BUF0
	MOVEM X1,X2
	CAIN X1,BUF0
	ADDI X2,^D79
	ADDI X2,^D8
	MOVEM X2,WORD
	TRNE F,FND.F
	JRST GET2B
	SKIPN EOF
	JRST GET2C
	PUSH P,X1
	MOVE X1,CERNG
	PUSHJ P,ADDIN1
	CAMLE X1,LASLIN
	JRST QUEST
	POP P,X1
GET2C:	MOVE X2,CERNG
	SUB X2,FLRNG
	ADD X2,X1
	CAML	X2,WORD
	JRST NOROOM
GET2B:	SETZM (X1)
	HRR X3,X1
	HRL X3,X1
	AOJ X3,
	HRRZ X4,WORD
	AOJ X4,
	BLT X3,(X4)
	TRNE F,FND.F
	POPJ P,
	HRL X1,FLRNG
	BLT X1,(X2)
	TLZN F,LOAD.F	;LOAD COMMAND?
	PUSHJ P,DELLOC	; NO,DELETE LOCATIONS FROM TEXT
	JRST GETCOM

FNMIN:	MOVE X1,LASLIN
	CAMLE X1,CERNG
	POPJ P, ;OK BABY
	CAMGE X1,FLRNG
	POPJ P,
	MOVE LINE,X1
FNMIN1: MOVE DIC,(LINE)
	SKIPN EOF
	POPJ P,
	PUSHJ P,INLINE
	CAMLE LINE,CERNG
	AOJA LINE,FNMIN1
	POPJ P,


; R GO COMMAND- WRITE FILE AND RUN RPG AT START+1

GO:	SKIPN RPG
	JRST GO1	;WAS A MATTER FOR U,EH?
	TRO F,FIL.EQ
	MOVE X1,[RPGFIL,,OUTFIL]
	BLT X1,OUTFIL+3
GO1:	TLNN CH,TERM.F
	SETOM MODMAD
	SKIPN MODMAD
	JRST GO2
	TRO F,RPG.F
	PUSHJ P,WRITE+1
        TRO F,RPG.F
        PUSHJ P,QUIT+1
GO2:	MOVSI X1,1
	HRRI X1,RUNBLK
	RUN X1, ;ZONKO?
	JRST IOBAD

RUNBLK: SIXBIT/SYS/
	SIXBIT/RPG/
	0
	0
	0
	0

; ROUTINE TO INSERT LINES OF TEXT
; FORMAT A INSERT
FNDINS: MOVEI X1,FNDIN1
	MOVEM X1,COMAN
	PUSHJ P,CKTAB
	TRO F,INS.F
	POPJ P,
FNDIN1: PUSHJ P,FNDPR
	MOVE X1,LINE
	MOVEM X1,TMPLIN
	PUSHJ P,SETLI1
	PUSHJ P,SWITCH
	PUSHJ P,SETUP
	PUSHJ P,INS1
	MOVE X1,TMPLIN
	CAME X1,CURLIN
	AOJ X1,
	MOVEM X1,SVLIN
	SETOM MODMAD
	POPJ P,

QFLG:	400300,,21

	JRST FNDINS
INSERT: ASCIZ/NSERT/
	PUSHJ P,CKTAB
INS2:	SETOM MODMAD
	TRO F,INS.F
	TRNE F,MARK.F
	JRST [PUSHJ P,FNDINS
		SETZM FLRNG
		PUSHJ P,CKRNG
	JRST MARKST]
	PUSHJ P,SETLI1
	PUSHJ P,SWITCH
	PUSHJ P,SETUP
	SKIPN FLRNG
	PUSHJ P,LFORUP	;FIND OUT IF^,OR LF
	MOVE X1,FLRNG
	CAIG X1,DICT
	MOVEI X1,DICT+1
	CAMG X1,LASLIN
	JRST .+3
	MOVE X1,LASLIN
	AOJ X1,
	MOVEM X1,TMPLIN
	MOVEM X1,CURLIN
INS1: PUSHJ P,GETLIN
	JRST CKQ
	PUSHJ P,DEPCNT
	PUSHJ P,ADDLIN
	PUSHJ P,MAKDIC
	PUSHJ P,INSER
	PUSHJ P,SWITCH
	PUSHJ P,SETUP
	MOVE X1,TMPLIN
	MOVEM X1,CURLIN
	AOS TMPLIN
	JRST INS1

CKQ:	CAMN CH,QFLG
	JRST CKQ1
	CAME CH,CRFLG
	JRST CKQ2
	PUSHJ P,GNC
	JRST INS1+2
CKQ2:	TRNN F,FND.F
	JRST INS1
	CAMN CH,[XWD 500300,4]
	POPJ P,
	JRST INS1
CKQ1:	MOVE X1,TMPLIN
	CAMN X1,FLRNG
	JRST [TLNE F,QLIN
                PUSHJ P,RING
		JRST INS1]
	TLNN F,QLIN	;BEG OF LINE (FLAG ON?)
	JRST INS1	;NO
INS1A:	SOJ X1,
	PUSH P,FLRNG
	PUSH P,CERNG
	MOVEM X1,FLRNG
	MOVEM X1,CERNG
	MOVEM X1,TMPLIN
	PUSHJ P,DELLOC
	POP P,CERNG
	POP P,FLRNG
	JRST INS1



CKTAB:	TRNE F,TAB.CH	;HAVE TABS BE@N CHANGED
	JRST [SETO X1,
	GETLCH X1
	TLO X1,211
	SETLCH X1
	POPJ P, ]
	PUSHJ P,INITTT	;NO SO NOW BREAK ON ALLL
	POPJ P,

;JOIN COMMAND
; FORMAT N JOIN- JOINS LINES N AND N+1 WITH A LF
JOIN: ASCIZ/OIN/
	TLO F,JOIN.F
	MOVE LINE,FLRNG
	CAIG LINE,DICT
	JRST QUEST
	SETOM MODMAD
	MOVEM LINE,CURLIN
	MOVE DIC,(LINE)
	PUSHJ P,MAKPNT
	JRST QUEST ;DIC=0
	PUSHJ P,SETUP
	MOVE OLD ,DIC
	PUSHJ P,PUTNEW
	PUSHJ P,BACNEW
	MOVEI CH,12
	DPB CH,NEW
	AOJ LINE,
	MOVE DIC,(LINE) ;GET BEGIN NEXT LINE
	PUSHJ P,MAKPNT
	JRST QUEST ;DIC=0
	MOVE OLD,DIC
	PUSHJ P,PUTNEW
	PUSHJ P,DEPCNT
	PUSHJ P,ADDLIN
	PUSHJ P,MAKDIC
	MOVEM LINE,FLRNG
	MOVEM LINE,CERNG
	PUSHJ P,DELLOC
	SOJ LINE,
	MOVEM DIC,(LINE)
	JRST GETCOM

;COMMAND TO ERASE CONTENTS OF BUFFER N
;FORMAT N KILL

KILL: ASCIZ/ILL/
	MOVE X1,FLRNG
	SUBI X1,DICT
	CAIL X1,0
	CAILE X1,11
	JRST QUEST
KILL1: IMULI X1,^D9
	ADDI X1,BUF0
	MOVEM X1,X2
	CAIN X1,BUF0
	ADDI X2,^D81
	ADDI X2,^D9
	SETZM (X1)
	HRL X1,X1
	AOJ X1,
	BLT X1,(X2)
	TLNE F,LOAD.F
	POPJ P,
	JRST GETCOM

;ROUTINE TO CALCSTART OF BUFFER AND END OF BUFFER
BUFLIM: MOVE NEW,BUFNO
	IMULI NEW,^D418 ;GET START OF BUFFER
	ADDI NEW,BUF0
	MOVE X2,NEW
	CAIN X2,BUF0	;IS IT BUFFER 0?
	JRST [ MOVEI X2, BUF0+ ^D4179
	JRST BUFLI1]
	ADDI X2,^D417
BUFLI1: MOVEM X2,WORD
	POPJ P,

;LINES COMMAND
; FORMAT LINES P

LINES:	ASCIZ/ES/
	CAMN CH,EQFLG
	JRST LINEEQ
	TLNE CH,TERM.F	;TERMINATOR
	JRST [MOVEI NUM,^D54
		JRST LINES1]	;YES RESTORE
	TLNN CH,NUM.F
	JRST QUEST
	PUSHJ P,MAKOCT
	TLNN CH,TERM.F
	JRST QUEST
LINES1: MOVEM NUM,LINPAG
	JRST GETCOM
LINEEQ: MOVE X1,LINPAG
	PUSHJ P,PRTNUM
	TTCALL 3,CRLF
	JRST GETCOM
;LIST COMMAND- SAME AS SLASH

	JRST FNDSL
LIST:	ASCIZ/T/
	JRST SLCOM+1

;COMMAND TO LOAD UP BUFFERS
; FORMS-
; 1) N LOAD- LOAD BUFFER N FROM TTY
; 2) R;N LOAD- SAME AS GET ONLY DON'T DELETE LINES
; 3) LOAD FILENAME
; 4) A LOAD FILENAME - 3&4 SAME AS READ
	JRST FNDLD
LOAD: ASCIZ/OAD/
	SKIPE FLRNG
	TLNN CH,TERM.F	;EOF COM?
	JRST READ+1	;NO MUST BE FILENAME AFTER
	TLO F,LOAD.F	
	SKIPE BUFNO
	JRST GET+1	;SAME AS GET
	MOVE X1,FLRNG	;MUST BE FORM 1
	SUBI X1,DICT	;MAKE A NUMBER
	MOVEM X1,BUFNO
	PUSHJ P,KILL1
	MOVE X1,BUFNO
	IMULI X1,^D9
	ADDI X1,BUF0
	MOVEM X1,WORD+1
	MOVEM X1,X2
	CAIN X1,BUF0
	ADDI X2,^D81
	ADDI X2,^D9
	MOVEM X2,WORD
	MOVE LINE,X1
	PUSHJ P,SETUP
LOAD2:	PUSHJ P,GNC
	TLNN CH,ACT.F	;ACTION REQUIRED?
	JRST LOAD3	;NO
	CAMN CH,VFLG
	JRST LOAD4
	TLNN CH,LOAD.F	;SHOULD WE PAY AATTENTION TO 
	JRST LOAD5	;NO JUST PRINT &(CH)
	HLLI CH,0
	CAIN CH,4	; ^D?
	JRST [MOVEI CH,33
	AOS NCOUNT
	IDPB CH,NEW
	TRNN F,COMF.F!BUF.F
	TTCALL 3, CRLF
	JRST ENDLOD]	;YES END
	PUSHJ P,@CTTAB(CH)
	CAIE CH,21
	JRST LOAD2
	CAMN LINE,WORD+1
	JRST [TLNE F,QLIN
                PUSHJ P,RING
		JRST LOAD2]
	TLNN F,QLIN
	JRST LOAD2
	SOJ LINE,
	SETZM (LINE)
	JRST LOAD2
LOAD3:	PUSHJ P,CKCNT
	IDPB CH,NEW
	CAIN CH,15	;WER WE OVER ?
	JRST LOAD6	;YES
	JRST LOAD2
LOAD4:	PUSHJ P,VCNTRL
LOAD4A: PUSHJ P,CKCNT
	IDPB CH,NEW
	CAME CH,CRFLG
	JRST LOAD2
	PUSHJ P,GNC	;GET LF
LOAD6:	AOS NCOUNT
	PUSHJ P,DEPCNT
	PUSHJ P,ADDLIN
	PUSHJ P,MAKDIC
	MOVEM DIC,(LINE)
	AOJ LINE,
	PUSHJ P,SETUP
	CAMG LINE,WORD
	JRST LOAD2
	JRST NOROOM
LOAD5:	TLNN CH,CNTRL	;CONTROL CHAR?
	JRST LOAD4A	;NO MUST BE CR OR LF
	TRNE F,COMF.F!BUF.F
	JRST LOAD3
	HRRZ X1,CH
	ADDI X1,100
	TTCALL 1, CTTAB+46	;&
	TTCALL 1, X1
	JRST LOAD3

ENDLOD: MOVEI CH,15
	PUSHJ P,CKCNT
	IDPB CH,NEW
	MOVEI CH,12
	AOS NCOUNT
	PUSHJ P,DEPCNT
	PUSHJ P,ADDLIN
	PUSHJ P,MAKDIC
	MOVEM DIC,(LINE)
	JRST GETCOM

FNDMAR: TRO F,FND.F
	PUSHJ P,FNDMA2
	MOVEM NUM,SVNUM
	MOVEI X1,FNDMA1
	MOVEM X1,COMAN
	POPJ P,

FNDMA1: MOVE NUM,SVNUM
	PUSH P,CERNG
	PUSHJ P,MARK1
	POP P,CERNG
	POPJ P,


;MARK COMMAND

	JRST FNDMAR
MARK: ASCIZ/RK/
	PUSHJ P,CKRNG
	TRNE F,MARK.F
	JRST[ PUSHJ P,FNDMAR
	JRST MARKST]
FNDMA2: TLNN CH,NUM.F
	JRST QUEST
	PUSHJ P,MAKOCT
	CAIL NUM,1
	CAILE NUM,2
	JRST QUEST
	TRNE F,FND.F
	POPJ P,
	MOVE LINE,FLRNG
MARK1: MOVE DIC,(LINE)
	CAME DIC,MIN1
	JRST .+3
	PUSHJ P,MAKPN1
	JRST MARK2; DIC=0
	TRO DIC,(NUM)
	
	MOVEM DIC,(LINE)
	MOVEM LINE,CURLIN
MARK2:	TRNE F,FND.F
	POPJ P,
	AOJ LINE,
	CAMG LINE,CERNG
	JRST MARK1
	JRST GETCOM

;MODIFY COMMAND-SAME AS EDIT ONLY DON'T PRINT LINES BEING MODIFIED

	JRST FNDMD
MODIFY: ASCIZ/ODIFY/
	CAME CH,CRFLG
	JRST QUEST
	JRST EDIT+2

	JRST FNDOUT	;WHICH IS IT?
M:	TLNE CH,NUM.F
	JRST MARK+1	; DO WE MEAN MARKK?
	JRST EDIT+2	;AH WE MEAN MODIFY

FNDOUT: TLNE CH,NUM.F
	JRST FNDMAR
	JRST FNDMD1

;MOVE COMMAND
; FORMAT- A MOVE R

MOVE: ASCIZ/E/
	TLO F,MOVE.F	;SET MOVE FLAG
	SETZM WORD
	SETZM WORD+1
	SETZM WORD+2
	JRST COPY+1


;COMMAND TO TELL EDITOR TO TAKE INSTRUCTIONS FROM BUFFER
; IF A BELL CONDITION OCCURS
; FORMAT ONRING N
ONRING: ASCIZ/NRING/
	TLNN CH,NUM.F
	JRST QUEST
	PUSHJ	P,MAKOCT
	TLNN CH,TERM.F
	JRST QUEST
	CAIL NUM,0
	CAILE NUM,11
	JRST QUEST
	MOVEM NUM,RINBUF
	TRO F,ONR.F
	JRST GETCOM


;PAGE COMMAND

PAGE: ASCIZ/GE/
	SETZ NUM,
	TLNN CH,NUM.F
	JRST PAG1
	PUSHJ P,MAKOCT
PAG1:	MOVEM NUM,PAGNUM
	TLO F,PAG.F	;IND PAGE COMMAND
	JRST PRINT+1


FNDPT: TRO F,FND.F
	PUSHJ P,FNDPT1
	MOVEI X1,FNDPT2
	MOVEM X1,COMAN
	MOVEI X1,ENDPR2
	MOVEM X1,ENDCM
	POPJ P,

;PRINT COMMAND

	JRST FNDPT
PRINT:	ASCIZ/RINT/
	PUSHJ P,CKRNG;
	TRNE F,MARK.F
	JRST[PUSHJ P,FNDPT
	JRST MARKST]
FNDPT1: TRNN F,COMF.F!BUF.F
 TTCALL 3, DBLESP
	PUSHJ P,YORN
	TLZA F,DBSP.F	;NO DOUBLE SPACE
	TLO F,DBSP.F	;YES
	MOVE LINE,FLRNG
	PUSHJ P,SKIPLN	;SKIP 6
	TLNE F,PAG.F	;PAGE COMM?
	JRST PRDAT	;YES PRINT DATE
STRT:	MOVE DIC,(LINE)
	PUSHJ P,MAKPN1	;BECAUSE EVERYONE IN THE WORLD IS CRAZY
	JRST ENDPR3
	PUSHJ P,SKIPTP
STRTA:	TRNE F,FND.F
	POPJ P,
FNDPT2: MOVE DIC,(LINE)
	PUSHJ P,MAKPN1
	JRST STRT4 ;DIC=0
STRT1: ILDB CH,DIC
	CAIN CH,14	;FORMFEED?
	JRST FORMFD
	CAIN CH,12
	JRST [MOVEI CH,15
		PUSHJ P,PRTTEX
		MOVEI CH,12
		PUSHJ P,PRTTEX
		AOJA NUM,STRT1]
	CAIN CH,10	;;^H BACKSPCE
	JRST .+3
	CAIE CH,11
	PUSHJ P,ISUCN
	PUSHJ P,PRTTEX
	CAIE CH,15
	JRST STRT1
	MOVEI CH,12
STRT3: PUSHJ	P,PRTTEX
	AOJ NUM,	;ADD TO NO OF LINES
STRT4:	TRNE F,FND.F
	JRST FNDPT3
	SKIPN DONCHG
	MOVEM LINE,CURLIN
	AOJ LINE,	;	ADD TO LINE NUMBER
	CAMLE LINE,CERNG
	JRST ENDPR2
FNDPT3: CAMGE NUM,LINPAG
	JRST [TLNE F,DBSP.F
	PUSHJ P,MORELN
	TRNE F,FND.F
	POPJ P,
	JRST FNDPT2]
STRT2: PUSHJ P,SKIPLN
	JRST STRT
FORMFD: SKIPN NUM
	JRST STRT1
	 PUSHJ P,	ENDPRT
	PUSHJ P,ENDPR1
	PUSHJ P,SKIPTP+1
	JRST STRT1

ENDPR2: PUSHJ P,ENDPRT
	PUSHJ P,ENDPR1
ENDPR3:	PUSHJ P,SKIPLN
	SKIPLE OTTY+2
	PUSHJ P,PRTTE1
	TRNE F,FND.F
	POPJ P,
	JRST GETCOM
ENDPRT: MOVE X1,LINPAG
	SUB X1,NUM
	SKIPN X1
	POPJ P,
	PUSHJ P,SKIPLN+1
	POPJ P,

ENDPR1: PUSHJ P,SKIPLN
	PRMSG DOT
	POPJ P,

MORELN: MOVEI CH,12
	PUSHJ P,PRTTEX
	AOJ NUM,
	CAME NUM,LINPAG
	POPJ P,
	POP P,
	JRST STRT2

SKIPTP: PRMSG	DOT
	PRMSG CRLF
	SETZ NUM,
	TLNN F,PAG.F
	JRST [MOVEI X1,5
		JRST SKIPLN+1]
	MOVEI CH,40
	MOVEI X1,^D33
	PUSHJ P,PRTTEX
	SOJG X1,.-1
	MOVE X1,PAGNUM
	PUSHJ P,PRTNUM
	AOS PAGNUM
	PRMSG	CRLF
	MOVEI X1,4
	JRST SKIPLF
SKIPLN: MOVEI X1,5
	MOVEI CH,15
	PUSHJ P,PRTTEX
SKIPLF: MOVEI CH,12
	PUSHJ P,PRTTEX
	SOJG X1,.-1
	POPJ P,

PRDAT:	SKIPE PAGNUM
	JRST STRT
	PRMSG	DOT
	PRMSG CRLF
	MOVEI X1,^D35
	MOVEI CH,40
	PUSHJ P,PRTTEX
	SOJG X1,.-1
	CALL 11,[SIXBIT/DATE/]
	IDIVI 11,^D31
	MOVEM 11,13
	IDIVI 13,^D12
	AOJ 14,
	MOVEM 14,X1
	MOVEM CH,X4
	PUSHJ P,PRTNUM	;GET MONTH
	PRVAL CTTAB+55	;-
	AOJ 12,
	MOVEM 12,X1
	PUSHJ P,PRTNUM
	PRVAL CTTAB+55
	MOVE 13,X4	;RESTOR CH
	ADDI 13,^D64	;=1964-1900
	MOVEM 13,X1
	PUSHJ P,PRTNUM
	PRVAL CTTAB +40
	PRVAL CTTAB+40
	CALL 11,[SIXBIT/MSTIME/]
	IDIVI 11,^D1000
	IDIVI 11,^D60
	IDIVI 11,^D60
	MOVEM 11,X1
	PUSHJ P,PRTNUM
	PRVAL CTTAB+72	;:
	CAILE 12,11	;TWO DIGITS?
	JRST PRDAT1	;YES
	SETZ X1,	;GIVE US A ZERO
	PUSHJ P,PRTNUM
PRDAT1: MOVEM 12,X1
	PUSHJ P,PRTNUM
	PRMSG	CRLF
	MOVEI X1,2
	MOVEM X1,PAGNUM
	MOVEI X1,4
	PUSHJ P,SKIPLN+1
	SETZ NUM,
	JRST STRTA

DBLESP: ASCIZ/DOUBLE SPACE?/
DOT:	ASCIZ/.
/

; QUIT COMMAND

QUIT: ASCIZ/UIT/
	SKIPE MODMAD	;ANY MODIFICATIONS MADE?
	JRST QUIT2	;YES
QUIT1:	MOVE X1,SAVJBT
	TRNE X1,200	;ON,WERE WE IN TYMEX LAND
	JRST QUIT3
	TRZ X1,200	;NO
	CALLI X1,-11
QUIT3:	INIT TTY,0
	SIXBIT/TTY/
	0
	JRST IOBAD
	RELEASE TTY,0
	MOVE X1,SAVTTY
	TTCALL 7,X1
	MOVE 0,TMCSAV
	SETTMC
	MOVEI X1,QUIREE
	MOVEM X1,JOBREN
        TRNE F,RPG.F
        POPJ P,
QU2:    SETZ F,
		CALLI 1,EXIT
	JRST .-1
QUIT2:	MOVE X1,LASLIN
	CAIN X1,DICT
	JRST QUIT1	;NO TEXT
	TRNN F,COMF.F!BUF.F
	TTCALL 3, FILENW
	PUSHJ P,YORN
	JRST GETCOM	;NO
	JRST QUIT1	;YES

FILENW: ASCIZ/FILE NOT WRITTEN,OK? /
	
;ROUTINE TO READ FILES IN
; ACCEPTED FORMS-
; A READ L1,L2 FILENAME
; A READ FILENAME
;A READ
;READ

BIGNO: EXP 777777
INSPEC: EXP 1
	SIXBIT/DSK/
	XWD 0,IBUF
OUSPEC: EXP 1
	SIXBIT/DSK/
	XWD OBUF,IOBUF
READ: ASCIZ/EAD/
	SKIPN EOF	;DO WE HAVE TO FINISH READING ANY FILES?
	JRST .+3	;NO
	PUSHJ P,FINUP	;YES
	SETOM MODMAD
	SKIPN X1,FLRNG	;IS THERE A LINE NO SPECIFIED
	JRST [ ADD X1,LASLIN	;NO SO ADD 1 TO LASLIN
	AOJ X1,
	JRST READB]
	SETOM TRUTH
	CAIG X1,DICT
	MOVEI X1,DICT+1
	CAMG X1,LASLIN
	JRST .+3
	MOVE X1,LASLIN
	AOJ X1,
	MOVEM X1,CURLIN
	TLO F,FIRST	;SET FLAG TO SWITCH POINTERS AFTER FIRST LINE IS READ
READB:	MOVEM X1,TMPLIN ;THE NEXT LINE NO TO BE INSERTED
	MOVEM X1,READLN ;LINE TO BE READ
	MOVEI X1,1
	MOVEM X1,FLLIN
	MOVEI X1,BIGNO
	MOVEM X1,CELIN
	TRNE F,RPG.F
	JRST READ1A
	TLNN CH,NUM.F	;NOS AFTER READ
	TLOA F,TELLNO	;SET MODE TO TELL THE NUMBER OF CHARS IN FILE
	PUSHJ P ,LINLIM ;GET THE LINE NOS
READA:	TLNE	CH,TERM.F	;FILE SPECIFIED
	PUSHJ P,GETFIL	;NO 
READ1:	MOVEI X1,INFIL ;SETUP WHER NAME IS TO GO
	PUSHJ P,FILNAM	;GO GET NAME
	SETZM COLON
	HLRZ X1,INFIL+1
	CAIE X1,(SIXBIT/SIM/)
	CAIN X1,(SIXBIT/MAC/)
	SETOM COLON
	CAIN X1,(SIXBIT/FAL/)
	SETOM COLON
READ1A: MOVE X1,FLLIN
	MOVEM X1,L1
	MOVE X1,CELIN
	MOVEM X1,L2
 OPEN INF,INSPEC
	JRST IOBAD
	MOVEI X1,IBUFR
	MOVEM X1,JOBFF
	INBUF INF,1
	LOOKUP INF,INFIL
	JRST [ SETZM COLON
	TRNN F,RPG.F
		JRST FINWHY
		TTCALL 3,NOFILE
		JRST QUIT+1]
	TLZE F,TELLNO	;SHOULD WE GIVE A COUNT
	PUSHJ P,GETCNT	;YES
	MOVEI LINE,1	;LINE CONTAINS LINE NO OF FILE BEING READ
	MOVEM LINE,LINENO
	SETZM SEQNOS
	SETOM FRSTTM
	SKIPE TRUTH
	JRST READ1B	;READ WHOLE FILE IN
	SETOM EOF
	SETO DIC,
	PUSHJ P,INSER	;MAKE A -1 LINE TO INDICATE TO BE READ
	SOS READLN
	SETOM CURLIN
	JRST GETCOM
READ1B: SETZM TRUTH
	SETOM EOF
		SETO DIC,
	PUSHJ P,INSER
	SOS READLN
	PUSHJ P,FINUP
	SETOM MODMAD
	JRST GETCOM

;ROUTINE TO READ 1 LINE OF FILE IN

READFL: MOVE LINE,LINENO
	CAMLE LINE,L2	;LINE<=CELIN
	JRST ENDRD	;NO BIGGER SO DONE!
	CAMGE LINE,L1
	JRST READ2	;TOO LITTLE
	SKIPE DONGET
	JRST [MOVE CH,DONGET
		SETZM DONGET
		JRST READF1]
	PUSHJ P,INCHR	;GO GET FIRST CHAR
	JRST ENDRD	;ALL GONE!!
READF1:	MOVEI X1,^D259
	MOVNM X1,NCOUNT
	PUSH P,NEWPNT
	MOVE NEW,READPT
	MOVEM NEW,NEWPNT
	MOVE X1,READLN
	MOVEM X1,TMPLIN
READE:	PUSHJ P,CKRDCT
	IDPB CH,NEW
	CAIN CH,15
	JRST READD	;DONE WITH LINE
	PUSHJ P,INCHR
	MOVEI CH,15	;NO MORE CHARS
	JRST READE
READD:	PUSHJ P,DEPCNT
	PUSHJ P,ADDLIN	;ADD LINE TO UPDATE AREA
	PUSHJ P,MAKDIC	;MAKUP DICTIONARY POINTER
	PUSHJ P,INSER	;INSERT OR APPEND
	POP P,NEWPNT
	JRST READ4

READ2:	CAIN CH,15	;EOLINE?
	JRST READ2A	;YES
	PUSHJ P,INCHR
	JRST ENDRD	;EOF
	JRST READ2
READ2A: PUSHJ P,INCHR
	JRST ENDRD
	CAIE CH,12
	MOVEM CH,DONGET
	AOS LINENO
	JRST READFL

READ4:	PUSHJ P,INCHR
	JRST	ENDRD;EOF
READ4A: AOS LINENO
	CAIE CH,12
	MOVEM CH,DONGET
	POPJ P,


READPT: 10700,,RDLIN-1

CKRDCT: AOS NCOUNT
	SKIPGE NCOUNT
	POPJ P,
	CAIN CH,15
	POPJ P,
	CAIN CH,11
	JRST .+2
	IDPB CH,NEW
	TTCALL 3,LONGLN
	MOVEI CH,15
	POPJ P,

ENDRD:	SETZM EOF	;ALL OVER
	PUSH P,FLRNG
	PUSH P,CERNG
	MOVE X2,READLN
	MOVEM X2,FLRNG
	MOVEM X2,CERNG
	PUSHJ P,DELLOC
	SETZM READLN
	POP P,CERNG
	POP P,FLRNG
	POPJ P,


;ROUTINE CALLED FROM MAKPNT TO READ 1 LINE OF FILE AND PUT INTO DICT.

INLINE:	MOVE 0,[1,,SAVAC]
	BLT 0,SAVAC+15
 PUSHJ P,APR
 TRO F,TRAP.F
	PUSH P,TMPLIN
	PUSH P,CURLIN
	SETZ CH,
	PUSHJ P,READFL	;GO GET LINE
	SKIPN EOF	;DONE?
	JRST INLIN2	;YES
INLIN2: PUSHJ P,DISM
		MOVE 0,[SAVAC,,1]
	BLT 0,16
	POP P,CURLIN
	POP P,TMPLIN
	SETZM DIC
	SKIPE EOF
	MOVE DIC,(LINE)
	TLZN F,ALT.F
	POPJ P,
	JRST GETREA
ADDIN3: PUSH P,X1
	MOVE X1,LINE
	PUSHJ P,ADDIN1
	MOVE LINE,X1
	POP P,X1
	POPJ P,


; ROUTINE TO FINISH UP READING FILE BEFORE STARTING ANOTHER


FINUP:	MOVE 0, [1,,SAVAC]	;YEAH!!!
	BLT 0,SAVAC+15
	PUSH P,CURLIN
	PUSHJ P,APR
	TRO F,TRAP.F
	SETZ CH,
FINU3:	PUSHJ P,READFL	;GET A LINEIN
	SKIPE EOF
	AOJA LINE,FINU3
	PUSHJ P,DISM
	POP P,CURLIN
	SKIPLE CURLIN
	JRST FINU4
	MOVE X1,LASLIN
	MOVEM X1,CURLIN
FINU4:	MOVE 0,[SAVAC,,1]
	BLT 0,16
	TLZN F,ALT.F
	POPJ P,
	JRST GETREA

TRTAB:	MOVEI CH,40
	IDPB CH,NEW
	SETZ X1,
	MOVEI X3,^D259
	ADD X3,NCOUNT
	AOJ X3,
TRTAB2: MOVE X2,TABES(X1)
	CAML X2,X3
	JRST TRTAB1
	AOJ X1,
	CAIE X1,12
	JRST TRTAB2
	ADDI X2,10
	CAML X2,X3
	JRST TRTAB1
	JRST .-3
TRTAB1: CAMN X3,X2	;POSIT=TAB STOP
	JRST CPOPJ1	;YES
	PUSHJ P,CKCNT
	IDPB CH,NEW
	CAIN CH,15	;WERE WE TOO BIG FOR ME BRITCHES?
	JRST CPOPJ1	;YAS SUH
	AOJA X3,TRTAB1
;ROUTINE TO GET NO OF CHARS OF FILE AND PUT OUT ON TTY
GETCT:	HLRE X1,OUTFIL+3
	SKIPA
GETCNT: HLRE X1,INFIL+3
	TRNE F,COMF.F!BUF.F
	POPJ P,
	JUMPL X1,GETC2
	IMULI X1,1200
	JRST GETC3
GETC2:	MOVNS X1,X1
	IMULI X1,5
GETC3:	PUSHJ P,PRTNUM
	TTCALL 3, CHARS
	POPJ P,

CHARS:	ASCIZ/ CHRS
/

;ROUTINE TO OUTPUT DECI NOS 

PRTNUM: IDIVI X1,^D10
	JUMPE X1,PRTN1
	PUSH P,X2
	PUSHJ P,PRTNUM
	POP P,X2
PRTN1:	MOVEI CH,60(X2)
	TLNE F,PAG.F	;PAGE COMMAND?
	JRST PRTN2	;YES
	TTCALL 1,CH
	POPJ P,
PRTN2:	PUSHJ P,PRTTEX
	POPJ P,

	POPJ P,


;ROUTINE TO GET CHAR FROM INFIL
;SKIP RETURN ALL IS WEELL
;IMMED RETURN IMPLIES EOF

INCHR:	SOSLE IBUF+2
	JRST INOK
	IN INF,0
	JRST INOK1
	STATZ INF,740000
	JRST IOBAD
	POPJ P, ;EOF
INOK1:	SKIPE SEQNOS	;HAVE SEQ NOS?
	JRST INOK8	;YES
	SKIPN FRSTTM	;FIRST TIME?
	JRST INOK	;NO
INOK8:	MOVEI X4,IBUFR+202
INOK7:	MOVEI X2,3
INOK3: MOVE X1,IBUFR(X2)
	TRNN X1,1
	JRST INOK2	;NO T SEQ NOS
	CAMN X1,[201004020101]	;BLANK WITH BIT 35
	JRST INOK4	;MUST BE A SILLY
	SETZM IBUFR(X2) ;ZAPPO
	SETZ X3,
	DPB X3,[POINT 7,IBUFR+1(X2),6]
	JRST INOK5
INOK4:	SETZM IBUFR(X2)
	AOJ X2,
	MOVEI X1,30	;^L BELIEVE IT OR NOT
	MOVEM X1,IBUFR(X2)
INOK5:	SETOM SEQNOS
INOK2: AOJ X2,
	CAIG X2,202
	JRST INOK3
	ADDI X4,203
	CAIG X4,IBUFR+202
	JRST INOK7
	SETZM FRSTTM
INOK: ILDB CH,IBUF+1
	JUMPE CH,INCHR	;NULL
	AOS (P)
	POPJ P,

;ROUTINES TO GET FILE NAME FROM TTY

TO:	ASCIZ/TO: /
FROM: ASCIZ/FROM: /

TOFIL: TRNN F,COMF.F!BUF.F
	TTCALL 3, TO
	JRST GETFI1
GETFIL: TRNN F,COMF.F!BUF.F
	TTCALL 3, FROM
GETFI1: PUSHJ P,SETUP
	TLO F,COMW.F
	TLO F,ALT.F	;ALLOW FOR ABORT
	PUSHJ P,GETLIN
	JRST .-2
	MOVE NEW,NEWPNT
	PUSHJ P,GETCH	;GET FIRST CHAR
	TLZ F,COMW.F+ALT.F
	POPJ P,

;ROUTINE TO SETUP FILNAME FOR LOOKUP OR ENTER IN ADDR SPEC IN X1
PT6: POINT 6,0
FILNAM: SETZM (X1)
	SETZM 1(X1)
	SETZM 2(X1)
	SETZM 3(X1)
	MOVE X4,X1
	CAIN CH,"("
	PUSHJ P,GETPPN
	MOVEI X2,1
	HLL X1,PT6
FILN1:	HLL CH,CTTAB(CH)
	TLNN CH,NUM.F!LET.F
	JRST BRACKET
	HLLI CH,0
	SUBI CH,40
	CAILE X2,6
	JRST QUEST
	IDPB CH,X1	;DEPOS FIRST CHAR
	ILDB CH,NEW
	CAIE CH,175
	CAIN CH,15	;CR?
	POPJ P,
	CAIN CH,"."
	JRST FILEXT
	AOJA X2,FILN1
FILEXT: AOJ X1,
	HLL X1,PT6
	MOVEI X2,1
FILEX1: ILDB CH,NEW
	CAIE CH,175
	CAIN CH,15
	POPJ P,
	HLL CH,CTTAB(CH)
	TLNN CH,NUM.F!LET.F	;NO.?
	JRST BRACKET	;NO
	HLLI CH,0
	SUBI CH,40
	CAILE X2,3
	JRST QUEST
	IDPB CH,X1
	AOJA X2,FILEX1

BRACKET:	CAME CH,CTTAB+133
	JRST QUEST
	MOVEI X1,","
	SETZ X3,
	MOVEI X2,1
BRAK2:	ILDB CH,NEW
	CAMN CH,X1
	JRST BRAK1
	CAILE X2,6
	JRST QUEST
	HLL CH,CTTAB(CH)
	TLNN CH,NUM.F
	JRST QUEST
	HLLI CH,0
	SUBI CH,60
	IMULI X3,10
	ADD X3,CH
	AOJA X2,BRAK2
BRAK1:	CAIN X1,"]"
	JRST BRAK3
	PUSH P,X3
	MOVEI X1,"]"
	JRST BRAK2-2
BRAK3:	POP P,CH
	HRLM CH,3(X4)
	HRRM X3,3(X4)
	POPJ P,
;ROUTINE TO GET DIFFERENT PPN FROM USER

GETPPN: SETZM SAVPPN
	SETZM SAVPPN+1
		PUSH P,X1
	MOVEI X2,1
	MOVE X1,[POINT 6,SAVPPN]
GETPP2: ILDB CH,NEW
	CAIN CH,")"	;END?
	JRST GETPP1	;YES
	CAILE X2,14
	JRST QUEST
	SUBI CH,40
	IDPB CH,X1
	AOJA	X2,GETPP2
GETPP1: ILDB CH,NEW
	POP P,X1
	MOVEI X2,SAVPPN
	MOVEM X2,3(X1)
	POPJ P,



;	ROUTINE TO SETUP LINE LIMITS IN FLLIN AND CELIN 
;AS SPECIFIED IN L1,L2 TYPE COMMANDS
LINLIM: PUSH P,CH
	PUSH P,NEW
		PUSHJ P,MAKOCT
	MOVEM NUM,FLLIN
	CAIE CH,","
	JRST LINL2
	PUSHJ P,GETCH
	TLNE CH,NUM.F
	JRST LINL1
	CAIN CH,"$"
	JRST [MOVE NUM,BIGNO
	JRST LINL1+1]
	JRST QUEST
LINL1:	PUSHJ P,MAKOCT
	MOVEM NUM,CELIN
	CAIN CH,40 ;SPCE?
	PUSHJ P,GETCH	;YES GET FIRST NON BLANK
	POP P,
	POP P,
	POPJ P,
LINL2:	POP P,NEW
	POP P,CH
	MOVEI X1,1
	MOVEM X1,FLLIN
	TLO F,TELLNO	;REALLY TELL NOS
	POPJ P,

;ROUTINE TO PLACE LINE IN INPUT BUFFER INTO NEWLIN
;OLD CONTAINS POINTER TO LINE TO BE TRANSFERED

PUTNEW: TLO F,HFLAG+NOPR.F
	TLNE F,EDI.F
	TLZ F,NOPR.F
	TLNN F,JOIN.F	;JOIN COMMAND
	MOVE NEW,NEWPNT ;NO
	MOVEI X1,^D259
	MOVNM X1,NCOUNT
	PUSHJ P,D1
	ILDB CH,OLD
	CAIE CH,12	;LF?
	MOVEI CH,12
	AOS NCOUNT
	IDPB CH,NEW
	PUSHJ P,DEPCNT
	TLZ F,HFLAG+NOPR.F
	POPJ P,

;ROUTINE TO ADD LINE CONTAINED IN NEWLIN TO UPDATE AREA

ADDLIN: MOVE X1,NCOUNT	;GET NO CHARS IN LINE
	CAMLE X1,UPCNT	;DO WE HAVE ENOUGH ROOM IN AREA
	PUSHJ P,OUTBUF	;NO SO GET RID OF BUFFER
	MOVE NEW,NEWPNT
	MOVE OLD,UPPNT	;GET WHERE TO PLACE IN AREA
ADDL1:	ILDB CH,NEW
	SKIPN CH	;NULL?
	JRST .-2	;YES
	IDPB CH,OLD
	SOS UPCNT
	CAIE CH,15
	JRST ADDL1
	MOVEI CH,12
	IDPB CH,OLD
	SOS UPCNT
	POPJ P,

;ROUTINE TO INPUT TMP FILE INTO WINDOW AREA

INBUF:	XCT INBLK	;USETI (AS DETERMINED BEFORE ENTERING)
	INPUT TMP,WINLST
	HRRZ X1,INBLK
	MOVEM X1,WINBLK
	POPJ P,


;ROUTINE TO OUTPUT UPDATE ARE TO TMP FILE(REMBER TO TURN OFF INCOREBIT
;IN DICTIONARY

OUTBUF: MOVEI X1,DICT+1 ;START ZEROING INCORE BIT
	MOVE X3,LASLIN
	PUSHJ P,OUTBU1
	MOVEI X1,BUF0
	MOVEI X3,BUF0+^D90
	PUSHJ P,OUTBU1
	XCT TMPBLK	;DONE ZEROING DO USETO
	OUTPUT TMP,TMPLST
	AOS TMPBLK	;INCREASE BLOCK NO
	PUSHJ P,INITUP	;INIT PARAMETERS
	POPJ P,
OUTBU1: HLRZ X2,(X1)
	CAIE X2,777777	;DON'T TURN OFF
	TRZ X2,400000
	HRLM X2,(X1)
	AOJ X1,
	CAMG X1,LASLIN
	JRST OUTBU1	;
	POPJ P,

;ROUTINE TO CONVERT POINTER INTO DICTIONARY FORMAT
;UPPNT CONTAINS POINTER TO FIRST CHAR-POINTER TO BE CONVERTED
;OLD CONTAINS POINTER TO NEXT LINE
;FORMAT OF DIC POINTER
; BIT 1 ON IF INCORE (UPDATE AREA)
;BIT 1-18 IS BUFFER NO
;BIT 19-26 IS WORD NO WITHIN BUFFER
;BIT 27-29 FIRST 9 BITS OF REG POINTER E.G. 440
;BIT 30-33 UNUUSED
;BIT 34-35 USED FOR MARK(1 OR 2)
;JUST ADD 000700 TO LEFT HALF
;ON EXIT DIC WILL CONTAIN POINTER TO BE INSERTED INTO DICTIONARY

MAKDIC: HRLZ DIC,TMPBLK ;GET BLOCK NO
	TLO DIC,400000	;TURN ON INCORE
	MOVEI X2,UPDATE;	GET BEGINNING OR AREA
	HRRZ X3,UPPNT	;GET WORD
	SUB X3,X2	;X2 COINTAINS WORD NO REL TO START OF UPDATE
	DPB X3,WRDDIC	;DEPOSIT IN DIC
	LDB X2,CHRUP	;GET CHAR
	SETZ X3,
	CAMN X2,CHARPS(X3)
	SKIPA
	AOJA X3,.-2
	DPB X3,CHRDIC	;DEPOS
	MOVEM OLD,UPPNT ;UPDATE POINTER
	POPJ P,

WRDDIC: POINT 9,DIC,26
CHRUP: POINT 9 ,UPPNT,8
CHRDIC: POINT 3,DIC,29

CHARPS: EXP 440
	EXP 350
	EXP 260
	EXP 170
	EXP 100
	EXP 010


;ROUTINE TO CONVERT DIC POINTER IN DIC TO REG POINTER
;NORMAL RETURN IS SKIP
;IF DIC =0 SKIP RETURN


BIT34=2
BIT35=1
MAKPN1: AOS (P) ;NORMAL RETURN IS SKIP
		CAME DIC,MIN1
	JRST MAKP2
	PUSHJ P,MAKPNT
	SOS (P) ;DIC=0
	SKIPE EOF
	POPJ P,
	SETOM CERNG	;END OF FILE LASLIN FOUND
	POPJ P,
MAKPNT: AOS (P) ;NORMAL RETURN IS SKIP
		CAMN DIC,MIN1
MAKP3:	PUSHJ P,INLINE
	SKIPN DIC
	JRST [SOS (P)
		POPJ P,]
MAKP2:	SKIPN DONZER
	SETZM MRKDIC
	TRNN DIC,BIT34!BIT35
	JRST .+3
	LDB X1,[POINT 2,DIC,35]
	MOVEM X1,MRKDIC
	 LDB X1,CHRDIC	;GET CHAR 
	MOVE X1,CHARPS(X1)
	LSH X1,9	;SHIFT 9 PLACES TO LEFT
	ADDI X1,700
	HRLZ X1,X1
	LDB X2,WRDDIC	;GET WORD
	HRR X1,X2
	HLRZ X3,DIC
	TRZE X3,400000	;INCORE?
	JRST[	ADDI X1,UPDATE
	MOVEM X1,DIC
	POPJ P,]	;YES
	HRRM X3,INBLK	;PREPARE FOR	USETI 
	ADDI X1,WINDOW	
MAKP1:	MOVEM X1,DIC
	CAMN X3,WINBLK
	POPJ P,
	JRST INBUF	;GO GET

;
;ROUTINE TO INSER A POINTER INTO DICTIONARY
;1 INSERTION AT A TIME
;TMPLIN CONTAINS LIN NO TO BE INSERTED
;DIC CONTAINS PTER TO BE INSERTED

INSER: AOS LASLIN	;INCREASE LASLINE IN DICT
	MOVE NUM,LASLIN
	CAMLE NUM,JOBREL	;ENOUGH SPACE?
	PUSHJ P,GETCOR	;NO GET CORE
INSER1: CAMN NUM,TMPLIN ;IS THIS THE LINE TO 
	JRST APPEN	;YES STICK IT IN
	MOVE X1,-1(NUM)
	MOVEM X1,(NUM)
	SOJA NUM,INSER1
APPEN:	MOVEM DIC,(NUM) ;MAKE THE INSERT
	MOVEM NUM,CURLIN
	SKIPN EOF
	POPJ P,
	CAMG NUM,READLN
	AOS READLN
	POPJ P,

;ROUTINE TO EXPAND CORE FOR DICTIONARY

GETCOR: HRRZ X1,JOBREL
	ADDI X1,2000
	CALLI X1,CORE
	JRST TOOBIG
	POPJ P,

TOOBIG: TTCALL 3, BIGCOR
	JRST GETCOM

FNDREP: TRO F,FND.F
	PUSHJ P,FNDRE1
	MOVEM NUM,SVNUM
	MOVEI X1,STREP0
	MOVEM X1,COMAN
	MOVEI X1,FNDRE3
	MOVEM X1,ENDCM
	POPJ P,

FNDRE3: MOVE NUM,SVNUM
	JRST STREP2

;REPLACE COMMAND-
;FORMAT- R REPLACE L1,L2 FILE


REPSPE: 1
	SIXBIT/DSK/
	0,,RBUF

	JRST FNDREP
REPLAC: ASCIZ/LACE/
	PUSHJ P,CKRNG
	TRNE F,MARK.F
	JRST [PUSHJ P,FNDREP
	JRST MARKST]
FNDRE1: TLNN CH,NUM.F
	JRST QUEST
	PUSHJ P,LINLIM
	TLNE CH,TERM.F
	PUSHJ P,TOFIL
	MOVEI X1,REPFIL
	PUSHJ P,FILNAM
	MOVE X1,[XWD REPFIL,OUTFIL]
	BLT X1,OUTFIL+3
	OPEN REPF,REPSPE
	JRST IOBAD
	MOVEI X1,RBUFR
	MOVEM X1,JOBFF
	INBUF REPF,1
	LOOKUP REPF,REPFIL
	PUSHJ P,REPNEW
	OPEN OUTF,OUSPEC
	JRST IOBAD
	MOVEI X1,OBUFR
	MOVEM X1,JOBFF
	ENTER OUTF,OUTFIL
	JRST IOBAD
	OUTBUF OUTF,1
	MOVEI NUM,1
REP0:	PUSHJ P,RINCHR
	JRST STREPL
REP1:	CAML NUM,FLLIN
	JRST STREPL
REP3:	PUSHJ P,OUTCHR
	CAIN CH,15
	JRST REP2
	PUSHJ P,RINCHR
	JRST	STREPL
	JRST REP3
REP2:	MOVEI CH,12
	PUSHJ P,OUTCHR
	PUSHJ P,RINCHR
	JRST STREPL
REP4:	CAIN CH,12
	AOJA NUM,REP0
	AOJA NUM,REP1
STREPL: TRNE F,FND.F
	POPJ P,
	MOVE LINE,FLRNG
STREP0: MOVE DIC,(LINE)
	PUSHJ P,MAKPN1
	JRST STREP3 ;DIC=0
STREP1: ILDB CH,DIC
	PUSHJ P,OUTCHR
	CAIE CH,15
	JRST STREP1
	MOVEI CH,12
	PUSHJ P,OUTCHR
STREP3:	TRNE F,FND.F
	POPJ P,
	SKIPN DONCHG
	MOVEM LINE,CURLIN
	AOJ LINE,
	CAMG LINE,CERNG
	JRST STREP0

STREP2: PUSHJ P,RINCHR
	JRST ENDREP
	CAIE CH,15
	JRST STREP2
	PUSHJ P,RINCHR
	JRST ENDREP
	CAIN CH,12
	AOJA NUM,END1
	AOJA NUM,END2

END1:	PUSHJ P,RINCHR
	JRST ENDREP
END2:	CAMLE NUM,CELIN
	JRST ENDREP

END4:	CAIN CH,15
	JRST END3
	PUSHJ P,RINCHR
	JRST ENDREP
	JRST END4
END3:	PUSHJ P,RINCHR
	JRST ENDREP
	CAIN CH,12
	AOJA NUM,END1
	AOJA NUM,END2

ENDREP: PUSHJ P,OUTCHR
	PUSHJ P,RINCHR
	JRST ENDRE1
	JRST ENDREP
ENDRE1: CLOSE OUTF,
	TRNE F,FND.F
	POPJ P,
	JRST GETCOM

RINCHR:	SOSLE RBUF+2
	JRST RINOK
	IN REPF,0
	JRST RINOK
	STATZ REPF,740000
	JRST IOBAD
	POPJ P, ;EOF
RINOK: ILDB CH,RBUF+1
	JUMPE CH,RINCHR	;NULL
	AOS (P)
	POPJ P,
REPNEW: MOVEI X1,1
	MOVEM X1,FLLIN
	MOVEI X1,BIGNO
	MOVEM X1,CELIN
	POPJ P,
	JRST FNDWR
SAVE: ASCIZ/VE/
	JRST WRITE+1

;SUBSTITUTE COMMAND

FOR: ASCIZ/" FOR "/
WAIT: ASCIZ/WAIT? /
OK: ASCIZ/OK? /

FNDSUB: TRO F,FND.F
	PUSHJ P,FNDSU2
	MOVEI X1,SUB1
	MOVEM X1,COMAN
	MOVEI X1,SUB2
	MOVEM X1,ENDCM
	POPJ P,

	JRST FNDSUB
SUBSTI: ASCIZ/UBSTITUTE/
	TRNE F,MARK.F
	JRST [PUSHJ P,FNDSUB
	JRST MARKST]
FNDSU2: SETZM NEWCHS
	HRLI X1,NEWCHS
	HRRI X1,NEWCHS+1
	BLT X1,NEWCHS+^D103
	PUSHJ P, CKRNG	;SETUP RANGE
	TRNN F,COMF.F!BUF.F
	TTCALL 1, CTTAB+42	;"
	TLO F,COMW.F+DEL.F+SUBS.F
	SETZM DELIM	;FOOL GETLIN
	PUSHJ P,SUBS1	;GET FIRST JUNK TO SUBSTITUTE
	MOVE NEW,NTXPT	;
	TLO F,NOPR.F
	PUSHJ P,SUBS2	;PUT IN NEWCHS
	TLZ F,NOPR.F
	TRNN F,COMF.F!BUF.F
	TTCALL 3, FOR	;FIND WAT TO LOOK FOR
	PUSHJ P,SUBS1
	MOVE NEW,OLTXPT
	PUSHJ P,SUBS2
	CAMN NEW,OLTXPT
	JRST QUEST	;MAK SURE WE HAVE SOMETHING TO LOOK FOR
	TRNE F,COMF.F!BUF.F
	JRST .+3
	TTCALL 1, CTTAB+42
	TTCALL 3,CRLF
	TTCALL 3, WAIT
	PUSHJ P, YORN
	TRZA F,WAIT.F	;DON'T WAIT
	TRO F,WAIT.F
	SETZM NOSUBS	;INIT COUNTER
	MOVE X1,CURLIN
	MOVEM X1,CHGCUR
	TRNE F,FND.F
	POPJ P,
	MOVE LINE,FLRNG
SUB1: MOVE DIC,(LINE)
	PUSHJ P,MAKPN1
	JRST SUB4 ;DIC=0
	PUSHJ P,SETUP
	MOVEM LINE,ENDLIN
	MOVEM LINE,CURLIN
	MOVEM LINE,BEGLIN
	MOVEM LINE,TMPLIN
	MOVEM DIC,BEGPRT
	MOVEM DIC,BEGSER
	TLZ F,SUBMAD
	PUSHJ P,FNDTXT	;FND THE TEXT,AND MAK SUB IF NECESSARY
SUB4:	TRNE F,FND.F
	POPJ P,
	MOVE X4,DONCHG
	MOVEM X4,CURLIN
	AOJ LINE,
	CAMG LINE,CERNG
	JRST SUB1
SUB2:	TRNE F,COMF.F!BUF.F
	JRST SUB3
	SKIPE NOSUBS
	JRST .+3
	MOVE X1,CHGCUR
	MOVEM X1,CURLIN
	MOVE X1,NOSUBS
	PUSHJ P,PRTNUM	;PRINT NO OF SUBSTIT MADE
	TTCALL 3, CRLF
SUB3:	TRZ F,WAIT.F
	JRST GETCOM



NTXPT: POINT 7,NEWCHS
OLTXPT: POINT 7,OLDCHS

SUBS1:	PUSHJ P,SETUP
	PUSHJ P,GETLIN
	JRST .-1
	HLLI CH,0
	CAIN CH,15
	JRST [PUSHJ P,GNC
		IDPB CH,NEW
		POPJ P,]
	CAIN CH,4	;^D
	POPJ P,
	JRST SUBS1+1

SUBS2: MOVE OLD,NEWPNT
	ILDB CH,OLD
	CAIN CH,0
	POPJ P,
	TLNN F,NOPR.F
	JRST SUBS3
	CAIN CH,177
	JRST QUEST	;^G ILLEGAL
SUBS3:	IDPB CH,NEW
	JRST SUBS2+1

;ROUTINE TO FIND TEXT WITHIN FILE THAT MATCHES CHARS IN OLDCHS

;TXP/ PTER TO FILE TEXT
;SPT/ PTER TO SEARCH LST
;SCH/ CHA TO SEARCH FOR
;CH/ FILE TEXT CHAR
;BEGLIN/ BEGINN TEXT LINE TO START MAKING SUBST IF FOUND
;ENDLIN/ END	"	"	"	"	"	"	"	"
;BEGPT/ PTER TO FIRST CHAR TO START MAKING SUBST
;ENDPT/ END OF SUBSTI POINTER
;BEGSER/ WHERE TO START NEXT SEARCHING WITHIN LINE

ZERO: 0
FNDTXT: MOVE SPT,OLTXPT ;GET CHAR TO LOOK FOR POINTER
	MOVE TXP,BEGSER ;GET WHERE TO START
	ILDB SCH,SPT	;GET FIRST SERCH CHAR
FNDTX1: ILDB CH,TXP
	CAIN SCH,177	;NULL?
	JRST [CAIN CH,15
		JRST FNDTXX
		JRST FNDTX2]
	CAMN SCH,CH	;OR EQUAL
	JRST FNDTX2	;YES
	CAIE CH,15	;EOF LINE?
	JRST FNDTXA	;NO
FNDTXX:	TLZE F,SUBMAD	;YES HAVE WE MADE SUBSTIN THIS LINE
	PUSHJ P,MAKSU5	;YES
	POPJ P, ;NO

FNDTXB: PUSHJ P,MAKSU8
	MOVE LINE ,BEGLIN
	SOJ LINE,
	TLZ F,SUBMAD
	POPJ P,

FNDTXA: TLNN F,SUBMAD	;ANY SUBST MADE IN LINE YT?
	JRST FNDTX1	;NO
	PUSHJ P,CKCNT	;YES SO ADD THIS CHAR
	IDPB CH,NEW
	JRST FNDTX1


FNDTX2: MOVEM TXP,BEGPT ;SAVE WHERE IT STARTS
	MOVEM TXP,BEGSER
FNDTX3: CAIN CH,15
	JRST ANOTER
	SKIPN TXP
	JRST FNDTX7
	ILDB SCH,SPT
	CAIN SCH,0	;END?
	JRST FNDTX5	;YES
FNDTX6: ILDB CH,TXP
	CAIN SCH,177
	JRST[CAIN CH,15
		JRST FNDTXY
		JRST FNDTX3]
	CAMN SCH,CH
	JRST FNDTX3	;STILL GOING STRONG
FNDTXY:	TLNN F,SUBMAD	;ANY SUBS?
	JRST FNDTX7	;NO
	LDB CH,BEGPT	;GET LAST CHAR
	PUSHJ P,CKCNT
	IDPB CH,NEW
	JRST FNDTXT

FNDTX7: MOVE X1,BEGLIN
	CAMN X1,ENDLIN
	JRST FNDTXT
	MOVEM X1,LINE
	POPJ P,
FNDTX5: MOVEM TXP,ENDPT
	JRST MAKSUB	;START SUBSTITUTING

ANOTER: ILDB SCH,SPT	;GET NEXT CHR
	ILDB SCH,SPT	;GET NEXT CHAR
	CAIN SCH,0	;END?
	JRST [SETZM ENDPT
		JRST MAKSUB]
	AOS ENDLIN
	MOVE X3,ENDLIN
	CAMLE X3,CERNG
	JRST SUB2
	MOVE DIC,(X3)
	PUSH P,LINE
	MOVE LINE,X3
	PUSHJ P,MAKPN1
	JRST FNDTX7	;END DIC=0
	POP P, LINE
	MOVE TXP,DIC
	JRST FNDTX6	;FINISH UP

;ROUTINE TO MAKE SUBSTITUTION USING INFO ABOVE (DESCRIBE BEFORE FNDTXT)
MAKSUB: MOVE X4,CURLIN
	MOVEM X4,DONCHG
	TRNN F,WAIT.F
	JRST MAKSU1	;NO MAK SUB
MAKSU:	MOVE DIC,(X4)
	PUSH P,LINE
	MOVE LINE,X4
	PUSHJ P,MAKPN1
	JFCL ;DIC CANNOT BE ZERO-HAH HAH
	POP P,LINE
MAKSUA: MOVE X2,BEGPRT
MAKSU0: ILDB CH,X2
	PUSHJ P,PRTTEX
	CAIE CH,15
	JRST MAKSU0
	MOVEI CH,12
	PUSHJ P,PRTTEX
	AOJ X4,
	CAMG X4,ENDLIN
	JRST [MOVE DIC,(X4)
	PUSH P,LINE
	MOVE LINE,X4
	PUSHJ P,MAKPN1
	JFCL	;DIC CNNOT BE 0
	POP P ,LINE
	MOVE X2,DIC
	JRST MAKSU0]
	SKIPLE OTTY+2
	PUSHJ P,PRTTE1
	TTCALL 3, OK
	PUSHJ P,YORN
	SKIPA	;NO
	JRST MAKSU1	;YES
	MOVE X1,BEGPT
	LDB CH,X1
	TLNE F,SUBMAD	;ANY SUB MADE?
	IDPB CH,NEW	;YASMAM
	MOVEM X1,BEGPRT
	MOVEM X1,BEGSER
	JRST FNDTXT
MAKSU1: MOVE X1,ENDPT
	MOVEM X1,BEGPRT
	MOVEM X1,BEGSER
	MOVE X4,CURLIN
MAKSU7: MOVE DIC,(X4)
	PUSH P,LINE
	MOVE LINE,X4
	PUSHJ P,MAKPN1
	JFCL
	POP P,LINE
MAKSU3: ILDB CH,DIC
	CAMN DIC,BEGPT
	JRST MAKSU2	;START SUBSTITUTING
	TLNE F,SUBMAD	;HAVE WE MADE SUBSTITUTIONS?
	JRST[ CAIE CH,15
	JRST MAKSU3
	AOJA X4,MAKSU7]
	PUSHJ P,CKCNT
	IDPB CH,NEW
	CAIE CH,15
	JRST MAKSU3
	PUSHJ P,MAKSU5
	AOJ X4,
	JRST MAKSU7
MAKSU2: MOVE SPT,NTXPT ;GET STUFF TO SUBSTITUTE
	SETOM MODMAD
	TLO F,SUBMAD	;IN CASE DELETING CHARS
MAKS2A: ILDB CH,SPT
	CAIN CH,0
	JRST MAKSU4	;LOAD UP REST OF LINE
	PUSHJ P,CKCNT
	IDPB CH,NEW
	TLO F,SUBMAD	;INDIC SUBST MADE
	CAIE CH,15
	JRST MAKS2A
	IBP SPT ;GO PAST LF
	PUSHJ P,MAKIN1
	PUSHJ P,SETUP	;INIT 
	JRST MAKS2A

MAKINS: PUSHJ P,CKCNT
	IDPB CH,NEW	;ADD THE CR
MAKIN1: AOS NCOUNT	;ADD 1 FOR LF
	PUSHJ P,DEPCNT
	PUSHJ P,ADDLIN
	PUSHJ P,MAKDIC
	SKIPN MRKDIC
	JRST MAKIN2
	MOVE X1,MRKDIC
	TRO DIC,(X1)
MAKIN2:	PUSHJ P,INSER
	AOS BEGLIN
	AOS ENDLIN
	MOVE LINE,BEGLIN
	SOJ LINE,
	AOS TMPLIN
	AOS CERNG
	PUSHJ P,SETUP
	POPJ P,

MAKSU4: AOS NOSUBS	;ADD TO NO OF SUBSTITUTIONS
	SETOM MODMAD	;INDIC MOD MADE
	MOVE X1,ENDLIN
	MOVEM X1,CURLIN
	SKIPN ENDPT
	JRST MAKSU8+1
	MOVE DIC,(X1)
	PUSH P,LINE
	MOVE LINE,X1
	PUSHJ P,MAKPN1
	JFCL
	POP P,LINE
	CAMN DIC,ENDPT
	JRST MAKSU8
	MOVE TXP,ENDPT
	MOVEM TXP,BEGPRT
	MOVEM TXP,BEGSER
	JRST FNDTXT
MAKSU8: SOS ENDLIN
	PUSHJ P,MAKSU5+1
	MOVE LINE ,BEGLIN
	SOJ LINE,
	POPJ P,
MAKSU5: PUSHJ P,MAKINS ;INSERT LINE
	PUSH P,FLRNG
	PUSH P,CERNG
	MOVE X3,BEGLIN
	MOVEM X3,FLRNG
	MOVE X4,ENDLIN
	MOVEM X4,CERNG
	PUSHJ P,DELLOC	;DELL OLD BEGLIN-ENDLIN
	POP P ,CERNG
	POP P,FLRNG
	MOVE X3,BEGLIN
	MOVE X4,ENDLIN
	SUB X4,X3
	AOJ X4, ;HOW MANY LINES DELETED
	MOVE X3,CERNG
	SUB X3,X4
	MOVEM X3,CERNG
	PUSHJ P,SETUP	;CLEAR COUNT AND SUCH
	POPJ P,


;TABS COMMAND
;FORMAT- TABS N1,N2,...,NN

EQFLG:	400000,,75

TABS: ASCIZ/ABS/
	CAMN CH,CRFLG
	JRST RESTAB
	CAMN CH,EQFLG	;=?
	JRST TABEQ
	TRO F,TAB.CH	;U BLEW IT
	SETO X1,
	GETLCH X1
	TLO X1,10	;NO TAB 
	SETLCH X1
	SETZ X1,	;X1 USED AS CNTR
TABS1:	TLNE CH,TERM.F ;DONE
	JRST GETCOM ;YES
	CAIN CH,","
	AOJ X1,; INC TAB STOP POSIT
	TLNN CH,NUM.F	;NUMB?
	JRST [ PUSHJ P,GETCH
	JRST TABS1 ]
	PUSHJ P,MAKOCT
	CAILE X1,11
	JRST QUEST
	MOVEM NUM,TABES(X1)
	JRST TABS1

RESTAB: MOVE X1,[TABINT,,TABES]
	BLT X1,TABES+11
	JRST GETCOM
TABEQ:	SETZ X3,
	MOVE X1,TABES(X3)
	PUSHJ P,PRTNUM
	CAIN X3,11
	JRST TABEQ1
	TTCALL 1,CTTAB+54
	AOJA X3,TABEQ+1
TABEQ1: TTCALL 3,CRLF
	JRST GETCOM


FNDUNM: TRO F,FND.F
	PUSHJ P,FNDUN3
	MOVEM NUM,SVNUM
	MOVEI X1,FNDUN1
	MOVEM X1,COMAN
	POPJ P,

FNDUN1: MOVE NUM,SVNUM
	PUSHJ P,FNDUN2
	POPJ P,

	JRST FNDUNM
UNMARK: ASCIZ/NMARK/
	PUSHJ P,CKRNG
	TRNE F,MARK.F
	JRST [PUSHJ P,FNDUNM
	JRST MARKST]
FNDUN3: TLNN CH,NUM.F
	JRST QUEST
	PUSHJ P,MAKOCT
	CAIL NUM,1
	CAILE NUM,2
	JRST QUEST
	TRNE F,FND.F
	POPJ P,
	MOVE LINE,FLRNG
FNDUN2: MOVE DIC,(LINE)
	TRZ DIC,(NUM)
	MOVEM DIC,(LINE)
	TRNE F,FND.F
	POPJ P,
	SKIPN DONCHG
	MOVEM LINE,CURLIN
	AOJ LINE,
	CAMG LINE,CERNG
	JRST FNDUN2
	JRST GETCOM


;VERSION COMMAND
VERSN: ASCIZ/ERSION/
	MOVE X1,137
	PUSHJ P,PRTOCT
	TTCALL 3,CRLF
	JRST GETCOM
PRTOCT: IDIVI X1,10
	JUMPE X1,PRTOC1
	PUSH P,X2
	PUSHJ P,PRTOCT
	POP P,X2
PRTOC1: MOVEI CH,60(X2)
	TTCALL 1,CH
	POPJ P,

;WRITE COMMAND
;FORMAT- R WRITE (CR) OR R WRITE (LF)
; SHORT FORM- R WRITE FILENAME

OLDFIL: ASCIZ/ OLD FILE /
NEWFIL: ASCIZ/ NEW FILE /
	JRST FNDWR
WRITE:	ASCIZ/RITE/
	PUSHJ P,CKRNG
	TRNE F,MARK.F
	JRST [PUSHJ P,FNDWR
	JRST MARKST]
FNDAP1: TLZ F,COMBLK	;ASSUME NO COMPRESS BLANKS
	TLNN CH,TERM.F	;EOF COMMAND
	JRST WRIT1	;NO FILE NAME GIVEN
	HLLI CH,0
	CAIN CH,12;	LF?
	JRST QUEST;TLO F,COMBLK
	TRNN F,RPG.F
	JRST WRIT1B
	SKIPE RPG
	JRST WRIT1A
	SKIPN INFIL
	JRST WRIT1B
	MOVE 0,[INFIL,,OUTFIL]
	BLT 0,OUTFIL+3
	TRO F,FIL.EQ
	JRST	WRIT1A
WRIT1B:	PUSHJ P,TOFIL	;PUT OUT TO:
WRIT1:	MOVEI X1,OUTFIL
	PUSHJ P,FILNAM	;GET FILE NAME
WRIT1A: OPEN OUTF,OUSPEC
	JRST IOBAD
	MOVEI X1,1
	HRRM X1,OUTBLK
	PUSH P,OUTFIL+3
	SETZM	RENF
	LOOKUP OUTF,OUTFIL
	JRST WRIT2	;NEW FILE
	SETZM PROTEC
	LDB X1,[POINT 8,OUTFIL+2,8]
	SKIPN X1
	SETOM RENF
	DPB X1,[POINT 8,PROTEC,8]
	TLNE F,APPEN.F	;APPEND COMMAND?
	PUSHJ P,SETOUT	;YESSIR
	TLNN F,APPEN.F	;APPEND COM?
	PUSHJ P,RELS	;NO 
	TRNN F,RPG.F
	JRST WRIT3B
	TRNE F,FIL.EQ
	PUSHJ P,WRIT4B
WRIT3B: SKIPN RPG
 JRST WRIT3C
	PUSHJ P,CKNAME
	JRST WRIT4A
WRIT3C:	TRNE F,COMF.F!BUF.F
	JRST WRIT3
	TTCALL 3, OLDFIL
	JRST WRIT2A
WRIT2:	HRRZ X1,OUTFIL+1
	TRNE X1,BIT34
	JRST QUEST
	SETZM PROTEC
	HLRZ X1,OUTFIL+3
	CAMN X1,PROJEC
	JRST	.+4
	SETOM RENF
	MOVEI X1,11
	DPB X1,[POINT 8,PROTEC,8]
	TRNN F,RPG.F
	JRST WRIT2B
	TRNE F,FIL.EQ
	PUSHJ P,WRIT4B
WRIT2B: SKIPN RPG
 JRST WRIT2C
	PUSHJ P,CKNAME
	JRST WRIT4A
WRIT2C:	TRNE F,COMF.F!BUF.F
	JRST WRIT3
	TTCALL 3, NEWFIL
WRIT2A: TTCALL 0,CH
	JRST WRIT3+1
WRIT3:	PUSHJ P,GNC
	HLLI CH,0
	CAIE CH,33
	CAIN CH,175
	JRST ALTM2
	CAIE CH,15
	JRST ALTM2
WRIT4:	PUSHJ P,GNC
WRIT4A: PUSHJ P,ZEROUT
	MOVEI X1,OBUFR
	MOVEM X1,JOBFF
	OUTBUF OUTF,1
	POP P,OUTFIL+3
	MOVE X1,PROTEC
	MOVEM X1,OUTFIL+2
	ENTER OUTF,OUTFIL
	JRST QUEST	;WRITE LOCKED?
	XCT OUTBLK	;DO A USETO
	TRNE F,FND.F
	POPJ P,
	MOVE LINE,FLRNG
	MOVE X1,LASLIN
	CAIG X1,DICT
	JRST FNDAP3	;NO TEXT
WRIT7:	MOVE DIC,(LINE)
	PUSHJ P,MAKPN1
	JRST FNDAP3 ;DIC=0
WRIT6:	ILDB CH,DIC
;	TLNN F,COMBLK	;SHOULD WE CHECK FOR BLANKS
	JRST WRIT5
	CAIE CH,40	;YES -IS IT ONE?
	JRST WRIT5
	MOVEI X2,1
	TLZ F,BLANK.F
WRIT6B: ILDB X1,DIC
	CAIE X1,40
	JRST WRIT6A
	AOJ X2, ;KEEP A COUNT OF HOW MANY BLANKS
	TLO F,BLANK.F
	JRST WRIT6B
WRIT6A: TLZE F,BLANK.F
	PUSHJ P,OUTTAB
	DEC DIC
WRIT5:	PUSHJ P,OUTCHR
	CAIE CH,15
	JRST WRIT6	
	MOVEI CH,12
	PUSHJ P,OUTCHR
FNDAP3: TRNE F,FND.F
	POPJ P,
	SKIPN DONCHG
	MOVEM LINE,CURLIN
	AOJ LINE,
	CAMG LINE,CERNG
	JRST WRIT7
FNDAP2: CLOSE OUTF,
	SETZM OUTFIL+2
	HLLZS OUTFIL+1
        PUSH P,OUTFIL+3
	LOOKUP OUTF,OUTFIL
	JRST OVER	;GULP-WAT DID I DO?
	PUSHJ P,GETCT	;REPORT THE NO OF CHARS
        POP P,OUTFIL+3
	SKIPE RENF
	PUSHJ P,RENAF
	SETZM MODMAD	;INDICATE WE WROTE ON A FILE
	TRNE F,FND.F!RPG.F
	POPJ P,
	JRST GETCOM



RENAF:  HRLI X1,OUTFIL
        HRRI X1,RENFIL+2
        BLT X1,RENFIL+4
        MOVE X1,OUTFIL+3
        MOVEM X1,RENFIL+1
        SETZ X1,
        DPB X1,[POINT 9,RENFIL+4,8]
        MOVEI X1,4
        MOVEM X1,RENFIL
        RENAME OUTF,RENFIL
        JFCL
        POPJ P,
CKNAME: MOVE X1,RPGFIL
	CAME X1,OUTFIL
	JRST CPOPJ1
	HLRZ X1,RPGFIL+1
	HLRZ X2,OUTFIL+1
	CAME X1,X2
	AOS (P)
	POPJ P,
WRIT4B: TRNE F,COMF.F!BUF.F
	POPJ P,
	MOVE NEW,[POINT 6,OUTFIL]
	MOVE X1,[600,,OUTFIL]
	ILDB CH,NEW
	PUSHJ P,PRTSIX
	MOVE NEW,[POINT 6,OUTFIL+1]
	MOVE X1,[POINT 6,OUTFIL+1,17]
	ILDB CH,NEW
	SKIPN CH
	JRST WRIT4C
	TTCALL 1,CTTAB+56	; .
	PUSHJ P,PRTSIX
WRIT4C: TTCALL 3,CRLF
	POPJ P,

FILEEQ: ASCIZ/FILENAME= /

PRTSIX: SKIPN CH
	POPJ P,
	ADDI CH,40
	TTCALL 1,CH
	CAMN NEW,X1
	POPJ P,
	ILDB CH,NEW
	JRST PRTSIX

OUTTAB: IDIVI X2,10
	SKIPE X3	;ANY REMAINDER?
	AOJ X2, ;YES
	MOVEI CH,11
OUTTA1: CAIN X2,1
	POPJ P,
	PUSHJ P,OUTCHR
	SOJA X2,OUTTA1
RELS:	RELEASE OUTF,
	OPEN OUTF,OUSPEC
	JRST IOBAD
	POPJ P,
SETOUT: HLRE X1,OUTFIL+3
	JUMPGE X1,SETOU1
	MOVNS X1,X1
	IDIVI X1,200
	AOJ X1,
	SKIPE X2
SETOU1: AOJ X1,
	HRRM X1,OUTBLK
	POPJ P,

ZEROUT: SETZM OUTFIL+2
	SETZM OUTFIL+3
	HLLZS OUTFIL+1
	POPJ P,

OUTCHR: SOSLE OBUF+2
	JRST OUTOK
	OUT OUTF,0
	JRST OUTOK
	CLOSE OUTF,40	;DON'T DELETE PREV FILE
	JRST OVER
OUTOK:	IDPB CH,OBUF+1
	POPJ P,

;INITIAL DATA ,MAKE CHANGES HERE WHEN EVER MAKE CHANGES BETWEEN 
;NEWPNT AND PLIST

INIDAT: 10700,,NEWLIN-1
	10700,,OLDLIN-1 ;OLDPNT
	SIXBIT/###EDT/	;SYSTMP
	SIXBIT/TMP/
	0
	0
	SIXBIT/EDT/	;BLOCK
	IOWD ^D15,BUFFA
	0
TABINT: 11
	21
	31
	41
	51
	61
	71
	101
	111
	121
	2	;PAGNUM
	^D54	;LINPAG
	IOWD 200,WINDOW ;WINLST
	0
	USETI OUTF, 1	;WRITIN
	USETO OUTF,1	;OUTBLK
	USETI TMP,1	;INBLK
	USETO TMP,1	;TMPBLK
	IOWD 200,UPDATE ;TMPLST
	0
	0	;UUOH
	JRST UUOHAN
	-1	;RINBUF
	XWD -200,PLIST	;PLIST


IFN PURE,<RELOC>

; THIS MUST BE HERE

RPG:	BLOCK 1	;-1=RPG ENTRANCE,0=NORMAL
PROJEC: BLOCK 1
SAVTTY: BLOCK 1

;NON ZERO LOW SEGMENT DATA

NEWPNT:	BLOCK 1
OLDPNT:	BLOCK 1
SYSTMP:		BLOCK 4
BLOCK:	BLOCK 3

TABES:	BLOCK 12
PAGNUM: BLOCK 1	;USED FOR PAGE
LINPAG: BLOCK 1		;NO LINES PER PAGE
WINLST: BLOCK 2
WRITIN: BLOCK 1
OUTBLK: BLOCK 1
INBLK: BLOCK 1
TMPBLK: BLOCK 1
TMPLST: BLOCK 2
UUOH: BLOCK 2
RINBUF: BLOCK 1
PLIST: BLOCK 1
	BLOCK 200	;FFROM HERE DOWN ZEROED ON INITIALIZATION

TTYWID: BLOCK 1
TMCSAV: BLOCK 1
SAVJBT: BLOCK 1
SVJOB:	BLOCK 1
BUFFA: BLOCK ^D15
RPGFIL: BLOCK 4
COMFIL: BLOCK 4
ICOMF:	BLOCK 3 ;COMMAND FILE BUFFER HEADER
COMBUF: BLOCK 203	;COMMAND FILE BUFFER

;
;INFO FROM HERE DOWN IS ZEROED DURING CLEAR COMMAND
;TBES IS REFILLED


;LOW SEGMENT MUST ALWAYS BEGIN WITH TMPFIL AND END WITH DICT

TMPFIL: BLOCK 4 ;TEMPORARY WORK FILE 
RENF:	BLOCK 1
GIVCNT: BLOCK 1
EOR:	BLOCK 1
CHGCUR: BLOCK 1
PROTEC: BLOCK 1
DONCHG: BLOCK 1
DONGET: BLOCK 1
DONZER: BLOCK 1
COLON:	BLOCK 1
MODMAR: BLOCK 1
TABB:	BLOCK 1
LINFND: BLOCK 1
ENDSER: BLOCK 1
SAVPPN: BLOCK 2
SAVAC: BLOCK 17

SVNUM: BLOCK 1
;STOR FOR FIND COMMAND-MUST BEGIN WITH FCMND AND END WITH OHIER

FCMND: BLOCK ^D52
SOURCE: BLOCK ^D50
SRCPNT: BLOCK ^D50
POLISH: BLOCK ^D25
OHIER: BLOCK ^D25

NOFNDS: BLOCK 1
SVLIN: BLOCK 1
TRUTH: BLOCK 1
POLPT: BLOCK 1
SVPTR: BLOCK 1
DATE:	BLOCK 3
NOSUBS: BLOCK 1; COUNTER FOR NO OF SUBSTITU
BEGPRT: BLOCK 1	;WHERE TO START PRINTING FROM IN MAKSUB
BEGLIN: BLOCK 1;	STRT LINE TO MAK SUBS IN
ENDLIN: BLOCK 1; END LINE TO MAK SUBST
BEGPT: BLOCK 1 ;POINTER TO FIRST CHAR TO START SUBS FOR
ENDPT: BLOCK 1 ;POINTER TO CHAR TO PICK UP
BEGSER: BLOCK 1 ;POINTER TO CHAR TO START SEARCH IN
NEWCHS: BLOCK ^D52
OLDCHS: BLOCK ^D52
ITTY:	BLOCK 3
INTTY:	BLOCK 23
OTTY: BLOCK 3
TTYBUF: BLOCK 23
REPFIL: BLOCK 4
RBUF:	BLOCK 3
RBUFR:	BLOCK 203
IBUF: BLOCK 3
IBUFR: BLOCK 203
INFIL:	BLOCK 4 ;FILE TO READ FROM
IOBUF: BLOCK 3
OBUF:	BLOCK 3
OBUFR:	BLOCK 203
RENFIL: BLOCK 5
OUTFIL: BLOCK 4;	FILE TO WRITE TO
NOBLKS=1

WINBLK: BLOCK 1	;BLOCK NO CONTAIN IN WINDOW
WINDOW: BLOCK NOBLKS*200

TMPSTA: BLOCK 1	;0-READ,-1 MEANS WRITE
UPPNT:	BLOCK 1	;POINTER TO UPDATE AREA
UPCNT:BLOCK 1
UPDATE: BLOCK NOBLKS*200	;UPDATE AREA
SRCLST: BLOCK ^D50
ENDSRC: BLOCK 1
SSER:	BLOCK 1	;SAVE POINTER- USED IN ADRCAL
STSER:	BLOCK 1
CURTMP: BLOCK 1; CURLIN USED IN CALCULTEING-DURING ADRCAL
MODMAD: BLOCK 1 ;	STATUS OF DICTION
L1:	BLOCK 1
L2:	BLOCK 1
SEQNOS: BLOCK 1
FRSTTM: BLOCK 1
LINENO: BLOCK 1
EOF:	BLOCK 1
READLN: BLOCK 1
FLLIN: BLOCK 1
CELIN: BLOCK 1
FLRNG:BLOCK 1
CERNG:BLOCK 1
BUFNO:BLOCK 1
LASLIN: BLOCK 1	;LAST LINE IN DICTIONARY
PRECUR: BLOCK 1 ;CURLIN BEFORE ALTMODE HIT-OIY
CURLIN: BLOCK 1	;CURRENT LINE NO IN DICT
TMPLIN: BLOCK 1	; TEMP LINE NO (CURRENT LINE DURING CALCULATIONS
COMAN:	BLOCK 1	;ADRES TO GO TO(COMAND)
ENDCM: BLOCK 1
WORD:	BLOCK 3

NEWLIN: BLOCK ^D52

OLDLIN: BLOCK ^D52
RDLIN:	BLOCK ^D52

NCOUNT: BLOCK 1
OCOUNT: BLOCK 1
DUMDUM: BLOCK 1
DELIM:	BLOCK 1


MRKDIC: BLOCK 1
MARKNO: BLOCK 1	;MARK NO TO USE
BUFCOM: BLOCK	^D53
BUFLIN: BLOCK 1
BUFPT: BLOCK 1	;POINTER TO BUFFER IN USE
BUF0:	BLOCK ^D91	;BUFFER STORAGE
DICT:	BLOCK 1	;THIS AREA MUST BE LAST ALWAYS-IT EXPANDS AND CONTRACTS

IFN PURE,<RELOC>
	LIT	;DO LITERALS IN HIGH SEGMENT
	END START0
WrC