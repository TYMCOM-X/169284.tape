      SUBROUTINE BJT
C
        DOUBLE PRECISION MODNAM,MNAME,NAME
C
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      DIMENSION VBEO(1),VBCO(1),CJBE(1),CJBC(1),VCCS(1),CCCS(1)
      EQUIVALENCE (VBEO(1),TRACUR(1)),(VBCO(1),TRACUR(2)),
     1   (CJBE(1),TRACUR(3)),(CJBC(1),TRACUR(4)),(VCCS(1),TRACUR(5)),
     2   (CCCS(1),TRACUR(6))
C
C
      ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) RETURN
      ISPOT=ICURNT(7)
      DO 1000 I=ISTART,ISTOP
C
C  LOOKUP MATRIX POINTERS
C
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      LOC4=NODPLC(LOC+3)
      LOC5=NODPLC(LOC+4)
      LOC6=NODPLC(LOC+5)
      IF (MODE.EQ.2) GO TO 50
      LOC7=MATLOC(IFIND+1)
      LOC8=MATLOC(IFIND+2)
      LOC9=MATLOC(IFIND+3)
      LOC10=MATLOC(IFIND+4)
      LOC11=MATLOC(IFIND+5)
      LOC12=MATLOC(IFIND+6)
      LOC13=MATLOC(IFIND+7)
      LOC14=MATLOC(IFIND+8)
      LOC15=MATLOC(IFIND+9)
      LOC16=MATLOC(IFIND+10)
      LOC17=MATLOC(IFIND+11)
      LOC18=MATLOC(IFIND+12)
      IFIND=IFIND+12
C
C  DC ANALYSIS
C
   50 TYPE=SYMVAL(MNAM,1)
      GBPR=SYMVAL(MNAM,4)*VALUE(I)
      GCPR=SYMVAL(MNAM,5)*VALUE(I)
      GEPR=SYMVAL(MNAM,6)*VALUE(I)
      CSAT=SYMVAL(MNAM,12)*VALUE(I)
C
C  INITIALIZE JUNCTIONS ON FIRST DC PASS
C
      IF (IPASS1.EQ.0) GO TO 60
      IF (MODE.GT.1) GO TO 60
      VBE=VT*ALOG(VT/(1.414*CSAT))
      VBC=-1.0
      GO TO 70
   60 VBE=TYPE*(VNIM1(LOC5)-VNIM1(LOC6))
      VBC=TYPE*(VNIM1(LOC5)-VNIM1(LOC4))
      IF (IPASS1.EQ.1) GO TO 70
      CALL JUNCT(VBE,VBEO(ISPOT),CSAT,VT)
      CALL JUNCT(VBC,VBCO(ISPOT),CSAT,VT)
   70 VBEO(ISPOT)=VBE
      VBCO(ISPOT)=VBC
C
C  EBERS-MOLL TRANSISTOR MODEL
C
   80 IF (KIND(MNAM).EQ.2) GO TO 200
      BF=SYMVAL(MNAM,2)
      BR=SYMVAL(MNAM,3)
      OVA=SYMVAL(MNAM,15)
C
C  COMPUTE EQUIVALENT DC CONDUCTANCES
C
      IF (VBE.GT.0.0) GO TO 82
      EVBE=1.0
      CBE=CSAT*VBE/VT
      GO TO 84
   82 EVBE=EXP(VBE/VT)
      CBE=CSAT*(EVBE-1.0)
   84 IF (VBC.GT.0.0) GO TO 86
      EVBC=1.0
      CBC=CSAT*VBC/VT
      GO TO 88
   86 EVBC=EXP(VBC/VT)
      CBC=CSAT*(EVBC-1.0)
   88 FVA=1.0-OVA*VBC
      GSAT=CSAT/VT
      GPI=(GSAT/BF)*EVBE+GMIN
      GMU=(GSAT/BR)*EVBC+GMIN
      GO=(CBE-CBC)*OVA+GSAT*EVBC*FVA
      GM=GSAT*EVBE*FVA-GO
      GMPGO=GM+GO
      CC=(CBE-CBC)*FVA-CBC/BR-GMIN*VBC
      CB=CBE/BF+CBC/BR+GMIN*(VBE+VBC)
      CEQBC=TYPE*(CC-VBE*(GMPGO)+VBC*(GMU+GO))
      CEQBE=TYPE*(-CC-CB+VBE*GMPGO+VBE*GPI-VBC*GO)
      IF (MODE.EQ.1) GO TO 400
C
C  CHARGE STORAGE ELEMENTS
C
  110 CCS=SYMVAL(MNAM,7)*VALUE(I)
      TF=SYMVAL(MNAM,8)
      TR=SYMVAL(MNAM,9)
      CZBE=SYMVAL(MNAM,10)*VALUE(I)
      CZBC=SYMVAL(MNAM,11)*VALUE(I)
      PE=SYMVAL(MNAM,13)
      PC=SYMVAL(MNAM,14)
      IF (VBE.GE.0.0) GO TO 112
      SARG=SQRT(1.0-VBE/PE)
      CTBE=2.0*(TF*CBE+2.0*PE*CZBE*(1.0-SARG))/DELTA
      CAPBE=TF*CSAT/VT+CZBE/SARG
      GO TO 114
  112 CTBE=2.0*(TF*CBE+VBE*CZBE*(1.0+VBE/(4.0*PE)))/DELTA
      CAPBE=TF*CSAT*EVBE/VT+CZBE*(1.0+VBE/(2.0*PE))
  114 IF (VBC.GE.0.0) GO TO 116
      SARG=SQRT(1.0-VBC/PC)
      CTBC=2.0*(TR*CBC+2.0*PC*CZBC*(1.0-SARG))/DELTA
      CAPBC=TR*CSAT/VT+CZBC/SARG
      GO TO 320
  116 CTBC=2.0*(TR*CBC+VBC*CZBC*(1.0+VBC/(4.0*PC)))/DELTA
      CAPBC=TR*CSAT*EVBC/VT+CZBC*(1.0+VBC/(2.0*PC))
      GO TO 320
C
C  GUMMEL-POON TRANSISTOR MODEL
C
  200 C1=SYMVAL(MNAM,2)
      C3=SYMVAL(MNAM,3)
      OVA=SYMVAL(MNAM,13)
      OVB=SYMVAL(MNAM,14)
      C2=SYMVAL(MNAM,15)
      OIK=SYMVAL(MNAM,16)/VALUE(I)
      VTE=SYMVAL(MNAM,17)
      C4=SYMVAL(MNAM,18)
      OIKR=SYMVAL(MNAM,19)/VALUE(I)
      VTC=SYMVAL(MNAM,20)
      IF (VBE.GT.0.0) GO TO 202
      EVE=1.0
      EVEN=1.0
      CBE=CSAT*VBE/VT
      CBEN=CSAT*VBE/VTE
      GO TO 204
  202 EVE=EXP(VBE/VT)
      EVEN=EXP(VBE/VTE)
      CBE=CSAT*(EVE-1.0)
      CBEN=CSAT*(EVEN-1.0)
  204 IF (VBC.GT.0.0) GO TO 206
      EVC=1.0
      EVCN=1.0
      CBC=CSAT*VBC/VT
      CBCN=CSAT*VBC/VTC
      GO TO 208
  206 EVC=EXP(VBC/VT)
      EVCN=EXP(VBC/VTC)
      CBC=CSAT*(EVC-1.0)
      CBCN=CSAT*(EVCN-1.0)
C
C  DETERMINE BASE CHARGE TERMS
C
  208 Q1=1.0+OVA*VBC+OVB*VBE
      Q2=OIK*CBE+OIKR*CBC
      SQARG=SQRT(Q1*Q1+4.*Q2)
      QB=(Q1+SQARG)/2.
      DQBDVE=(OVB+(1.0/SQARG)*(Q1*OVB+2.0*OIK*CSAT*EVE/VT))/2.0
      DQBDVC=(OVA+(1.0/SQARG)*(Q1*OVA+2.0*OIKR*CSAT*EVC/VT))/2.0
      IF (QB.GT.1.0E-5) GO TO 210
      QB=1.E-5
      DQBDVE=0.
      DQBDVC=0.
C
C  DETERMINE DC INCREMENTAL CONDUCTANCES
C
  210 CC=(CBE-CBC)/QB-C3*CBC-C4*CBCN-GMIN*VBC
      CB=C1*CBE+C2*CBEN+C3*CBC+C4*CBCN+GMIN*(VBE+VBC)
      GPI=CSAT*((C1/VT)*EVE+(C2/VTE)*EVEN)+GMIN
      GMU=CSAT*((C3/VT)*EVC+(C4/VTC)*EVCN)+GMIN
      GO=(CSAT*EVC/VT+(CBE-CBC)*DQBDVC/QB)/QB
      GM=(CSAT*EVE/VT-(CBE-CBC)*DQBDVE/QB)/QB-GO
      GMPGO=GM+GO
      CEQBC=TYPE*(CC-VBE*(GMPGO)+VBC*(GMU+GO))
      CEQBE=TYPE*(-CC-CB+VBE*GMPGO+VBE*GPI-VBC*GO)
      IF (MODE.EQ.1) GO TO 400
C
C  CHARGE STORAGE ELEMENTS
C
  310 CCS=SYMVAL(MNAM,7)*VALUE(I)
      TF=SYMVAL(MNAM,8)
      TR=SYMVAL(MNAM,9)
      CZBE=SYMVAL(MNAM,10)*VALUE(I)
      CZBC=SYMVAL(MNAM,11)*VALUE(I)
      PE=SYMVAL(MNAM,21)
      XME=SYMVAL(MNAM,22)
      PC=SYMVAL(MNAM,23)
      XMC=SYMVAL(MNAM,24)
      IF (VBE.GE.0.0) GO TO 312
      ARG=1.0-VBE/PE
      SARG=EXP(XME*ALOG(ARG))
      CTBE=2.0*(TF*CBE+PE*CZBE*(1.0-ARG/SARG)/(1.0-XME))/DELTA
      CAPBE=TF*CSAT/VT+CZBE/SARG
      GO TO 314
  312 CTBE=2.0*(TF*CBE+VBE*CZBE*(1.0+XME*VBE/(2.0*PE)))/DELTA
      CAPBE=TF*CSAT*EVE/VT+CZBE*(1.0+XME*VBE/PE)
  314 IF (VBC.GE.0.0) GO TO 316
      ARG=1.0-VBC/PC
      SARG=EXP(XMC*ALOG(ARG))
      CTBC=2.0*(TR*CBC+PC*CZBC*(1.0-ARG/SARG)/(1.0-XMC))/DELTA
      CAPBC=TR*CSAT/VT+CZBC/SARG
      GO TO 320
  316 CTBC=2.0*(TR*CBC+VBC*CZBC*(1.0+XMC*VBC/(2.0*PC)))/DELTA
      CAPBC=TR*CSAT*EVC/VT+CZBC*(1.0+XMC*VBC/PC)
C
C  SMALL SIGNAL PARAMETERS
C
  320 IF (MODE.GT.2) GO TO 350
      TRACUR(ISPOT+2)=CC
      TRACUR(ISPOT+3)=CB
      TRACUR(ISPOT+4)=GPI
      TRACUR(ISPOT+5)=CAPBE
      TRACUR(ISPOT+6)=GMU
      TRACUR(ISPOT+7)=CAPBC
      TRACUR(ISPOT+8)=GM
      TRACUR(ISPOT+9)=GO
      GO TO 900
C
C  TRANSIENT ANALYSIS
C
  350 IF (IPASS1.EQ.0) GO TO 360
      CJBE(ISPOT)=CTBE
      CJBC(ISPOT)=CTBC
      VCCS(ISPOT)=VNIM1(LOC4)
      CCCS(ISPOT)=0.0
C
C  UPDATE ON FIRST ITERATION OF EACH TIMEPOINT
C
  360 IF (MODE.EQ.3) GO TO 370
      CJBE(ISPOT)=CTBE*(1.0+DELTA/DELOLD)-CJBE(ISPOT)
      CJBC(ISPOT)=CTBC*(1.0+DELTA/DELOLD)-CJBC(ISPOT)
      CCCS(ISPOT)=(2.0*CCS/DELOLD)*(VNIM1(LOC4)-VCCS(ISPOT))-CCCS(ISPOT)
      VCCS(ISPOT)=VNIM1(LOC4)
C
C  COMPUTE CAPACITOR EQUIVALENT CONDUCTANCES AND CURRENT SOURCES
C
  370 GTEMP=2.0*CAPBE/DELTA
      GPI=GPI+GTEMP
      CEQBE=CEQBE+TYPE*(GTEMP*VBE-CTBE+CJBE(ISPOT))
      GTEMP=2.0*CAPBC/DELTA
      GMU=GMU+GTEMP
      CEQBC=CEQBC+TYPE*(GTEMP*VBC-CTBC+CJBC(ISPOT))
      GCCS=2.0*CCS/DELTA
      CEQCS=GCCS*VCCS(ISPOT)+CCCS(ISPOT)
      GO TO 500
C
C  ZERO TRANSIENT CONDUCTANCES DURING DC ANALYSIS
C
  400 GCCS=0.0
      CEQCS=0.0
C
C  LOAD CURRENT EXCITATION VECTOR
C
  500 VN(LOC4)=VN(LOC4)+CEQCS-CEQBC
      VN(LOC5)=VN(LOC5)+CEQBC+CEQBE
      VN(LOC6)=VN(LOC6)-CEQBE
C
C  LOAD Y MATRIX, REAL TERMS
C
      YNL(LOC1)=YNL(LOC1)+GCPR
      YNL(LOC2)=YNL(LOC2)+GBPR
      YNL(LOC3)=YNL(LOC3)+GEPR
      YNL(LOC4)=YNL(LOC4)+GMU+GO+GCPR+GCCS
      YNL(LOC5)=YNL(LOC5)+GBPR+GPI+GMU
      YNL(LOC6)=YNL(LOC6)+GPI+GEPR+GMPGO
      YNL(LOC7)=YNL(LOC7)-GCPR
      YNL(LOC8)=YNL(LOC8)-GBPR
      YNL(LOC9)=YNL(LOC9)-GEPR
      YNL(LOC10)=YNL(LOC10)-GCPR
      YNL(LOC11)=YNL(LOC11)-GMU+GM
      YNL(LOC12)=YNL(LOC12)-GMPGO
      YNL(LOC13)=YNL(LOC13)-GBPR
      YNL(LOC14)=YNL(LOC14)-GMU
      YNL(LOC15)=YNL(LOC15)-GPI
      YNL(LOC16)=YNL(LOC16)-GEPR
      YNL(LOC17)=YNL(LOC17)-GO
      YNL(LOC18)=YNL(LOC18)-GPI-GM
  900 ISPOT=ISPOT+15
 1000 CONTINUE
      RETURN
      END
