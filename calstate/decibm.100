IDENTIFICATION DIVISION.
PROGRAM-ID. DECIBM.
AUTHOR. TYMSHARE.
DATE-WRITTEN. JAN 1973.
REMARKS.   DERIVED FROM        DECB35.CBL PROGRAM;
	    REQUIRES ASSEMBLY-CODE FILE "ASCODE"
            CONTAINING ENTRY POINTS:
	          CONVRT,
	          GETKAR,
	          PUTWRD,
                  COBAID.
 

         
ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
SOURCE-COMPUTER. PDP-10.
OBJECT-COMPUTER. PDP-10.
SPECIAL-NAMES. CHANNEL (1) IS TOP-OF-FORM,
            CONSOLE IS TTY.

INPUT-OUTPUT SECTION.
FILE-CONTROL.
	SELECT SOURCE-FILE ASSIGN TO DSK.
	SELECT OUTPUT-FILE ASSIGN TO DSK.
	SELECT LISTING-FILE ASSIGN TO DSK.
DATA DIVISION.
FILE SECTION.

FD SOURCE-FILE
	LABEL RECORDS ARE STANDARD;
	VALUE OF IDENTIFICATION IS SOURCE-ID
	DATA RECORD IS SOURCE-RECORD.

01	SOURCE-RECORD; DISPLAY-7.
	02 COLS-7-80.
	  03 COLS-7-71		PICTURE X(65).
	  03 FILLER		PICTURE X(9).
	02 DUMMY REDEFINES COLS-7-80.
	  03 THIS-CHARACTER	PICTURE X.
	  03 COLS-8-72		PICTURE X(65).
	  03 COLS-73-80		PICTURE X(8).

FD OUTPUT-FILE
	LABEL RECORDS ARE STANDARD
	VALUE OF IDENTIFICATION IS OUTPUT-ID;
	DATA RECORDS ARE SHORTEST-LINE, NEXT-SHORTEST-LINE,
		NEXT-LONGEST-LINE, LONGEST-LINE.

01	NEXT-LONGEST-LINE	PICTURE X(82); DISPLAY-7.
01	NEXT-SHORTEST-LINE	PICTURE X(74); DISPLAY-7.
01	SHORTEST-LINE		PICTURE X(66); DISPLAY-7.
01	LONGEST-LINE; DISPLAY-7.
	02 LL-BREAKDOWN.
          03  ONE-FOUR          PICTURE X(4).
          03  COL-FIVE          PICTURE X.
	  03 FILLER		PICTURE X(61).
	  03 COLS-73-102.
	    04 FILLER		PICTURE X(8).
	    04 COLS-81-102.
	      05 FILLER		PICTURE X(8).
	      05 COLS-89-102	PICTURE X(14).
	02 LL-OCCURS REDEFINES LL-BREAKDOWN.
	  03 OUTPUT-CHARACTER OCCURS 96 TIMES; PICTURE X.

FD LISTING-FILE
	LABEL RECORDS ARE STANDARD;
	VALUE OF IDENTIFICATION IS LISTING-ID 
           .

01	LIST-OUTPUT; DISPLAY-7.
	02 LISTING-FLAG-OUT 	PICTURE XXX.
	02 FILLER		PICTURE XXX.
	02 LIST-SOURCE		PICTURE X(96).
01      L-O-2; DISPLAY-7.
        02  LO2         PICTURE X(90).
        02  LO2B        PICTURE X(12).
01      L-O-3; DISPLAY-7.
        02  LO3         PICTURE X(85).
        02  LO3B        PICTURE X(17).
01      L-O-4; DISPLAY-7.
        02  LO4         PICTURE X(80).
        02  LO4B        PICTURE X(22).
01      L-O-5; DISPLAY-7.
        02  LO5         PICTURE X(75).
        02  LO5B        PICTURE X(27).
01      L-O-6; DISPLAY-7.
        02  LO6         PICTURE X(70).
        02  LO6B        PICTURE X(32).
01      L-O-7; DISPLAY-7.
        02  LO7         PICTURE X(65).
        02  LO7B        PICTURE X(37).
01      L-O-8; DISPLAY-7.
        02  LO8         PICTURE X(69).
        02  LO8B        PICTURE X(42).
01      L-O-9; DISPLAY-7.
        02  LO9         PICTURE X(55).
        02  LO9B        PICTURE X(47).
01      L-O-10; DISPLAY-7.
        02  LO10        PICTURE X(50).
        02  LO10B       PICTURE X(52).
01      L-O-11; DISPLAY-7.
        02  LO11        PICTURE X(45).
        02  LO11B       PICTURE X(57).
01      L-O-12; DISPLAY-7.
        02  LO12        PICTURE X(40).
        02  LO12B       PICTURE X(62).
01      L-O-13; DISPLAY-7.
        02  LO13        PICTURE X(35).
        02  LO13B       PICTURE X(67).
01      L-O-14; DISPLAY-7.
        02  LO14        PICTURE X(30).
        02  LO14B       PICTURE X(72).
01      L-O-15; DISPLAY-7.
        02  LO15        PICTURE X(25).
        02 LO15B        PICTURE X(77).
01      L-O-16; DISPLAY-7.
        02  LO16        PICTURE X(20).
        02  LO16B       PICTURE X(82).
01      L-O-17; DISPLAY-7.
        02  LO17        PICTURE X(15).
        02  LO17B       PICTURE X(87).
01      L-O-18; DISPLAY-7.
        02  LO18        PICTURE X(10).
        02  LO18B       PICTURE X(92).
01      L-O-19; DISPLAY-7.
        02  LO19        PICTURE X(5).
        02  LO19B       PICTURE X(97).
WORKING-STORAGE SECTION.

01	MISCELLANEOUS-STUFF.
	02 NEXT-CHARACTER	PICTURE X.
	02 CCOLUMN		PICTURE S99; COMP.
	02 FIRST-COLUMN		PICTURE S99; COMP.
	02 I			PICTURE S99; COMP.
	02 J			PICTURE S99; COMP.
	02 TEMP-2		PICTURE S99; COMP.
	02 HOLD-CHARACTER	PICTURE X.
	02 LAST-CHARACTER	PICTURE X.
        02 HOLD-PUNCTUATION     PICTURE X.
        02 LINE-NUM             PICTURE 9(6).
        02 FLAGS-ONLY-LIST      PICTURE X.
          88 FLAGS-ONLY   VALUE "X".
        02 EDIT-NUM-FLAG        PICTURE X.
          88 EDIT-NUMS  VALUE "X".
        02 TTY-LISTING          PICTURE X.
          88 AT-TTY  VALUE "X".
        02 CONV-OUTPUT          PICTURE X.
          88 CONV-OUT  VALUE "X".
        02 FLAG-COLLAT-TEST PICTURE X.
          88 FLAG-COLLATE VALUE "X".
        02 FLAG-COMPARE-TEST PICTURE X.
          88 FLAG-COMPARE VALUE "X".

	02 EOL-FLAG		PICTURE X.
	  88 END-OF-LINE VALUE "X".
	02 EOF-FLAG		PICTURE X.
	  88 END-OF-SOURCE VALUE "X".
	02 PUNCTUATION-CHARACTER	PICTURE X.
	  88 PUNCTUATION VALUE ".", ";", ",".
          88 PERIOD-THERE  VALUE ".".
	02 SKIP-FLAG		PICTURE X.
	  88 SKIPPING-STUFF VALUE "X".
	02 DELETION-FLAG	PICTURE X.
	  88 DELETING-STUFF VALUE "X".
	02 REMARKS-FLAG		PICTURE X.
	  88 WITHIN-REMARKS	VALUE "X".
        02 LABEL-DONE           PICTURE X.
          88 LABEL-CLAUSE-DONE  VALUE "X".
        02 IDENT-DONE           PICTURE X.
          88 IDENT-CLAUSE-DONE  VALUE "X".
01      JOB-COUNT               PICTURE S99 ; COMP.
01      SELECT-FILE-NAME        PICTURE X(36).
01      LISTING-FLAG    PICTURE XXX.
01      EDIT-NUMBER-COUNT       PICTURE 99999.

01	PROGRAM-NAME.
	02 PN-CHAR PICTURE X OCCURS 9 TIMES.
01	TEMPORARY-NAME.
	02 TN-CHAR PICTURE X OCCURS 10 TIMES.

*****************************************************************
*CONVERSION TABLE FOR 029 CARD MODE TO RCA CHARACTERS

01	CONVERSION-TABLE-029-RCA; DISPLAY-7.
	02 FILLER       PICTURE X(2); VALUE " !".     
	02 FILLER	PICTURE X;    VALUE QUOTE.
	02 FILLER	PICTURE X(13); VALUE "#$%&'()*+,-./".
	02 FILLER	PICTURE X(16); VALUE "0123456789:;<=>?".
	02 FILLER	PICTURE X(16); VALUE "@ABCDEFGHIJKLMNO".
	02 FILLER	PICTURE X(16); VALUE "PQRSTUVWXYZ[\]^_".

01	TO-HOLD-A-WORD		PICTURE X(70).
        88 COLLATE-TEST-FLAG    VALUE "COLLATE TESTS","FLAG COLLATE",
                                      "FLAG COLLATE TESTS","COLLATE",
                                      "COLLAT","COLLA","COLL","COL".
        88 COMPARE-TEST-FLAG    VALUE "COMPARE TESTS","FLAG COMPARE",
                                      "FLAG COMPARE TESTS","COMPARE",
                                      "FLAG COMPARISONS","COMPAR",
                                      "COMPA","COMP","COM","COMPARI",
                                      "COMPARISONS","COMPARISON",
                                      "COMPARISO","COMPARIS".
        88 NUMBERED             VALUE "NUMBERED","NUMBERE","NU",
                                      "NUMBER","NUMBE","NUMB","N",
                                      "NUM","NUMBERS","EDIT NUMBERS",
                                      "LINE NUMBERS".
        88 LIST-FLAGS-ONLY      VALUE "LIST FLAGS ONLY",
                                      "FLAGS ONLY","LIST FLAGS",
                                      "FLAGS","FLAG","FLA","FL","F".
        88 LIST-AT-TTY          VALUE "LIST AT TTY",
                                      "LIST TTY","AT TTY","TTY","TT",
                                      "TERMINAL","TERMINA","TERMIN",
                                      "TERMI","TERM","TER","TE","T".
        88 OUTPUT-CONV          VALUE "CONVENTIONAL OUTPUT","CONVENTIONAL",
                                      "CONVENTIONA","CONVENTION",
                                      "CONVENTIO","CONVENTI","CONVENT",
                                      "CONVEN","CONVE","CONV","CON".
        88 ENDING-WORD          VALUE "E","EN","END".
        88 QUITING              VALUE "Q","QU","QUI","QUIT",
                                      "STOP","EXIT","END","E","EN".
        88 NEEDS-HELP           VALUE "?","H","HE","HEL","HELP","/H".
        88 RUNNING              VALUE "R","RU","RUN","GO","START".
        88 INSTRUCTIONS         VALUE "I","IN","INS","INST","INSTR",
                                      "INSTRU","INSTRUC","INSTRUCT",
                                      "INSTRUCTI","INSTRUCTIO",
                                      "INSTRUCTION","INSTRUCTIONS".
        88 RETURN-ONLY          VALUE SPACES.
	88 W-ASSIGN		VALUE "ASSIGN".
	88 W-SPECIAL-NAMES	VALUE "SPECIAL-NAMES".
	88 W-IDENTIFICATION	VALUE "IDENTIFICATION","ID".
	88 W-PROGRAM-ID		VALUE "PROGRAM-ID".
	88 W-SELECT		VALUE "SELECT".
	88 W-ENVIRONMENT	VALUE "ENVIRONMENT".
	88 W-SOURCE-COMPUTER	VALUE "SOURCE-COMPUTER".
	88 W-OBJECT-COMPUTER	VALUE "OBJECT-COMPUTER".
	88 W-I-O-CONTROL	VALUE "I-O-CONTROL".
	88 W-DATA		VALUE "DATA".
	88 W-PROCEDURE		VALUE "PROCEDURE".
	88 W-APPLY		VALUE "APPLY".
	88 W-OMITTED		VALUE "OMITTED".
	88 W-COMP-1        	VALUE "COMP-1".
        88 W-DISPLAY-X		 VALUE "DISPLAY-6", "DISPLAY-7".
        88 FLAG-3-CLAUSE        VALUE "SKIP1","SKIP2","SKIP3","DAY","DATE",
                                "TIME","FIXED","SYMBOL","PRINT","POSITION",
                                "END-OF-PAGE","EOP","CURRENT-DATE",
                                "TIME-OF-DAY","USER-NUMBER","DISPLAY-ST",
                                "EXHIBIT","NAMED","CHANGED","SIZE",
                                "SIGNED","POINT","SYSIN","SYSOUT",
                                "COUNT","SYSPUNCH".
	88 FLAG-CLAUSE		VALUE  "BLOCK", "CONTAINS","COMP-2","COMP-3",
                                      "CHARACTERS", "COPY","CLASS","COMP-4",
		              "DATE-WRITTEN","INCLUDE","LEADING","ID",
                                "IDENTIFICATION","LABEL".
	88 W-ZERO		VALUE "0", "ZERO".
	88 W-NOTE		VALUE "NOTE".
        88 ENV-WORDS-FLAGGED-3  VALUE "PARITY","BINARY","CHANNEL",
                                "DENSITY","DATE".
        88 ENV-WORDS-FLAGGED    VALUE "RERUN,","REEL","MULTIPLE",
                                     "POSITION","CONSOLE","SYSIN",
                                "SYSOUT","SYSPUNCH".
        88 PD-WORDS-FLAGGED-3   VALUE "DISPLAY-ST","TIME-OF-DAY",
                                "EXHIBIT","COMP-3","COMP-2","COMP-4",
                                "SIZE","SIGNED","POINT","FIXED","COUNT",
                                "SYMBOL","PRINT","POSITION","DISP",
                                "END-OF-PAGE","EOP","SKIP1","SKIP2",
                                "SKIP3","DATE","DAY","TIME".
	88 PD-WORDS-FLAGGED	VALUE "ENTER", "TRANSFORM", "CALL",
                                      "REWRITE", "DELETE","CHANNEL",
					"SWITCH","USER-NUMBER","UPON",
                                "INCLUDE",
                                        "NEXT-PAGE","NAMED","CHANGED",
					"COPY","POSITIONING","TOP-OF-PAGE".
	88 W-TODAY      	VALUE "TODAY".
	88 W-TTY    		VALUE "TTY".
        88 W-TO                 VALUE "TO".
	88 W-REMARKS		VALUE "REMARKS".
	88 W-FD			VALUE "FD".
        88 W-LABEL              VALUE "LABEL".
        88 COLLATE-TEST         VALUE ">","<","GREATER","LESS".
        88 COMPARE-TEST         VALUE "=","EQUAL","EQUALS".
        88 W-FD-FLAG-WORD       VALUE "DATE-WRITTEN","BLOCK",
                                      "USER-NUMBER","CONTAINS",
                                      "CHARACTERS","COPY".
        88 W-RECORD             VALUE "RECORD","RECORDS".
        88 W-IS-ARE             VALUE "IS","ARE".
        88 W-IS                 VALUE "IS".
        88 W-VALID-TYPE         VALUE "OMITTED","STANDARD","   ",
                                      "USASI","NON-STANDARD".
        88 W-FD-KEY-WORD        VALUE "BLOCK","RECORD","LABEL",
                                      "VALUE","DATE-WRITTEN","DATA",
                                      "USER-NUMBER","REPORT","REPORTS".


01	THIS-WORD REDEFINES TO-HOLD-A-WORD.
	02 WORD-CHARACTER OCCURS 70 TIMES; PICTURE X.

01      SAVE-THIS-WORD          PICTURE X(70).

01	SOURCE-ID.
	02 SOURCE-ID-NAME       PICTURE X(6).
	02 SOURCE-ID-EXT	PICTURE XXX.

01	OUTPUT-ID.
	02 OUTPUT-ID-NAME	PICTURE X(6).
	02 OUTPUT-ID-EXT	PICTURE XXX.

 01     LISTING-ID.
        02   LISTING-ID-NAME     PICTURE X(6).
        02   LISTING-ID-EXT      PICTURE XXX.

01	PROGRAM-NAME-PERIOD.
	02 PROGRAM-NAME-WORK		PICTURE X(6).
	02 FILLER		PICTURE X	VALUE ".".
01      LINE-COUNT-REC.
        02 FILLER               PICTURE X(6)
          VALUE  "LINE  ".
        02 EDIT-NUMBER          PICTURE ZZZZ9.


01      NEW-IDENT-CLAUSE.
        02 FILLER               PICTURE X(32)
            VALUE "     VALUE OF IDENTIFICATION IS ".
        02 IDENT-CLAUSE-ID      PICTURE X(36)
            VALUE SPACES.

01      NEW-LABEL-CLAUSE.
        02 FILLER               PICTURE X(23)
            VALUE "     LABEL RECORDS ARE ".
        02 LABEL-CLAUSE-TYPE    PICTURE X(36)
            VALUE SPACES.
01      CONV-REC; DISPLAY-7.
        02 C-R-SEQ      PICTURE 9(6).
        02 C-R-CONT-REM PICTURE X(96).
        02 C-R-REG REDEFINES C-R-CONT-REM.
          03 C-R-COL7           PICTURE X.
          03 C-R-REG-SOURCE     PICTURE X(95).
        02 C-R-OUT  REDEFINES C-R-REG.
          03 FILLER     PICTURE X(66).
          03 C-R-COL-73-ON.
          04 C-R-ID     PICTURE X(8).
          04 FILLER     PICTURE X(22).
01      PERIOD-LINE.
        02 FILLER       PICTURE X(10)  VALUE SPACES.
        02 FILLER       PICTURE X      VALUE ".".


PROCEDURE DIVISION.

MAIN SECTION.

START.    DISPLAY "DEC TO IBM     COBOL PROGRAM CONVERSION AID".
        DISPLAY "DEVELOPMENT VERSION.".
	MOVE ZERO TO JOB-COUNT.

 


RESTART.
        MOVE LOW-VALUES TO MISCELLANEOUS-STUFF.
        MOVE ZERO TO EDIT-NUMBER-COUNT.
        MOVE SPACES TO SOURCE-ID, OUTPUT-ID.
        MOVE SPACES TO CONV-REC.

FETCH-COMMAND.
        DISPLAY ":" WITH NO ADVANCING.
        ACCEPT THIS-WORD.
        IF RETURN-ONLY GO TO FETCH-COMMAND.
        IF COLLATE-TEST-FLAG  MOVE "X" TO FLAG-COLLAT-TEST,
            GO TO FETCH-COMMAND.
        IF NUMBERED  MOVE "X" TO EDIT-NUM-FLAG,
            GO TO FETCH-COMMAND.
        IF COMPARE-TEST-FLAG  MOVE "X" TO FLAG-COMPARE-TEST,
            MOVE "X" TO FLAG-COLLAT-TEST,
            GO TO FETCH-COMMAND.
        IF ENDING-WORD ENTER MACRO COBAID.
        IF QUITING  STOP RUN.
        IF LIST-FLAGS-ONLY  MOVE "X" TO FLAGS-ONLY-LIST,
            GO TO FETCH-COMMAND.
        IF LIST-AT-TTY  MOVE "X" TO TTY-LISTING,
            GO TO FETCH-COMMAND.
        IF OUTPUT-CONV  MOVE "X" TO CONV-OUTPUT,
            GO TO FETCH-COMMAND.
        IF RUNNING GO TO RESTART-1.
        IF NEEDS-HELP
          DISPLAY "LEGAL COMMANDS:",
          DISPLAY ">",
          DISPLAY ">  HELP  (OR)  ?     REPRINTS THIS LIST",
          DISPLAY ">  TERMINAL          LISTING-FILE AT TERMINAL",
          DISPLAY ">  FLAGS             FLAGGED LINES ONLY IN LISTING",
          DISPLAY ">  NUMBERED          EDITING NUMBERS FOR FLAGGED LINES",
          DISPLAY ">  CONVENTIONAL      SEQUENCE NUMBERS AND ID ADDED",
          DISPLAY ">  COLLATE           FLAG TESTS OF  >  <",
          DISPLAY ">  COMPARE           FLAG TESTS OF  >  <  =",
          DISPLAY ">  RUN               BEGINS EXECUTION",
          DISPLAY ">  QUIT              QUITS TO EXEC",
          DISPLAY ">  INSTRUCTIONS      HOW TO EXECUTE THE PROGRAM",
          DISPLAY ">",
          DISPLAY"ANY OF THESE COMMANDS MAY BE SHORTENED.",
          GO TO FETCH-COMMAND.
        IF INSTRUCTIONS
          DISPLAY "INSTRUCTIONS:",
          DISPLAY "  INPUT:",
          DISPLAY "     COBOL SOURCE PROGRAM IN PDP-10 STANDARD",
          DISPLAY "     FORMAT, USING DOUBLE-QUOTES FOR THE",
          DISPLAY "     STRING DELIMITER.",
          DISPLAY "  OUTPUT:",
          DISPLAY "     1)  NEW SOURCE FILE WITH MODIFICATIONS;",
          DISPLAY "       THE NAME OF THE NEW FILE IS ACCEPTED FROM",
          DISPLAY "       THE TERMINAL. IF THE EXTENSION IS BLANK, ",
          DISPLAY "       " QUOTE ".CBL" QUOTE "IS SUBSTITUTED.",
          DISPLAY "       IF THE USER TYPES A SPACE AND A CARRIAGE RETURN,",
          DISPLAY "       THE NAME OF THE FILE WILL BE THE SAME AS",
          DISPLAY "       THE INPUT FILE BUT WITH THE  .CBL  EXTENSION.",
          DISPLAY"     2)  A LISTING FILE SHOWING THE SOURCE FILE",
          DISPLAY "       WITH FLAGS OF POSSIBLE PROBLEM AREAS.",
          DISPLAY "       THE NAME OF THIS FILE IS THE SAME AS",
          DISPLAY "       THE OUTPUT FILE BUT WITH THE .LST  EXTENSION.",
          DISPLAY "       FLAGGING OCCURS AT 3 LEVELS: DENOTED BY *1*,"
          DISPLAY "       *2*, *3*; SEE COBAID MANUAL FOR DETAILED EXPLANATION."
          DISPLAY " ",
          GO TO FETCH-COMMAND.
        DISPLAY "FOR ASSISTANCE, TYPE HELP.".
        GO TO FETCH-COMMAND.

RESTART-1.
        DISPLAY "DEC-FORMAT FILE NAME: " WITH NO ADVANCING.
        ACCEPT TEMPORARY-NAME.
	MOVE 0 TO I; MOVE SPACES TO PROGRAM-NAME.
	PERFORM CONVERT-ID VARYING J FROM 1 BY 1 UNTIL J > 10.
	IF I > 9 DISPLAY "?NAME TOO LONG"; GO TO RESTART-1.
	IF PROGRAM-NAME = "QUIT" OR  PROGRAM-NAME = "EXIT"   STOP RUN.

        MOVE PROGRAM-NAME TO SOURCE-ID.
        
 RESTART-2.
        DISPLAY "IBM-FORMAT OUTPUT FILE NAME: " WITH NO ADVANCING.
        ACCEPT TEMPORARY-NAME.
        MOVE 0 TO I; MOVE SPACES TO PROGRAM-NAME.
        PERFORM CONVERT-ID VARYING J FROM 1 BY 1 UNTIL J > 10.
        IF I > 9 DISPLAY "?NAME TOO LONG"; GO TO RESTART-2.
        IF PROGRAM-NAME IS EQUAL TO SPACES MOVE 
            SOURCE-ID-NAME TO OUTPUT-ID-NAME
        ELSE MOVE PROGRAM-NAME TO OUTPUT-ID.
        IF OUTPUT-ID-EXT IS EQUAL TO SPACES MOVE "CBL" TO 
            OUTPUT-ID-EXT.
        MOVE OUTPUT-ID-NAME TO LISTING-ID-NAME.
        MOVE OUTPUT-ID-NAME TO PROGRAM-NAME-WORK.
        MOVE "LST" TO LISTING-ID-EXT.
         

	OPEN INPUT SOURCE-FILE; OPEN OUTPUT OUTPUT-FILE.
        OPEN OUTPUT LISTING-FILE;

	MOVE SPACES TO LONGEST-LINE, LIST-OUTPUT.
	MOVE SPACE TO NEXT-CHARACTER.
	READ SOURCE-FILE; AT END DISPLAY "?SOURCE FILE HAS NO DATA";
			GO TO ALL-DONE-1.

	MOVE 1 TO CCOLUMN. MOVE 10 TO LINE-NUM.
	PERFORM CONVERSION TC-EXIT.
	MOVE THIS-CHARACTER TO NEXT-CHARACTER.
	MOVE 1 TO J.


******************************************************************
*	PROCESS THE IDENTIFICATION DIVISION
******************************************************************

PROCESS-IDENTIFICATION.
	PERFORM GET-WORD; IF END-OF-SOURCE
		DISPLAY "?NO IDENTIFICATION DIVISION"; GO TO ALL-DONE.
	IF NOT W-IDENTIFICATION GO TO PROCESS-IDENTIFICATION;
        ELSE IF FIRST-COLUMN > 5
                 DISPLAY "CHECK THAT INPUT FILE IS DEC STANDARD FORMAT",
                 GO TO ALL-DONE;
            ELSE DISPLAY "---IDENT.".
	PERFORM PUT-WORD.
	PERFORM GET-WORD; IF END-OF-SOURCE DISPLAY "?NO PROGRAM   ";
		GO TO ALL-DONE.

PROCESS-ID-1.
	IF W-REMARKS MOVE "X" TO REMARKS-FLAG.
        IF W-ENVIRONMENT
          DISPLAY "---ENVIR.",
          MOVE SPACE TO REMARKS-FLAG,
          MOVE 1 TO FIRST-COLUMN,
          PERFORM PUT-WORD;
        ELSE PERFORM PUT-WORD
             PERFORM GET-WORD,
             IF END-OF-SOURCE
               DISPLAY "?NO ENVIRONMENT DIVISION",
               GO TO ALL-DONE;
             ELSE GO TO PROCESS-ID-1.
******************************************************************
*	PROCESS ENVIRONMENT DIVISION
******************************************************************

PROCESS-ENVIRONMENT SECTION 01.
PROCESS-ENV-0.
	PERFORM GET-WORD; IF END-OF-SOURCE GO TO PROCESS-ENV-9.
PROCESS-ENV-1.
	IF W-SOURCE-COMPUTER GO TO PROCESS-SOURCE-COMPUTER.
	IF W-OBJECT-COMPUTER GO TO PROCESS-OBJECT-COMPUTER.
	IF W-SPECIAL-NAMES GO TO PROCESS-SPECIAL-NAMES.
        IF W-SELECT GO TO PROCESS-SELECT.
	IF W-ASSIGN GO TO PROCESS-ASSIGN.

PROCESS-ENV-2.
        IF ENV-WORDS-FLAGGED  MOVE "*2*" TO LISTING-FLAG.
        IF ENV-WORDS-FLAGGED-3  MOVE "*3*" TO LISTING-FLAG.
        IF W-DATA PERFORM PUT-WORD; DISPLAY "---DATA.";
            GO TO PROCESS-DATA-DIVISION.
	PERFORM PUT-WORD.
	GO TO PROCESS-ENVIRONMENT.

 
*       SELECT.  ALLOW USER TO CHOOSE DEVICE.
 
PROCESS-SELECT.
        PERFORM PUT-WORD.
        PERFORM GET-WORD.
        MOVE THIS-WORD TO SELECT-FILE-NAME.
        PERFORM PUT-WORD.
        PERFORM GET-WORD.
        IF NOT W-ASSIGN GO TO PROCESS-ENV-2.
        PERFORM PUT-WORD.
        PERFORM GET-WORD.
        MOVE PUNCTUATION-CHARACTER TO HOLD-PUNCTUATION.
        IF W-TO PERFORM GET-WORD.
        MOVE SPACE TO PUNCTUATION-CHARACTER.
        MOVE "TO" TO THIS-WORD, PERFORM PUT-WORD.
PROCESS-SELECT-2.
        DISPLAY "SELECT " SELECT-FILE-NAME.
        DISPLAY "ASSIGN TO  " WITH NO ADVANCING.
        ACCEPT THIS-WORD.
        MOVE "."              TO PUNCTUATION-CHARACTER.
        PERFORM PUT-WORD.
        MOVE "*1*" TO LISTING-FLAG.
        PERFORM WRITE-LINE.  MOVE "*2*" TO LISTING-FLAG.
        GO TO PROCESS-ASSIGN-2.
 
*	ASSIGN TO DISK: REPLACES ALL OTHER ASSIGNS.


PROCESS-ASSIGN.
	PERFORM PUT-WORD.
	MOVE "DISK." TO THIS-WORD.
	PERFORM PUT-WORD.
        MOVE "*3*" TO LISTING-FLAG.
	PERFORM WRITE-LINE.  MOVE "*2*" TO LISTING-FLAG.
PROCESS-ASSIGN-2.
	PERFORM GET-WORD; IF END-OF-SOURCE GO TO PROCESS-ENV-9.
        MOVE "*2*" TO LISTING-FLAG.
	IF FIRST-COLUMN IS LESS THAN 6
	MOVE SPACES TO LISTING-FLAG; GO TO PROCESS-ENV-1.
	IF W-SELECT MOVE SPACES TO LISTING-FLAG
           GO TO PROCESS-SELECT.
        IF PERIOD-THERE
            PERFORM PUT-WORD,
                GO TO PROCESS-ENV-0.
        PERFORM PUT-WORD.
	GO TO PROCESS-ASSIGN-2.

PROCESS-ENV-9.
	DISPLAY "?NO DATA DIVISION"; GO TO ALL-DONE.

*	SOURCE-COMPUTER. REPLACE PARAGRAPH WITH <S/360.>

PROCESS-SOURCE-COMPUTER.
	PERFORM PUT-WORD.
        MOVE "*2*" TO LISTING-FLAG.
	MOVE "S/360. " TO THIS-WORD; PERFORM PUT-WORD.
PROCESS-SC-0. PERFORM WRITE-LINE; MOVE "X" TO SKIP-FLAG.
PROCESS-SC-1. PERFORM GET-WORD; IF END-OF-SOURCE GO TO PROCESS-ENV-9.
	IF FIRST-COLUMN IS LESS THAN 6
		MOVE SPACE TO SKIP-FLAG; GO TO PROCESS-ENV-1.
	GO TO PROCESS-SC-1.

*	OBJECT-COMPUTER. REPLACE PARAGRAPH WITH <S/360.  SEGMENT-LIMIT IS 01.>

PROCESS-OBJECT-COMPUTER.
	PERFORM PUT-WORD.
        MOVE "*2*" TO LISTING-FLAG.
	MOVE "S/360 " TO THIS-WORD; PERFORM PUT-WORD.
	MOVE "SEGMENT-LIMIT" TO THIS-WORD; PERFORM PUT-WORD.
	MOVE "IS" TO THIS-WORD; PERFORM PUT-WORD.
	MOVE "01." TO THIS-WORD; PERFORM PUT-WORD.
	GO TO PROCESS-SC-0.
 
*	SPECIAL-NAMES.  FLAG   ANY PREVIOUS SPECIAL-NAMES PARAGRAPH.

PROCESS-SPECIAL-NAMES.
	MOVE "*2*" TO LISTING-FLAG.
	PERFORM PUT-WORD.
PROCESS-SN-0.
	PERFORM GET-WORD; IF END-OF-SOURCE GO TO PROCESS-ENV-9.
	IF FIRST-COLUMN IS LESS THAN 6 
	       MOVE SPACES TO LISTING-FLAG; GO TO PROCESS-ENV-1.
	PERFORM PUT-WORD; MOVE "*2*" TO LISTING-FLAG.
	GO TO PROCESS-SN-0.


*****************************************************************
*	PROCESS DATA DIVISION
*******************************************************************

PROCESS-DATA-DIVISION SECTION 02.

PROCESS-DD-0.
	PERFORM GET-WORD; IF END-OF-SOURCE GO TO PROCESS-DD-9.
        IF FLAG-CLAUSE GO TO PUT-OUT-FLAG.
        IF FLAG-3-CLAUSE    MOVE "*3*" TO LISTING-FLAG,
                            GO TO PROCESS-DD-1.
	IF W-COMP-1 GO TO PROCESS-COMP-1.
	IF W-DISPLAY-X GO TO PROCESS-DISPLAY-X.
        NOTE
	IF W-FD GO TO PROCESS-FD.

PROCESS-DD-1.
        IF W-PROCEDURE PERFORM PUT-WORD; DISPLAY "---PROC.";
            GO TO PROCESS-PROCEDURE.
	PERFORM PUT-WORD.
	GO TO PROCESS-DD-0.

PROCESS-DD-9.
	DISPLAY "?NO PROCEDURE DIVISION"; GO TO ALL-DONE.


PUT-OUT-FLAG.
	MOVE "*2*" TO LISTING-FLAG; GO TO PROCESS-DD-1.

*	DISPLAY-6, -7.  CHANGE TO DISPLAY AND FLAG.

PROCESS-DISPLAY-X.
	MOVE "*1*" TO LISTING-FLAG.
	MOVE "DISPLAY" TO THIS-WORD.
	PERFORM PUT-WORD.
	GO TO PROCESS-DD-0.
 
*	COMP-1.     CHANGE TO COMPUTATIONAL AND FLAG.
 
PROCESS-COMP-1.
	MOVE "*1*" TO LISTING-FLAG.
	MOVE "COMPUTATIONAL" TO THIS-WORD.
	PERFORM PUT-WORD.
	GO TO PROCESS-DD-0.

*	AFTER FD CLAUSE INSERT VALUE OF IDENTIFICATION.


PROCESS-FD.
        NOTE
	PERFORM PUT-WORD.
        MOVE "*2*" TO LISTING-FLAG.
	PERFORM GET-WORD.
        DISPLAY "FILE= " THIS-WORD.
	PERFORM PUT-WORD.
	PERFORM WRITE-LINE.
        MOVE SPACES TO IDENT-DONE, LABEL-DONE,
                       IDENT-CLAUSE-ID, LABEL-CLAUSE-TYPE.
PROCESS-FD-1.
        NOTE
        PERFORM GET-WORD; IF END-OF-SOURCE GO TO PROCESS-DD-9.
PROCESS-FD-2.
        NOTE
        IF FIRST-COLUMN LESS THAN 6
           GO TO PROCESS-FD-9.
        IF W-LABEL GO TO PROCESS-LABEL-CLAUSE.
        IF W-IDENTIFICATION GO TO PROCESS-IDENT-CLAUSE.
        IF W-FD-FLAG-WORD MOVE "*2*" TO LISTING-FLAG.
        IF PERIOD-THERE MOVE SPACE TO PUNCTUATION-CHARACTER.
        PERFORM PUT-WORD.
        GO TO PROCESS-FD-1.

PROCESS-LABEL-CLAUSE.
        NOTE
        PERFORM PUT-WORD.
        PERFORM GET-WORD.
        IF W-RECORD PERFORM PUT-WORD, PERFORM GET-WORD.
        IF W-IS-ARE PERFORM PUT-WORD, PERFORM GET-WORD.
        IF PERIOD-THERE MOVE SPACE TO PUNCTUATION-CHARACTER.
        IF W-VALID-TYPE PERFORM PUT-WORD, MOVE "X" TO 
          LABEL-DONE; GO TO PROCESS-FD-1.
        MOVE SPACES TO LONGEST-LINE.
PROCESS-L-C-1.
        NOTE
        PERFORM GET-WORD.
        IF W-FD-KEY-WORD   NEXT SENTENCE
        ELSE IF FIRST-COLUMN < 6 NEXT SENTENCE
             ELSE GO TO PROCESS-L-C-1.
BUILD-NEW-LABEL-CLAUSE.
        NOTE
        DISPLAY "LABEL RECORDS ARE " WITH NO ADVANCING.
        MOVE THIS-WORD TO SAVE-THIS-WORD.
        ACCEPT THIS-WORD.
        IF NOT W-VALID-TYPE
           DISPLAY "OMITTED,STANDARD,USASI,NON-STANDARD...",
           GO TO BUILD-NEW-LABEL-CLAUSE.
        IF THIS-WORD NOT EQUAL TO SPACES
          MOVE THIS-WORD TO LABEL-CLAUSE-TYPE,
          MOVE NEW-LABEL-CLAUSE TO NEXT-SHORTEST-LINE,
          MOVE "*1*" TO LISTING-FLAG,
          PERFORM WRITE-LINE.
        MOVE "X" TO LABEL-DONE.
        MOVE SAVE-THIS-WORD TO THIS-WORD.
        GO TO PROCESS-FD-2.

PROCESS-IDENT-CLAUSE.
        NOTE
        MOVE SPACES TO LONGEST-LINE. MOVE "X" TO SKIP-FLAG.
        PERFORM GET-WORD.
        IF W-IS PERFORM GET-WORD. MOVE SPACE TO SKIP-FLAG.
        IF W-FD-KEY-WORD  OR  FIRST-COLUMN LESS THAN 6
          OR PUNCTUATION AND THIS-WORD EQUALS SPACES
           GO TO  BUILD-IDENT-CLAUSE.
        MOVE THIS-WORD TO IDENT-CLAUSE-ID.
        MOVE NEW-IDENT-CLAUSE TO NEXT-SHORTEST-LINE.
        MOVE "*2*" TO LISTING-FLAG.
        PERFORM WRITE-LINE.
        MOVE "X" TO IDENT-DONE.
        GO  TO PROCESS-FD-1.
BUILD-IDENT-CLAUSE.
        NOTE
        MOVE SPACES TO LONGEST-LINE.
        DISPLAY "VALUE OF IDENTIFICATION IS (ENTER QUOTED STRING)"
           WITH NO ADVANCING.
        MOVE THIS-WORD TO SAVE-THIS-WORD.
        ACCEPT THIS-WORD.
        IF THIS-WORD EQUAL TO SPACES  NEXT SENTENCE
        ELSE MOVE THIS-WORD TO IDENT-CLAUSE-ID,
             MOVE NEW-IDENT-CLAUSE TO NEXT-SHORTEST-LINE,
             MOVE "*1*" TO LISTING-FLAG,
             PERFORM WRITE-LINE.
        MOVE SAVE-THIS-WORD TO THIS-WORD.
        MOVE "X" TO IDENT-DONE.
        GO TO PROCESS-FD-2.

PROCESS-FD-9.
        NOTE
        IF NOT LABEL-CLAUSE-DONE GO TO BUILD-NEW-LABEL-CLAUSE.
        IF NOT IDENT-CLAUSE-DONE GO TO BUILD-IDENT-CLAUSE.
        MOVE PERIOD-LINE TO SHORTEST-LINE, PERFORM WRITE-LINE.
        GO TO PROCESS-DD-1.




*************************************************************
*	PROCESS THE PROCEDURE DIVISION
*************************************************************

PROCESS-PROCEDURE SECTION 03.

PROCESS-PD-0.
	PERFORM GET-WORD; IF END-OF-SOURCE
	   PERFORM PUT-WORD,
	   PERFORM WRITE-LINE,
	   MOVE "END-OF-JOB." TO SHORTEST-LINE,
	   PERFORM WRITE-LINE,
        DISPLAY "END OF "  PROGRAM-NAME-PERIOD;
	   GO TO ALL-DONE.
        IF W-NOTE GO TO PROCESS-NOTE.

	IF W-TODAY       GO TO PROCESS-TODAYS-DATE-PD.
	IF W-TTY     GO TO PROCESS-TTY.
        IF FLAG-COLLATE
          IF COLLATE-TEST MOVE "*2*" TO LISTING-FLAG
          ELSE IF FLAG-COMPARE
                 IF COMPARE-TEST MOVE "*2*" TO LISTING-FLAG.

PROCESS-PD-1.
	IF PD-WORDS-FLAGGED MOVE "*2*" TO LISTING-FLAG.
        IF PD-WORDS-FLAGGED-3 MOVE "*3*" TO LISTING-FLAG.
PROCESS-PD-2.
	PERFORM PUT-WORD; GO TO PROCESS-PD-0.


PROCESS-NOTE.
        IF PERIOD-THERE  GO TO PROCESS-PD-2.
        PERFORM PUT-WORD.
        PERFORM GET-WORD.
        GO TO PROCESS-NOTE.

*	TODAY      .  SUBSTITUTE CURRENT-DATE AND FLAG.

PROCESS-TODAYS-DATE-PD.
	MOVE "CURRENT-DATE" TO THIS-WORD.
	PERFORM PUT-WORD.
	MOVE "*1*" TO LISTING-FLAG.
	GO TO PROCESS-PD-0.

PROCESS-TTY.
	MOVE "*2*" TO LISTING-FLAG.
	PERFORM PUT-WORD.
	GO TO PROCESS-PD-0.


*******************************************************************
*	PROCESSING COMPLETE FOR THAT PROGRAM
*******************************************************************

ALL-DONE.
	IF THIS-WORD NOT EQUAL TO SPACES OR PUNCTUATION-CHARACTER NOT EQUAL TO SPACE
		PERFORM PUT-WORD.
	PERFORM WRITE-LINE.
ALL-DONE-1.
	CLOSE SOURCE-FILE, OUTPUT-FILE.
        IF AT-TTY  CLOSE LISTING-FILE WITH DELETE
        ELSE  CLOSE LISTING-FILE.
	ADD 1 TO JOB-COUNT.
	GO TO RESTART.
********************************************************************
*	GET A WORD FROM THE SOURCE FILE
********************************************************************

GET-WORD SECTION.

GW-1.
        IF END-OF-SOURCE  GO TO GW-EXIT.
	IF END-OF-LINE PERFORM WRITE-LINE; PERFORM GET-CHARACTER THRU GC-EXIT;
	ELSE
		IF COLS-7-80 = SPACES AND NEXT-CHARACTER = SPACE
		PERFORM WRITE-LINE; PERFORM GC-1 THRU GC-EXIT;
	ELSE
		PERFORM GET-CHARACTER THRU GC-EXIT.
	IF THIS-CHARACTER EQUALS SPACE GO TO GW-1.

	IF WITHIN-REMARKS
        IF CCOLUMN LESS THAN 6 MOVE 6 TO FIRST-COLUMN
        ELSE MOVE CCOLUMN TO FIRST-COLUMN   ELSE

	MOVE CCOLUMN TO FIRST-COLUMN.
	MOVE SPACES TO THIS-WORD; MOVE 0 TO I.
        IF THIS-CHARACTER EQUALS QUOTE
            IF J = 1 SUBTRACT 0 FROM FIRST-COLUMN GIVING J;
                GO TO GW-7;
            ELSE GO TO GW-7.

GW-5. ADD 1 TO I.
	MOVE THIS-CHARACTER TO WORD-CHARACTER (I).
	PERFORM GET-WORD-CHARACTER THRU GWC-EXIT.
	IF THIS-CHARACTER IS NOT EQUAL TO SPACE GO TO GW-5.
	IF I IS EQUAL TO ZERO NEXT SENTENCE ELSE
	MOVE WORD-CHARACTER (I) TO PUNCTUATION-CHARACTER;
		IF PUNCTUATION MOVE SPACE TO WORD-CHARACTER (I);
		ELSE MOVE SPACE TO PUNCTUATION-CHARACTER.
	GO TO GW-EXIT.

GW-7.
	IF NOT SKIPPING-STUFF
	MOVE THIS-CHARACTER TO OUTPUT-CHARACTER (J); ADD 1 TO J.

GW-8. PERFORM GET-CHARACTER THRU GC-EXIT.
	IF THIS-CHARACTER EQUALS QUOTE
	AND NOT SKIPPING-STUFF
		MOVE QUOTE TO OUTPUT-CHARACTER (J);
		ADD 1 TO J; GO TO GW-1.
	IF THIS-CHARACTER EQUALS QUOTE
		GO TO GW-1.

	IF NOT END-OF-LINE GO TO GW-7.

GW-8A.
	PERFORM WRITE-LINE.
	IF THIS-CHARACTER IS NOT EQUAL TO "-" MOVE "*3*" TO LISTING-FLAG;
		GO TO GW-1.
	MOVE "-" TO OUTPUT-CHARACTER (1); MOVE 2 TO J.

GW-9.
	PERFORM GET-CHARACTER THRU GC-EXIT.
	IF END-OF-LINE GO TO GW-8A.
	IF THIS-CHARACTER EQUALS SPACE ADD 1 TO J; GO TO GW-9.
	IF THIS-CHARACTER IS NOT EQUAL TO QUOTE MOVE "*3*" TO LISTING-FLAG.
	GO TO GW-7.

GW-EXIT. EXIT.
********************************************************************
*	PUT CURRENT WORD ONTO OUTPUT LINE.
********************************************************************

PUT-WORD SECTION.
PW-0. IF THIS-WORD EQUALS SPACES GO TO PW-2.
	IF J = 1 NEXT SENTENCE; ELSE
		SUBTRACT 1 FROM J GIVING TEMP-2;
		MOVE OUTPUT-CHARACTER (TEMP-2) TO HOLD-CHARACTER;
		IF HOLD-CHARACTER = SPACE OR "(" NEXT SENTENCE;
		ELSE ADD 1 TO J.
	IF NOT DELETING-STUFF AND J     IS LESS THAN FIRST-COLUMN
		COMPUTE J = FIRST-COLUMN    .

	ENTER MACRO PUTWRD USING THIS-WORD, OUTPUT-CHARACTER (J), J, LISTING-FLAG.

PW-1.
	MOVE SPACES TO THIS-WORD.

PW-2.
	MOVE PUNCTUATION-CHARACTER TO OUTPUT-CHARACTER (J).
	MOVE SPACE TO PUNCTUATION-CHARACTER.
	ADD 1 TO J.


******************************************************************
*	WRITE OUT A LINE.
******************************************************************

WRITE-LINE SECTION.

WL-1.
	IF DELETING-STUFF AND SHORTEST-LINE EQUAL TO SPACES
	GO TO WL-EXIT.
	IF SKIPPING-STUFF OR DELETING-STUFF AND J = 1 GO TO WL-EXIT.
        ADD 1,  EDIT-NUMBER-COUNT GIVING 
            EDIT-NUMBER-COUNT, EDIT-NUMBER.
        
        IF NOT CONV-OUT  GO TO SKIP-CONV-OUT.
        IF OUTPUT-CHARACTER(1) EQUAL TO "-" OR "*"
          MOVE LONGEST-LINE TO C-R-CONT-REM;
        ELSE IF OUTPUT-CHARACTER(1) EQUALS " "
                IF ONE-FOUR EQUALS SPACES
                   AND  COL-FIVE NOT EQUAL TO " "
                      MOVE LONGEST-LINE TO C-R-REG-SOURCE
                ELSE  MOVE LONGEST-LINE TO C-R-CONT-REM
             ELSE MOVE LONGEST-LINE TO C-R-REG-SOURCE.
        IF C-R-COL-73-ON EQUAL TO SPACES
          MOVE PROGRAM-NAME TO C-R-ID;
        ELSE DISPLAY "**LINE " EDIT-NUMBER-COUNT " TOO LONG".
        MOVE LINE-NUM TO C-R-SEQ.
        ADD 10 TO LINE-NUM.
        MOVE CONV-REC TO LONGEST-LINE.
        MOVE SPACES TO CONV-REC.
SKIP-CONV-OUT.
        IF FLAGS-ONLY
          IF LISTING-FLAG EQUAL TO SPACES GO TO SKIP-LIST.

        IF EDIT-NUMS
           IF LISTING-FLAG NOT EQUAL SPACES
              MOVE LINE-COUNT-REC TO LIST-OUTPUT,
              IF AT-TTY  DISPLAY LO17
              ELSE WRITE LO17.
        MOVE SPACES TO LIST-OUTPUT.
        MOVE LISTING-FLAG TO LISTING-FLAG-OUT.

	MOVE LONGEST-LINE TO LIST-SOURCE.
        IF AT-TTY DISPLAY LIST-OUTPUT, GO TO SKIP-LIST.
 
        IF LO10B EQUALS SPACES
          IF LO15B EQUALS SPACES
            IF LO17B EQUALS SPACES
              IF LO19B EQUALS SPACES WRITE LO19
              ELSE IF LO18B EQUALS SPACES WRITE LO18
                   ELSE WRITE LO17;
            ELSE IF LO16B EQUALS SPACES WRITE LO16
                 ELSE WRITE LO15;
          ELSE IF LO12B EQUALS SPACES
                 IF LO14B EQUALS SPACES WRITE LO14
                 ELSE IF LO13B EQUALS SPACES WRITE LO13
                      ELSE WRITE LO12;
               ELSE IF LO11B EQUALS SPACES WRITE LO11
                    ELSE WRITE LO10;
        ELSE IF LO6B EQUALS SPACES
               IF LO8B EQUALS SPACES
                 IF LO9B EQUALS SPACES WRITE LO9
                 ELSE WRITE LO8;
               ELSE IF LO7B EQUALS SPACES WRITE LO7
                    ELSE WRITE LO6;
             ELSE IF LO3B EQUALS SPACES
                    IF LO5B EQUALS  SPACES WRITE LO5
                    ELSE IF LO4B EQUALS SPACES WRITE LO4
                         ELSE WRITE LO3;
                  ELSE IF LO2B EQUALS SPACES WRITE LO2
                       ELSE WRITE LIST-OUTPUT.

SKIP-LIST.
	IF COLS-73-102 OF LONGEST-LINE EQUAL SPACES
		WRITE SHORTEST-LINE BEFORE 1 LINE; ELSE
	IF COLS-81-102 OF LONGEST-LINE EQUAL SPACES
		WRITE NEXT-SHORTEST-LINE BEFORE 1 LINE; ELSE
	IF COLS-89-102 OF LONGEST-LINE EQUAL SPACES
		WRITE NEXT-LONGEST-LINE BEFORE 1 LINE; ELSE
		WRITE LONGEST-LINE BEFORE 1 LINE.

	MOVE SPACES TO LONGEST-LINE, LISTING-FLAG.

WL-EXIT. MOVE SPACE TO EOL-FLAG, DELETION-FLAG; MOVE 1 TO J.
*********************************************************************
*	GET A CHARACTER FROM SOURCE LINE
*********************************************************************

GET-CHARACTER SECTION.

GC-0.
	IF END-OF-SOURCE GO TO ALL-DONE.
	IF NEXT-CHARACTER IS NOT EQUAL TO SPACE
		MOVE NEXT-CHARACTER TO THIS-CHARACTER;
		MOVE SPACE TO NEXT-CHARACTER;
		GO TO GC-EXIT.

	ADD 1 TO CCOLUMN.
	IF CCOLUMN IS NOT EQUAL TO 73
		ENTER MACRO GETKAR USING CCOLUMN; GO TO GC-EXIT.

	MOVE "X" TO EOL-FLAG.
GC-1.
	MOVE 1 TO CCOLUMN.
	READ SOURCE-FILE; AT END MOVE "X" TO EOF-FLAG;
                GO TO GC-EXIT.

*       TEST FOR COMMENT LINE

        IF THIS-CHARACTER NOT EQUAL "*" GO TO CONVERSION.
        IF J NOT EQUAL  1  OR END-OF-LINE PERFORM WRITE-LINE.
        MOVE DUMMY OF SOURCE-RECORD TO NEXT-SHORTEST-LINE.
        PERFORM WRITE-LINE.
        GO TO GC-1.


CONVERSION.
	ENTER MACRO CONVRT USING COLS-7-80, CONVERSION-TABLE-029-RCA, 66.

GC-EXIT. EXIT.
********************************************************************
*	GET A CHARACTER FOR A WORD.
*	IF NOT SPACE, RETURN.
*	IF SPACE, SCAN UNTIL NON-SPACE OR CONTINUATION.
*	IF CONTINUATION, SCAN UNTIL NON-SPACE.
*	IF NOT CONTINUATION, RETURN A SPACE.
********************************************************************

GET-WORD-CHARACTER SECTION.

GWC-0.
	PERFORM GET-CHARACTER THRU GC-EXIT.
	IF CCOLUMN NOT EQUAL TO 1 AND THIS-CHARACTER NOT EQUAL TO SPACE
		GO TO GWC-EXIT.

GWC-1. IF CCOLUMN EQUALS 1 GO TO GWC-3.
GWC-2.
	PERFORM GET-CHARACTER THRU GC-EXIT.
	IF THIS-CHARACTER EQUALS SPACE GO TO GWC-1.
	MOVE THIS-CHARACTER TO NEXT-CHARACTER.
	MOVE SPACE TO THIS-CHARACTER.
	GO TO GWC-EXIT.

GWC-3. IF THIS-CHARACTER EQUALS SPACE GO TO GWC-EXIT.
	IF THIS-CHARACTER NOT EQUAL TO "-" GO TO GWC-5.
GWC-4.
	PERFORM GET-CHARACTER THRU GC-EXIT.
	IF CCOLUMN EQUALS 1 GO TO GWC-3.
	IF THIS-CHARACTER EQUALS SPACE GO TO GWC-4.
	GO TO GWC-EXIT.

GWC-5. MOVE THIS-CHARACTER TO NEXT-CHARACTER; MOVE SPACE TO THIS-CHARACTER.

GWC-EXIT. EXIT.
*****************************************************************
*	SCAN PROGRAM-NAME, LOOKING FOR THE "." DELIMITER
*****************************************************************

CONVERT-ID SECTION 49.

CONVERT-ID-1.
	MOVE TN-CHAR (J) TO TN-CHAR (1).
	IF TN-CHAR (1) IS NOT EQUAL TO "." GO TO CONVERT-ID-2.

	IF I IS GREATER THAN 6 MOVE 10 TO I, J;
		ELSE MOVE 6 TO I.
	GO TO CONVERT-ID-EXIT.

CONVERT-ID-2.
	IF TN-CHAR (1) IS EQUAL TO SPACE NEXT SENTENCE; ELSE
	IF I IS GREATER THAN 8 MOVE 10 TO I, J;
		ELSE ADD 1 TO I; MOVE TN-CHAR (1) TO PN-CHAR (I).

CONVERT-ID-EXIT. EXIT.
  lß