      SUBROUTINE NOISE
C
        DOUBLE PRECISION MODNAM,MNAME,NAME
C
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(16),IDI,IDO
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/AC/JACFLG,FSTART,FSTOP,IDFREQ,FINCR,INOISE,NOSPRT,
     1   NOSOUT,NOSIN
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
      COMMON/TBLOK/FNDATA(25,5)
C
C
      DIMENSION PUTOUT(2,101,5)
      EQUIVALENCE (PUTOUT(1,1,1),ROUT(1,1))
      DIMENSION VNPART(2,1)
      EQUIVALENCE (VNPART(1,1),VN(1))
C
C
      KILL=0
      IF (ICALC.GT.1) GO TO 10
      FOURKT=4.0*CHARGE*VT
      TWOQ=2.0*CHARGE
      IPNOD=IOPND(NOSOUT)
      INNOD=IONND(NOSOUT)
      KNTR=1
   10 IF (NOSPRT.EQ.0) GO TO 30
      IF (KNTR.GT.ICALC) GO TO 30
      KILL=1
      KNTR=KNTR+NOSPRT
        WRITE(IDO,11)TEMPS(ITEMNO),FREQ(ICALC)
11      FORMAT(/1X,'*****NOISE ANALYSIS (SQ V/HZ) TEMPERATURE',F10.3,
     1  ' DEG C'//1X,'FREQUENCY',1PE10.3,' HZ'/)
C
C  OBTAIN THE ADJOINT CIRCUIT SOLUTION
C
   30 IF (KILL.EQ.1) GO TO 40
      IF (INOISE.EQ.0) RETURN
   40 VNRMS=0.0
      VREAL=VNPART(1,IPNOD)-VNPART(1,INNOD)
      VIMAG=VNPART(2,IPNOD)-VNPART(2,INNOD)
      VOUT=SQRT(VREAL*VREAL+VIMAG*VIMAG)
      IF (VOUT.LT.1.0E-20) VOUT=1.0E-20
      DO 50 I=1,NUMNOD
      VNPART(1,I)=0.0
   50 VNPART(2,I)=0.0
      VNPART(1,IPNOD)=-1.0
      VNPART(1,INNOD)=1.0
      IF (NUMVS.EQ.0) GO TO 70
      DO 60 I=1,NUMVS
      IELNUM=MATLOC(I)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      VNPART(1,NODE2)=VNPART(1,NODE2)+VNPART(1,NODE1)
      VNPART(2,NODE2)=VNPART(2,NODE2)+VNPART(2,NODE1)
   60 CONTINUE
   70 CALL ACASOL
      VNPART(1,1)=0.0
      VNPART(2,1)=0.0
      IF (NUMVS.EQ.0) GO TO 100
      DO 80 I=1,NUMVS
      IELNUM=MATLOC(NUMVS-I+1)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      VNPART(1,NODE1)=VNPART(1,NODE2)
      VNPART(2,NODE1)=VNPART(2,NODE2)
   80 CONTINUE
C
C  RESISTORS
C
  100 ISTOP=LOCATE(2)-1
      IF (ISTOP.LT.1) GO TO 200
  110 DO 130 I=1,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      VREAL=VNPART(1,NODE1)-VNPART(1,NODE2)
      VIMAG=VNPART(2,NODE1)-VNPART(2,NODE2)
      VNO1=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*VALUE(I)
      VNRMS=VNRMS+VNO1
      IF (KILL.EQ.0) GO TO 130
      WRITE (IDO,121) NAME(I),VNO1
121     FORMAT(1X,A10,': ',1PE8.2)
  130 CONTINUE
C
C  BIPOLAR JUNCTION TRANSISTORS
C
  200 ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) GO TO 300
        IF(KILL.EQ.0)GO TO 210
        WRITE(IDO,201)
201     FORMAT(/1X,'NAME',8X,'RB',7X,'RC',7X,'RE',7X,'IB',7X,'IC',7X,
     1  'FN',7X,'TOTAL')
  210 ISPOT=ICURNT(7)
      DO 220 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE4=NODPLC(LOC+3)
      NODE5=NODPLC(LOC+4)
      NODE6=NODPLC(LOC+5)
      MNAM=MNAME(I)
C
C  BASE RESISTANCE
C
      VREAL=VNPART(1,NODE2)-VNPART(1,NODE5)
      VIMAG=VNPART(2,NODE2)-VNPART(2,NODE5)
      VNO1=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,4)*VALUE(I)
C
C  COLLECTOR RESISTANCE
C
      VREAL=VNPART(1,NODE1)-VNPART(1,NODE4)
      VIMAG=VNPART(2,NODE1)-VNPART(2,NODE4)
      VNO2=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,5)*VALUE(I)
C
C  EMITTER RESISTANCE
C
      VREAL=VNPART(1,NODE3)-VNPART(1,NODE6)
      VIMAG=VNPART(2,NODE3)-VNPART(2,NODE6)
      VNO3=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,6)*VALUE(I)
C
C  BASE CURRENT SHOT NOISE AND FLICKER NOISE
C
      VREAL=VNPART(1,NODE5)-VNPART(1,NODE6)
      VIMAG=VNPART(2,NODE5)-VNPART(2,NODE6)
      VTEMP=VREAL*VREAL+VIMAG*VIMAG
      ARG=ABS(TRACUR(ISPOT+3))
      IF (ARG.LT.1.0E-20) ARG=1.0E-20
      VNO4=VTEMP*TWOQ*ARG
      VNO6=VTEMP*FNDATA(MNAM,1)*EXP(FNDATA(MNAM,2)*ALOG(ARG))
     1   *TWOPI/OMEGA
C
C  COLLECTOR CURRENT SHOT NOISE
C
      VREAL=VNPART(1,NODE4)-VNPART(1,NODE6)
      VIMAG=VNPART(2,NODE4)-VNPART(2,NODE6)
      VNO5=(VREAL*VREAL+VIMAG*VIMAG)*TWOQ*ABS(TRACUR(ISPOT+2))
      ISPOT=ISPOT+15
      VNTOT=VNO1+VNO2+VNO3+VNO4+VNO5+VNO6
      VNRMS=VNRMS+VNTOT
      IF (KILL.EQ.0) GO TO 220
      WRITE (IDO,221) NAME(I),VNO1,VNO2,VNO3,VNO4,VNO5,VNO6,VNTOT
  220 CONTINUE
221     FORMAT(1X,A9,7(1X,1PE8.2))
C
C  JUNCTION DIODES
C
  300 ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) GO TO 400
      IF (KILL.EQ.0) GO TO 310
        WRITE(IDO,301)
301     FORMAT(/1X,'NAME',4X,'RS',7X,'ID',7X,'FN',7X,'TOTAL')
  310 DO 320 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      MNAM=MNAME(I)
C
C  OHMIC RESISTANCE
C
      VREAL=VNPART(1,NODE1)-VNPART(1,NODE3)
      VIMAG=VNPART(2,NODE1)-VNPART(2,NODE3)
      VNO1=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,1)*VALUE(I)
C
C  JUNCTION SHOT NOISE AND FLICKER NOISE
C
      VREAL=VNPART(1,NODE3)-VNPART(1,NODE2)
      VIMAG=VNPART(2,NODE3)-VNPART(2,NODE2)
      VTEMP=VREAL*VREAL+VIMAG*VIMAG
      ARG=ABS(TRACUR(ISPOT+1))
      IF (ARG.LT.1.0E-20) ARG=1.0E-20
      VNO2=VTEMP*TWOQ*ARG
      VNO3=VTEMP*FNDATA(MNAM,1)*EXP(FNDATA(MNAM,2)*ALOG(ARG))
     1   *TWOPI/OMEGA
      ISPOT=ISPOT+5
      VNTOT=VNO1+VNO2+VNO3
      VNRMS=VNRMS+VNTOT
      IF (KILL.EQ.0) GO TO 320
      WRITE (IDO,221) NAME(I),VNO1,VNO2,VNO3,VNTOT
  320 CONTINUE
C
C  JFETS
C
  400 ISTART=LOCATE(9)
      ISTOP=LOCATE(10)-1
      IF (ISTART.GT.ISTOP) GO TO 500
      IF (KILL.EQ.0) GO TO 410
      WRITE (IDO,501)
  410 ISPOT=ICURNT(9)
      DO 420 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE4=NODPLC(LOC+3)
      NODE5=NODPLC(LOC+4)
      MNAM=MNAME(I)
C
C  DRAIN RESISTANCE
C
      VREAL=VNPART(1,NODE1)-VNPART(1,NODE4)
      VIMAG=VNPART(2,NODE1)-VNPART(2,NODE4)
      VNO1=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,5)*VALUE(I)
C
C  SOURCE RESISTANCE
C
      VREAL=VNPART(1,NODE3)-VNPART(1,NODE5)
      VIMAG=VNPART(2,NODE3)-VNPART(2,NODE5)
      VNO2=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,6)*VALUE(I)
C
C  DRAIN CURRENT SHOT NOISE AND FLICKER NOISE
C
      VREAL=VNPART(1,NODE4)-VNPART(1,NODE5)
      VIMAG=VNPART(2,NODE4)-VNPART(2,NODE5)
      VTEMP=VREAL*VREAL+VIMAG*VIMAG
      VNO3=VTEMP*FOURKT*2.0*ABS(TRACUR(ISPOT+3))/3.0
      ARG=ABS(TRACUR(ISPOT+2))
      IF (ARG.LT.1.0E-20) ARG=1.0E-20
      VNO4=VTEMP*FNDATA(MNAM,1)*EXP(FNDATA(MNAM,2)*ALOG(ARG))
     1   *TWOPI/OMEGA
      ISPOT=ISPOT+10
      VNTOT=VNO1+VNO2+VNO3+VNO4
      VNRMS=VNRMS+VNTOT
      IF (KILL.EQ.0) GO TO 420
      WRITE (IDO,221) NAME(I),VNO1,VNO2,VNO3,VNO4,VNTOT
  420 CONTINUE
C
C  MOSFETS
C
  500 ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) GO TO 600
      IF (KILL.EQ.0) GO TO 510
      WRITE (IDO,501)
501     FORMAT(/1X,'NAME',4X,'RD',7X,'RS',7X,'ID',7X,'FN',7X,'TOTAL')
  510 ISPOT=ICURNT(10)
      DO 520 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE5=NODPLC(LOC+4)
      NODE6=NODPLC(LOC+5)
      MNAM=MNAME(I)
C
C  DRAIN RESISTANCE
C
      VREAL=VNPART(1,NODE1)-VNPART(1,NODE5)
      VIMAG=VNPART(2,NODE1)-VNPART(2,NODE5)
      VNO1=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,7)*VALUE(I)
C
C  SOURCE RESISTANCE
C
      VREAL=VNPART(1,NODE3)-VNPART(1,NODE6)
      VIMAG=VNPART(2,NODE3)-VNPART(2,NODE6)
      VNO2=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,8)*VALUE(I)
C
C  DRAIN CURRENT SHOT NOISE AND FLICKER NOISE
C
      VREAL=VNPART(1,NODE5)-VNPART(1,NODE6)
      VIMAG=VNPART(2,NODE5)-VNPART(2,NODE6)
      VTEMP=VREAL*VREAL+VIMAG*VIMAG
      VNO3=VTEMP*FOURKT*2.0*ABS(TRACUR(ISPOT+3))/3.0
      ARG=ABS(TRACUR(ISPOT+2))
      IF (ARG.LT.1.0E-20) ARG=1.0E-20
      VNO4=VTEMP*FNDATA(MNAM,1)*EXP(FNDATA(MNAM,2)*ALOG(ARG))
     1   *TWOPI/OMEGA
      ISPOT=ISPOT+15
      VNTOT=VNO1+VNO2+VNO3+VNO4
      VNRMS=VNRMS+VNTOT
      IF (KILL.EQ.0) GO TO 520
      WRITE (IDO,221) NAME(I),VNO1,VNO2,VNO3,VNO4,VNTOT
  520 CONTINUE
C
C  COMPUTE EQUIVALENT INPUT NOISE VOLTAGE
C
  600 VNOUT=SQRT(VNRMS)
      VNIN=VNOUT/VOUT
      IF (KILL.EQ.0) GO TO 620
      WRITE (IDO,601) VNRMS,VNOUT
601     FORMAT(/1X,'TOTAL OUTPUT NOISE: ',1PE10.3,' SQ V/HZ = ',
     1  E10.3,' V/RT HZ')
        WRITE (IDO,602)IONAM(NOSOUT),NAME(NOSIN),VOUT
602     FORMAT(2X,A10,'/',A10,' NOISE: '1PE10.3,' V/RT HZ')
        WRITE (IDO,603)NAME(NOSIN),VNIN
603     FORMAT(1X,'EQUIVALENT INPUT NOISE AT ',A10,': ',1PE10.3,
     1  ' V/RT HZ')
  620 IF (INOISE.EQ.0) RETURN
      PUTOUT(1,ICALC,INOISE)=VNOUT
      PUTOUT(2,ICALC,INOISE)=VNIN
      RETURN
      END
  