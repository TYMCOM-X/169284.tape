      SUBROUTINE MOSFET
C
        DOUBLE PRECISION MODNAM,MNAME,NAME
C
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      DIMENSION VBDO(1),VBSO(1),CDRO(1),CJBD(1),CJBS(1),VCGS(1),
     1   CCGS(1),VCGD(1),CCGD(1),VCGB(1),CCGB(1),VGSO(1),VGDO(1)
      EQUIVALENCE (VBDO(1),TRACUR(1)),(VBSO(1),TRACUR(2)),
     1   (CDRO(1),TRACUR(3)),(CJBD(1),TRACUR(4)),(CJBS(1),TRACUR(5)),
     2   (VCGS(1),TRACUR(6)),(CCGS(1),TRACUR(7)),(VCGD(1),TRACUR(8)),
     3   (CCGD(1),TRACUR(9)),(VCGB(1),TRACUR(10)),(CCGB(1),TRACUR(11)),
     4   (VGSO(1),TRACUR(12)),(VGDO(1),TRACUR(13))
C
C
      ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) RETURN
      ISPOT=ICURNT(10)
      DO 1000 I=ISTART,ISTOP
C
C  LOOKUP MATRIX POINTERS
C
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      LOC4=NODPLC(LOC+3)
      LOC5=NODPLC(LOC+4)
      LOC6=NODPLC(LOC+5)
      IF (MODE.EQ.2) GO TO 50
      LOC7=MATLOC(IFIND+1)
      LOC8=MATLOC(IFIND+2)
      LOC9=MATLOC(IFIND+3)
      LOC10=MATLOC(IFIND+4)
      LOC11=MATLOC(IFIND+5)
      LOC12=MATLOC(IFIND+6)
      LOC13=MATLOC(IFIND+7)
      LOC14=MATLOC(IFIND+8)
      LOC15=MATLOC(IFIND+9)
      LOC16=MATLOC(IFIND+10)
      LOC17=MATLOC(IFIND+11)
      LOC18=MATLOC(IFIND+12)
      LOC19=MATLOC(IFIND+13)
      LOC20=MATLOC(IFIND+14)
      LOC21=MATLOC(IFIND+15)
      LOC22=MATLOC(IFIND+16)
      IFIND=IFIND+16
C
C  DC ANALYSIS
C
   50 TYPE=SYMVAL(MNAM,1)
      VTO=SYMVAL(MNAM,2)
      PHI=SYMVAL(MNAM,3)
      BETA=SYMVAL(MNAM,4)*VALUE(I)
      GAMMA=SYMVAL(MNAM,5)
      XLAMB=SYMVAL(MNAM,6)
      GDPR=SYMVAL(MNAM,7)*VALUE(I)
      GSPR=SYMVAL(MNAM,8)*VALUE(I)
      CSAT=SYMVAL(MNAM,15)
C
C  INITIALIZE ON FIRST DC PASS
C
      IF (IPASS1.EQ.0) GO TO 60
      IF (MODE.GT.1) GO TO 60
      CDRO(ISPOT)=0.0
      VBS=-1.0
      VBD=-1.0
      VGS=0.0
      VGD=0.0
      GO TO 70
C
C  DETERMINE INTERNAL BRANCH VOLTAGES AND SUBSTRATE JUNCTION MODELS
C
   60 VBD=TYPE*(VNIM1(LOC4)-VNIM1(LOC5))
      VBS=TYPE*(VNIM1(LOC4)-VNIM1(LOC6))
      VGS=TYPE*(VNIM1(LOC2)-VNIM1(LOC6))
      VGD=TYPE*(VNIM1(LOC2)-VNIM1(LOC5))
      IF (IPASS1.EQ.1) GO TO 70
      CALL JUNCT(VBD,VBDO(ISPOT),CSAT,VT)
      CALL JUNCT(VBS,VBSO(ISPOT),CSAT,VT)
      CALL FETLIM(VGS,VGSO(ISPOT),VTO)
      CALL FETLIM(VGD,VGDO(ISPOT),VTO)
   70 VBDO(ISPOT)=VBD
      VBSO(ISPOT)=VBS
      VGSO(ISPOT)=VGS
      VGDO(ISPOT)=VGD
      VDS=VGS-VGD
      VGB=VGS-VBS
C
C  COMPUTE SUBSTRATE JUNCTION EQUIVALENT CONDUCTANCES
C
   80 IF (VBD.GT.0.0) GO TO 82
      GBD=CSAT/VT+GMIN
      CBDJ=GBD*VBD
      CEQBD=0.0
      GO TO 84
   82 ARG=EXP(VBD/VT)
      CBDJ=CSAT*(ARG-1.0)+GMIN*VBD
      GBD=CSAT*ARG/VT+GMIN
      CEQBD=TYPE*(VBD*GBD-CBDJ)
   84 IF (VBS.GT.0.0) GO TO 86
      GBS=CSAT/VT+GMIN
      CBSJ=GBS*VBS
      CEQBS=0.0
      GO TO 100
   86 ARG=EXP(VBS/VT)
      CBSJ=CSAT*(ARG-1.0)+GMIN*VBS
      GBS=CSAT*ARG/VT+GMIN
      CEQBS=TYPE*(VBS*GBS-CBSJ)
C
C  COMPUTE DRAIN CURRENT AND DERIVITIVES FOR NORMAL MODE
C
  100 IF (VDS.LT.0.0) GO TO 200
      IF (VBS.GT.0.0) GO TO 102
      SARG=SQRT(PHI-VBS)
      VGST=VGS-VTO-GAMMA*SARG
      ARG=GAMMA/(2.0*SARG)
      GO TO 105
  102 SARG=SQRT(PHI)
      VGST=VGS-VTO-GAMMA*(SARG-VBS/(2.0*SARG))
      ARG=GAMMA/(2.0*SARG)
C
C  NORMAL MODE, CUTOFF REGION
C
  105 IF (VGST.GT.0.0) GO TO 110
      CDRAIN=0.0
      GM=0.0
      GDS=0.0
      GMBS=0.0
      GO TO 300
C
C  NORMAL MODE, SATURATION REGION
C
  110 IF (VGST.GT.VDS) GO TO 120
      BETAP=BETA*(1.0+XLAMB*VDS)
      CDRAIN=BETAP*VGST*VGST
      GM=2.0*BETAP*VGST
      GDS=XLAMB*BETA*VGST*VGST
      GMBS=GM*ARG
      GO TO 300
C
C  NORMAL MODE, LINEAR REGION
C
  120 BETAP=BETA*(1.0+XLAMB*VDS)
      CDRAIN=BETAP*VDS*(2.0*VGST-VDS)
      GM=2.0*BETAP*VDS
      GDS=2.0*BETAP*(VGST-VDS)+XLAMB*BETA*VDS*(2.0*VGST-VDS)
      GMBS=GM*ARG
      GO TO 300
C
C  COMPUTE DRAIN CURRENT AND DERIVITIVES FOR INVERSE MODE
C
  200 IF (VBD.GT.0.0) GO TO 202
      SARG=SQRT(PHI-VBD)
      VGDT=VGD-VTO-GAMMA*SARG
      ARG=GAMMA/(2.0*SARG)
      GO TO 205
  202 SARG=SQRT(PHI)
      VGDT=VGD-VTO-GAMMA*(SARG-VBD/(2.0*SARG))
      ARG=GAMMA/(2.0*SARG)
C
C  INVERSE MODE, CUTOFF REGION
C
  205 IF (VGDT.GT.0.0) GO TO 210
      CDRAIN=0.0
      GM=0.0
      GDS=0.0
      GMBS=0.0
      GO TO 300
C
C  INVERSE MODE, SATURATION REGION
C
  210 IF (VGDT.GT.-VDS) GO TO 220
      BETAP=BETA*(1.0-XLAMB*VDS)
      CDRAIN=-BETAP*VGDT*VGDT
      GM=-2.0*BETAP*VGDT
      GDS=XLAMB*BETA*VGDT*VGDT-GM*(1.0+ARG)
      GMBS=GM*ARG
      GO TO 300
C
C  INVERSE MODE, LINEAR REGION
C
  220 BETAP=BETA*(1.0-XLAMB*VDS)
      CDRAIN=BETAP*VDS*(2.0*VGDT+VDS)
      GM=2.0*BETAP*VDS
      GDS=2.0*BETAP*(VGDT-VDS*ARG)-XLAMB*BETA*VDS*(2.0*VGDT+VDS)
      GMBS=GM*ARG
C
C  COMPUTE EQUIVALENT DRAIN CURRENT SOURCE, CHECK CHANGE IN DRAIN
C  CURRENT FROM LAST ITERATION, STORE DC RESULTS, AND ZERO TRANSIENT
C  CONDUCTANCES
C
  300 CDREQ=TYPE*(VGS*GM+VDS*GDS+VBS*GMBS-CDRAIN)
      CDRAIN=CDRAIN-CBDJ
      TOL=PERTOL*ABS(CDRAIN)
      IF (TOL.LT.1.0E-9) TOL=1.0E-9
      DIFF=ABS(CDRAIN-CDRO(ISPOT))
      IF (DIFF.GT.TOL) IFINAL=0
      CDRO(ISPOT)=CDRAIN
      IF (MODE.GT.1) GO TO 310
      GCGS=0.0
      CEQGS=0.0
      GCGD=0.0
      CEQGD=0.0
      GCGB=0.0
      CEQGB=0.0
      GO TO 500
C
C  CHARGE STORAGE ELEMENTS
C
  310 CGS=SYMVAL(MNAM,9)*VALUE(I)
      CGD=SYMVAL(MNAM,10)*VALUE(I)
      CGB=SYMVAL(MNAM,11)*VALUE(I)
      CZBD=SYMVAL(MNAM,12)*VALUE(I)
      CZBS=SYMVAL(MNAM,13)*VALUE(I)
      PHIB=SYMVAL(MNAM,14)
      IF (VBD.GE.0.0) GO TO 312
      SARG=SQRT(1.0-VBD/PHIB)
      CTBD=4.0*PHIB*CZBD*(1.0-SARG)/DELTA
      CAPBD=CZBD/SARG
      GO TO 314
  312 CTBD=2.0*VBD*CZBD*(1.0+VBD/(4.0*PHIB))/DELTA
      CAPBD=CZBD*(1.0+VBD/(2.0*PHIB))
  314 IF (VBS.GE.0.0) GO TO 316
      SARG=SQRT(1.0-VBS/PHIB)
      CTBS=4.0*PHIB*CZBS*(1.0-SARG)/DELTA
      CAPBS=CZBS/SARG
      GO TO 320
  316 CTBS=2.0*VBS*CZBS*(1.0+VBS/(4.0*PHIB))/DELTA
      CAPBS=CZBS*(1.0+VBS/(2.0*PHIB))
C
C  SMALL SIGNAL PARAMETERS
C
  320 IF (MODE.GT.2) GO TO 350
      TRACUR(ISPOT+3)=GM
      TRACUR(ISPOT+4)=GDS
      TRACUR(ISPOT+5)=GMBS
      TRACUR(ISPOT+6)=GBD
      TRACUR(ISPOT+7)=CAPBD
      TRACUR(ISPOT+8)=GBS
      TRACUR(ISPOT+9)=CAPBS
      GO TO 900
C
C  TRANSIENT ANALYSIS
C
  350 IF (IPASS1.EQ.0) GO TO 360
      VCGS(ISPOT)=VGS
      CCGS(ISPOT)=0.0
      VCGD(ISPOT)=VGD
      CCGD(ISPOT)=0.0
      VCGB(ISPOT)=VGB
      CCGB(ISPOT)=0.0
      CJBD(ISPOT)=CTBD
      CJBS(ISPOT)=CTBS
C
C  UPDATE AT FIRST ITERATION OF EACH TIMEPOINT
C
  360 IF (MODE.EQ.3) GO TO 370
      CCGS(ISPOT)=(2.0*CGS/DELOLD)*(VGS-VCGS(ISPOT))-CCGS(ISPOT)
      VCGS(ISPOT)=VGS
      CCGD(ISPOT)=(2.0*CGD/DELOLD)*(VGD-VCGD(ISPOT))-CCGD(ISPOT)
      VCGD(ISPOT)=VGD
      CCGB(ISPOT)=(2.0*CGB/DELOLD)*(VGB-VCGB(ISPOT))-CCGB(ISPOT)
      VCGB(ISPOT)=VGB
      CJBD(ISPOT)=CTBD*(1.0+DELTA/DELOLD)-CJBD(ISPOT)
      CJBS(ISPOT)=CTBS*(1.0+DELTA/DELOLD)-CJBS(ISPOT)
C
C  COMPUTE CAPACITOR CONDUCTANCES AND EQUIVALENT CURRENT SOURCES
C
  370 GCGS=2.0*CGS/DELTA
      CEQGS=TYPE*(VCGS(ISPOT)*GCGS+CCGS(ISPOT))
      GCGD=2.0*CGD/DELTA
      CEQGD=TYPE*(VCGD(ISPOT)*GCGD+CCGD(ISPOT))
      GCGB=2.0*CGB/DELTA
      CEQGB=TYPE*(VCGB(ISPOT)*GCGB+CCGB(ISPOT))
      GTEMP=2.0*CAPBD/DELTA
      GBD=GBD+GTEMP
      CEQBD=CEQBD+TYPE*(GTEMP*VBD-CTBD+CJBD(ISPOT))
      GTEMP=2.0*CAPBS/DELTA
      GBS=GBS+GTEMP
      CEQBS=CEQBS+TYPE*(GTEMP*VBS-CTBS+CJBS(ISPOT))
C
C  LOAD CURRENT VECTOR
C
  500 VN(LOC2)=VN(LOC2)+CEQGS+CEQGD+CEQGB
      VN(LOC4)=VN(LOC4)+CEQBS+CEQBD-CEQGB
      VN(LOC5)=VN(LOC5)+CDREQ-CEQGD-CEQBD
      VN(LOC6)=VN(LOC6)-CDREQ-CEQGS-CEQBS
C
C  LOAD Y MATRIX
C
      YNL(LOC1)=YNL(LOC1)+GDPR
      YNL(LOC2)=YNL(LOC2)+GCGD+GCGS+GCGB
      YNL(LOC3)=YNL(LOC3)+GSPR
      YNL(LOC4)=YNL(LOC4)+GBD+GBS+GCGB
      YNL(LOC5)=YNL(LOC5)+GDPR+GDS+GCGD+GBD
      YNL(LOC6)=YNL(LOC6)+GSPR+GDS+GCGS+GBS+GM+GMBS
      YNL(LOC7)=YNL(LOC7)-GDPR
      YNL(LOC8)=YNL(LOC8)-GCGB
      YNL(LOC9)=YNL(LOC9)-GCGD
      YNL(LOC10)=YNL(LOC10)-GCGS
      YNL(LOC11)=YNL(LOC11)-GSPR
      YNL(LOC12)=YNL(LOC12)-GCGB
      YNL(LOC13)=YNL(LOC13)-GBD
      YNL(LOC14)=YNL(LOC14)-GBS
      YNL(LOC15)=YNL(LOC15)-GDPR
      YNL(LOC16)=YNL(LOC16)-GCGD+GM
      YNL(LOC17)=YNL(LOC17)-GBD+GMBS
      YNL(LOC18)=YNL(LOC18)-GDS-GM-GMBS
      YNL(LOC19)=YNL(LOC19)-GCGS-GM
      YNL(LOC20)=YNL(LOC20)-GSPR
      YNL(LOC21)=YNL(LOC21)-GBS-GMBS
      YNL(LOC22)=YNL(LOC22)-GDS
  900 ISPOT=ISPOT+15
 1000 CONTINUE
      RETURN
      END
    