TITLE TELECOPY - THE CONTROLLER FOR PDP-10 TELECOPY

EXTERNAL SENDIT, GETIT

INTERN PUTCH, GETCH, PUTSPC, GETSPC
INTERN SYSLST, SYSBLK, AUXCHN, SLVTYP
INTERN SIZDON, RETZAP, OUTDEC


LOC 137
5
RELOC

F=0
A=1
B=2
C=3
D=4
E=5
N=11
N1=12
SIDE=12
N2=13
AUX=13
N3=14
WD=14
INDEX=15
BP=15
CH=16	;CHARACTER FOR TRANS. - DO NOT CHANGE
P=17	;DO NOT CHANGE

;FLAGS IN RIGHT HALF OF F
SKPSP==1	;SPECIAL CHARS. NOT DELIMETERS (EXCEPT CR)
NOSTRT==2	;HAVEN'T STARTED CONVERSATION WITH SLAVE YET
STAROK==4	;ALLOW STAR ON INPUT (FOR USER NAME)
DOTOK==10	;ALLOW DOT ON INPUT (FOR USER NAME)

MAXSYS==^D38
JBTUNM==-22
JBTUN1==-21

ONLC==122
OFFLC==123

AUXPTR==5

PPN==1
FILE==2
ERR==3
SIZE==5
UNAME==5
SYS==7
FNAME==10

START:	RESET
	SETZM	AUXCIR
	SETZM	AUXCHN
	SETZM	SLVTYP
	SETZM TYPTIM
	MOVE P,[IOWD 40,PDL]
	SKIPE THISUSR	;HAVE WE BEEN HERE BEFORE?
	JRST GOTUSR	;YES...SKIP IT
	INIT	1,17
	SIXBIT	/SYS/
	0
	JRST SYSERR
	HRROI N,JBTUNM
	GETTAB N,	;GET THIS USERS NAME
	SETZ N,
	MOVEM N,THISUSR
	HRROI N1,JBTUN1
	GETTAB N1,
	SETZ N1,
	MOVEM N1,THISUSR+1
	PUSHJ P,GETUSR	;SEE IF TYMSHARE BIT IS ON
	JRST SYSERR
	RELEASE 1,
GOTUSR:	INIT 1,17
	SIXBIT /DSK/
	0
	JRST SYSERR
	SKIPE	THISYS	;HAVE WE BEEN HERE BEFORE?
	JRST GOTSYS
	LOOKUP	1,SYSNO
	JRST NOSYSN
	INPUT	1,SYSLST
	MOVEI A,^D82
	MOVE A,SYSBLK(A)
	MOVEM	A,THISYS
	CLOSE	1,
	MOVNI A,20
	GETTAB A,
	JRST SYSERR
	HRLS A,A
	SETLIC A,	;RETAIN ONLY USERS LICENSE
GOTSYS:	MOVEI A,INTTAB	;SET UP INTERRUPTS
	HRLI A,6
	INTADR A,
	JFCL
	SETO A,
	HRLI A,4
	TINASS A,	;ESCAPE ON 4
	JFCL
	MOVSI A,420000
	INTENB A,
	JFCL
GETCMD:	CLOSE 1,40	;CLOSE WITHOUT WRITING IF OPEN
	SETOM TIMFLG
	SETZB	F,ZERO
	MOVE P,[IOWD 40,PDL]
	MOVE A,[XWD ZERO,ZERO+1]
	BLT	A,ZEREND
	MOVEI	SIDE,DBLK
	OUTSTR	[ASCIZ/
: /]
	MOVEI N,6
	PUSHJ P,GETSIX
	CAIN CH,"("
	JRST [SETOM NOPRMT
		JRST RUNROU]
	SETO B,
	MOVE A,WD
GETMSK:	JUMPE A,GOTMSK
	LSH A,6
	LSH B,-6
	JRST GETMSK
GOTMSK:	MOVE A,[XWD -CMDSIZ,CMDTAB]
NXTENT:	JUMPL B,TRYNXT
	MOVE C,0(A)
	ANDCM	C,B
	CAMN WD,C
	JRST GOTMAT
TRYNXT:	AOBJN	A,NXTENT
	OUTSTR	[ASCIZ/
?
/]
	CLRBFI
	JRST GETCMD

GOTMAT:	HRRZS	A,A
	SUBI A,CMDTAB
	JRST @CMDISP(A)

RUNROU:	SKIPN	NOPRMT
	OUTSTR	[ASCIZ/
COPY FROM FILE: /]
	MOVEI	SIDE,DBLK
	SKIPE NOPRMT
	PUSHJ P,[PUSHJ P,GETFIL+3
		AOS (P)
		POPJ P,0]
	PUSHJ P,GETFIL
	SKIPN NOPRMT
	OUTSTR	[ASCIZ/
COPY TO FILE: /]
	MOVEI	SIDE,RBLK
	PUSHJ P,GETFIL
	MOVE A,THISYS
	CAME	A,DBLK+SYS
	CAMN	A,RBLK+SYS
	SKIPA
	JRST CIR2
	MOVE	A,DBLK+SYS
	CAMN	A,RBLK+SYS
	JRST CIR0
	CAME	A,THISYS
	JRST RECIP


;THIS SYSTEM IS THE DONOR SYSTEM
DONOR:	MOVEI	A,5
	MOVEM	A,DBLK
	MOVEI	SIDE,DBLK
	PUSHJ P,GET10
	MOVEI	A,DBLK+UNAME
	MOVEM A,DBLK+PPN
	LOOKUP	1,DBLK
	JRST CHKERR
	MOVE A,DBLK+SIZE
	IMULI A,5	;GET NO. OF CHARACTERS
	MOVEM A,SIZDON	;SAVE SIZE IN NO. OF CHARACTERS WRITTEN
	MOVE A,RBLK+SYS
	PUSHJ P,GETCIR
	PUSHJ P,TELLC	;TELL 940 IF WE HAVE LC OR NOT
	MOVEI	CH,102	;SLAVE IS RECIP.
	PUSHJ P,PUTSPC
	MOVEI	A,^D12
	MOVE	BP,[POINT 6,RBLK+UNAME]
	MOVEI	CH,105	;USER NAME IS COMING
	PUSHJ P,SNDNAM
	PUSHJ P,GETSPC
	MOVEI SIDE,RBLK
	CAIE CH,104
	JRST BADNAM
	MOVEI A,^D72
	MOVE BP,[POINT 6,RBLK+FNAME]
	MOVEI CH,103
	PUSHJ P,SNDNAM
	PUSHJ P,GETSPC
	CAIN CH,106
	JRST GETOPN
	CAIE CH,104
	JRST BADFIL
	PUSHJ P,CHKOLD
GETOPN:	PUSHJ P,MAKOPN
	MOVEI SIDE,DBLK
	SETZ A,
	SKIPE TYPTIM
	MOVEI A,1
	PUSHJ P,SENDIT
BACK:	SETOM TIMFLG	;IGNORE INTERRUPTS TIL TRANSFER STARTS AGAIN
	JUMPN A,@ERRRET(A)
	PUSHJ P,CRLF
	MOVEI SIDE,DBLK
	PUSHJ P,FILOUT
	OUTSTR [ASCIZ/ COPIED TO /]
	MOVEI SIDE,RBLK
	PUSHJ P,FILOUT
	PUSHJ P,CRLF
	PUSHJ P,CRLF
	PUSH P,B
	MOVE A,B
	PUSHJ P,OUTDEC
	POP P,B
	OUTSTR [ASCIZ/ CHARS. TRANSMITTED
/]
	SKIPN TYPTIM
	JRST GETCMD
	JUMPN C,.+2
	MOVEI C,1
	MOVE A,B
	IDIV A,C
	PUSHJ P,OUTDEC
	OUTSTR [ASCIZ/ CHARS. PER SEC.
/]
	JRST GETCMD


;THIS SYSTEM IS THE RECIPIENT SYSTEM
RECIP:	MOVEI A,5
	MOVEM A,RBLK
	MOVEI SIDE,RBLK
	PUSHJ P,GET10
	MOVEI A,RBLK+UNAME
	MOVEM A,RBLK+PPN
	LOOKUP 1,RBLK
	PUSHJ P,[HRRZ A,RBLK+3
		JUMPN A,NOOPN
		AOS (P)
		AOS (P)
		POPJ P,0]
	TRO F,NOSTRT
	PUSHJ P,CHKOLD
	CLOSE 1,
	SETZ A,
	HRRM A,RBLK+3
	SETZM RBLK+4	;MAKE SURE FILE GETS PRESENT DATE,TIME
	ENTER 1,RBLK
	JRST NOOPN
	MOVE A,DBLK+SYS
	PUSHJ P,GETCIR
	PUSHJ P,TELLC	;TELL 940 IF WE HAVE LC OR NOT
	MOVEI CH,101
	PUSHJ P,PUTSPC
	MOVEI A,^D12
	MOVE BP,[POINT 6,DBLK+UNAME]
	MOVEI SIDE,DBLK
	MOVEI CH,105
	PUSHJ P,SNDNAM
	PUSHJ P,GETSPC
	CAIE CH,104
	JRST BADNAM
	MOVEI A,^D72
	MOVE BP,[POINT 6,DBLK+FNAME]
	MOVEI CH,103
	PUSHJ P,SNDNAM
	PUSHJ P,GETSPC
	CAIE CH,104
	JRST FNFERR
	MOVEI SIDE,DBLK
	PUSHJ P,MAKOPN
	MOVEI CH,111
	MOVE A,SLVTYP	;GIVE 940 A PUSH...ONLY IF PDP-10 IS CONTROLLER
	CAIN A,1
	PUSHJ P,PUTSPC
	SETZ A,
	SKIPE TYPTIM
	MOVEI A,1
	PUSHJ P,GETIT
	JRST BACK

NOOPN:	MOVEI CH,107	;TELL DONOR TO FORGET IT.
	PUSHJ P,PUTSPC
	JRST CHKERR

VERSION:	OUTSTR	[ASCIZ/
VERSION /]
	MOVE A,137
	PUSHJ P,OUTDEC
	OUTSTR [ASCIZ/
/]
	JRST GETCMD


HELP:	OUTSTR	HELMSG
	SKIPE TYMBIT
	OUTSTR TYMHELP
	JRST GETCMD

INSTRU:	OUTSTR INSMSG
	SKIPE TYMBIT
	OUTSTR TYMINS
	JRST GETCMD

SETIME:	SKIPN TYMBIT
	JRST TRYNXT+1
	MOVEI A,1
	MOVEM A,TYPTIM
	JRST GETCMD

ALTTYP:	CLRBFI	;CLEAR INPUT BUFFER
	MOVEI CH,107	;TELL SALVE WE'RE THRU
	SKIPE AUXCHN
	PUSHJ P,PUTSPC
	MOVEI A,GETCMD
	MOVEM A,INTTAB+6
	DISMIS

RESTLC:	SKIPA A,[OFFLC]
SETLC:	MOVEI A,ONLC
	MOVEM A,LC
	JRST GETCMD

ENDIT:	HRLOI A,377777
	INTENB A,	;DISABLE ALL CHANNELS
	 JFCL
	SETO A,
	ZAPCIR A,
	CLOSE 1,
	RESET
	SETZ A,
	SETLCH ,A
	EXIT


;ROUTINE TO INPUT "N" OR FEWER CHARACTERS INTO INBLK
;FIRST WORD ALSO COPIED TO "WD"
;DELIMITER SAVED IN "CH"
GETSIX:	MOVE BP,[POINT 6,INBLK]
	SETZB	WD,INBLK
	MOVE	CH,[XWD INBLK,INBLK+1]
	BLT	CH,INBLK+17
SIXL:	PUSHJ P,GETONE
	JRST [		SETLCH ,[0]	;DELIMETER FOUND - RETURN
		TRZ F,SKPSP
		MOVE WD,INBLK
		POPJ  P,0]
	SUBI	CH," "
	SOJL N,SIXL
	IDPB CH,BP
	JRST SIXL


;ROUTINE TO INPUT 1 CHARACTER - SKIP RETURN IF CHARACTER NOT A DELIMITER
;IF SKPSP # 0, CR ONLY IS CONSIDERED A DELIMITER
;INPUT BUFFER IS CLEARED ON A CR
GETONE:	INCHWL	CH
	CAIN CH," "
	JRST GETONE
	CAIL CH,140
	SUBI	CH," "
	CAIN CH,15	;CARRIAGE RETURN?
	JRST	[INCHWL CH
		POPJ P,0]
	TRNE F,SKPSP
	JRST GOTONE
	CAIE CH,"("
	CAIN CH,":"
	POPJ P,0
	CAIN CH,")"
	POPJ P,0
	CAIE CH,"."
	JRST NOTDOT
	TRNN F,DOTOK
	POPJ P,0
	JRST GOTONE
NOTDOT:	TRNE F,STAROK
	CAIE CH,"*"
	SKIPA
	JRST GOTONE
	CAIGE CH,"0"
	JRST SYNERR
	CAIG CH,"9"
	JRST GOTONE
	CAIL CH,"A"
	CAILE CH,"Z"
	JRST SYNERR
GOTONE:	AOS (P)
	POPJ P,0


;ROUTINE TO INPUT A COMMAND STRING OF THE FORM:
;	(NAME:SYSTEM)FILENAME
;"SIDE" POINTS TO EITHER RBLK OR DBLK
;VALIDITY OF SYSTEM NUMBER IS CHECKED
GETFIL:	INCHWL CH
	CAIE CH,"("
	JRST SYNERR
	TRO F,STAROK+DOTOK
	MOVEI N,^D12
	PUSHJ P,GETSIX
	SKIPN A,INBLK
	JRST SYNERR
	TRZ F,STAROK+DOTOK
	MOVEM A,UNAME(SIDE)
	MOVE A,INBLK+1
	MOVEM A,UNAME+1(SIDE)
	CAIE CH,":"
	JRST SYNERR
	MOVEI N,2
	PUSHJ P,GETSIX
	JUMPL N,SYNERR	;NO GARBAGE AFTER SYSTEM NO.
	CAIE CH,")"
	JRST SYNERR
	PUSHJ P,GETSYS
	MOVEM CH,SYS(SIDE)
	MOVEI N,^D72
	TRO F,SKPSP
	PUSHJ P,GETSIX
	SKIPN INBLK
	JRST SYNERR
	CAIE CH,12
	JRST SYNERR
	HRLZI A,INBLK
	HRR A,SIDE
	ADDI A,FNAME
	HRRZ B,A
	ADDI B,^D15
	BLT A,@B
	POPJ P,0


;SYSTEM NUMBER IS COMPUTED AND CHECKED FOR VALIDITY
GETSYS:	MOVE A,INBLK
	AND A,[XWD 77,777777]
	JUMPN A,BADSYS
	MOVE BP,[POINT 6,INBLK]
	ILDB CH,BP
	SUBI CH,20
	ILDB B,BP
	JUMPE B,CHKSYS
	SUBI B,20
	IMULI CH,^D10
	ADD CH,B
CHKSYS:	CAMN CH,THISYS
	POPJ P,0
	CAILE  CH,MAXSYS
	JRST BADSYS
	MOVEI B,1
	CAIL CH,^D31
	MOVEI B,3
	MOVEM B,SLVTYP
	POPJ P,0


;ROUTINE TO MAKE A SIXBIT STRING ASCII
STRSIX:	MOVE C,[POINT  6,A]
	MOVEI B,6
	ILDB CH,C
	JUMPE CH,.+4
	ADDI CH,40	;MAKE ASCII
	IDPB CH,BP
	SOJG B,.-4
	POPJ P,0


;ROUTINE TO MAKE SYSTEM NUMBER ASCII
MAKSYS:	IDIVI A,^D10
	JUMPE A,.+3
	ADDI A,"0"
	IDPB A,BP
	ADDI B,"0"
	IDPB B,BP
	POPJ P,0


;ROUTINE TO DETERMINE THE LENGTH OF A SIXBIT STRING
;MAX. LENGTH IN "A"
;PTR IN "BP"
;RETURN SIZE OF STRING IN "N"
GETSIZ:	SETZB B,N
	ILDB CH,BP
	JUMPE CH,GETS2
	JUMPE B,GETS1
	ADD N,B
	AOJ N,
	SETZ B,
	JRST GETNXT
GETS1:	AOJA N,GETNXT
GETS2:	AOJ B,
GETNXT:	SOJG A,GETSIZ+1
	POPJ P,0


;ROUTINE TO BREAK DOWN A STRING IN FNAME INTO PDP-10
;FORMAT OF FILENAME AND EXTENSION IN DBLK+FILE OR RBLK+FILE
GET10:	MOVE BP,[POINT 6,FILE(SIDE)]
	MOVE B,[POINT 6,FNAME(SIDE)]
	MOVEI C,6
GETN:	ILDB CH,B
	CAIN CH,"."-40
	JRST GETEXT
	JUMPE CH,[POPJ P,0]
	PUSHJ P,CHKCHR
	IDPB CH,BP
	SOJG C,GETN
GET1:	CAIN CH,0
	POPJ P,0
	PUSHJ P,CHKCHR
	ILDB CH,B
	CAIN CH,"."-40
	JRST GETEXT
	JRST GET1
GETEXT:	SETZ CH,
	SOJL C,.+3
	IDPB CH,BP
	JRST .-2
	MOVEI C,3
	ILDB CH,B
	JUMPE CH,[POPJ P,0]
	PUSHJ P,CHKCHR
	IDPB CH,BP
	SOJG C,.-4
	POPJ P,0


CHKCHR:	CAIGE CH,"0"-40
	JRST NOOPN
	CAIG CH,"9"-40
	POPJ P,0
	CAIL CH,"A"-40
	CAILE CH,"Z"-40
	JRST NOOPN
	POPJ P,0

;ROUTINE TO BUILD A CIRCUIT, START UP SLAVE AND HANDSHAKE
GETCIR:	SETZM TIMFLG	;STOP IGNORING INTERRUPTS
	CAMN A,AUXCIR
	POPJ P,0
	SETOM TIMFLG	;DON'T PAY ATTENTION TO TIMEOUTTS NOW...
	HRLOI B,377777
	INTENB B,	;DISABLE ALL CHANNELS
	 JFCL
	SKIPE B,AUXCHN	;GET CHANNEL NO. OF EXISTING CIRCUIT
	ZAPCIR B,
	MOVEM A,AUXCIR
	MOVE BP,[POINT 7,AUXSTR]
	MOVE A,THISUSR
	PUSHJ P,STRSIX
	MOVE A,THISUSR+1
	PUSHJ P,STRSIX
	MOVEI CH,":"
	IDPB CH,BP
	MOVE A,AUXCIR
	PUSHJ P,MAKSYS
	MOVEI CH,";"
	IDPB CH,BP
	MOVEI A,AUXSTR
	CREAUX A,
	 JRST NOCIR
	MOVEM A,AUXCHN
	HRLS A
	HRRI A,4
	AUXCAL A,15	;2 CR'S IN CASE A DET. JOB ON PDP-10
	AUXCAL A,15
	HRRI A,AUXPTR
	MOVE C,SLVTYP
	MOVE B,[POINT 7,GOMES]	;PDP-10 MESSAGE
	CAIN C,1
	HRRI	B,GOMES1	;940 MESSAGE
	AUXCAL A,B		;SEND START-UP STRING
	MOVEI A,INTTAB	;INTERRUPT TABLE MESSAGE
	HRLI	A,6
	INTADR	A,	;DISABLE AND CLEAR INTERRUPT SYSTEM
	 JFCL
	HRRZ A,AUXCHN
	HRLI A,7001
	TINASS A,	;SET UP CIR. ZAP (7) ON CHANNEL 1
	 JFCL
	HRLI A,4
	HRRI A,2
	INTASS A,	;SET UP TIMER (4) ON CHANNEL 2
	 JFCL
	HRRZ A,AUXCHN
	HRLI A,5003
	TINASS A,	;SET UP CHARS. LOST ON INPUT (5) ON CHANNEL 3
	 JFCL
	MOVSI A,760000
	INTENB A,	;ENABLE INTERRUPT 1 AND 2
	 JFCL
	MOVE A,[XWD 1,^D120]	;2 MINUTES
	SETTIM A,
	 JFCL
	SETZM TIMFLG
	HRLZ A,AUXCHN
	HRRI A,1
TRYAGN:	AUXCAL A,CH
	JRST .-1
	CAIE CH,1
	JRST TRYAGN
	AUXCAL A,CH
	JRST .-1
	CAIE CH,2
	JRST TRYAGN
	AUXCAL A,CH
	JRST .-1
	CAIE CH,3
	JRST TRYAGN
	AUXCAL A,CH
	JRST .-1
	CAIE CH,4
	JRST TRYAGN
	AUXCAL A,CH
	JRST .-1
	CAIE CH,1
	CAIN CH,3
	SKIPA
	JRST F107
	MOVEM CH,SLVTYP
	HRLZ A,AUXCHN
	HRRI A,4
	AUXCAL A,1
	AUXCAL A,2
	AUXCAL A,3
	AUXCAL A,4
	AUXCAL A,3	;TELL SLAVE WE ARE A PDP-10
	POPJ P,0

TELLC:	MOVE A,SLVTYP
	CAIN A,3	;IF PDP-10 - RETURN
	POPJ P,0
	MOVE CH,LC
	PUSHJ P,PUTSPC
	POPJ P,0

;ROUTINE TO OUTPUT SYNC CHARACTER AND A CODE
PUTSPC:	PUSH P,CH
	MOVEI CH,252
	PUSHJ P,PUTCH
	POP P,CH
	PUSHJ P,PUTCH
	POPJ P,0


;ROUTINE TO INPUT SYNC CHARACTER AND A CODE
GETSPC:	PUSHJ P,GETCH
	CAIN CH,7	;BELL?
	JRST GETSPC	;IGNORE IT
	CAIE CH,252
	JRST F107
	PUSHJ P,GETCH
	CAIE CH,107
	POPJ P,0
F107:	OUTSTR [ASCIZ/
SYNCHRONIZATION PROBLEM - ABORTING.
/]
	PUSHJ P,ZAPIT
	JRST GETCMD


;ROUTINE TO OUTPUT 1 CHARACTER OVER AUX. CIRCUIT
PUTCH:	HRLZ	AUX,AUXCHN
	HRRI AUX,4
	AUXCAL AUX,(CH)
	POPJ P,0

;ROUTINE TO INPUT 1 CHARACTER FROM AUX. CIRCUIT
GETCH:	HRLZ AUX,AUXCHN
	HRRI AUX,1
	AUXCAL AUX,CH
	JRST .-1
	AOS TIMFLG	;KEEP COUNT OF CHARS. RECIEVED
	POPJ P,0

;ROUTINE TO OUTPUT LENGTH OF A NAME AND A SIXBIT NAME
SNDNAM:	PUSHJ P,PUTSPC
	PUSH P,BP
	PUSHJ P,GETSIZ
	POP P,BP
	MOVE CH,N
	PUSHJ P,PUTCH
	ILDB CH,BP
	ADDI CH,40	;SEND IN ASCII
	PUSHJ P,PUTCH
	SOJG N,.-3
	POPJ P,0


;ASK USER IF ITS OK TO WRITE ON OLD FILE
CHKOLD:	MOVE CH,TIMFLG
	MOVEM CH,SAVFLG
	SETOM TIMFLG
	OUTSTR	[ASCIZ/
OKAY TO WRITE ON OLD FILE? /]
	PUSHJ P,YESNO
	SKIPA
	POPJ P,0
	MOVEI CH,107
	TRZN F,NOSTRT	;CHECK TO SEE IF WE SHOULD TELL SLAVE OR NOT
	PUSHJ P,PUTSPC	;NOT OKAY TO COPY - TELL SLAVE TO FORGET IT
	JRST GETCMD

;TELL SLAVE TO OPEN FILE AND CHECK FOR GOOD OPEN
MAKOPN:	MOVEI CH,121
	PUSHJ P,PUTSPC
	PUSHJ P,GETSPC
	CAIE CH,104
	JRST BADOPN
	OUTSTR [ASCIZ/
COPY STARTING

/]
	POPJ P,0

YESNO:	INCHWL CH
	CAIN CH," "
	JRST .-2
	CAIE CH,"Y"
	CAIN CH,"Y"+40
	JRST [AOS (P)
		JRST Y1]
	CAIE CH,"N"
	CAIN CH,"N"+40
	JRST Y1
	OUTSTR [ASCIZ/
PLEASE TYPE 'Y' OR 'N': /]
	CLRBFI
	JRST YESNO
Y1:	MOVE CH,SAVFLG
	MOVEM CH,TIMFLG
WTLF:	CAIN CH,12
	POPJ P,0
	INCHWL CH
	JRST WTLF

;GET CONTROLLING USER'S NAME IN LUD AND KEEP HIS TYMSHARE BIT
GETUSR:	LOOKUP 1,LUDLOK
	JRST SYSERR
	PUSHJ P,HASHER
	MOVEM N,HUNAME
	MOVEM N1,HLOC
RD:	USETI 1,@HLOC
	INPUT 1,SYSLST
	STATZ 1,760000
	JRST SYSERR
	MOVE A,HUNAME
	SETZ INDEX,
SETDEX:	SKIPN ,SYSBLK(INDEX)
	POPJ P,0
	LDB C,[POINT 7,SYSBLK+2(INDEX),35]
	CAMN A,SYSBLK+4(INDEX)
	JRST [LDB A,[POINT 1,SYSBLK+3(INDEX),21]
		MOVEM A,TYMBIT
		AOS (P)
		POPJ P,0]
	SKIPG ,SYSBLK(INDEX)
	JRST .+3
	ADD INDEX,C
	JRST SETDEX
	HRRZ C,SYSBLK(INDEX)
	MOVEM C,HLOC
	JRST RD

HASHER:	MOVEI B,0
	MOVE C,[555555555555]
	MOVE D,[361275431652]
	MOVE E,[612754316523]
	JSR RND
	JSR RND
	JSR RND
	JSR RND
	XOR E,D
	MOVE  N,E
	TLZ N,400000
	IDIVI N,^D887
	MOVE N,E
	XOR N,C
	ADDI N1,1
	POPJ P,0

RND:	0
	ADD D,N
	ROTC N,-22
	MOVEI A,5
RND1:	MOVE N2,C+1(B)
	MUL N2,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
	ADDM N3,D+1(B)
	AOJE B,RND2
	MOVNI B,1
	TRNE D,1
	SKIPL E
	MOVEI B,0
	EXCH C,E
RND2:	SOJG A,RND1
	JRST  @RND

GOMES:	ASCIZ/SET LOGOUT
R FILTFR
/
GOMES1:	ASCIZ/
FILTFR
/


HELMSG:	ASCIZ/

LEGAL COMMANDS ARE:

  RUN		BEGINS EXECUTION
  ONLC		SET LOWER CASE MODE (FOR 10-940 ONLY)
  OFFLC		TURN OFF LOWER CASE MODE (DEFAULT)
  VERSION	LATEST UPDATE
  HELP (OR ?)	REPRINTS THIS LIST
  INSTRUCTIONS	HOW TO EXECUTE THIS PROGRAM
  QUIT		EXITS TO EXEC

ANY OF THESE COMMANDS MAY BE SHORTENED TO THE
SHORTEST UNIQUE COMMAND.
/

TYMHELP:	ASCIZ/

AS TYMSHARE PERSONNEL THERE IS ONE ADDITIONAL LEGAL COMMAND:

  TIME		SETS TIME SWITCH TO PROVIDE PROGRESS REPORTS
/

INSMSG:	ASCIZ/

WHEN THE RUN COMMAND IS ISSUED, THE PROGRAM WILL REQUEST THE
"COPY FROM" FILE AND THEN THE "COPY TO" FILE.  THE SYNTAX FOR
ENTERING THIS INFORMATION IS:
	(USERNAME:SYSTEM NUMBER)FILE NAME  C.R.
FOR EXAMPLE:  (JONES:7)DATA  C.R.

TO INPUT THE STRING WITHOUT THE PROMPTS, TYPE THE INFORMATION
DIRECTLY AFTER THE ":".

IF AN ATTEMPT IS MADE TO COPY TO AN EXISTING FILE, THE PROGRAM
WILL ASK FOR CONFIRMATION.

THE MESSAGE "COPY STARTING" WILL BE TYPED WHEN THE TRANSFER OF THE
DATA STARTS.

UPON COMPLETION, THE MESSAGE "FILE NAME COPIED" WILL BE
DISPLAYED ON THE TERMINAL.
/

TYMINS:	ASCIZ/

YOU ARE RECIEVING THE FOLLOWING PRINTOUT BECAUSE YOU HAVE
TYMSHARE LICENSE:

IN ADDITION TO THE COMMANDS LISTED ABOVE, THE FOLLOWING
APPLIES TO TYMSHARE PERSONNEL ONLY:

YOU MAY RECIEVE PROGRESS REPORTS PERIODICALLY BY TYPING
TIME WHEN AT THE COMMAND DISPATCHER.  IT IS ONLY NECESSARY
TO DO THIS ONCE TO SET THE SWITCH.  THIS WILL ALSO REPORT
THE RATE PER SECOND CHARACTERS ARE BEING TRANSFERRED.
/
;ERROR MESSAGES
ERRRET:	GETCMD
	SYSERR	;OUT OF SYNC
	SYSERR	;DISK ERROR
	SYSERR	;TOO MANY RETRIES

TIMADR:	MOVEM A,SAVA
	SKIPN A,TIMFLG
	JRST	[OUTSTR	[ASCIZ/
CIRCUIT NOT RESPONDING - ABORTING.
/]
		PUSHJ P,ZAPIT
		SETZM RETFLG
		JRST TIM1]
	SKIPL TIMFLG
	SETZM TIMFLG
	SETOM RETFLG
TIM1:	MOVE A,[XWD 1,^D120]
	SETTIM A,
	 JFCL
	MOVE A,SAVA
	SKIPE RETFLG
	DISMIS
	MOVEI A,GETCMD
	MOVEM A,INTTAB+2
	DISMIS

BUFZAP:	MOVEI CH,120	;SAY BUFFER ZAPPED
	PUSHJ P,PUTSPC
NXT:	PUSHJ P,GETCH
	CAIE CH,252	;LOOP TIL 252,120 IS FOUND
	JRST NXT
	PUSHJ P,GETCH
	CAIN CH,107
	JRST F107
	CAIE  CH,120
	JRST NXT
	MOVE CH,RETZAP	;GET RETURN ADDRESS
	MOVEM CH,INTTAB+4
	DISMIS

ZAPADR:	OUTSTR	[ASCIZ/
CIRCUIT ZAPPED - ABORTING.
/]
	SETZM AUXCHN
	SETZM AUXCIR
	MOVEI A,GETCMD
	MOVEM A,INTTAB
	DISMIS

ZAPIT:	SETOM TIMFLG
	HRLOI B,377777
	INTENB B,
	JFCL
	MOVE A,AUXCHN
	ZAPCIR A,
	SETZM AUXCHN
	SETZM AUXCIR
	POPJ P,0

CMDTAB:	SIXBIT /RUN/
	SIXBIT /VERSIO/
	SIXBIT /HELP/
	SIXBIT /INSTRU/
	SIXBIT /QUIT/
	SIXBIT /ONLC/
	SIXBIT /OFFLC/
	SIXBIT /TIME/
CMDSIZ==.-CMDTAB

CMDISP:	RUNROU
	VERSION
	HELP
	INSTRU
	ENDIT
SETLC
	RESTLC
	SETIME

SYNERR:	OUTSTR	[ASCIZ/
SYNTAX ERROR - PLEASE TYPE INSTRUCTIONS.
/]
	CLRBFI
	JRST GETCMD

BADSYS:	OUTSTR	[ASCIZ/
TELECOPY NOT IMPLEMENTED ON THAT SYSTEM.
/]
	CLRBFI
	JRST GETCMD

SYSERR:	OUTSTR	[ASCIZ/
SYSTEM ERROR - ABORTING.
/]
	PUSHJ P,ZAPIT
	JRST GETCMD

CIR2:	OUTSTR	[ASCIZ/
YOU MUST EITHER COPY FROM OR COPY TO SYSTEM /]
	MOVE A,THISYS
	PUSHJ P,OUTDEC
	PUSHJ P,CRLF
	JRST GETCMD

CIR0:	OUTSTR	[ASCIZ/
"COPY TO" AND "COPY FROM" SYSTEM MUST NOT BE THE SAME.
/]
	JRST GETCMD

BADNAM:	OUTSTR	[ASCIZ/
USER NAME INVALID OR NO UFD/]
	JRST FINERR

BADOPN:	CAIN CH,116
	JRST PROERR
	OUTSTR	[ASCIZ/
CANNOT OPEN FILE /]
	PUSHJ P,FILOUT
	JRST FINERR


BADFIL:	CAIE CH,116
	JRST FNFERR
PROERR:	OUTSTR	[ASCIZ/
PROTECTION FAILURE - FILE /]
	PUSHJ P,FILOUT
	JRST FINERR

NOSYSN:	OUTSTR	[ASCIZ/
CANNOT GET THIS SYSTEM NUMBER.
CALL Q.A.
/]
	EXIT

FNFERR:	OUTSTR	[ASCIZ/
UNABLE TO LOCATE FILE /]
	PUSHJ P,FILOUT
	JRST FINERR

CHKERR:	HRRZ B,ERR(SIDE)
	JUMPE B,FNFERR
	CAIN B,1
	JRST BADNAM
	CAIN A,2
	JRST PROERR
	JRST BADOPN+2

;OUTPUT SIXBIT FILE NAME TO TTY
FILOUT:	MOVE BP,[POINT 6,FNAME(SIDE)]
	ILDB CH,BP	;GET A SIXBIT CHARAACTERR
	JUMPE CH,[POPJ P,0]	;IF BLANK - THAT'S ALL
	ADDI CH,40	;MAKE ASCII
	OUTCHR CH	;OUTPUT IT TO TTY
	JRST FILOUT+1	;AND GET NEXT

FINERR:	OUTSTR [ASCIZ/ ON SYSTEM /]
	MOVE A,SYS(SIDE)
	PUSHJ P,OUTDEC
	PUSHJ P,CRLF
	JRST GETCMD

NOCIR:	SETZM AUXCIR
	HLRZ B,A
	JUMPE B,SUPERR
	CAIL B,ER10NM
	JRST ENX10
	OUTSTR @ERR10(B)
	JRST GETCMD

ENX10:	OUTSTR	[ASCIZ/
UNRECOGNIZED ERROR FROM MONITOR NUMBER /]
	MOVE A,B
COMAXR:	PUSHJ P,OUTDEC
	PUSHJ P,CRLF
	JRST GETCMD
ERR10:	0
	[ASCIZ /
TOO MANY CIRCUITS
/]
	[ASCIZ/
BAD USER NAME
/]
	[ASCIZ/
OUT OF CHANNELS FROM HOST

	[ASCIZ/
NO RESPONSE TO REQUEST
/]
	[ASCIZ/
NO RESPONSE TO STRING
/]
	[ASCIZ/
NO CIRCUIT ESTABLISHED
/]
	[ASCIZ/
ERROR RESPONSE TO REQUEST
/]
ER10NM=.-ERR10

SUPERR:	CAIL A,ERSPNM
	JRST ENXSUP	;UNRECOGNIZED SUP RESPONSE
	OUTSTR @ERRSUP(A)
	JRST GETCMD

ENXSUP:	OUTSTR [ASCIZ/
UNRECOGNIZED ERROR FROM SUPERVISOR - NUMBER /]
	JRST COMAXR

ERRSUP:	0
	[ASCIZ/
BAD NAME FORMAT
/]
	[ASCIZ/
NAME NOT IN MUD
/]
	[ASCIZ/
BAD MUD
/]
	[ASCIZ/
SYSTEM UNAVAILABLE
/]
ERSPNM==.-ERRSUP

CRLF:	OUTSTR [ASCIZ/
/]
	POPJ P,0

OUTDEC:	IDIVI A,^D10
	HRLM B,(P)
	SKIPE A
	PUSHJ P,OUTDEC
	HLRZ A,(P)
	OUTCHI "0"(A)
	POPJ P,0

LUDLOK:	SIXBIT /LUD/
	SIXBIT /SYS/
	0
	0
SYSNO:	SIXBIT/BILDAT/
	SIXBIT  /CTL/
	0
	1,,44355
SYSLST:	IOWD 200,SYSBLK
	0
INTTAB:	0
	ZAPADR
	0
	TIMADR
	0
	BUFZAP
	0
	ALTTYP
	BLOCK ^D62
AUXCIR:	0	;SYSTEM CIRCUIT IS BUILT TO
AUXCHN:	0	;CHANNNEL OF AUX.CIR.
THISYS:	0	;THIS SYSTEM NO.
SLVTYP:	0	;TYPE OF SLAVE(1=940,3=PDP)
TIMFLG:	0	;NO. OF CHARS RECIEVED SINCE LAST TIME INTERRUPT
		;IF -1, IGNORE SINCE WE ARE WAITING FOR TTY INPUT
SAVFLG:	0	;SAVE PREVIOUS VALUE OF TIMFLG
HLOC:	0
HUNAME:	0
THISUSR:	BLOCK 2	;CONTROLLING USER
TYMBIT:	0	;AND HIS TYMSHARE BIT (FROM LUD)
TYPTIM:	0	;IF # 0, TYMSHARE USER WANTS STATS TYPED.
LC:	OFFLC


;TO BE ZEROED BETWEEN RUNS
ZERO:	0
NOPRMT:	0	;0 IF PROMPT IS NEDED
DBLK:	BLOCK ^D23
RBLK:	BLOCK ^D23
AUXSTR:	BLOCK 7
INBLK:	BLOCK ^D15
SYSBLK:	BLOCK ^D161
SIZDON:	0	;SIZE OF DONOR FILE IN WORDS.
RETZAP:	0	;ADDRESS FOR RETURN FROM BUFFER LOST
SAVA:	0	;SAVE AC ON INTERRUPT
RETFLG:	0	;RETURN TO GETCMD IF = 0, DISMISS IF # 0
ZEREND:	0

PDL:	BLOCK 40
END	START

 Y@ ä