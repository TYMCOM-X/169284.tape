TITLE	BASIC    G.C. REVISION	23-JAN-1970 NANCY AVNER
SUBTTL		PARAMETERS AND TABLES
;UDATED WITH VERSION RECEIVED FROM APPLIED LOGIC ON 11-FEB-69
;FILE  CAPABILITIES BY T.MCGETTIGAN 18-MAR-69
;EDITED BY N. PAPPAS (COURTESY OF CCC) 25-MAR-69
;CYPH "OLD" PATCH 28 MAR 69
;ALC FIXES OF 31-MAR-69 BY N. PAPPAS ON 3-APR-69
;ISI MODIFICATIONS 1 MAY 70/DOH

	JOBVER=137
	VERNUM=13
	GCREV==6	;GC REVISION NO.

	LOC JOBVER
	XWD	GCREV,VERNUM
	RELOC
	HISEG
;LOADING INSTRUCTIONS:  ASSEMBLE AND LOAD THIS FILE.
;IF LOADING WITH DDT, SYMBOLS SHOULD BE MOVED BELOW LH(JOBSA)

;CONDITIONAL ASSEMBLY PARAMETERS
;  THE VERSION OF BASIC SUPPLIED BY APPLIED LOGIC CORPORATION
;  IS ASSEMBLED WITH THE PARAMETERS BELOW SET TO -1.  APPLIED
;  LOGIC CORPORATION HAS NOT NECESSARILY TESTED VERSIONS OF
;  BASIC ASSEMBLED OTHERWISE AND DOES NOT GUARANTEE TO
;  MAINTAIN ANY SUCH VERSIONS.

FTRND=-1		;-1 IF RANDOM FACILITY, ELSE 0
FTMAT=-1		;-1 IF MATRIX FACILITY, ELSE 0
FTSTR=-1		;-1 IF STRING FACILITY, ELSE 0

	MLON
;AC DEFINITIONS
			;PRINCIPAL USES:
F=7			;FLAGS
T=1			;POINTER TO NXCH
T1=2
A=3			;SEARCH ARGUMENT
B=4			;POINTER AFTER SEARCH
C=5			;XWD CHARACTER-FLAGS,CHAR
D=6			;BUILD INSTS HERE
N=0			;RUNTIME ACCUMULATOR REGISTER
R=12			;POINTER TO ROLL BEING USED
LP=16
L=16
E=10
G=11
X1=13			;)
X2=14			;)TEMP REGS
X3=15			;)
P=17			;PUSHDOWN LIST

;OP CODE ROLL
	EXTERNAL RESWRD

	EXTERNAL NEWOL1,SPEC,CORN,CORNTO,NLN,STOR0,STOR1,TFLAG
	EXTERNAL CECAD,CECON,CEFCL,CEFOR,CEGSB,CENXT,CESCA,CEARA
	EXTERNAL	CEPTM,CESAD,CESEX,CESTM,CEVSP,FLARA
	EXTERNAL	FLCAD,FLCON,FLFCL,FLFOR,FLGSB,FLLIT,FLFCL
	EXTERNAL	FLNXT,FLPTM,FLSAD,FLSCA,FLSEX,FLSTM,FLVSP
	EXTERNAL	CELAD,FLLAD
	EXTERNAL	CECOD,FLCOD,CETMP,FLTMP,CEARG,FLARG,CESVR,FLSVR
	EXTERNAL CETXT,FLTXT,CELIN,FLLIN
	EXTERNAL	ARAROL ,ARATOP ,ARGROL ,C3 ,CADROL ,CEIL ,CMDROL ,CODROL 
	EXTERNAL	COMTOP ,CONROL ,CURDEV ,CUREXT ,CURNAM ,DATAFF ,DATLIN ,DECROL 
	EXTERNAL	DETER ,DRMBUF ,ELETOP ,ES2 ,FADROL ,FCLROL ,FCNLNK ,FCNROL 
	EXTERNAL	FILDIR ,FLOOR ,FMTPNT ,FORROL ,FRSTLN ,FUNAME ,FUNLOW ,FUNSTA 
	EXTERNAL	GSBROL ,HPOS ,IFNROL ,INPFLA ,JOBFF ,JOBOPC ,JOBREL ,JOBREN
	EXTERNAL	JOBSA ,LADROL ,LASTLN  ,LINROL ,LITROL 
	EXTERNAL	LOWEST ,LOWSTA ,LZ ,MINFLG ,MORFLA ,NUMFLA ,NUMRES ,NXTROL 
	EXTERNAL	OLDFLA ,ONCESW ,PAKFLA ,PINPUT ,PIVOT ,PLIST ,PREAD ,PSHPNT 
	EXTERNAL	PSHROL ,PTMROL ,REGPNT ,RELROL ,RENFLA ,ROLMSK ,ROLTOP ,RUNFLA 
	EXTERNAL	SADROL ,SB1M1 ,SB2M1 ,SCAROL ,SEQPNT ,SEXROL ,STAROL ,STMROL 
	EXTERNAL	STRFLA ,SVRBOT ,SVRROL ,SVRTOP ,SX ,TABVAL ,TEMP1 ,TEMP2 
	EXTERNAL	TEMP3 ,TMPLOW ,TMPPNT ,TMPROL ,TOPSTG ,TTYBUF ,TXTROL
	EXTERNAL	TYO ,VARBOT ,VARFRE ,VARROL ,VECT1 ,VECT2 ,VPAKFL ,VRANX 
	EXTERN	VSPROL,TYI,CONLIN,FLDSPC,FLDLEN,FMTFLG,CONCTR
	EXTERN	LETSW,PDP6SW,MTIME,DSKIFL,DSKOFL,IBDSK,MATFLG
	EXTERN	REFROL,PRJPRG,FILEXT,PRNTSW,SHRTFL,CONLOC,USEFLG,CONFLG
	EXTERN	VRANT,FLREF,CEREF,REFROL,EOFLOC


IFNDEF INTRSW,<INTRSW==0>	;SET ZERO FOR NO INTERUPT HANDLING CODE




;FLAGS IN LEFT HALF OF CTTAB+<LETTER> FOR <LETTER> BELOW 100,	
;FLAGS IN RIGHT HALF OF CTTAB+<LETTER-100> OTHERWISE.

DEFINE WWW (FL,VAL)<
	XLIST
FL=<	Z	0,(VAL)>
	LIST>

WWW F.APOS,1B0		;	'
WWW F.COMA,1B1		;	,
WWW F.CR,1B2		;	<RETURN, OR LF,VT,FFEED>
WWW F.DIG,1B3		;	<NUMERAL>
WWW F.DOLL,1B17
WWW F.EQAL,1B4		;	=
WWW F.ESC,1B5		;	<ESCAPE OR ALTMODE>
WWW F.LCAS,1B6		;	<LOWER CASE LETTER>
WWW F.LETT,1B7		;	<LOWER OR UPPER CASE LETTER>
WWW F.STR,1B8		;	(
WWW F.MINS,1B9		;	-
WWW F.PER,1B10		;	.
WWW F.PLUS,1B11		;	+
WWW F.QUOT,1B12		;	"
WWW F.RPRN,1B13		;	)
WWW F.SLSH,1B14		;	/
WWW F.STAR,1B15		;	*
WWW F.SPTB,1B16		;	<SPACE OR TAB>

F.NU=0			;ASCII CODES THAT ARE TREATED AS NULLS.
F.OTH=0			;OTHER CHARACTERS ANALYSED BY BASIC WITHOUT THE USE OF FLAGS.

F.TERM=F.CR+F.APOS	;EITHER TERMINATES THE ANALYZABLE PORTION OF A BASIC STATEMENT.

CTTAB:
	XWD	F.NU,	F.STR	;NULL	, @
	XWD	F.STR,	F.LETT	;	, A
	XWD	F.STR,	F.LETT	;	, B
	XWD	F.STR,	F.LETT	;	, C
	XWD	F.STR,	F.LETT	;	, D
	XWD	F.STR,	F.LETT	;	, E
	XWD	F.STR,	F.LETT	;	, F
	XWD	F.STR,	F.LETT	;	, G
	XWD	F.STR,	F.LETT	;	, H
	XWD	F.SPTB,	F.LETT	;TAB	, I
	XWD	F.CR,	F.LETT	;LF	, J
	XWD	F.CR,	F.LETT	;VER.TAB, K
	XWD	F.CR,	F.LETT	;FFEED	, L
	XWD	F.CR,	F.LETT	;CR	, M
	XWD	F.STR,	F.LETT	;	, N
	XWD	F.STR,	F.LETT	;	, O
	XWD	F.STR,	F.LETT	;	, P
	XWD	F.STR,	F.LETT	;	, Q
	XWD	F.STR,	F.LETT	;	, R
	XWD	F.STR,	F.LETT	;	, S
	XWD	F.STR,	F.LETT	;	, T
	XWD	F.STR,	F.LETT	;	, U
	XWD	F.STR,	F.LETT	;	, V
	XWD	F.STR,	F.LETT	;	, W
	XWD	F.STR,	F.LETT	;	, X
	XWD	F.STR,	F.LETT	;	, Y
	XWD	F.STR,	F.LETT	;	, Z
	XWD	F.ESC,	F.STR	;ESC	, [
	XWD	F.STR,	F.STR	;	, \
	XWD	F.STR,	F.STR	;	, ]
	XWD	F.STR,	F.OTH	;	, ^
	XWD	F.STR,	F.OTH	;	, _

	XWD	F.SPTB,	F.STR		;SPACE	, <ACCENT GRAVE>
	XWD	F.STR,	F.LETT+F.LCAS	; !	, <LOWER CASE> A
	XWD	F.QUOT,	F.LETT+F.LCAS	; "	, <LOWER CASE> B
	XWD	F.STR,	F.LETT+F.LCAS	; #	, <LOWER CASE> C
	XWD	F.DOLL,	F.LETT+F.LCAS	; $	, <LOWER CASE> D
	XWD	F.STR,	F.LETT+F.LCAS	; %	, <LOWER CASE> E
	XWD	F.OTH,	F.LETT+F.LCAS	; &	, <LOWER CASE> F
	XWD	F.APOS,	F.LETT+F.LCAS	; '	, <LOWER CASE> G
	XWD	F.OTH,	F.LETT+F.LCAS	; (	, <LOWER CASE> H
	XWD	F.RPRN,	F.LETT+F.LCAS	; )	, <LOWER CASE> I
	XWD	F.STAR,	F.LETT+F.LCAS	; *	, <LOWER CASE> J
	XWD	F.PLUS,	F.LETT+F.LCAS	; +	, <LOWER CASE> K
	XWD	F.COMA,	F.LETT+F.LCAS	; , 	, <LOWER CASE> L
	XWD	F.MINS,	F.LETT+F.LCAS	; -	, <LOWER CASE> M
	XWD	F.PER,	F.LETT+F.LCAS	; .	, <LOWER CASE> N
	XWD	F.SLSH,	F.LETT+F.LCAS	; /	, <LOWER CASE> O
	XWD	F.DIG,	F.LETT+F.LCAS	; 0	, <LOWER CASE> P
	XWD	F.DIG,	F.LETT+F.LCAS	; 1	, <LOWER CASE> Q
	XWD	F.DIG,	F.LETT+F.LCAS	; 2	, <LOWER CASE> R
	XWD	F.DIG,	F.LETT+F.LCAS	; 3	, <LOWER CASE> S
	XWD	F.DIG,	F.LETT+F.LCAS	; 4	, <LOWER CASE> T
	XWD	F.DIG,	F.LETT+F.LCAS	; 5	, <LOWER CASE> U
	XWD	F.DIG,	F.LETT+F.LCAS	; 6	, <LOWER CASE> V
	XWD	F.DIG,	F.LETT+F.LCAS	; 7	, <LOWER CASE> W
	XWD	F.DIG,	F.LETT+F.LCAS	; 8	, <LOWER CASE> X
	XWD	F.DIG,	F.LETT+F.LCAS	; 9	, <LOWER CASE> Y
	XWD	F.OTH,	F.LETT+F.LCAS	; :	, <LOWER CASE> Z
	XWD	F.OTH,	F.STR		; ;	, <LEFT BRACE>
	XWD	F.OTH,	F.STR		; <	, <VERTICAL BAR>
	XWD	F.EQAL,	F.STR		; =	, <RIGHT BRACE>
	XWD	F.OTH,	F.STR		; >	, <TILDE>
	XWD	F.STR,	F.STR		; ?	, <RUBOUT>

DEFINE FAIL (A,AC)<
	XLIST
	XWD	001000+AC'00,[ASCIZ /A/]
	LIST
>


	EXTERN	LINNUM,LNINS,CCLFLG,CCRNFL,FDXHDX
	EXTERNAL TABFG,PINPNM
	SUBTTL	COMMAND SCANNER AND EDITOR
;COLD START

	EXTERNAL SKFLG,IFIFG,ODF,INITDN
	LPT=12
BASIC:	EXTERNAL 	CHRFLA
	SETZM	CHRFLA		;TOM S. FOR BLANK AS FILE DELIMETER
	CALLI	0
	MOVE	P,PLIST
	SETOM	RENFLA		;ALLOW REENTERS.
	HLRZ	T,JOBSA
	MOVEM	T,FLTXT		;TXTROL ON BOTTOM OF FREE SPACE
	MOVEM	T,CETXT
	MOVE	T,JOBREL	;LINROL ON TOP
	MOVEM	T,FLLIN
	MOVEM	T,CELIN
	HRLM	T,JOBSA		;MAKE SAVE COMMAND SAVE ALL
	HRRZI	T,REENTR
	HRRM	T,JOBREN
	MOVE	T, CELIN
	MOVEI	X1, LINROL
	PUSHJ	P, CLOB		;CLOBBER ALL HIGHER ROLLS WITH "CELIN"


BASI1:	PUSHJ	P,TTYIN		;SET UP BUFFERS AND INIT TTY
	JRST	FIXUP+1

FIXUP:	OUTPUT			;WRITE LAST MESSAGE
	EXTERN JOBAPR,JOBTPC
	MOVEI	X1,12
CLR:	SETOM	LNINS-1(X1)
	SOJG	X1,CLR
	MOVEI	X1,TXTROL
	MOVEM	X1,TOPSTG	;EDIT TIME. ONLY TXTROL IS STODGY.
;				;OTHER ROLLS MOVE.
				;FALL INTO MAINLP
;MAIN LOOP FOR EDITOR/MONITOR

	EXTERNAL NOSKIP
MAINLP:	MOVE P,PLIST
	PUSHJ	P,LOCKOF		;TURN OFF REENTR LOCK
CCLRUN:	MOVE T1,[XWD 1,RESWRD]
	CALL T1,[SIXBIT/TMPCOR/]
	JRST	NOTEMP		;NO FILE, (OR NO TMPCOR UUO!)
	SETOM	OLDFLA
	JRST	NEWOL0

	EXTERNAL ERRFLG
	EXTERNAL IFILE,FRSTSEQ,STSEQ,ENDSEQ,SEQINC,NEWSEQ
	EXTERNAL OFILE,LINB0
NOTEMP:	CALLI	N, 30	;GET JOB NR
	MOVEI	A, 3	;CONVERT TO SIXBIT
	IDIVI	N, ^D10
	ADDI	T, 20
	LSHC	T, -6
	SOJG	A, .-3	;FORCE 3 DIGITS
	HRRI T1,(SIXBIT/RES/)
	MOVSI	A, (SIXBIT/TMP/)
	MOVSI	N, (SIXBIT/DSK/)
	MOVEM	N, NEWOL1
	MOVEI 0,1
	MOVEM 0,SPEC
	SETZM NEWOL1+1
	OPEN SPEC
	JRST NODEVC
	LOOKUP 0,T1
	JRST NOCUR
	INPUT 0,RESWRD+1
	SETOM	OLDFLA
	JRST	CCLOLD


	INTERNAL CMDFLO,STAFLO,DECFLO,RELFLO,IFNFLO,CMDCEI,STACEI
INTERNAL DECCEI,RELCEI,IFNCEI
CMDFLO:STAFLO:DECFLO:RELFLO:IFNFLO:0
CMDCEI:STACEI:DECCEI:RELCEI:IFNCEI:0
CORCLB:	SETZM	CONLOC
	MOVEI	X1, TXTROL
	MOVEM	X1, TOPSTG
TCLOB:	PUSH	P, T
	MOVE	T, CELIN
	MOVEI	X1, LINROL
	PUSHJ	P, CLOB
	MOVEM	T, VARBOT
	MOVEM	T, VARFRE
	POP	P, T
	POPJ	P,
;HERE ON EDIT COMMAND

	F.WIPE==1
	F.RBT=1		;RUBOUT BEING PROCESSED
	F.2ND=2 		;2ND OR SUBSEQUENT RUBOUT SEEN

GETT2:	SETZM	BADGNN
	INBUF	1


GETT1:	PUSHJ	P,INLINE
	SETOM NOSKIP
	PUSHJ	P,GETNUM
	JRST	NONMBR
	MOVEM	N,BADGNN		;LAST GOOD LINE WEAVED
	PUSHJ	P,LOCKON
	PUSHJ	P,ERASE
	PUSHJ	P,INSERT
	PUSHJ	P,LOCKOF
	JRST	GETT1



NONMBR:	TLNN	C, F.TERM	;WAS THIS A BLANK LINE?
	JRST	BADGET	;NO
	JRST	GETT1	;YES.  IGNORE.
CCLOLD:NEWOL0:	PUSHJ	P, CORCLB
	MOVEI 0,1
	MOVEM 0,SPEC
	MOVEI 0,TYI
	MOVEM 0,NEWOL1+1
	OPEN	SPEC
	JRST	NODEVC		;ILLEGAL DEV NAME. BOMB CURNAM.
	LOOKUP IFILE
	JRST	NOCUR
	MOVE 0,IFILE
	MOVEM 0,CURNAM
	MOVE	C, NEWOL1
	CALLI	C, 4		;GET CHARACTERISTICS FOR THIS DEV.
	TLNN	C, 2		;CAPABLE OF DOING INPUT?
	JRST	NOIN		;NO!  COMPLAIN
NEWOL2:	MOVE	C,[XWD F.CR,15]
	PUSHJ	P,LINL1		;HAVING ACCEPTED THE NAME, DO A "DELETE"
	PUSHJ	P,SCRER1
	MOVE	X1,NEWOL1
	MOVEM	X1,CURDEV
	JRST	GETT2	;NOW GET THE OLD FILE

;ROUTINE TO CHANGE CURRENT NAME

;THE FLAGS ARE
			REST.F=1	;COPY  THE REST (APOST SEEN)
			TOQU.F=2	;COPY TO QUOTE SIGN
			COMM.F=4	;LINE NUMBER FOLLOWS ANY COMMA
			NUM.F=10	;NEXT NUMBER IS LINE NUMBER

		USE.F==20	;PRINT USE UR USING STATEMENT.

SCRER1:	MOVE	X1,FLTXT
	MOVEM	X1,CETXT
	MOVE	X1,FLLIN
	MOVEM	X1,CELIN
	PUSHJ	P, CORCLB
	POPJ	P,


SUBTTL	COMMAND SUBROUTINES
;ROUTINE TO PICK UP FILE NAME AND SET UP FOR DSK ACTION

;ROUTINE TO CONVERT NEXT ATOM TO SIXBIT
;ROUTINE TO SET LINE LIMITS


;A NONPRINTING ROUTINE SIMILAR TO PRTNUM:

MAKNUZ: SETZM @SEQPNT ;CLEAR JUNK BEFORE LINE NO CALC
MAKNUM:	IDIVI	X1,^D10
	JUMPE	X1,MAKN1
	PUSH	P,X2
	PUSHJ	P,MAKNUM
	POP	P,X2
MAKN1:	MOVEI	X2,60(X2)
	IDPB	X2,SEQPNT
	POPJ	P,

;ROUTINE TO ERASE LINE.  LINE NO IN N.

ERASE:	HRLZ	A,N		;LOOK FOR LINE
	MOVEI	R,LINROL
	PUSHJ	P,SEARCH
	POPJ	P,		;NONE.  GO TO INSERTION

	MOVE	D,(B)		;PICK UP LOC OF LINE
	HRLI	D,440700	;MAKE BYTE POINTER
	MOVEI	T1,0		;TO USE IN DEPOSITING
ERAS1:	ILDB	C,D		;GET CHAR
	DPB	T1,D		;CLOBBER IT
	CAIE	C,15		;CARRIAGE RET?
	JRST	ERAS1		;NO.  GO FOR MORE

	SETOM	PAKFLA		;MARK FACT THAT THERE IS A HOLE

	MOVEI	E,1		;REMOVE ENTRY FROM LINE TABLE
	JRST	CLOSUP

;HERE WE HAVE A LINE OF INPUT AND THERE IS NO EXISTING LINE

INSERT:	MOVE	T1,[POINT 7,LINB0]
	MOVE	T,G		;RESTORE PNTR TO 1ST CHR
INSE2:	ILDB	C,T		;GET NEXT CHAR
INSE3:	CAIN	C,15		;CHECK FOR CAR RET
	JRST	INSE4
	IDPB	C,T1
	JRST	INSE2

INSE4:	JUMPL	T1,CPOPJ	;CR SEEN.  DONE IF JUST DELETION
	IDPB	C,T1		;STORE THE CR
	MOVEI	C,0		;CLEAR REST OF WORD
	TLNE	T1,760000
	JRST	.-3
	JRST	NEWLIN

;AT THIS POINT, N CONTAINS A LINE NUMBER AND LINB0 CONTAINS
;A NON-EMPTY INSERTED LINE.  T1 CONTAINS ADDRESS OF LAST
;WORD OF THE LINE.

NEWLIN:	MOVEI	T1,(T1)		;COMPUTE LINE LENGTH
	SUBI	T1,LINB0-1

	ADD	T1,CETXT	;COMPUTE NEW CEILING OF TEXT ROLL
	CAMGE	T1,FLLIN	;ROOM FOR LINE PLUS LINROL ENTRY?
	JRST	NEWL1		;YES
NEWL0:	SUB	T1,CETXT	;ASK FOR MORE CORE
	MOVE	E,T1
	ADDI	E,1
	PUSHJ	P,PANIC
	ADD	T1,CETXT

NEWL1:	MOVE	D,CETXT	;LOC OF NEW LINE
	MOVE	T,D		;CONSTRUCT BLT PNTR
	HRLI	T,LINB0
	BLT	T,-1(T1)	;MOVE THE LINE
	MOVEM	T1,CETXT	;STORE NEW CEILING


;HERE, LINE IS IN PLACE, ITS LOC IN D, LINE NUMBER IN N.
;MUST STILL PUT LINE NUMBER IN LINROL.

NEWNBR:	PUSH	P,D		;*****JUST IN CASE*****
	MOVEI	R,LINROL
	HRLZ	A,N
	PUSHJ	P,SEARCH
	JRST	.+2
	HALT	.		;*****IMPOSSIBLE CONDITION*****

	MOVEI	E,1
	PUSHJ	P,OPENUP	;MAKE ROOM FOR IT
	POP	P,D		;*****OTHER HALF OF JUST IN CASE*****
	HRRI	A,(D)		;CONSTRUCT LINROL ENTRY
	MOVEM	A,(B)		;STORE ENTRY
	POPJ	P,		;ALL DONE

SUBTTL COMMAND ERROR MESSAGES
COMM1:	PUSHJ	P,INLMES
	ASCIZ /WHAT?
/
COMM1B:	OUTPUT
	RESET
	PUSHJ	P, TTYIN
	PUSHJ	P, INLMES
	ASCIZ/
/
	CALLI 12

NOCUR:	HRRZ	A,IFILE+1
	JUMPE	A, NOGET
	PUSHJ	P, TTYIN
	PUSHJ	P, INLMES
ASCIZ/FILE IN USE OR PROTECTED
/
	JRST	COMM1B

NOGET:	PUSHJ	P,TTYIN
	PUSHJ	P,INLMES
	ASCIZ	/FILE NOT SAVED
/
	JRST	COMM1B
NOOUT:	PUSHJ	P, TTYIN
	PUSHJ	P, INLMES
	ASCIZ	/CANNOT OUTPUT TO THIS DEVICE
/
	JRST	COMM1B

NOIN:	PUSHJ	P, TTYIN
	PUSHJ	P, INLMES
	ASCIZ	/CANNOT INPUT FROM THIS DEVICE
/
	JRST	COMM1B


TTYIN:	SETZM	TABFG		;FORMAT ON TABS
	MOVEI	T,TTYBUF	;SET UP TTY BUFFS
	MOVEM	T,JOBFF
	INIT	1
	SIXBIT	/TTY/
	XWD	TYO,TYI
	HALT	.-3
	INBUF	1
	OUTBUF	1
	POPJ	P,

	EXTERN BADGNN,BADMSG,CRLFMS
BADGET:	MOVEI	T,BADMSG
	MOVE	X1,[POINT 7,BADGNN]
	MOVEM	X1,SEQPNT
	MOVE	X1,BADGNN		;LAST GOOD LINE NUMBER.
	TLNN	X1,-1		;HAS IT BEEN CHANGED ALREADY?
	PUSHJ P,MAKNUZ  ;NO, MAKE THE NUMBER
	CALL	T,[SIXBIT /DDTOUT/ ]
	MOVEI	T,CRLFMS
	CALL	T,[SIXBIT /DDTOUT/ ]
	JRST	GETT1


NODEVC:
NODEV:
	PUSHJ	P,TTYIN
	PUSHJ	P,INLMES
	ASCIZ /NO SUCH DEVICE
/
	JRST	COMM1B




;HERE ON UNDECODABLE COMMAND. MAY BE A STATEMENT.  HANDLE AS SHORT PROGRAM

	EXTERN	SHRTFL

LASTCH:	PUSHJ P,BACK1
	LDB C,T
	HLL C,CTTAB(C)
	TRNE C,100
	HRL C,CTTAB-100(C)
	TLNE C,F.SPTB	;SPACE OR TAB?
	JRST LASTCH	;YES BACK UP ANOTHER
	POPJ P,

ALPHSX:	SKIPA	D,[Z (F.LETT)]
ATOMSX:	HRLZI	D,F.DIG+F.LETT
	TLZ	B,777777	;SET LH OF A+1 TO 0
	MOVEI	A,0
	MOVE	X1,[POINT 6,A]
ATOMS1:	TDNN	C,D
	POPJ	P,
	PUSHJ	P,SCNLTN	;PACK THIS LETTER INTO A.
	JFCL		;SCNLTN HAS SKIP RETURN.
	TLNE	X1,770000
	JRST	ATOMS1
	POPJ	P,

LINLIM:	TLNE	C,F.TERM
	JRST	LINL1
	PUSHJ	P,QSA
	ASCIZ /--/
	JRST	.+1		;ALLOW OPTIONAL "--" BEFORE NUMBERS.
	PUSHJ	P,GETNUM
LINL1:	MOVEI	N,0
	MOVEM	N,FRSTLN
	TLNE	C,F.TERM
	JRST	LINL2
	TLNN	C,F.COMA
	JRST	COMM1
	PUSHJ	P,NXCH
	PUSHJ	P,GETNUM
LINL2:	MOVSI	N,1
	MOVEM	N,LASTLN
	POPJ	P,
RESERR:	PUSHJ	P,INLMES
	ASCIZ /COMMAND ERROR (YOU MAY NOT OVERWRITE LINES OR CHANGE THEIR ORDER)
/
	JRST	COMM1B
	MOVE	X2,CEIL(R)
	HRRZ	X1,CEIL-1(R)	;SLIDE ROLL DOWN  NEXT TO LOWER ROLL
	ADD	X2,X1
	HRL	X1,FLOOR(R)	;SET UP BLT TO MOVE ROLL
	SUB	X2,FLOOR(R)
	HRRZM	X1,FLOOR(R)	;SET NEW ROLL FLOOR
	BLT	X1, (X2)
	MOVEM	X2,CEIL(R)
	POPJ	P,

	DEFINE R(A)
<	IRP	A
<	EXP	DO'A+1	>>
OUTPT:	R<1,2,3,4,5,6,7,8,9,10>
	DEFINE R(A)
<	IRP	A
<	EXP	DO'A+2
	EXTERN	DO'A	>>
OUTCNT:	R<1,2,3,4,5,6,7,8,9,10>
	DEFINE R(A)
<	IRP	A
<OPS'A:	OCT	1
	SIXBIT/DSK/
	XWD	DO'A,0
	INTERN	OPS'A	>>
	R<1,2,3,4,5,6,7,8,9>
	DEFINE R(A)
<	IRP	A
<IPS'A:	OCT	1
	SIXBIT/DSK/
	XWD	0,DI'A
	INTERN	IPS'A	>>
	R<1,2,3,4,5,6,7,8,9>

	DEFINE R(A)
<	IRP	A
<	EXP	DI'A+1
	EXTERN	DI'A	>>
INPT:	R<1,2,3,4,5,6,7,8,9>

	DEFINE	R(A)
<	IRP	A
<	EXP	DI'A+2	>>
INCNT:	R<1,2,3,4,5,6,7,8,9>

	DEFINE R(A)
<	IRP	A
<	EXP	DBUF'A
	EXTERN	DBUF'A,LINB'A	>>
BA:	R<1,2,3,4,5,6,7,8,9,10>

	DEFINE R(A)
<	IRP	A
<	POINT 7,LINB'A	>>
LINPT:	R<0,1,2,3,4,5,6,7,8,9,10>


	EXTERN	IFIFG,ODF
PRINT:	HRLI	T,440700
PRINT1:	ILDB	C,T
	CAMN	C,D
	POPJ	P,
	PUSHJ P,OUCH
	JRST PRINT1
OUCH:	SKIPE	ODF
	JRST	DSKOT
	SKIPG TYO +2
	OUTPUT
	SOS TYO+2
	IDPB	C,TYO+1
OUCH1:	SKIPN	ODF
	JRST	OUCH2
	CAIE	C,12
	AOSA	HPOS(LP)		;UPDATE LINE LOCATION COUNT
	SETZM	HPOS(LP)
	POPJ	P,
OUCH2:	CAIE	C,12
	AOSA	HPOS
	SETZM	HPOS
	POPJ	P,
DSKOT:	SOSG	@OUTCNT-1(LP)
	JRST	DOS
DSKOT2:	IDPB	C,@OUTPT-1(LP)
	JRST	OUCH1
	EXTERNAL OUTDSK,STADSK
DOS:	DPB	LP,[POINT 4,OUTDSK,12]
	XCT	OUTDSK
	JRST	DSKOT2

PRTNUM:	IDIVI	T,^D10
	JUMPE	T,PRTN1
	PUSH	P,T1
	PUSHJ	P,PRTNUM
	POP	P,T1
PRTN1:	MOVEI	C,60(T1)
	AOS NUMCOT
	JRST	OUCH



INLMS0: SETZM ODF
INLMES:	EXCH	T,(P)	;GET MSG ADR AND SAVE T.
	MOVEI	D,0		;END ON NULL
	PUSHJ	P,PRINT		;PRINT THE MESSAGE
	EXCH	T,(P)
	JRST	CPOPJ1	;RTN AFTER MSG.
PCRLF:	SKIPN	ODF
	JRST	.+5
	SKIPE	NLN-1(LP)
	JRST	.+3
	SKIPE	FMTFLG
	MOVEI	A, 2
	PUSHJ	P,INLMES
	ASCIZ	/
/
	PUSH	P,LP
	SKIPN	ODF
	SETZ	LP,
	SKIPN	NLN-1(LP)
	SETOM	LNINS-1(LP)
	SETZM	NUMFLA(LP)		;DURING EXECUTION, SIGNAL THAT
	SETZM	TABVAL(LP)
	POP	P,LP
	OUTPUT
	POPJ P,
				;A NUMBER WAS NOT THE LAST ITEM PRINTED
SUBTTL COMPILER MAIN LOOP

;BEGINNING OF COMPILATION

RUNCCL:	JFCL
	MOVE T,FLCOD
	MOVEI X1,LINROL
	PUSHJ	P, CLOB		;CLOBBER TIME
	PUSHJ	P,LOCKON	;PROTECT REST OF COMPILATION
	PUSHJ	P,PRESS		;GUARANTEE SOURCE DOESN'T MOVE!!!
	MOVEI	X1,	TXTROL
	MOVEM	X1,TOPSTG		;TXT,LIN,CODROLS ARE STODGY. OTHERS MOVE.
	SETZM	CONLOC
	MOVEI	R,LINROL
	PUSHJ	P,SLIDRL	;SLIDE LINROL DOWN NEXT TO TXTROL.
	JRST RESER
SLIDRL:	MOVE	X2,CEIL(R)
	HRRZ	X1,CEIL-1(R)	;SLIDE ROLL DOWN  NEXT TO LOWER ROLL
	ADD	X2,X1
	HRL	X1,FLOOR(R)	;SET UP BLT TO MOVE ROLL
	SUB	X2,FLOOR(R)
	HRRZM	X1,FLOOR(R)	;SET NEW ROLL FLOOR
	BLT	X1, (X2)
	MOVEM	X2,CEIL(R)
	POPJ	P,


UXIT:	;EXIT
	JFCL
	CALLI 12

	SUBTTL UTILITY SUBROUTINES
;SUBROUTINES FOR GENERAL ROLL MANIPULATION

CLOSUP:	MOVN	X1,E		;COMPUTE NEW END OF ROLL
	ADDB	X1,CEIL(R)	;AND STORE IT
	MOVE	X2,B		;CONSTRUCT BLT WORD
	ADD	X2,E
	MOVS	X2,X2
	HRR	X2,B
	BLT	X2,-1(X1)		;MOVE DOWN TOP OF ROLL
	POPJ	P,

CLOB:	MOVEI	T1,COMTOP	;ROUTINE TO CLOBBER ALL MOVEABLE ROLLS
CLOB1:	MOVEM	T,FLOOR(T1)		;T CONTAINS CLOBBER VALUE.
	MOVEM	T,CEIL(T1)
	CAILE	T1,1(X1)		;DO NOT CLOBBER ROLLS <=(X1)
	SOJA	T1,.-3
	POPJ	P,



OPEN2:	MOVE	X2,E		;IS THERE ROOM ABOVE THIS STODGY ROLL?
	ADD	X2,CEIL(R)	;THE NEW CEILING
	CAMLE	X2,FLOOR+1(R)
	JRST	OPENU0		;NO ROOM, PACK OTHER ROLLS UP
	ADDM	E,CEIL(R)	;THERE IS ROOM, INCREMENT CEILING
	POPJ	P,

OPENU0:	SUB	B,FLOOR(R)
	PUSHJ	P,PANIC
	ADD	B,FLOOR(R)

OPENUP:	CAMG	R,TOPSTG		;OPEN UP THE TOP STODGY ROLL?
	JRST	OPEN2		;YES. OPEN UPWARDS, NOT DOWN
	MOVN	X2,E
	MOVE	X3,TOPSTG	;DO NOT MOVE STODGY ROLLS
	ADD	X2,FLOOR+1(X3)
	CAMGE	X2,CEIL+0(X3)
	JRST	OPENU0		;NEED MORE ROOM
	HRL	X2,FLOOR+1(X3)	;CONSTRUCT BLT WORD
	SUB	B,E		;FIRST WORD OF GAP
	BLT	X2,-1(B)	;MOVE ROLLS DOWN

	MOVEI	X1,1(X3)		;ADJUST POINTERS FOR ROLLS JUST BLT'D.
	MOVN	X2,E
OPEN1:	ADDM	X2,FLOOR(X1)
	CAML	X1,R
	POPJ	P,
	ADDM	X2,CEIL(X1)
	AOJA	X1,OPEN1


;RPUSH - PUSH A ON TOP OF DESIGNATED ROLL

RPUSH:	MOVEI	E,1
	PUSHJ	P,BUMPRL	;MAKE ROOM
	MOVEM	A,(B)		;STORE WORD
	POPJ	P,

;ROUTINE TO ADD TO END OF ROLL
;E CONTAINS SIZE, R CONTAINS ROLL NUMBER

BUMPRL:	MOVE	B,CEIL(R)
	ADD	B,E
	CAIE	R,ROLTOP
	SKIPA	X1,FLOOR+1(R)
	HRRZ	X1,JOBREL
	CAMLE	B,X1
	JRST	BUMP1
	EXCH	B,CEIL(R)
	P,

BUMP1:	MOVE	B,CEIL(R)
	CAIE	R,CODROL
	CAIN	R,SEXROL
	JRST	.+2
	JRST	OPENUP
	ADDI	E,^D10		;***EXTRA 10 LOCS
	PUSHJ	P,OPENUP
	MOVNI	X1,^D10	;TAKE BACK THE 10 LOCS
	ADDM	X1,CEIL(R)
	POPJ	P,


;BINARY SEARCH OF SORTED ROLL
;CALL WITH KEY IN A
;RETURN IN B ADDRS OF FIRST
;ENTRY NOT LESS THAN KEY
;SKIP RETURN IF LEFT SIDES EQUAL

SEARCH:	MOVE	B,FLOOR(R)
	SKIPA	X1,CEIL(R)
SEAR1:	MOVEI	B,1(X2)
	CAIGE	B,(X1)
	JRST	SEAR2
	CAML	B,CEIL(R)
	POPJ	P,
	JRST	SEAR3

SEAR2:	MOVEI	X2,@X1
	ADD	X2,B
	ASH	X2,-1
	CAMLE	A,(X2)
	JRST	SEAR1
	HRRI	X1,0(X2)
	CAIGE	B,(X1)
	JRST	SEAR2

SEAR3:	HLLZ	X3,(B)
	CAMN	X3,A
	AOS	(P)

	POPJ	P,
;ROUTINE TO QSA FOR "THEN" OR "GOTO" (USED IN "IF", "ON" STATEMENTS)
THENGO:	PUSHJ	P,QSA
	ASCIZ /THEN/
	CAIA
	POPJ	P,
	PUSHJ	P,QSA
	ASCIZ /GOTO/
	FAIL <ILLEGAL FORMAT>
	POPJ	P,

;COMMON SUBROUTINE RETURNS

CPOPJ1:	AOS	(P)
CPOPJ:	POPJ	P,

;ERROR RETURNS

ILFORM:	FAIL	<ILLEGAL FORMULA>
ILVAR:	FAIL	<ILLEGAL VARIABLE>
GRONK:	FAIL	<ILLEGAL FORMAT>
ILLINS:	FAIL	<ILLEGAL INSTRUCTION>


;COMPILATION ERROR


FAILER:;ROUTINES TO ALLOW AND DELAY REENTRY.
;LOCKON TEMPORARILY PREVENTS REENTRY
;LOCKOF ALLOWS REENTRY AND REENTERS IF THERE IS A STANDING REQUEST
;REENTR MAKES A REENTRY OR MAKES A REQUEST AND CONTINUES
LOCKON:	SKIPGE	RENFLA
	SETZM	RENFLA		;TURN ON REENTER PROTECT
	POPJ	P,

LOCKOF:	SKIPLE	RENFLA
	JRST	BASIC		;ACT ON OLD REENTER REQUEST
	SETOM	RENFLA		;ALLOW REENTER
	POPJ	P,

REENTR:	SETZM	PRNTSW		;CLEAR LINE PRINTER SWITCH
	SETZM	CONLOC		;AND THE "PROCEED" POINTER
	SKIPGE	RENFLA
	JRST	BASIC		;REENTER IF ALLOWED
	AOS	RENFLA		;MAKE REQUEST BY SETTING FLAG PLUS
	JRST	2,@JOBOPC


IFN INTRSW,<;******* CODE TO HANDLE INTERUPT TOM S. 18-AUG-70
INTRPT:	JUMPLE	L,INTRP1+1
	SKIPN	FLOFLG		;ADDITION TOM S. 11-NOV-70
	JRST	INTRP1
	PUSHJ	P,INLMS0
	ASCIZ	/AT- /
	PUSHJ	P,GOSR3
	OUTPUT		;END MOD TOM
INTRP1:	TTCALL	2, C		;DID HE SAY SOMETHING
	POPJ	P,		;NO GO AWAY
	CAIE	C, 7		;YES.	MIGHT IT BE BONG ?!
	POPJ	P,		;NOPE GO AWAY THEN
INTRP0:	POP	P,T1		;YES FAKE A JSP
	EXTERNAL	BELFLG
	SETOM	BELFLG	
	JRST	UPAUSE		;DO A PAUSE
	;******** END MOD T.S.>

;ROUTINE TO READ CHARACTER, SKIPPING BLANKS
;CALL:	MOVE	T,<POINTER TO CHAR BEFORE FIRST>
;	PUSHJ	P,NXCH
;	...	RETURN, C:= (<FLAGS>)CHARACTER

NXCHS:	ILDB	C,T		;DOESNT SKIP TAB OR BLANK
	CAIE	C," "
	CAIN	C,11
	POPJ	P,
	CAIA			;SKIP INTO NXCH

NXCH:	ILDB 	C,T		;FETCH NEXT CHARACTER
	HLL	C,CTTAB(C)	;GET FLAGS FROM CTTAB
	TRNE	C,100
	HRL	C,CTTAB-100(C)
	TLNE	C,F.SPTB	;SPACE OR TAB?
	SKIPE NOSKIP	;SEE IF WE SHOULD IGNORE
	POPJ P,	;NO USE SPACE OR TAB AS DELIMITER
	JRST	NXCH		;YES. IGNORE
NXCHD:	ILDB C,T
NXCHD2:	HLL C,CTTAB(C)
	TRNE C,100
	HRL C,CTTAB-100(C)
	POPJ P,


;GET NEXT CHAR, BUT CHECK FOR ILLEGAL CHARS (CHARS THAT COULD ONLY BE IN A STRING)
;SCAN INITIAL LETTER, LETTER IS PLACED LEFT
;JUSTIFIED IN A, 7-BIT ASCII.

SCNLT1:	HRRZ	A,C
	ROT	A,-7
	JRST	NXCH

;SCAN SECOND LETTER, NON-SKIP RETURN IF NOT LETTER.
;MAKE 7-BIT LETTER LEFT JUST IN A
;INTO 6-BIT. THAN PUT 6-BIT CURRENT LETTER IN A.

SCNLT2:	TLNN	C,F.LETT
	POPJ	P,
SCN2:	TLNN	A,400000	;ENTER HERE TO PROCESS NON-LETTER CHARS
	TLZA	A,200000
	TLO	A,200000
	LSH	A,1
	MOVE	X1,[POINT 6,A,5]
	JRST	SCNLTN	

;ENTER HERE TO SCAN SECOND CHAR EVEN IF BOTH ARE NOT LETTERS.


;SCAN THIRD LETTER, NON-SKIP IF NOT LETTER.
;PUT 6-BIT LETTER TO 3RD 6-BIT FIELD IN A.


;NOW PUT 6-BIT LETTER INTO A, ADJUSTING LOWER CASE, INCREMENTING POINTER.

SCNLTN:	TLNN	C,F.LCAS
	TRC	C,40
	IDPB	C,X1
	AOS	(P)
	JRST	NXCH

;QUOTE SCAN AND TEST
;CALL WITH PATTERN ADDRS IN X1
;SKIP IF EQUAL.	C,T UPDATED TO LAST CHAR SCANNED.
QST:	HRLI	X1,440700	;MAKE BYTE PNTR TO PATTERN
QST1:	ILDB	X2,X1		;GET PATTERN CHAR
	JUMPE	X2,CPOPJ1	;DONE ON NULL
	SUBI	X2,(C)
	JUMPE	X2,.+4		;DO CHARACTERS MATCH?
	TLNE	C,F.LCAS	;NO. LOWER CASE LETTER?
	CAME	X2,[ EXP -40]	;YES. SAME LETTER OF ALPHABET?
	JRST	QST2		;NO. MATCH FAILS
	PUSHJ	P,NXCH
	JRST	QST1
QST2:	ILDB	X2,X1		;ON FAIL
	JUMPN	X2,.-1		;SKIP TO NULL
	POPJ	P,


;QUOTE SCAN OR FAIL
;CALL WITH INLINE PATTERN
;GO TO GRONK IF NO MATCH

QSF:	POP	P,X1
	PUSHJ	P,QST
	JRST	GRONK
	JRST	1(X1)


;QUOTE SCAN WITH ANSWER
;CALL WITH INLINE PATTERN
;SKIP ON SUCCESS		;ON FAIL, RETURN WITH C,T RESTORED

QSA:	POP	P,X1		;GET PATTERN ADDRESS
	PUSH	P,C		;SAVE C,T
	PUSH	P,T
	PUSHJ	P,QST		;SAVE STRING
	JRST 	.+2
	JRST	QSA1		;MATCH
	POP	P,T		;NO MATCH.  BACK UP
	POP	P,C
	JRST	1(X1)

QSA1:	POP	P,X2
	POP	P,X2
	JRST	2(X1)

;ROUTINE TO READ NEXT INTEGER FROM SCANNED LINE
;CALL:	MOVE	T,POINTER TO FIRST CHAR
;	PUSHJ	P,GETNUM
;	...	FAIL RETURN
;	...	SUCCESS RETURN, INTEGER IN N

GETNU:	TDZA	X1,X1		;GET A NUMBER OF ANY LENGTH.
GETNUM:	MOVEI	X1,5		;GET A NUMBER OF AT MOST 5 DIGS
	TLNN	C,F.DIG		;NUMERAL?
	JRST NODIG
	MOVEI	N,-60(C)	;YES.  ACCUMULATE FIRST DIGIT
GETN1:	MOVE	G,T		;SAVE PNTR FOR USE BY INSERT
	PUSHJ	P,NXCH		;GET NEXT CHAR
	SOJE	X1,FIVE	;EXIT FIVE DIGITS ALREADY
	TLNN	C,F.DIG		;NUMERAL?
	JRST FIVE	;NO RETURN
	IMULI	N,^D10		;YES.  ACCUMULATE NUMBER
	ADDI	N,-60(C)
	JRST	GETN1		;GO FOR MORE
FIVE:	SETZM	NOSKIP	;TURN OFF FLAG TO IGNORE BLANKS AND TABS
	JRST CPOPJ1
NODIG:	SETZM NOSKIP
	POPJ P,

	EXTERN	STODSK
;ROUTINE TO READ A LINE INTO LINB0
;CALL:	PUSHJ	P,INLINE

INLINE:	PUSH	P, X1
	SETZ	X1,
	SETO	T1,
	SKIPE	IFIFG
	SKIPA	T, LINPT(LP)
	MOVE	T, LINPT
	JRST	INLI1A
INLI1:	SKIPE	IFIFG
	JRST	INLA
	ILDB	C, TYI+1
	JRST	INLB
INLA:	SOSG	@INCNT-1(LP)
	PUSHJ P,DSKIN
INLA2:	ILDB	C,@INPT-1(LP)
INLB:IFN INTRSW,<CAIN	C, 7		;INTERRUPT REQUEST?
	JRST	INTR		;YES.  GO>
	CAIE	C,15		;CR?
	CAIN	C, 0
	SOJA	T1, INLI1A
	CAIN	C,"_"		;DELETE?
	JRST	INLI3		;YES
	CAIE	C,30		;<CONTROL-X>?
	CAIN	C,175		;ALTMODE?
	JRST	INLI4		;YES.  IGNORE LINE
	CAIE	C, 176
	CAIN	C, 33		;SOME OTHER KIND OF ALTMODE?
	JRST	INLI4
	CAIE	C,21	;IGNORE XON,XOFF
	CAIN	C,23
	SOJA	T1,INLI1A
	CAIG	C,14		;LINE TERMINATOR?
	CAIGE	C,12
	CAIA
	JRST	INLI2		;YES.  GO FINISH UP
INLB2:	CAIGE	T1,^D94	;ROOM FOR CHAR+1 MORE? (5*23-1)
	IDPB	C,T		;STORE CHAR
INLI1A:	SKIPE	IFIFG
	AOJA	T1,INLI1
	SOSLE	TYI+2		;MORE INPUT?
	AOJA	T1,INLI1	;YES.  BUMP COUNT AND GO GET MORE
	INPUT
	STATZ	20000
	JRST	BASIC0
	STATO	740000
	AOJA	T1,INLI1
INLSYS:	PUSHJ	P,TTYIN
	PUSHJ	P,INLMES
	ASCIZ /SYSTEM ERROR/
	JRST	UXIT

INLI2:	MOVEI	C,15		;DONE.  PUT CR IN BFR.
	IDPB	C,T
	POP	P,X1
RESCAN:	SKIPN	IFIFG
	SKIPA	T,LINPT
	MOVE	T,LINPT(LP)
	SKIPE	IFIFG
	JRST	INLI8
	SETZM	HPOS		;CARRIAGE POSITION := LFT MRGN
	JRST	NXCH		;GET FIRST CHAR AND RETURN
INLI8:	SETZM	HPOS(LP)
	JRST	NXCH

INLI3:	JUMPLE	T1,INLI1B	;BACKARROW HANDLER.  IGNORE IF AT LEFT
	PUSHJ	P, BACK1	;BACK UP BYTE POINTER
	SUBI	T1,2
	JRST	INLI1A
INLI1B:	SETO	T1,
	JRST	INLI1A

INLI4:	MOVEI	T,DELMSG	;ACKNOWLEDGE ALTMODE
	CALL	T,[SIXBIT /DDTOUT/]

	POP	P,X1
	JRST	INLINE

DELMSG:	ASCIZ	/ DELETED
/

IFN INTRSW,<INTR:	SKIPN	INTFLA	;MOD FOR ENA & DIS TOM S.
	JRST	INLB2		;13-NOV-70
	SKIPE	IFIFG		;DSK READ?
	JRST	INLB2		;YES.  STORE BELL.
	HRRZ	C, -1(P)	;WHERE DID WE COME FROM?
	CAIE	C, GETT1+1	;READING A DISK FILE?
	JRST	INTRP0		;NO.  INTERRUPT SIGNAL
	MOVEI	C, 7		;YES.  RESTORE THE BELL
	JRST	INLB2		;AND STORE IT.>

;ROUTINE TO START READING NEXT LINE OF PROGRAM
NXLINE:	MOVE	T,FLLIN
	ADDI	T,(L)
	MOVE	T,(T)
	MOVS	D,T		;SAVE LINE START
        HRRZM   D,THELIN
	HRLI	T,440700
	MOVE	G,FLREF	;SETUP REFROL REFERENCE.
	ADDI	G,(L)
	JRST	NXCH



DSKIN:  DPB     LP,[POINT 4,INDSK,12] ;DISK INPUT
	XCT	INDSK
	DPB	LP,[POINT 4,STADSK,12]
	XCT	STADSK
	JRST	EOFFAL
	DPB	LP,[POINT 4,STODSK,12]
	XCT	STODSK
	POPJ P,
	POP P,	;ERROR
	JRST INLSYS

EOFFAL:	JRST UXIT	;EOF
	EXTERN	INDSK

	EXTERN	THELIN


BASIC0:	SETZM	CCLFLG
	SETZM	CCRNFL
	PUSHJ	P, TTYIN	;GRAB A TTY
	JRST	RUNCCL

;ROUTINE TO BACK UP BYTE POINTER IN T ONE OR TWO BYTES:

BACK2:	PUSH	P, C
	MOVEI	C, 3
	JRST	.+3
BACK1:	PUSH	P, C
	MOVEI	C, 4
	IBP	T
	SOJG	C, .-1
	SUBI	T, 1
	POP	P, C
	POPJ	P,

PANIC:	PUSHJ	P,PRESS		;COMPRESS MEMORY
	MOVE	X3,TOPSTG	;IS THERE ROOM BETWEEN STODGY AND
	MOVE	X1,FLOOR+1(X3)	;MOVEABLE ONES?
	SUB	X1,CEIL(X3)
	CAML	X1,E		;ENOUGH ROOM?
	POPJ	P,

	MOVE	X1,JOBREL	;EXPAND BY 1K
	ADDI	X1,2000
	CALL	X1,[SIXBIT /CORE/]
	JRST	.+2		;CANT
	JRST	PANIC		;OK.  GO MOVE ROLLS

PANIC1:	PUSHJ	P,INLMS0
	ASCIZ	/OUT OF ROOM/
	JRST	UXIT


PRESS:	PUSH	P,G		;SAVE AC
	SKIPN	PAKFLA		;ARE LINES PACKED?
	JRST	PRESS5		;YES
	SETZM	PAKFLA

PRESS1:	MOVE	X1,FLTXT	;LOOK FOR EMPTY SPACE
PRESS2:	CAML	X1,CETXT		;TRHOUGH LOOKING?
	JRST	PRESS5
	SKIPE	(X1)		;A FREE WORD?
	AOJA	X1,PRESS2	;NO

	MOVEI	X2,1(X1)	;YES
PRESS3:	CAML	X2,CETXT
	JRST	PRESS4		;FREE TO END
	SKIPN	(X2)
	AOJA	X2,PRESS3	;LOOK FOR NON-FREE WORD

	SUB	X1,X2		;X1 :=-LNG OF MOVE
	MOVE	X3,FLLIN
PRES3A:	CAML	X3,CELIN	
	JRST	PRES3B
	HRRZ	G,(X3)
	CAML	G,X2
	ADDM	X1,(X3)
	AOJA	X3,PRES3A

PRES3B:	MOVE	G,CETXT
	ADD	G,X1
	MOVEM	G,CETXT
	ADD	X1,X2
	HRL	X2,X1
	MOVSS	X2
	BLT	X2,-1(G)
	JRST	PRESS2

PRESS4:	MOVE	X3,X1	;KLUDGE IF FREE TO END--MOVE IT ABOVE
	SUB	X3,X1
	ADDM	X3,CETXT
;ROUTINE TO MOVE ROLLS UP

PRESS5:	MOVEI	G,ROLTOP	;HIGHEST MOVABLE ROLL
	MOVE	X1,JOBREL	;X1 IS PREVIOUS FLOOR
				;NOTE: TOP WORD OF USR CORE IS LOST

PRESS6:	MOVE	X2,CEIL(G)	;GET OLD CEIL AND FLOOR
	MOVE	X3,FLOOR(G)
	SUBI	X2,1		;SET UP X2 FOR POP LOOP
	ORCMI	X2,777777
	MOVEM	X1,CEIL(G)	;NEW CEILING

PRESS7:	CAILE	X3,(X2)		;DONE?
	JRST	PRESS8
	POP	X2,-1(X1)	;MOVE ONE WORD
	SOJA	X1,PRESS7

PRESS8:	MOVEM	X1,FLOOR(G)	;NEW FLOOR
	SOS	G		;GO TO NEXT LOWER ROLL
	CAMLE	G,TOPSTG	;IS THIS ROLL MOVEABLE?
	JRST	PRESS6		;YES. GO PRESS IT.
PRESS9:	POP	P,G	;RESTORE G
	POPJ	P,	;RETURN

;SPACE CHECK, PANIC AND PRESS FOR VARIABLE SPACE, WHICH
;RANGES FROM (VARBOT) TO (JOBREL).  NEXT AVAILABLE WORD IS
;(VARFRE). THE MAXIMUM LENGTH ALLOWED FOR A LITERAL OR
;INPUT STRING IS
	F.SAVE=100
LIST0:	TROA	F, F.SAVE
LIST1:	TRZ	F, F.SAVE	;LISTING, NOT SAVING
	MOVE	A,FLLIN
LIST2:	CAML	A,CELIN	;READ LINE LIMITS
	JRST	LIST3		;DONE IF NO MORE
	HLRZ	T,(A)		;T := LINE NO
	CAMG	T,LASTLN
	CAMGE	T,FRSTLN	;AFTER FIRST TO PRINT?
	AOJA	A,LIST2		;NO
LIST2A:	MOVE T,TYO+2
	JUMPLE T,LIST22
	IDIVI T,5
	HRRZ X2,(A)	;GET BINNING OF LINE
	HRRZ X1,1(A)	;GET BEGIN OF NEXT LINE
	CAMN X1,CELIN	;IS IT LAST LINE
	MOVE X1,CETXT	;YES
	SUB X1,X2	;CALC THE NO OF WORDS
	CAMLE T,X1	;ENOUGH ROOM?
	JRST LIST2B	;YES
	OUTPUT	;NO
	JRST LIST2A	;START OVER
LIST2B:	JUMPE T1,LIST22
	SETZ C,
	PUSHJ P,OUCH
	SOJG T1,.-2
	EXTERN NUMCOT
LIST22:	HLRZ T,(A)
	SETZM NUMCOT
	PUSHJ P,PRTNUM
	MOVE T,NUMCOT
	SUBI T,5
	MOVE T1,@TYO+1
	JUMPE T,LIST23
LIST21:	LSH T1,-7
	TLO T1,300000
	IBP TYO+1
	SOS TYO+2
	AOJL T,LIST21
LIST23:	TRO T1,1
	MOVEM T1,@TYO+1
LIST25:	MOVE	T,(A)
	MOVEI	D,15		;QUOTE CHAR
	PUSHJ	P,PRINT
	PUSHJ	P,INLMES
	ASCIZ /
/
	AOJA	A,LIST2

LIST3:	CLOSE
;	TRZN	F, F.SAVE	;SAVING FILE?
	POPJ P,
	SETZM	FILDIR+3	;YES.  SET UP FOR RENAME.
	MOVSI	A, 055000
	IORM	A, FILDIR+2	;SET GOOD PROTECTION
	RENAME	FILDIR
	JRST	.+2
	JRST	LIST3
	PUSHJ	P, TTYIN	;GET THE TTY BACK
	PUSHJ	P, INLMES
ASCIZ/ATTEMPT TO SET <055> PROTECTION FAILED.  ERROR CODE /
	HRRZ	T, FILDIR+1
	ADDI	T, 60
	PUSHJ	P, PRTNUM
	JRST	LIST3


	DSK=13

;ROUTINE TO RENUMBER THE BASIC PROGRAM THAT IS IN CORE.
;THE COMMAND IS 
;       RESEQUENCE NN,MM,LL
;WHERE NN IS THE FIRST NUMBER AND LL IS THE STEP VALUE.
;IF OMITTED, LL, OR BOTH NUMBERS=10

;ALL LINE NUMBERS LESS THAN MM WILL NOT BE RESEQUENCED. MM MUST NOT
;BE GREATER THAN NN

;A NUMBER IS A LINE NUMBER IF:
;IT IS THE FIRST ATOM ON A LINE.
;	IT FOLLOWS AN ATOM BEGINNING WITH THE LETTERS:
;		"GOS"   OR   "GOT"   OR   "THE"  OR "USE"  OR "USI"
;ALSO, AFTER THE ATOM "GOTO" HAS BEEN IDENTIFIED, THE NUMBER

;FOLLOWING A COMMA IS A LINE NUMBER.
;REENTRY IS NOT ALLOWED DURING "RESEQUENCE".

RESER:	MOVE	N,STSEQ	;GET SECOND NO.
	HRRZM	N,LOWEST
	MOVEI	N,^D1		;IF FIRST ARG=0, ASSUME FIRST LINE=1
	SKIPE NEWSEQ
	MOVE N,NEWSEQ
	MOVEM	N,FRSTLN
	MOVEI N,^D10
	SKIPE SEQINC
	MOVE N,SEQINC

RES1:	MOVEM	N,LASTLN	;SAVE INCREMENT
;	MOVE A,ENDSEQ
;	CAIN A,^D99999
;	JRST ALL1
;	MOVEI R,LINROL
;	PUSHJ P,SEARCH
;	JFCL
;	MOVEM B,UPLIN
	EXTERNAL UPLIN
SEQ1:	HRLZ	A,LOWEST	;SEARCH FOR FIRST LINE TO CHANGE
	MOVEI	R,LINROL
	PUSHJ	P,SEARCH
	JFCL
;	CAIN A,1
;	MOVE B ,FLLIN
;	CAME B,FLLIN
;	JRST NO1
;	MOVE R, UPLIN
;	CAMN R,CELIN
;	SKIPA
;	JRST NO1
;	MOVEI R,^D99999
;	MOVEM R,HGLIN
;	JRST ALLIN
;	EXTERNAL HGLIN
;ALL1:	MOVE B,CELIN
;	MOVEM B,UPLIN
;	JRST SEQ1
;NO1:	MOVE R,ENDSEQ
;	MOVEM R,HGLIN
	CAMN B,FLLIN
	JRST ALLIN
	HLRZ N,-1(B)
	CAML N,FRSTLN
	JRST	RESERR
ALLIN:	MOVN	X2,B
	ADD	X2,CELIN	;THIS IS THE NUMBER OF LINES TO RESEQ
	SUBI	X2,1
	IMUL	X2,LASTLN
	ADD	X2,FRSTLN
	CAILE	X2,^D99999
	JRST	SEQOV
	PUSHJ	P,LOCKON	;DONT ALLOW REENTRY.
	MOVE	E,CELIN	;COMPUTE NUMBER OF LINES
	SUB	E,B
	JUMPE	E,UXIT	;NOTHING TO RENUMBER
	MOVN	L,E
	MOVSI	L,(L)
	SUB	B,FLLIN
	MOVEM	B,LOWSTA
	HRR	L,B
	PUSH	P,L		;SAVE L FOR SECOND LOOP.
	HRL	B,B		
	SUB	L,B

;THE LOOP THAT COPIES EACH LINE FOLLOWS:
SEQ2:	MOVE	D,[POINT 7,LINB0]	;BUILD EACH LINE IN LINB0. THEN REINSERT IT.
	MOVEM	D,SEQPNT
	HRRZ	F,L
	ADD	F,FLLIN
	HRRZ	T,(F)
	HRLI	T,440700	;POINTER TO OLD LINE IS IN G
				;F USED AS A FLAG REGISTER FOR " ' ETC.
;THE FLAGS ARE
			REST.F=1	;COPY  THE REST (APOST SEEN)
			TOQU.F=2	;COPY TO QUOTE SIGN
			COMM.F=4	;LINE NUMBER FOLLOWS ANY COMMA
			NUM.F=10	;NEXT NUMBER IS LINE NUMBER

		USE.F==20	;PRINT USE UR USING STATEMENT.

;THE CHARACTER/ATOM LOOP:
SEQ3:	PUSHJ	P,NXCHS		;GET NEXT CHAR, EVEN IF SPACE OR TAB
	TLNE	C,F.CR
	JRST	SEQCR
	TLNE	C,F.QUOT		;TEST FOR QUOTE CHAR
	TLCA	F,TOQU.F		;REVERSE QUOTE SWITCH AND COPY THIS CHAR
	TLNE	F,TOQU.F
	JRST	SEQCPY
	TLNE	C,F.APOS
	TLOA	F,REST.F		;APOST SEEN, COPY REST
	TLNE	F,REST.F
	JRST	SEQCPY
	MOVE	G,T			;SAVE POINTER
	TLNN	F,NUM.F		;EXPECTING A LINE NUMBER?
	JRST	SEQ4			;NO. LOOK FOR KEYW ATOMS
	CAIN	C, ":"
	TLZA	F, NUM.F!COMM.F	;":" INDICATES PRINT USING STATEMENT WITH NO #
	TLNN	C,F.DIG
	JRST	SEQCPY
	JRST	SEQNUM			;GO TRANSLATE LIN NUMBER
SEQ4:	TLNE	F,COMM.F
	TLNN	C,F.COMA
	JRST	.+3
	TLO	F,NUM.F		;THIS COMMA IMPLIES NUMBER TO FOLLOW
	JRST	SEQCPY
	PUSHJ	P,ALPHSX		;PUT NEXT ALL-LETTER ATOM IN A
	MOVEI	B,SEQTND-SEQTBL	;SET INDEX FOR TABLE OF KEYWORDS PRECEDING LINE NUMBERS
	MOVE	T,G			;RESET CHAR POINTER TO START OF ATOM.
	CAMN	A,SEQTBL(B)
	TLOA	F,NUM.F+COMM.F		;WE FOUND A KEYWORD
	SOJGE	B,.-2
	CAIE	B, 2		;PRINT USE
	CAIN	B, 3		;OR PRINT USING?
	TLO	F, USE.F
	LDB	C,T
SEQCPY:	IDPB	C,SEQPNT
	JRST	SEQ3


SEQTBL:	SIXBIT /GOSUB/		;TABLE OF KEYWORDS PRECEDINGLINE NUMBERS
	SIXBIT /GOTO/
	SIXBIT	/USE/
	SIXBIT	/USING/
SEQTND:	SIXBIT /THEN/

SEQNUM:	TLZE	F, USE.F
	TLZ	F, NUM.F+COMM.F
	PUSH	P,G		;SAVE POINTER IN CASE OF "GLOBAL" LINE NUMBER
	PUSHJ	P,GETNUM
	HALT	.
	CAMGE	N,LOWEST
	JRST	SEQB1		;DONT RESEQ THIS NUMBER
	MOVEI	R,LINROL
	HRLZ	A,N
	PUSHJ	P,SEARCH
	JRST	SEQBAD
SEQCAL:	SUB	B,FLLIN
	SUB	B,LOWSTA
	IMUL	B,LASTLN
	ADD	B,FRSTLN		;THIS IS THE NEW LINE NUMBER
	MOVE	X1,B
	PUSHJ	P,MAKNUM		;DEPOSIT THE NUMBER IN LINB0
	POP	P,X1			;CLEAR PLIST A LITTLE
SEQCA1:	TLZ	F,NUM.F
	LDB	C,T
	PUSHJ	P,NXCHS+1
	JRST	SEQ3+1
SEQBAD:	MOVE	T,N		;PRINT "GLOBAL" LINE NUMBER
	PUSHJ	P,PRTNUM
	PUSHJ	P,INLMES
	ASCIZ / IN LINE /
	HLRZ	T,(F)
	PUSHJ	P,PRTNUM
	PUSHJ	P,PCRLF
;IN A ROLL, AND THEN PRINTED AT THE END, WITH REENTRY ALLOWED.
SEQB1:	POP	P,T			;POINT TO BAD NUMBER.
	LDB	C,T
	TLZ	F,NUM.F
	JRST	SEQCPY			;COPY IT
SEQCR:	IDPB	C,SEQPNT
	HLRZ	N,(F)
	PUSHJ	P,ERASE		;ERASE OLD LINE COPY
	MOVE	T1,SEQPNT		;POINT TO END OF LINE FOR NEWLIN
	PUSHJ	P,NEWLIN		;INSERT NEW ONE WITH OLD LINE NUMBER.
	AOBJN	L,SEQ2			;DO NEXT LINE
	POP	P,L
	ADD	L,FLLIN
	MOVE	N,FRSTLN
	HRLM	N,(L)
	ADD	N,LASTLN
	AOBJN	L,.-2
	SKIPN ERRFLG
	JRST GOON
	JRST UXIT
GOON:	PUSHJ P, SAVER0
RUNRES: CALLI 12
	HRRI T1,RUNBLK
	CALLI T1,35
	HALT
RUNBLK:	SIXBIT/SYS/
	SIXBIT/SUDS/
	0
	0
	0
	0
	0
SEQOV:	PUSHJ	P,INLMES
	ASCIZ /COMMAND ERROR (LINE NUMBERS MAY NOT EXCEED 99999)
/
	JRST	FIXUP

;ROUTINE TO SAVE PROGRAM

SAVFIL:
SAVER0:	MOVE A,IFILE
	MOVEM A,OFILE
	MOVE A,[XWD OFILE,FILDIR]	;GET READY TO SET UP OUTPUT FILE NAME
	BLT A,FILDIR+3
	MOVE	A, SAVE1	;WHAT DEVICE?
	CALLI	A, 4
	TLNN	A, 1		;CAN I DO OUTPUT TO IT?
	JRST	NOOUT		;NO.
	OPEN	SAVI
	EXTERN	SAVE1,SAVI
	JRST	NODEV		;ILLEGAL DEVICE NAME
	PUSHJ	P,LOCKON		;DONT ALLOW REENTRY UNTIL 
;					;SAVE IS CHANGED TO BUILD TEMP FILE AND RENAME.
	TLNN	A, 4		;DIRECTORY DEVICE?
	JRST	SAVE2		;NO.  NO PROBLEM
	LOOKUP	FILDIR		;YES, DOES IT EXIST?
	JFCL
SAVE2:	CLOSE			;OTHERWISE REPLACE WILL APPEND.
	SETZM FRSTLN
	MOVSI N,1
	MOVEM N,LASTLN
	SETZM	FILDIR+2	;ASSURE CURRENT DATE
	SETZM	FILDIR+3
	HLLZS	FILDIR+1	;D FIX,N.A.

	ENTER	FILDIR
	JFCL
	OUTBUF	1
	JRST	LIST0
	END BASIC

(`=î